
&НаСервере
Процедура АБК_ПриСозданииНаСервереПосле(Отказ, СтандартнаяОбработка)
	
	Если СтрНайти(Список.ТекстЗапроса, "ВТ_Адреса") Тогда
		Возврат;	
	КонецЕсли;
	
	ТекстВТ_Адреса = "ВЫБРАТЬ
	|	КонтрагентыКонтактнаяИнформация.Ссылка КАК Ссылка,
	|	КонтрагентыКонтактнаяИнформация.Вид КАК Вид,
	|	МАКСИМУМ(КонтрагентыКонтактнаяИнформация.Представление) КАК Представление
	|ПОМЕСТИТЬ ВТ_Адреса
	|ИЗ
	|	Справочник.Контрагенты.КонтактнаяИнформация КАК КонтрагентыКонтактнаяИнформация
	
	|СГРУППИРОВАТЬ ПО
	|	КонтрагентыКонтактнаяИнформация.Ссылка,
	|	КонтрагентыКонтактнаяИнформация.Вид";
	
	Список.ТекстЗапроса = ТекстВТ_Адреса + ОбщегоНазначенияБГУ.ТекстРазделителяЗапросовПакета() + Список.ТекстЗапроса; 
	
	ДобавитьПолеАдресаВЗапрос("АдресПоПропискеФизическиеЛица",		"ЮридическийАдресКонтрагенты",	"ЮрАдрес", 			"ЮрАдрес / Прописка");
	ДобавитьПолеАдресаВЗапрос("АдресМестаПроживанияФизическиеЛица",	"ФактическийАдресКонтрагенты",	"ФактАдрес", 		"ФактАдрес / Проживание");
	ДобавитьПолеАдресаВЗапрос("EmailФизическиеЛица",				"EmailКонтрагенты",				"Email", 			"Email");
	ДобавитьПолеАдресаВЗапрос("ДругаяИнформацияФизЛица",			"ДругаяИнформацияКонтрагенты",	"ДругаяИнформация", "Другая информация");
	ДобавитьПолеАдресаВЗапрос("ТелефонРабочийФизическиеЛица",		"ТелефонКонтрагенты",			"Телефон", 			"Телефон");	
	
КонецПроцедуры   

&НаСервере 
Процедура ДобавитьПолеАдресаВЗапрос(ВидАдресаФЛ, ВидАдресаЮЛ, ИмяПоля, Заголовок)
	
	Если Элементы.Найти("АБК_Список"+ИмяПоля) <> Неопределено Тогда
		Возврат;	
	КонецЕсли;
	
	ИмяТаблицы = "ВТ_Адреса" + ИмяПоля;
	
	Список.ТекстЗапроса = СтрЗаменить(Список.ТекстЗапроса, "Справочник.Контрагенты КАК СправочникКонтрагенты",
	"Справочник.Контрагенты КАК СправочникКонтрагенты
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_Адреса КАК "+ИмяТаблицы+"
	|		ПО "+ИмяТаблицы+".Ссылка = СправочникКонтрагенты.Ссылка 
	|			И
	|				ВЫБОР
	|					КОГДА СправочникКонтрагенты.ЮридическоеФизическоеЛицо ССЫЛКА Справочник.ФизическиеЛица
	|						ТОГДА "+ИмяТаблицы+".Вид = Значение(Справочник.ВидыКонтактнойИнформации."+ВидАдресаФЛ+") 
	|					ИНАЧЕ "+ИмяТаблицы+".Вид = Значение(Справочник.ВидыКонтактнойИнформации."+ВидАдресаЮЛ+")
	|				КОНЕЦ ");   
	
	Список.ТекстЗапроса = СтрЗаменить(Список.ТекстЗапроса, "СправочникКонтрагенты.ПометкаУдаления,", 
	"СправочникКонтрагенты.ПометкаУдаления,
	|	"+ИмяТаблицы+".Представление КАК "+ИмяПоля+",");

	ДопКолонкаАдреса = Элементы.Вставить("АБК_Список"+ИмяПоля, Тип("ПолеФормы"), Элементы.Список, Элементы.ЭДО); 
	ДопКолонкаАдреса.Вид = ВидПоляФормы.ПолеВвода; 
	ДопКолонкаАдреса.ПутьКДанным = "Список."+ИмяПоля;
	ДопКолонкаАдреса.Заголовок = Заголовок;
	
КонецПроцедуры

