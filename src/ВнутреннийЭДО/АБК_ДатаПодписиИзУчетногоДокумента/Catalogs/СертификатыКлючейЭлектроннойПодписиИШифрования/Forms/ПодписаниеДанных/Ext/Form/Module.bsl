&НаСервере
&После("ПриСозданииНаСервере")
Процедура АБК_ДатаПодписанияВнутрЭДПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ДатаПодписи =  ТекущаяДата();
	
	ДобавляемыеРеквизиты	= Новый Массив;
	
	// Опишем ревизиты формы
	Реквизит_ДатаПодписи = Новый РеквизитФормы("ДатаПодписи",	Новый ОписаниеТипов("Дата", Новый КвалификаторыДаты(ЧастиДаты.ДатаВремя)));
	Реквизит_ИспользоватьДатуДокумента = Новый РеквизитФормы("ИспользоватьДатуДокумента", Новый ОписаниеТипов("Булево"));
	// Заполним массив после описания реквизитов формы
	ДобавляемыеРеквизиты.Добавить(Реквизит_ДатаПодписи);
	ДобавляемыеРеквизиты.Добавить(Реквизит_ИспользоватьДатуДокумента);
	
	// Добавим новые реквизиты в форму
	ЭтаФорма.ИзменитьРеквизиты(ДобавляемыеРеквизиты);
	
	ЭтаФорма.ДатаПодписи = ДатаПодписи;
	ЭтаФорма.ИспользоватьДатуДокумента = ИСТИНА;
	//Проверка включать реквизит или нет 	
	ГруппаФормы = ЭтаФорма.Элементы.Найти("ГруппаПоля");
		Если ГруппаФормы <> Неопределено Тогда
			ЭлементФормыДата = ЭтаФорма.Элементы.Добавить("ДатаПодписи", Тип("ПолеФормы"), ГруппаФормы);        
			ЭлементФормыДата.Вид = ВидПоляФормы.ПолеВвода;
			ЭлементФормыДата.ПутьКДанным = "ДатаПодписи";
			ЭлементФормыДата.Заголовок = "Дата подписания";
			ЭлементФормыДата.Высота = 1;
			ЭлементФормыИспользоватьДату = ЭтаФорма.Элементы.Добавить("ИспользоватьДатуДокумента", Тип("ПолеФормы"), ГруппаФормы);        
			ЭлементФормыИспользоватьДату.Вид = ВидПоляФормы.ПолеПереключателя;
			ЭлементФормыИспользоватьДату.ПутьКДанным = "ИспользоватьДатуДокумента";
			ЭлементФормыИспользоватьДату.Заголовок = "Дата подписи из документа";
			ЭтаФорма.Элементы.ДатаПодписи.Доступность = ЛОЖЬ;
			ЭлементФормыИспользоватьДату.УстановитьДействие("ПриИзменении", "АБК_ДатаПодписанияВнутрЭДИспользоватьДатуДокументаПриИзменении");
		КонецЕсли;
	//Конец проверки
	
	
КонецПроцедуры

&НаКлиенте
Процедура АБК_ДатаПодписанияВнутрЭДИспользоватьДатуДокументаПриИзменении(Элемент)
	
	Если ЭтаФорма.ИспользоватьДатуДокумента Тогда
		ЭтаФорма.Элементы.ДатаПодписи.Доступность = ЛОЖЬ;
	Иначе
		ЭтаФорма.Элементы.ДатаПодписи.Доступность = ИСТИНА;
	КонецЕсли;
		
КонецПроцедуры

&НаКлиенте
&ИзменениеИКонтроль("ПодписатьДанныеПослеОбработкиПередВыполнением")
Процедура АБК_ДатаПодписанияВнутрЭДПодписатьДанныеПослеОбработкиПередВыполнением(Результат, Контекст)

	Если ПеременныеОчищены() Тогда
		Возврат;
	КонецЕсли;
	
	Если Результат.Свойство("ОписаниеОшибки") Тогда
		ОбработатьОшибку(Контекст.Оповещение, Новый Структура("ОписаниеОшибки", Результат.ОписаниеОшибки), Новый Структура);
		Возврат;
	КонецЕсли;
	
	Контекст.Вставить("ИдентификаторФормы", УникальныйИдентификатор);
	Если ТипЗнч(ФормаОбъекта) = Тип("ФормаКлиентскогоПриложения") Тогда
		Контекст.ИдентификаторФормы = ФормаОбъекта.УникальныйИдентификатор;
	ИначеЕсли ТипЗнч(ФормаОбъекта) = Тип("УникальныйИдентификатор") Тогда
		Контекст.ИдентификаторФормы = ФормаОбъекта;
	КонецЕсли;
	
	ПараметрыВыполнения = Новый Структура;
	ПараметрыВыполнения.Вставить("ОписаниеДанных",     ОписаниеДанных);
	ПараметрыВыполнения.Вставить("Форма",              ЭтотОбъект);
	ПараметрыВыполнения.Вставить("ИдентификаторФормы", Контекст.ИдентификаторФормы);
	ПараметрыВыполнения.Вставить("ЗначениеПароля",     СвойстваПароля.Значение);
	ПараметрыВыполнения.Вставить("СертификатАдрес",    СертификатАдрес);
	ПараметрыВыполнения.Вставить("СертификатВерен",    Контекст.СертификатВерен);
	ПараметрыВыполнения.Вставить("ТребуетсяПроверка",  Контекст.ТребуетсяПроверка);
	#Вставка
	Если ИспользоватьДатуДокумента Тогда 
		ПараметрыВыполнения.Вставить("БратьДатуДокумента",Истина);
	Иначе
		ПараметрыВыполнения.Вставить("БратьДатуДокумента",Ложь);  
		ПараметрыВыполнения.Вставить("ДатаПодписи",ДатаПодписи);
	КонецЕсли;	
	#КонецВставки
	ПараметрыВыполнения.Вставить("ПолноеПредставлениеДанных",
		ЭлектроннаяПодписьСлужебныйКлиент.ПолноеПредставлениеДанных(ЭтотОбъект));
	ПараметрыВыполнения.Вставить("ТекущийСписокПредставлений", ТекущийСписокПредставлений);
	
	Контекст.Вставить("ПараметрыВыполнения", ПараметрыВыполнения);
	
	Если ЭлектроннаяПодписьКлиент.СоздаватьЭлектронныеПодписиНаСервере()
	   И ВыполнятьНаСервере <> Ложь Тогда
		
		Если ЗначениеЗаполнено(СертификатНаСервереОписаниеОшибки) Тогда
			Результат = Новый Структура("Ошибка", СертификатНаСервереОписаниеОшибки);
			СертификатНаСервереОписаниеОшибки = Новый Структура;
			ПодписатьДанныеПослеВыполненияНаСторонеСервера(Результат, Контекст);
		Иначе
			// Попытка подписания на сервере.
			ЭлектроннаяПодписьСлужебныйКлиент.ВыполнитьНаСтороне(Новый ОписаниеОповещения(
					"ПодписатьДанныеПослеВыполненияНаСторонеСервера", ЭтотОбъект, Контекст),
				"Подписание", "НаСторонеСервера", Контекст.ПараметрыВыполнения);
		КонецЕсли;
	Иначе
		ПодписатьДанныеПослеВыполненияНаСторонеСервера(Неопределено, Контекст);
	КонецЕсли;

КонецПроцедуры


