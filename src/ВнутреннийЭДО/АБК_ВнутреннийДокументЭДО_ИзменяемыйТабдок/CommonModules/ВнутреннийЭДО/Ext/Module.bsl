Функция АБК_ОписаниеЭлектронногоДокумента(Параметры) Экспорт

	Описание = Новый Структура;
	Описание.Вставить("АдресОсновногоФайла", "");
	Описание.Вставить("ИмяФайла", "");

	ДвоичныеДанныеДокумента = АБК_ДвоичныеДанныеУчетногоДокумента(Параметры);

	Если ДвоичныеДанныеДокумента = Неопределено Тогда
		Возврат Описание;
	КонецЕсли;

	Описание.АдресОсновногоФайла = ПоместитьВоВременноеХранилище(ДвоичныеДанныеДокумента, Новый УникальныйИдентификатор);

	ПредставлениеДокумента = ВнутреннийЭДОКлиентСервер.ПредставлениеДокументаПоСвойствам(Параметры);
	//ПредставлениеДокументаЛатиницей = СтроковыеФункции.СтрокаЛатиницей(ПредставлениеДокумента);
	//
	//ИмяБезРасширения = СтрШаблон("%1_ver_%2", ПредставлениеДокументаЛатиницей,
	//	Формат(ТекущаяДатаСеанса(), "ДФ=yyyy-MM-dd_HH-mm-ss; ДП=_"));
	ИмяБезРасширения = ПредставлениеДокумента;
	ИмяБезРасширения = СтрЗаменить(ИмяБезРасширения, " ", "_");
	ИмяБезРасширения = ОбщегоНазначенияКлиентСервер.ЗаменитьНедопустимыеСимволыВИмениФайла(ИмяБезРасширения, "_");

	ПолноеИмяФайла = ИмяБезРасширения + ".zip";

	Описание.ИмяФайла = ПолноеИмяФайла;

	Возврат Описание;

КонецФункции

Функция АБК_ДвоичныеДанныеУчетногоДокумента(Параметры)

	Если Параметры.Свойство("ИдентификаторДокумента") Тогда
		ИдентификаторДокумента = Параметры.ИдентификаторДокумента;
	Иначе
		ИдентификаторДокумента = ОбщегоНазначения.ИдентификаторОбъектаМетаданных(ТипЗнч(Параметры.УчетныйДокумент));
	КонецЕсли;

	КомандыПечати = КомандыПечатиОбъекта(
	ОбщегоНазначения.ОбъектМетаданныхПоИдентификатору(ИдентификаторДокумента));

	КомандаПечати = КомандаПечатиПоИдентификатору(КомандыПечати, Параметры.ИдентификаторКомандыПечати);

	Если КомандаПечати = Неопределено Тогда
		Возврат Неопределено;
	КонецЕсли;

	КомандаПечати = ОбщегоНазначения.СтрокаТаблицыЗначенийВСтруктуру(КомандаПечати);
	КомандаПечати.ДополнительныеПараметры.Вставить("ИсключитьШтампы");

	ОбъектУчета = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Параметры.УчетныйДокумент);

	НастройкиСохранения = УправлениеПечатью.НастройкиСохранения();
	НастройкиСохранения.ФорматыСохранения.Добавить(ТипФайлаТабличногоДокумента.MXL);
	//НастройкиСохранения.ФорматыСохранения.Добавить(ТипФайлаТабличногоДокумента.PDF);
	НастройкиСохранения.УпаковатьВАрхив = Истина;
	НастройкиСохранения.ПереводитьИменаФайловВТранслит = Истина;

	// Привилегированный режим требуется при получении печатной формы актуального документа для сравнения
	// с сохраненной в файле при открытии формы ЭД подписантом внутреннего документа, не имеющим прав на учетный документ
	УстановитьПривилегированныйРежим(Истина);

	РезультатПечати = УправлениеПечатью.АБК_НапечататьВФайл(КомандаПечати, ОбъектУчета, НастройкиСохранения, Параметры.ТабличныйДокумент);

	Если ЗначениеЗаполнено(РезультатПечати) Тогда
		Возврат РезультатПечати[0].ДвоичныеДанные;
	КонецЕсли;

	Возврат Неопределено;

КонецФункции
