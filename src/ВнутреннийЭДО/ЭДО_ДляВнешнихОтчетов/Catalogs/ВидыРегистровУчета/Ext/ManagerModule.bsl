
&ИзменениеИКонтроль("ПолучитьВидРегистраБухгалтерскогоУчетаДляОтчета")
Функция ЭДО_ДляВнешнихОтчетов_ПолучитьВидРегистраБухгалтерскогоУчетаДляОтчета(ПолноеИмяОбъектаМетаданных, ВариантОтчета, Наименование)
	
	#Удаление
	ИдентификаторОтчета = ОбщегоНазначения.ИдентификаторОбъектаМетаданных(ПолноеИмяОбъектаМетаданных);
	#КонецУдаления
	#Вставка
	Если Лев(ПолноеИмяОбъектаМетаданных, 7) = "Внешний" Тогда
		ИдентификаторОтчета = Справочники.ДополнительныеОтчетыИОбработки.НайтиПоРеквизиту("ИмяОбъекта", Сред(ПолноеИмяОбъектаМетаданных, 14));
	Иначе
		ИдентификаторОтчета = ОбщегоНазначения.ИдентификаторОбъектаМетаданных(ПолноеИмяОбъектаМетаданных);
	КонецЕсли;
	#КонецВставки
	
	ИдентификаторВарианта = Неопределено;
	
	Если НЕ ВариантОтчета = Неопределено Тогда
		
		ИдентификаторВарианта = ВариантыОтчетов.ВариантОтчета(ИдентификаторОтчета, ВариантОтчета);
		
	КонецЕсли;
	
	Запрос = Новый Запрос();
	Запрос.Параметры.Вставить("Отчет", ИдентификаторОтчета);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ВидыРегистровУчета.Ссылка
	|ИЗ
	|	Справочник.ВидыРегистровУчета КАК ВидыРегистровУчета
	|ГДЕ
	|	ВидыРегистровУчета.Отчет = &Отчет";
	
	Если Не ИдентификаторВарианта = Неопределено Тогда
		
		Запрос.Текст = Запрос.Текст + "
		|	И ВидыРегистровУчета.ВариантОтчета = &ВариантОтчета
		|";
		
		Запрос.Параметры.Вставить("ВариантОтчета", ИдентификаторВарианта);
		
	КонецЕсли;
	
	Если Не Наименование = "" Тогда
		
		Запрос.Текст = Запрос.Текст + "
		|	И ВидыРегистровУчета.Наименование = &Наименование
		|";
		
		Запрос.Параметры.Вставить("Наименование", Наименование);
		
	КонецЕсли;
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() Тогда
		Возврат Выборка.Ссылка;
	КонецЕсли;
	
	НовыйВидРегистра = Справочники.ВидыРегистровУчета.СоздатьЭлемент();
	НовыйВидРегистра.Отчет = ИдентификаторОтчета;
	НовыйВидРегистра.ВариантОтчета = ИдентификаторВарианта;
	НовыйВидРегистра.Наименование = ?(ЗначениеЗаполнено(Наименование), Наименование,
		?(ЗначениеЗаполнено(НовыйВидРегистра.ВариантОтчета),
			ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ИдентификаторВарианта, "Наименование"),
			ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ИдентификаторОтчета, "Наименование")));
	
	НовыйВидРегистра.Записать();
	
	Возврат НовыйВидРегистра.Ссылка;
	
КонецФункции
