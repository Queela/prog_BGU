
&ИзменениеИКонтроль("ОбъектыСоСпециальнымиКомандамиПечати")
Функция ЭДО_ДляВнешнихОтчетов_ОбъектыСоСпециальнымиКомандамиПечати()

	Объекты = Новый Соответствие;

	ТаблицаКоманд = УправлениеПечатью.СоздатьКоллекциюКомандПечати().Скопировать(
	, "Идентификатор, Представление, МенеджерПечати");

	Если ПравоДоступа("Чтение", Метаданные.Справочники.ВидыРегистровУчета) Тогда

		Запрос = Новый Запрос;
		Запрос.Текст =
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ВидыРегистровУчета.Отчет КАК Отчет,
		|	СУММА(ВЫБОР
		|			КОГДА ВидыРегистровУчета.ВариантОтчета = ЗНАЧЕНИЕ(Справочник.ВариантыОтчетов.ПустаяСсылка)
		|				ТОГДА 0
		|			ИНАЧЕ 1
		|		КОНЕЦ) КАК КоличестоВариантовОтчетов
		|ПОМЕСТИТЬ ОтчетыИВарианты
		|ИЗ
		|	Справочник.ВидыРегистровУчета КАК ВидыРегистровУчета
		|ГДЕ
		|	НЕ ВидыРегистровУчета.ЭтоГруппа
		|
		|СГРУППИРОВАТЬ ПО
		|	ВидыРегистровУчета.Отчет
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
		|	ВЫБОР
		|		КОГДА ВидыРегистровУчета.ВариантОтчета = ЗНАЧЕНИЕ(Справочник.ВариантыОтчетов.ПустаяСсылка)
		|			ТОГДА ВидыРегистровУчета.Отчет.Имя
		#Вставка
		|		КОГДА ВидыРегистровУчета.Отчет ССЫЛКА Справочник.ДополнительныеОтчетыИОбработки
		|			ТОГДА ВидыРегистровУчета.ВариантОтчета.КлючВарианта + ВидыРегистровУчета.Отчет.ИмяОбъекта
		#КонецВставки
		|		ИНАЧЕ ВидыРегистровУчета.ВариантОтчета.КлючВарианта + ВидыРегистровУчета.Отчет.Имя
		|	КОНЕЦ КАК Идентификатор,
		|	ВЫБОР
		|		КОГДА ВидыРегистровУчета.ВариантОтчета = ЗНАЧЕНИЕ(Справочник.ВариантыОтчетов.ПустаяСсылка)
		|			ТОГДА ВидыРегистровУчета.Отчет.Синоним
		|		ИНАЧЕ ВидыРегистровУчета.ВариантОтчета.Наименование
		|	КОНЕЦ КАК Представление
		|ИЗ
		|	Справочник.ВидыРегистровУчета КАК ВидыРегистровУчета
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ОтчетыИВарианты КАК ОтчетыИВарианты
		|		ПО ВидыРегистровУчета.Отчет = ОтчетыИВарианты.Отчет
		|ГДЕ
		|	НЕ ВидыРегистровУчета.ЭтоГруппа
		|	И НЕ(ОтчетыИВарианты.КоличестоВариантовОтчетов > 0
		|				И ВидыРегистровУчета.ВариантОтчета = ЗНАЧЕНИЕ(Справочник.ВариантыОтчетов.ПустаяСсылка))
		|
		|УПОРЯДОЧИТЬ ПО
		|	Представление";

		Выборка = Запрос.Выполнить().Выбрать();
		Пока Выборка.Следующий() Цикл
			НоваяКоманда = ТаблицаКоманд.Добавить();
			НоваяКоманда.Идентификатор = Лев(Выборка.Идентификатор, 100);
			НоваяКоманда.Представление = Выборка.Представление;
		КонецЦикла;
	КонецЕсли;

	Объекты.Вставить(Тип("ДокументСсылка.РегистрУчета"), ТаблицаКоманд);

	Возврат Объекты;

КонецФункции

&ИзменениеИКонтроль("КомандаПечатиПодходитПоУсловиям")
Функция ЭДО_ДляВнешнихОтчетов_КомандаПечатиПодходитПоУсловиям(КомандаПечати, УчетныйДокумент)

	Результат = Неопределено;

	Если ТипЗнч(УчетныйДокумент) = Тип("ДокументСсылка.РегистрУчета") Тогда

		#Удаление
		ДанныеРегистра = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(УчетныйДокумент,
		"ВидРегистра.ВариантОтчета.КлючВарианта, ВидРегистра.Отчет.Имя");
		#КонецУдаления
		#Вставка
		ДанныеРегистра = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(УчетныйДокумент,
		"ВидРегистра.ВариантОтчета.КлючВарианта, ВидРегистра.Отчет");
		Если ТипЗнч(ДанныеРегистра.ВидРегистраОтчет) = Тип("СправочникСсылка.ДополнительныеОтчетыИОбработки") Тогда
			ДанныеРегистра.Вставить("ВидРегистраОтчетИмя", ДанныеРегистра.ВидРегистраОтчет.ИмяОбъекта);
		Иначе
			ДанныеРегистра = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(УчетныйДокумент,
			"ВидРегистра.ВариантОтчета.КлючВарианта, ВидРегистра.Отчет.Имя");
		КонецЕсли;
		#КонецВставки

		ИдентификаторКомандыПечати = Лев(
		Строка(ДанныеРегистра.ВидРегистраВариантОтчетаКлючВарианта) + ДанныеРегистра.ВидРегистраОтчетИмя, 100);

		Результат = ИдентификаторКомандыПечати = КомандаПечати.Идентификатор;

	ИначеЕсли ТипЗнч(УчетныйДокумент) = Тип("СправочникСсылка.ВидыРегистровУчета") Тогда

		#Удаление
		ДанныеРегистра = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(УчетныйДокумент,
		"ВариантОтчета.КлючВарианта, Отчет.Имя");
		#КонецУдаления
		#Вставка
		ДанныеРегистра = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(УчетныйДокумент,
		"ВариантОтчета.КлючВарианта, Отчет");
		Если ТипЗнч(ДанныеРегистра.Отчет) = Тип("СправочникСсылка.ДополнительныеОтчетыИОбработки") Тогда
			ДанныеРегистра.Вставить("ОтчетИмя", ДанныеРегистра.Отчет.ИмяОбъекта);
		Иначе
			ДанныеРегистра = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(УчетныйДокумент,
			"ВариантОтчета.КлючВарианта, Отчет.Имя");
		КонецЕсли;
		#КонецВставки

		ИдентификаторКомандыПечати = Лев(
		Строка(ДанныеРегистра.ВариантОтчетаКлючВарианта) + ДанныеРегистра.ОтчетИмя, 100);

		Результат = ИдентификаторКомандыПечати = КомандаПечати.Идентификатор;

	КонецЕсли;

	Возврат Результат;

КонецФункции
