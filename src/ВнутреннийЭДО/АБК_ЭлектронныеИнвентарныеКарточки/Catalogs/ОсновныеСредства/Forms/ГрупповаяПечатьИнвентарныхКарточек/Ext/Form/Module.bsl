
&НаСервере
Процедура АБК_ЭлектронныеИнвентарныеКарточки_ПриСозданииНаСервереПеред(Отказ, СтандартнаяОбработка)
	ДополнитьФорму();
КонецПроцедуры

&НаКлиенте
Процедура КомандаСоздатьЭлектронныйДокумент(Команда)

	Если ЭтотОбъект.Организация.Пустая() ИЛИ НЕ ЗначениеЗаполнено(ЭтотОбъект.Период) Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(
			НСтр("ru = 'Не заполнены обязательные поля: Организация, Дата формирования'"));
		Возврат;	
	КонецЕсли;

	Если ЭтотОбъект.ОсновныеСредства.Количество() < 1 Тогда
		Вопрос = НСтр("ru = 'Список основных средств пуст. Заполнить?'");
		Завершение = Новый ОписаниеОповещения("ПослеВопросаЗаполнитьОсновныеСредства", ЭтотОбъект);
		
		ПоказатьВопрос(
			Завершение, Вопрос, РежимДиалогаВопрос.ДаНет, 20, КодВозвратаДиалога.Нет,
			"Внимание!", КодВозвратаДиалога.Нет);
	Иначе
		СоздатьЭлектронныеИнвентарныекарточки();		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеВопросаЗаполнитьОсновныеСредства(Ответ, ДопПараметры)

	Если Ответ = КодВозвратаДиалога.Да Тогда
		ЗаполнитьСписокСервер();
		СоздатьЭлектронныеИнвентарныекарточки();
	КонецЕсли;
		
КонецПроцедуры

&НаКлиенте
Процедура СоздатьЭлектронныеИнвентарныекарточки()
	
	ПараметрыВнутрЭД = ПодготовитьПараметрыДляСозданияВнутрЭД();
	
	Если ПараметрыВнутрЭД <> Неопределено Тогда
		ОткрытьФорму(
			"Документ.ВнутреннийЭлектронныйДокумент.ФормаОбъекта",
			ПараметрыВнутрЭД);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ПодготовитьПараметрыДляСозданияВнутрЭД()

	ПараметрыСоздания = Новый Структура;
	ТабДокумент = СоздатьПечатнуюФормуИнвентарныхКарточек();
	
	РегистрУчета = СоздатьРегистрУчета(ТабДокумент);
	Если РегистрУчета = Неопределено Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	ПараметрыСоздания.Вставить("Организация", ЭтотОбъект.Организация);
	ПараметрыСоздания.Вставить("УчетныйДокумент", РегистрУчета.Ссылка);
	ПараметрыСоздания.Вставить("ВидДокумента", "Инвентарная карточка (61н, ред. 157н)");
	ПараметрыСоздания.Вставить("АдресТабличногоДокумента", ПоместитьВоВременноеХранилище(ТабДокумент));
	
	ПараметрыСоздания.Вставить(
		"ИдентификаторДокумента", 
		ОбщегоНазначения.ИдентификаторОбъектаМетаданных("Справочник.ОсновныеСредства"));
			
	ПараметрыСоздания.Вставить("ИдентификаторКомандыПечати", "ИнвентарнаяКарточка61н");
	ПараметрыСоздания.Вставить("ДатаДокумента", ЭтотОбъект.Период);
	ПараметрыСоздания.Вставить("НомерДокумента", РегистрУчета.Номер);
	
	Возврат ПараметрыСоздания;
		
КонецФункции

&НаСервере
Функция СоздатьПечатнуюФормуИнвентарныхКарточек()

	МассивОбъектов = ЭтотОбъект.ОсновныеСредства.Выгрузить().ВыгрузитьКолонку("ОсновноеСредство");
	ПараметрыПечати = Новый Структура("ДатаСведений", ЭтотОбъект.Период);
	
	Возврат Справочники.ОсновныеСредства.ПечатьИнвентарнойКарточки61н(
		МассивОбъектов, ПараметрыПечати, Новый СписокЗначений, Новый Соответствие); 
                                                               
КонецФункции

&НаСервере
Функция СоздатьРегистрУчета(ТабДокумент)
    
    НачатьТранзакцию();
    Попытка
		РегистрУчета = Документы.РегистрУчета.СоздатьДокумент();
		РегистрУчета.Дата = ТекущаяДата();
	
		РегистрУчета.НачалоПериода = ЭтотОбъект.Период;  
	    РегистрУчета.КонецПериода  = ЭтотОбъект.Период;
	    РегистрУчета.Организация   = ЭтотОбъект.Организация;
	    
	    РегистрУчета.Описание = НСтр("ru = 'Электронная инвентарная карточка (" + 
	    	Строка(ЭтотОбъект.Период) + ")'"); 
	    	
	    РегистрУчета.ФорматСохраненияФайла = Перечисления.ФорматыСохраненияОтчетов.MXL;
	    РегистрУчета.Ответственный = Пользователи.ТекущийПользователь();
	    
		СсылкаНового = Документы.РегистрУчета.ПолучитьСсылку();
		IDРегистра = СсылкаНового.УникальныйИдентификатор();
		РегистрУчета.УстановитьСсылкуНового(СсылкаНового);
		
	    РегистрУчета.ВидРегистра = НайтиСоздатьВидРегистраИнвентарнаяКарточка(
	    	РегистрУчета.Ссылка.Метаданные());
	    
		ИмяВременнойПапки = ПолучитьИмяВременногоФайла();
		СоздатьКаталог(ИмяВременнойПапки);
		
	    ПолноеИмяФайла = 
	    	ОбщегоНазначенияКлиентСервер.ДобавитьКонечныйРазделительПути(ИмяВременнойПапки) + 
	    	ОбщегоНазначенияКлиентСервер.ЗаменитьНедопустимыеСимволыВИмениФайла(IDРегистра);
		
		ТабДокумент.Записать(ПолноеИмяФайла, ТипФайлаТабличногоДокумента.MXL);
		
		ДД = Новый ДвоичныеДанные(ПолноеИмяФайла);
		//ФайлХранилище = Новый ХранилищеЗначения(ДД, Новый СжатиеДанных(9));

		ИмяФайла = ОбщегоНазначенияКлиентСервер.ЗаменитьНедопустимыеСимволыВИмениФайла(РегистрУчета.Описание);
		
		РегистрУчета.Записать();
				
		ПараметрыФайла = Новый Структура();
		ПараметрыФайла.Вставить("Автор", Пользователи.ТекущийПользователь());
		ПараметрыФайла.Вставить("ВладелецФайлов", РегистрУчета.Ссылка);
		ПараметрыФайла.Вставить("ИмяБезРасширения", ИмяФайла);
		ПараметрыФайла.Вставить("РасширениеБезТочки", "mxl");
		ПараметрыФайла.Вставить("ВремяИзмененияУниверсальное", РегистрУчета.Дата);
		ПараметрыФайла.Вставить("Служебный", Истина);
		
		ДобавленныйФайл = РаботаСФайлами.ДобавитьФайл(
			ПараметрыФайла, ПоместитьВоВременноеХранилище(ДД), "", РегистрУчета.Описание);
		
		РегистрУчета.РегистрMXL = ДобавленныйФайл;
		РегистрУчета.Записать(РежимЗаписиДокумента.Запись);
		УдалитьФайлы(ИмяВременнойПапки);
		
		ЗафиксироватьТранзакцию();
		
		Возврат Новый Структура("Ссылка,Номер", РегистрУчета.Ссылка, РегистрУчета.Номер);
	Исключение
		ОтменитьТранзакцию();
	    ОбщегоНазначения.СообщитьПользователю(
	    	НСтр("ru = 'Электронная инвентарная карточка не создана. Возникла непредвиденная ошибка.'"));
	КонецПопытки;
                                                               
КонецФункции

Функция НайтиСоздатьВидРегистраИнвентарнаяКарточка(ОбъектМетаданных)
    
    ИДОбъектаМетаданных = ОбщегоНазначения.ИдентификаторОбъектаМетаданных(ОбъектМетаданных);
    
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ВидыРегистровУчета.Ссылка КАК Ссылка
	|ИЗ Справочник.ВидыРегистровУчета КАК ВидыРегистровУчета
	|ГДЕ
	|	ВидыРегистровУчета.Отчет = &ИдентификаторОбъектаМетаданных");
	
	Запрос.УстановитьПараметр("ИдентификаторОбъектаМетаданных", ИДОбъектаМетаданных);
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() Тогда
		Возврат Выборка.Ссылка;	
	КонецЕсли;
	
	ВидРегистра = Справочники.ВидыРегистровУчета.СоздатьЭлемент();
	ВидРегистра.Наименование = "ЭлектроннаяИнвентарнаяКарточка";
	ВидРегистра.Отчет = ИДОбъектаМетаданных;
	ВидРегистра.Записать();
	
	Возврат ВидРегистра.Ссылка;	

КонецФункции

&НаСервере
Процедура ДополнитьФорму()
    
	Если Элементы.Найти("СоздатьЭлектронныйДокумент") = Неопределено Тогда
	
	    Команда = Команды.Добавить("СоздатьЭлектронныйДокумент");
	    Команда.Действие = "КомандаСоздатьЭлектронныйДокумент";
	    Команда.Картинка = БиблиотекаКартинок.СоздатьНачальныйОбраз;
	    Команда.Заголовок = НСтр("ru = 'Создать электронный документ'");
	    Команда.Отображение = ОтображениеКнопки.КартинкаИТекст;
	
	    Кнопка = Элементы.Добавить(
		    "СоздатьЭлектронныйДокумент", Тип("КнопкаФормы"), ЭтотОбъект.КоманднаяПанель);
	    Кнопка.ИмяКоманды = "СоздатьЭлектронныйДокумент";
	КонецЕсли;
	
КонецПроцедуры
