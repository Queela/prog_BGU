
&ИзменениеИКонтроль("ТабличныйДокументИзДвоичныхДанныхАрхива")
Функция АБК_ЭлектронныеИнвентарныеКарточки_ТабличныйДокументИзДвоичныхДанныхАрхива(ДвоичныеДанные)
    #Удаление
	ТабДок = Новый ТабличныйДокумент;

	Если ДвоичныеДанные = Неопределено Тогда
		Возврат ТабДок;
	КонецЕсли;

	ВременныйКаталог = ФайловаяСистема.СоздатьВременныйКаталог();

	ЧтениеZipФайла = Новый ЧтениеZipФайла(ДвоичныеДанные.ОткрытьПотокДляЧтения());
	Для Каждого ЭлементZipФайла Из ЧтениеZipФайла.Элементы Цикл
		Если НРег(ЭлементZipФайла.Расширение) = "mxl" Тогда
			ЧтениеZipФайла.Извлечь(ЭлементZipФайла, ВременныйКаталог);
			Прервать;
		КонецЕсли;
	КонецЦикла;
	ЧтениеZipФайла.Закрыть();

	ТабДок.Прочитать(НайтиФайлы(ВременныйКаталог,"*.mxl")[0].ПолноеИмя);

	//@skip-check empty-except-statement
	Попытка
		ФайловаяСистема.УдалитьВременныйКаталог(ВременныйКаталог);
	Исключение
		// Обработка исключения не требуется
	КонецПопытки;

	Возврат ТабДок;
    #КонецУдаления
	#Вставка
	ТабДок = Новый ТабличныйДокумент;

	Если ДвоичныеДанные = Неопределено Тогда
		Возврат ТабДок;
	КонецЕсли;

	ТабличныеДокументыФайлов = Новый Массив; 
	ЧтениеZipФайла = Новый ЧтениеZipФайла(ДвоичныеДанные.ОткрытьПотокДляЧтения());
	Для н = 0 по ЧтениеZipФайла.Элементы.Количество() - 1 Цикл
	
		ВременныйКаталог = ФайловаяСистема.СоздатьВременныйКаталог();
		ТекущееЧтениеZipФайла = Новый ЧтениеZipФайла(ДвоичныеДанные.ОткрытьПотокДляЧтения());
		ЭлементZipФайла = ЧтениеZipФайла.Элементы[н];
		
		Если НРег(ЭлементZipФайла.Расширение) = "mxl" Тогда
			ВременныйТабДок = Новый ТабличныйДокумент;
			ТекущееЧтениеZipФайла.Извлечь(ЭлементZipФайла, ВременныйКаталог); 
			
			ВременныйТабДок.Прочитать(НайтиФайлы(ВременныйКаталог,"*.mxl")[0].ПолноеИмя);
			ТабличныеДокументыФайлов.Добавить(ВременныйТабДок);
		КонецЕсли; 
		
		ТекущееЧтениеZipФайла.Закрыть();
		
		//@skip-check empty-except-statement
		Попытка
			ФайловаяСистема.УдалитьВременныйКаталог(ВременныйКаталог);
		Исключение
			// Обработка исключения не требуется
		КонецПопытки;
	КонецЦикла;
	
	ЧтениеZipФайла.Закрыть(); 
	
	НуженРазделитель = ЛОЖЬ;
    Для Каждого ВременныйТабДок из ТабличныеДокументыФайлов Цикл
    
    	Если НуженРазделитель Тогда
			ТабДок.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		НуженРазделитель = ИСТИНА;
		ТабДок.Вывести(ВременныйТабДок);
	КонецЦикла;

	Возврат ТабДок;
	#КонецВставки

КонецФункции
