
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура АБК_УчетСтудентов4014хПриСозданииНаСервереПосле(Отказ, СтандартнаяОбработка)
	
	УчетПоСтудентам = АБК_УчетСтудентов.ИспользуетсяУчет4014х();
	
	Если НЕ УчетПоСтудентам Тогда
		Возврат;	
	КонецЕсли;
	
	ПолеПереносДБП = Элементы.Добавить("ПереносДБП", Тип("ПолеФормы"), Элементы.ГруппаПраваяКолонка); 
	ПолеПереносДБП.Вид = ВидПоляФормы.ПолеФлажка; 
	ПолеПереносДБП.ПутьКДанным = "Объект.ПереносДБП";
	ПолеПереносДБП.УстановитьДействие("ПриИзменении","ПереносДБППриИзменении"); 
	
	КолонкаСуммаПереносаДБП = Элементы.Вставить("ДоходыБудущихПериодовСуммаПереносаДБП", Тип("ПолеФормы"), Элементы.ДоходыБудущихПериодов ,Элементы.ДоходыБудущихПериодовПослеИзмененияВсего); 
	КолонкаСуммаПереносаДБП.Вид = ВидПоляФормы.ПолеВвода; 
	КолонкаСуммаПереносаДБП.ПутьКДанным = "Объект.ДоходыБудущихПериодов.СуммаПереносаДБП";
	КолонкаСуммаПереносаДБП.УстановитьДействие("ПриИзменении","ДоходыБудущихПериодовСуммаПереносаДБППриИзменении");
	
	МассивРеквизитов = Новый Массив;
	МассивРеквизитов.Добавить(Новый РеквизитФормы("СуммаПослеПереносаДБП",Новый ОписаниеТипов("Число",Новый КвалификаторыЧисла(15,2)),"Объект.ДоходыБудущихПериодов","Сумма после переноса ДБП",Истина));
	ИзменитьРеквизиты(МассивРеквизитов,);  
	
	КолонкаСуммаПослеПереносаДБП = Элементы.Вставить("ДоходыБудущихПериодовСуммаПослеПереносаДБП", Тип("ПолеФормы"), Элементы.ДоходыБудущихПериодов, Элементы.ДоходыБудущихПериодовПослеИзмененияВсего); 
	КолонкаСуммаПослеПереносаДБП.Вид = ВидПоляФормы.ПолеВвода; 
	КолонкаСуммаПослеПереносаДБП.ПутьКДанным = "Объект.ДоходыБудущихПериодов.СуммаПослеПереносаДБП";
	КолонкаСуммаПослеПереносаДБП.ТолькоПросмотр = Истина; 
	
	// Условное оформление
	ЭлементУсловногоОформленияСуммаПереносаДБП = УсловноеОформление.Элементы.Добавить(); 
	ЭлементОтбора = ЭлементУсловногоОформленияСуммаПереносаДБП.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ЭлементОтбора.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Объект.ДоходыБудущихПериодов.СчетДБП"); // имя поля
	ЭлементОтбора.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ЭлементОтбора.ПравоеЗначение = БухгалтерскийУчет.СчетПоКоду("401.41",Объект.Дата); 
	ЭлементОтбора.Использование = Истина;  
	
	ЭлементУсловногоОформленияСуммаПереносаДБП.Оформление.УстановитьЗначениеПараметра("ТолькоПросмотр", Истина);
	
	ПолеОформления = ЭлементУсловногоОформленияСуммаПереносаДБП.Поля.Элементы.Добавить();
	ПолеОформления.Поле = Новый ПолеКомпоновкиДанных("ДоходыБудущихПериодовСуммаПереносаДБП");
	ПолеОформления.Использование = Истина;
	// Условное оформление
	
	ПодготовитьФорму_ПереносДБП();
	
КонецПроцедуры      

&НаСервере
Процедура АБК_УчетСтудентов4014хПриЧтенииНаСервереПосле(ТекущийОбъект)
	ПодготовитьФорму_ПереносДБП();
КонецПроцедуры

&НаСервере
Процедура АБК_УчетСтудентов4014хПослеЗаписиНаСервереПосле(ТекущийОбъект, ПараметрыЗаписи)
	ПодготовитьФорму_ПереносДБП();
КонецПроцедуры

&НаСервере
&После("ЗаполнитьУчащимисяСервер")
Процедура АБК_УчетСтудентов4014хЗаполнитьУчащимисяСервер(Адреса) 
	
	ПодготовитьФорму_ПереносДБП();
	
	Если Объект.ПереносДБП Тогда
		
		ЭтаФорма.БухгалтерскиеОперации_ТиповаяОперация = Справочники.ТиповыеОперации.НайтиПоНаименованию("Перенос доходов будущих периодов (401.41 - 401.49)",Истина);
		БухгалтерскиеОперацииСервер.ПриИзмененииТиповойОперации(Элементы.БухгалтерскаяОперация,Истина);	
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФорм

#КонецОбласти

#Область ОбработчикиСобытийЭлементов

&НаКлиенте
Процедура ПереносДБППриИзменении(Элемент)
	Элементы.ДоходыБудущихПериодовСуммаПереносаДБП.Видимость = Объект.ПереносДБП;	
КонецПроцедуры

&НаКлиенте
Процедура ДоходыБудущихПериодовСуммаПереносаДБППриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.ДоходыБудущихПериодов.ТекущиеДанные;
	
	Если Строка(ТекущиеДанные.СчетДБП) <> "401.49" И НЕ ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(ТекущиеДанные,"СуммаПереносаДБП") Тогда
		Возврат;
	КонецЕсли;   
	
	ОбработатьИзменениеСтрокиДБП(ТекущиеДанные.ПолучитьИдентификатор());
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ПодготовитьФорму_ПереносДБП()
	
	Если НЕ АБК_УчетСтудентов.ИспользуетсяУчет4014х() Тогда
		Возврат;
	КонецЕсли;
	
	Если Элементы.Найти("ДоходыБудущихПериодовСуммаПереносаДБП") <> Неопределено Тогда
		Элементы.ДоходыБудущихПериодовСуммаПереносаДБП.Видимость = Объект.ПереносДБП;	
	КонецЕсли;
	Если Элементы.Найти("ДоходыБудущихПериодовСуммаПослеПереносаДБП") <> Неопределено Тогда
		Элементы.ДоходыБудущихПериодовСуммаПослеПереносаДБП.Видимость = Объект.ПереносДБП;	
	КонецЕсли;
	
	Если НЕ Объект.ПереносДБП Тогда
		Возврат;	
	КонецЕсли;
	
	ТЧДБП = Объект.ДоходыБудущихПериодов.Выгрузить(); 
	Счет401_41 = БухгалтерскийУчет.СчетПоКоду("401.41",Объект.Дата);
	
	Если ТЧДБП.Колонки.Найти("СуммаПослеПереносаДБП") <> Неопределено Тогда
		
		Для Каждого Строка из ТЧДБП Цикл 
			
			Если Строка.СчетДБП.Код <> "401.49" Тогда
				Продолжить;	
			КонецЕсли;   
			
			СтруктураОтбора = Новый Структура("Студент,Контрагент,Договор,КФО,КПС,КЭК,Подразделение,ДопСубконтоРасчетов"); 
			ЗаполнитьЗначенияСвойств(СтруктураОтбора,Строка); 
			СтруктураОтбора.Вставить("СчетДБП",Счет401_41);
			
			МассивСтрокПоОтбору = ТЧДБП.НайтиСтроки(СтруктураОтбора);  
			Если МассивСтрокПоОтбору.Количество() = 1 Тогда
				МассивСтрокПоОтбору[0].СуммаПослеПереносаДБП = МассивСтрокПоОтбору[0].ОстатокДБП + Строка.СуммаПереносаДБП;	 
				Строка.СуммаПослеПереносаДБП = Строка.ОстатокДБП - Строка.СуммаПереносаДБП;
			КонецЕсли;
			
		КонецЦикла;
		
		Объект.ДоходыБудущихПериодов.Загрузить(ТЧДБП);
		
	КонецЕсли;
	
КонецПроцедуры  

&НаСервере
Процедура ОбработатьИзменениеСтрокиДБП(ИдентификаторСтроки)

	СтрокаДокумента = Объект.ДоходыБудущихПериодов.НайтиПоИдентификатору(ИдентификаторСтроки);
	
	СтруктураОтбора = Новый Структура("Студент,Контрагент,Договор,КФО,КПС,КЭК,Подразделение,ДопСубконтоРасчетов"); 
	ЗаполнитьЗначенияСвойств(СтруктураОтбора,СтрокаДокумента); 
	СтруктураОтбора.Вставить("СчетДБП", БухгалтерскийУчет.СчетПоКоду("401.41",Объект.Дата));
	
	МассивСтрокПоОтбору = Объект.ДоходыБудущихПериодов.НайтиСтроки(СтруктураОтбора);  
	Если МассивСтрокПоОтбору.Количество() = 1 Тогда
		МассивСтрокПоОтбору[0].СуммаПослеПереносаДБП = МассивСтрокПоОтбору[0].ОстатокДБП + СтрокаДокумента.СуммаПереносаДБП;
		СтрокаДокумента.СуммаПослеПереносаДБП = СтрокаДокумента.ОстатокДБП - СтрокаДокумента.СуммаПереносаДБП;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
