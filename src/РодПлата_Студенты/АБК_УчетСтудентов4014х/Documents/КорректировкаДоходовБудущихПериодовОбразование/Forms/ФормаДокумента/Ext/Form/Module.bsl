
&НаКлиенте
&ИзменениеИКонтроль("ПересчитатьСуммыПриИзмененииВидаОперации")
Процедура АБК_УчетСтудентов4014хПересчитатьСуммыПриИзмененииВидаОперации()

	ЭтоДосрочноеПрекрашение = (Объект.ВидОперации = ПредопределенноеЗначение(
	"Перечисление.ВидыОперацийКорректировкиДБП.ДосрочноеПрекрашение"));

	Для Каждого СтрокаТаблицы Из Объект.ПорядокОплаты Цикл

		Если ЭтоДосрочноеПрекрашение Тогда

			Если НЕ Объект.СкорректироватьПорядокОплаты Тогда
				СтрокаТаблицы.СуммаВВалюте = 0;
				СтрокаТаблицы.СуммаВВалютеПослеИзменений = СтрокаТаблицы.СуммаВВалютеДоИзменений;
			ИначеЕсли НЕ СтрокаТаблицы.ПериодЗавершен Тогда // Отменяем оплаты по графику за незавершенные периоды обучения.
				СтрокаТаблицы.СуммаВВалюте = (-1) * СтрокаТаблицы.СуммаВВалютеДоИзменений;
				СтрокаТаблицы.СуммаВВалютеПослеИзменений = 0;
			Иначе // Завершенные периоды обучения не меняем.
				СтрокаТаблицы.СуммаВВалюте = 0;
				СтрокаТаблицы.СуммаВВалютеПослеИзменений = СтрокаТаблицы.СуммаВВалютеДоИзменений;
			КонецЕсли;

		Иначе // КорректировкаДоходовБудущихПериодов

			СтрокаТаблицы.СуммаВВалюте = 0;
			СтрокаТаблицы.СуммаВВалютеПослеИзменений = СтрокаТаблицы.СуммаВВалютеДоИзменений;

		КонецЕсли;

		ПорядокОплатыЗаполнитьДобавленныеКолонкиВСтрокеТаблицы(СтрокаТаблицы, Объект.Дата);

	КонецЦикла;

	Если Объект.ДоходыБудущихПериодов.Количество() > 0 Тогда

		Если ЭтоДосрочноеПрекрашение И НЕ Объект.СкорректироватьПорядокОплаты Тогда
			// Таб.часть ПорядокОплаты не используется, рассчитываем на базе остатков доходов будущих периодов.
			Для Каждого СтрокаДоходовБудущихПериодов Из Объект.ДоходыБудущихПериодов Цикл
				СтрокаДоходовБудущихПериодов.Всего = (-1) * СтрокаДоходовБудущихПериодов.ОстатокДБП;
				ДоходыБудущихПериодовЗаполнитьДобавленныеКолонкиВСтрокеТаблицы(СтрокаДоходовБудущихПериодов);
			КонецЦикла;
		Иначе 
			#Вставка
			Если АБК_УчетСтудентовКлиент.ИспользуетсяУчет4014х() И Объект.ДоходыБудущихПериодов.Количество() = 2 Тогда
				
				СуммаОстаток401_41 = 0;
				СуммаОстаток401_49 = 0;
				ТекущийГод = Год(Объект.Дата);
				МассивАктуальныхСтрок = Объект.ПорядокОплаты.НайтиСтроки(Новый Структура("ПериодЗавершен",Ложь));
				
				Для Каждого Строка из МассивАктуальныхСтрок Цикл
					Если Год(Строка.ОплатитьДо) = ТекущийГод Тогда
						СуммаОстаток401_41 = СуммаОстаток401_41 + Строка.СуммаВВалюте;
					ИначеЕсли Год(Строка.ОплатитьДо) > ТекущийГод Тогда
						СуммаОстаток401_49 = СуммаОстаток401_49 + Строка.СуммаВВалюте;	
					КонецЕсли;
				КонецЦикла;
				
				Если Прав(Объект.ДоходыБудущихПериодов[0].СчетДБП,2) = "41" Тогда
					Объект.ДоходыБудущихПериодов[0].Всего = СуммаОстаток401_41;
					Объект.ДоходыБудущихПериодов[1].Всего = СуммаОстаток401_49;
				Иначе
					Объект.ДоходыБудущихПериодов[1].Всего = СуммаОстаток401_41;
					Объект.ДоходыБудущихПериодов[0].Всего = СуммаОстаток401_49;	
				КонецЕсли;
			Иначе
			#КонецВставки
			// Итоговые суммы по таб.части ДоходыБудущихПериодов и ПорядокОплаты должны совпадать.
			// По умолчанию фиксируем изменение в первой строке, при необходимости можно изменить вручную.
			СтрокаДоходовБудущихПериодов = Объект.ДоходыБудущихПериодов[0];
			СтрокаДоходовБудущихПериодов.Всего = Объект.ПорядокОплаты.Итог("СуммаВВалюте");
			ДоходыБудущихПериодовЗаполнитьДобавленныеКолонкиВСтрокеТаблицы(СтрокаДоходовБудущихПериодов);  
			#Вставка
			КонецЕсли;
			#КонецВставки
		КонецЕсли;

	КонецЕсли;

КонецПроцедуры
