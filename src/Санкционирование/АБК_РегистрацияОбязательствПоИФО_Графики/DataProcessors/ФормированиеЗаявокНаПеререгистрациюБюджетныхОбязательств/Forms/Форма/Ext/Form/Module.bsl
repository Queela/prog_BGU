
&НаСервере
&ИзменениеИКонтроль("СформироватьДокументыСервер")
Процедура АБК_СформироватьДокументыСервер()
	
	Запрос = Новый Запрос;
	ТекстЗапроса = "ВЫБРАТЬ
	|	ТаблицаКПС.УстаревшийКПС,
	|	ТаблицаКПС.НовыйКПС
	|ПОМЕСТИТЬ ТаблицаКПС
	|ИЗ
	|	&ТаблицаКПС КАК ТаблицаКПС
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаДоговоров.Договор
	|ПОМЕСТИТЬ ТаблицаДоговоров
	|ИЗ
	|	&ТаблицаДоговоров КАК ТаблицаДоговоров
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Договоры.Договор КАК ДоговорОснованиеОбязательства,
	|	ВЫРАЗИТЬ(Договоры.Договор КАК Справочник.Договоры).ПометкаУдаления КАК ПометкаУдаления,
	|	ВЫРАЗИТЬ(Договоры.Договор КАК Справочник.Договоры).Долгосрочный КАК Долгосрочный,
	|	ВЫРАЗИТЬ(Договоры.Договор КАК Справочник.Договоры).УчетПредметаДоговора КАК УчетПредметаДоговора,
	|	ВЫРАЗИТЬ(Договоры.Договор КАК Справочник.Договоры).Организация КАК Организация,
	|	ВЫРАЗИТЬ(Договоры.Договор КАК Справочник.Договоры).Контрагент КАК Контрагент,
	|	ВЫРАЗИТЬ(Договоры.Договор КАК Справочник.Договоры).СчетКонтрагента КАК СчетКонтрагента,
	|	ВЫРАЗИТЬ(Договоры.Договор КАК Справочник.Договоры).ЗаключаетсяПоРезультатамКонкурсныхПроцедур КАК ЗаключаетсяПоРезультатамКонкурсныхПроцедур,
	|	ВЫРАЗИТЬ(Договоры.Договор КАК Справочник.Договоры).ПризнакЗавершенияКонкурсныхПроцедур КАК ПризнакЗавершенияКонкурсныхПроцедур
	|ПОМЕСТИТЬ СписокДоговоров
	|ИЗ
	|	ТаблицаДоговоров КАК Договоры
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|// ВыборкаРеквизитов
	|ВЫБРАТЬ
	|	СписокДоговоров.ДоговорОснованиеОбязательства,
	|	СписокДоговоров.ПометкаУдаления,
	|	СписокДоговоров.Долгосрочный,
	|	СписокДоговоров.УчетПредметаДоговора,
	|	ВЫБОР
	|		КОГДА СписокДоговоров.ЗаключаетсяПоРезультатамКонкурсныхПроцедур И НЕ СписокДоговоров.ПризнакЗавершенияКонкурсныхПроцедур
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыПринятыхОбязательств.ПринимаемоеОбязательствоПеререгистрация)
	|		КОГДА СписокДоговоров.УчетПредметаДоговора 
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыПринятыхОбязательств.ЗаявкаНаПеререгистрациюОбязательстваУчетПредмета)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ВидыПринятыхОбязательств.ЗаявкаНаПеререгистрациюОбязательства)
	|	КОНЕЦ КАК ВидОбязательства,
	|	СписокДоговоров.Организация,
	|	СписокДоговоров.Контрагент,
	|	СписокДоговоров.СчетКонтрагента КАК СчетКонтрагента,
	|	СрокиИсполненияИСуммыОбязательствСрезПоследних.ИФО КАК ИФО,
	|	СрокиИсполненияИСуммыОбязательствСрезПоследних.ЛицевойСчет КАК ЛицевойСчет,
	|	СрокиИсполненияИСуммыОбязательствСрезПоследних.ОрганКазначейства КАК ОрганКазначейства,
	|	СрокиИсполненияИСуммыОбязательствСрезПоследних.РеквизитыЛицевогоСчета КАК РеквизитыЛицевогоСчета,
	|	СрокиИсполненияИСуммыОбязательствСрезПоследних.Регистратор КАК Основание,
	|	СрокиИсполненияИСуммыОбязательствСрезПоследних.ВалютаРасчетов КАК Валюта,
	|	СрокиИсполненияИСуммыОбязательствСрезПоследних.СрокИсполнения,
	|	СрокиИсполненияИСуммыОбязательствСрезПоследних.АвансПроцентом,
	|	СрокиИсполненияИСуммыОбязательствСрезПоследних.СуммаАванса,
	|	СрокиИсполненияИСуммыОбязательствСрезПоследних.Сумма,
	|	СрокиИсполненияИСуммыОбязательствСрезПоследних.СуммаВВалюте
	|ИЗ
	|	СписокДоговоров КАК СписокДоговоров
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СрокиИсполненияИСуммыОбязательств.СрезПоследних(
	|				&Период,
	|				Обязательство В
	|					(ВЫБРАТЬ
	|						СписокДоговоров.ДоговорОснованиеОбязательства
	|					ИЗ
	|						СписокДоговоров КАК СписокДоговоров)) КАК СрокиИсполненияИСуммыОбязательствСрезПоследних
	|		ПО СписокДоговоров.ДоговорОснованиеОбязательства = СрокиИсполненияИСуммыОбязательствСрезПоследних.Обязательство
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПФОСрезПоследних.Обязательство,
	#Вставка
	|	ПФОСрезПоследних.ИФО КАК ИФО,
	#КонецВставки
	|	ПФОСрезПоследних.КлючАналитики.КФО КАК КФО,
	|	ПФОСрезПоследних.КлючАналитики.КПС КАК КПС,
	|	ПФОСрезПоследних.КлючАналитики.КЭК КАК КЭК,
	|	ПФОСрезПоследних.КлючАналитики.ОбъектФАИП КАК ОбъектФАИП,
	|	ПФОСрезПоследних.КлючАналитики.Номенклатура КАК Номенклатура,
	|	ПФОСрезПоследних.КлючАналитики.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	ПФОСрезПоследних.КлючАналитики.ПериодПлана КАК ПериодПлана,
	|	ПФОСрезПоследних.КлючАналитики.РазделЛицевогоСчета КАК РазделЛицевогоСчета,
	|	ПФОСрезПоследних.КлючАналитики.КодЦели КАК КодЦели,
	|	ПФОСрезПоследних.КлючАналитики.КодМероприятия КАК КодМероприятия,
	|	ПФОСрезПоследних.КлючАналитики.ДопКлассификация КАК ДопКлассификация,
	|	ПФОСрезПоследних.КлючАналитики.Подразделение КАК Подразделение,
	|	ПФОСрезПоследних.ОКП,
	|	ПФОСрезПоследних.Количество,
	|	ПФОСрезПоследних.Сумма,
	|	ПФОСрезПоследних.СуммаВВалюте,
	|	ПФОСрезПоследних.СуммаВВалютеБезусловная,
	|	ИСТИНА КАК ЗапретИзменения,
	|	ВЫБОР
	|		КОГДА ТаблицаКПС.НовыйКПС ЕСТЬ NULL 
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ КАК ТребуетсяСторнировать,
	|	ВЫБОР
	|		КОГДА ТаблицаКПС.НовыйКПС ЕСТЬ NULL 
	|			ТОГДА ПФОСрезПоследних.КлючАналитики.КПС
	|		ИНАЧЕ ТаблицаКПС.НовыйКПС
	|	КОНЕЦ КАК НовыйКПС
	|ИЗ
	|	РегистрСведений.ПланФинансированияОбязательств.СрезПоследних(
	|			&Период,
	|			обязательство В
	|				(ВЫБРАТЬ
	|					СписокДоговоров.ДоговорОснованиеОбязательства
	|				ИЗ
	|					СписокДоговоров КАК СписокДоговоров)) КАК ПФОСрезПоследних
	|		ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаКПС КАК ТаблицаКПС
	|		ПО ПФОСрезПоследних.КлючАналитики.КПС = ТаблицаКПС.УстаревшийКПС
	|ГДЕ
	|	ПФОСрезПоследних.Сумма <> 0";
	//|	И ПФОСрезПоследних.КлючАналитики.ПериодПлана.ДатаОкончания > &Период";
	Запрос.Текст = ТекстЗапроса;
	Запрос.УстановитьПараметр("Период", НачалоГода(Объект.ДатаФормированияДокументов) - 1); 
	Запрос.УстановитьПараметр("ТаблицаДоговоров", Объект.ДоговорыИИныеОснованияОбязательств.Выгрузить());
	Запрос.УстановитьПараметр("ТаблицаКПС", Объект.ПереносыКПС.Выгрузить());
	МассивРезультатов = Запрос.ВыполнитьПакет();
	
	// МассивРезультатов[0] - таблица КПС
	// МассивРезультатов[1] - таблица договоров
	// МассивРезультатов[2] - список договоров
	ВыборкаРеквизитов = МассивРезультатов[3].Выбрать();
	ТаблицаПФО = МассивРезультатов[4].Выгрузить();
	
	СтруктураПоиска = Новый Структура("Обязательство");
	БюджетВнебюджетПоКФО = РаботаСОбязательствамиПовтИсп.ПолучитьБюджетВнебюджетПоКФО();	
	
	Пока ВыборкаРеквизитов.Следующий() Цикл
		
		НовыйДокумент = Документы.РегистрацияОбязательствИСведенийПоДоговорам.СоздатьДокумент();
		НовыйДокумент.Дата = Объект.ДатаФормированияДокументов;
		
		НовыйДокумент.ВидОбязательства = ВыборкаРеквизитов.ВидОбязательства;
		
		НовыйДокумент.Основание 				= ВыборкаРеквизитов.Основание;
		НовыйДокумент.ИФО 						= ВыборкаРеквизитов.ИФО;
		НовыйДокумент.ЛицевойСчет 				= ВыборкаРеквизитов.ЛицевойСчет;
		НовыйДокумент.СчетКонтрагента 			= ВыборкаРеквизитов.СчетКонтрагента;
		НовыйДокумент.ОрганКазначейства 		= ВыборкаРеквизитов.ОрганКазначейства;
		НовыйДокумент.РеквизитыЛицевогоСчета	= ВыборкаРеквизитов.РеквизитыЛицевогоСчета;
		
		НовыйДокумент.Организация 				= ВыборкаРеквизитов.Организация;
		НовыйДокумент.Контрагент 				= ВыборкаРеквизитов.Контрагент;
		НовыйДокумент.Договор 					= ВыборкаРеквизитов.ДоговорОснованиеОбязательства;
		
		НовыйДокумент.ВалютаДокумента 			= ВыборкаРеквизитов.Валюта;
		СтруктураКурсаВалюты 					= РаботаСКурсамиВалют.ПолучитьКурсВалюты(ВыборкаРеквизитов.Валюта, Объект.ДатаФормированияДокументов); 
		НовыйДокумент.Курс 						= СтруктураКурсаВалюты.Курс;
		НовыйДокумент.Кратность 				= СтруктураКурсаВалюты.Кратность;
		
		НовыйДокумент.СрокИсполнения 			= ВыборкаРеквизитов.СрокИсполнения;
		НовыйДокумент.АвансПроцентом 			= ВыборкаРеквизитов.АвансПроцентом;
		НовыйДокумент.СуммаАванса 				= ВыборкаРеквизитов.СуммаАванса;
		
		НовыйДокумент.Сумма						= ВыборкаРеквизитов.Сумма;
		НовыйДокумент.СуммаВВалюте				= ВыборкаРеквизитов.СуммаВВалюте;
		
		НовыйДокумент.Руководитель 				= ОрганизационнаяСтруктураПовтИсп.ОтветственноеЛицоОрганизации(Объект.Организация, Перечисления.ОтветственныеЛицаОрганизаций.Руководитель, НовыйДокумент.Дата).ОтветственноеЛицоСсылка; 
		НовыйДокумент.ПечататьДолжность 		= НЕ НовыйДокумент.Руководитель.Пустая();
		
		// Заполнение ТЧ документа
		СтруктураПоиска.Обязательство = ВыборкаРеквизитов.ДоговорОснованиеОбязательства;
		МассивСтрок = ТаблицаПФО.НайтиСтроки(СтруктураПоиска);
		Для каждого СтрокаПФО Из МассивСтрок Цикл
			
			ИмяТЧ = БюджетВнебюджетПоКФО[СтрокаПФО.КФО];
			Если НЕ ПустаяСтрока(ИмяТЧ) Тогда
				
				ИмяТЧ = "РасшифровкаОбязательства" + ИмяТЧ;
				НоваяСтрока = НовыйДокумент[ИмяТЧ].Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаПФО);
				
				Если СтрокаПФО.ТребуетсяСторнировать Тогда
					
					СтрокаСторно = НовыйДокумент[ИмяТЧ].Добавить();
					ЗаполнитьЗначенияСвойств(СтрокаСторно, НоваяСтрока);
					СтрокаСторно.КПС = СтрокаПФО.НовыйКПС;
					СтрокаСторно.ЗапретИзменения = Истина;
					
					НоваяСтрока.Количество = 0;
					НоваяСтрока.Сумма = 0;
					НоваяСтрока.СуммаВВалюте = 0;
					
				КонецЕсли; 
				
			КонецЕсли; 
			
		КонецЦикла;
		
		НовыйДокумент.Комментарий = "#Создан автоматически";
		
		ДополнительныеРеквизиты = Новый Структура();
		БухгалтерскиеОперацииСервер.ДобавитьДанныеДляПроведения(НовыйДокумент, Объект.ТиповаяОперация, ДополнительныеРеквизиты);
		
		Если ТекущаяДата() < Объект.ДатаФормированияДокументов Тогда
			
			НовыйДокумент.Записать(РежимЗаписиДокумента.Запись);		
			
		Иначе
			
			Попытка
				НовыйДокумент.Записать(РежимЗаписиДокумента.Проведение, РежимПроведенияДокумента.Неоперативный);
			Исключение
				НовыйДокумент.Записать(РежимЗаписиДокумента.Запись);	
			КонецПопытки;
			
		КонецЕсли;
		
		НоваяСтрокаДокумента = Объект.СформированныеДокументы.Добавить();
		НоваяСтрокаДокумента.ЗаявкаНаПеререгистрацию = НовыйДокумент.Ссылка;
		
	КонецЦикла; 
	
	Элементы.Организация.ТолькоПросмотр = Истина;
	
КонецПроцедуры
