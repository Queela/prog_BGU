
&ИзменениеИКонтроль("ПолучитьТаблицуИзмененийОбязательства")
Функция АБК_ПолучитьТаблицуИзмененийОбязательства(Обязательство, Документ)

	СтруктураРеквизитовДокумента = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Документ, "Дата, Основание"); 
	СтруктураРеквизитовДоговора = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Обязательство, "ВидДоговора");

	Запрос = Новый Запрос;
	ТекстЗапроса = "ВЫБРАТЬ
	|	ПФО_До.КлючАналитики.КФО КАК КФО,
	|	ПФО_До.КлючАналитики.КПС КАК КПС,
	|	ПФО_До.КлючАналитики.КЭК КАК КЭК,
	|	ПФО_До.КлючАналитики.Номенклатура КАК Номенклатура,
	|	ПФО_До.КлючАналитики.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	ПФО_До.КлючАналитики.ПериодПлана КАК ПериодПлана,
	|	ПФО_До.КлючАналитики.РазделЛицевогоСчета КАК РазделЛицевогоСчета,
	|	ПФО_До.КлючАналитики.КодЦели КАК КодЦели,
	|	ПФО_До.КлючАналитики.КодМероприятия КАК КодМероприятия,
	|	ПФО_До.КлючАналитики.ДопКлассификация КАК ДопКлассификация,
	|	ПФО_До.КлючАналитики.Подразделение КАК Подразделение,
	|	ПФО_До.ОКП,
	|	ПФО_До.Сумма КАК Сумма,
	|	ПФО_До.СуммаВВалютеБезусловная КАК СуммаВВалютеБезусловная
	|ИЗ
	|	РегистрСведений.ПланФинансированияОбязательств.СрезПоследних(
	|			&Период,
	|			Обязательство = &Обязательство
	|				И &УсловиеРегистратор) КАК ПФО_До";
#Удаление
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&УсловиеРегистратор", "Регистратор <> &Регистратор");
	Запрос.УстановитьПараметр("Регистратор", Документ);		
#КонецУдаления
#Вставка
	Если СтруктураРеквизитовДоговора.ВидДоговора = Перечисления.ВидыДоговоровКонтрагентов.ИноеОбязательство Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&УсловиеРегистратор", "Регистратор = &Регистратор");
		Запрос.УстановитьПараметр("Регистратор", СтруктураРеквизитовДокумента.Основание);
	Иначе
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&УсловиеРегистратор", "Регистратор <> &Регистратор");
		Запрос.УстановитьПараметр("Регистратор", Документ);		
	КонецЕсли;
#КонецВставки
	Запрос.Текст = ТекстЗапроса;
	Запрос.УстановитьПараметр("Период", СтруктураРеквизитовДокумента.Дата);
	Запрос.УстановитьПараметр("Обязательство", Обязательство);

	ТаблицаИзменений = Запрос.Выполнить().Выгрузить();

	Возврат ТаблицаИзменений;

КонецФункции
