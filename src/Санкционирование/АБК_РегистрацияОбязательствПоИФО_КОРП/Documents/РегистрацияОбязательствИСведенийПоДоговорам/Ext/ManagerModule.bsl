
&Вместо("ИнициализироватьДанныеДокумента")
Процедура АБК_ИнициализироватьДанныеДокумента(ДокументСсылка, ДополнительныеСвойства)

	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
	Запрос.УстановитьПараметр("ПустаяДата", '00010101');
	
	ТаблицыЗапроса = Новый Массив;
	
	ТаблицыЗапроса.Добавить("СрокиИсполненияИСуммыОбязательств");
	ТаблицыЗапроса.Добавить("ПланФинансированияОбязательств");
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	СрокиИСуммы.Дата КАК Период,
	|	СрокиИСуммы.Договор КАК Обязательство,
	|	СрокиИСуммы.НачалоДействия КАК НачалоДействия,
	|	СрокиИСуммы.ОкончаниеДействия КАК ОкончаниеДействия,
	|	СрокиИСуммы.СрокИсполнения КАК СрокИсполнения,
	|	СрокиИСуммы.ВалютаДокумента КАК ВалютаРасчетов,
	|	СрокиИСуммы.Курс КАК КурсРасчетов,
	|	СрокиИСуммы.Кратность КАК КратностьРасчетов,
	|	СрокиИСуммы.СуммаВВалюте КАК СуммаВВалюте,
	|	СрокиИСуммы.Сумма КАК Сумма,
	|	СрокиИСуммы.АвансПроцентом КАК АвансПроцентом,
	|	СрокиИСуммы.СуммаАванса КАК СуммаАванса,
	|	ВЫБОР
	|		КОГДА СрокиИсполненияИСуммыОбязательств.Регистратор ЕСТЬ NULL
	|			ТОГДА &Ссылка
	|		ИНАЧЕ СрокиИсполненияИСуммыОбязательств.ПервичныйДокумент
	|	КОНЕЦ КАК ПервичныйДокумент,
	|	СрокиИСуммы.ИФО КАК ИФО,
	|	СрокиИСуммы.ЛицевойСчет КАК ЛицевойСчет,
	|	СрокиИСуммы.РеквизитыЛицевогоСчета КАК РеквизитыЛицевогоСчета,
	|	СрокиИСуммы.ОрганКазначейства КАК ОрганКазначейства,
	|	СрокиИСуммы.СчетКонтрагента КАК СчетКонтрагента,
	|	СрокиИСуммы.ТипСведений КАК ТипСведений,
	|	СрокиИСуммы.ПроцентПоЗайму КАК ПроцентПоЗайму,
	|	СрокиИСуммы.ВидОбязательства КАК ВидОбязательства,
	|	СрокиИСуммы.КазначейскоеСопровождение КАК КазначейскоеСопровождение,
	|	СрокиИСуммы.ОплатаВРублях КАК ОплатаВРублях
	|ИЗ
	|	Документ.РегистрацияОбязательствИСведенийПоДоговорам КАК СрокиИСуммы
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СрокиИсполненияИСуммыОбязательств КАК СрокиИсполненияИСуммыОбязательств
	|		ПО СрокиИСуммы.Основание = СрокиИсполненияИСуммыОбязательств.Регистратор
	|ГДЕ
	|	СрокиИСуммы.Ссылка = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Данные.Обязательство КАК Обязательство,
	|	Данные.ИФО КАК ИФО,
	|	Данные.КФО КАК КФО,
	|	Данные.КПС КАК КПС,
	|	Данные.КЭК КАК КЭК,
	|	Данные.РазделЛицевогоСчета КАК РазделЛицевогоСчета,
	|	Данные.ПериодПлана КАК ПериодПлана,
	|	Данные.КодЦели КАК КодЦели,
	|	Данные.КодМероприятия КАК КодМероприятия,
	|	Данные.ДопКлассификация КАК ДопКлассификация,
	|	Данные.Количество КАК Количество,
	|	Данные.Сумма КАК Сумма,
	|	Данные.СуммаВВалюте КАК СуммаВВалюте,
	|	Данные.СуммаВВалютеБезусловная КАК СуммаВВалютеБезусловная,
	|	Данные.СуммаИсполнено КАК СуммаИсполнено,
	|	Данные.СуммаИсполненоБезусловно КАК СуммаИсполненоБезусловно,
	|	Данные.Номенклатура КАК Номенклатура,
	|	Данные.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	Данные.ОКП КАК ОКП,
	|	Данные.Период КАК Период,
	|	Данные.ОбъектФАИП КАК ОбъектФАИП,
	|	Данные.Подразделение КАК Подразделение,
	|	ЕСТЬNULL(АналитикаПФО.КлючАналитики, НЕОПРЕДЕЛЕНО) КАК КлючАналитики
	|ИЗ
	|	(ВЫБРАТЬ
	|		Бюджет.Ссылка.Договор КАК Обязательство,
	// +АпогейБК
	//|		Бюджет.Ссылка.ИФО,
	|		ВЫБОР
	|			КОГДА Бюджет.ИФО = ЗНАЧЕНИЕ(Справочник.ИсточникиФинансовогоОбеспечения.ПустаяСсылка)
	|				ТОГДА Бюджет.Ссылка.ИФО
	|			ИНАЧЕ Бюджет.ИФО 
	|		КОНЕЦ КАК ИФО,
	// -АпогейБК
	|		Бюджет.КФО КАК КФО,
	|		Бюджет.КПС КАК КПС,
	|		Бюджет.КЭК КАК КЭК,
	|		Бюджет.РазделЛицевогоСчета КАК РазделЛицевогоСчета,
	|		Бюджет.ПериодПлана КАК ПериодПлана,
	|		Бюджет.КодЦели КАК КодЦели,
	|		Бюджет.КодМероприятия КАК КодМероприятия,
	|		Бюджет.ДопКлассификация КАК ДопКлассификация,
	|		СУММА(Бюджет.Количество) КАК Количество,
	|		СУММА(Бюджет.Сумма) КАК Сумма,
	|		СУММА(Бюджет.СуммаВВалюте) КАК СуммаВВалюте,
	|		СУММА(Бюджет.СуммаВВалютеБезусловная) КАК СуммаВВалютеБезусловная,
	|		СУММА(Бюджет.СуммаИсполнено) КАК СуммаИсполнено,
	|		СУММА(Бюджет.СуммаИсполненоБезусловно) КАК СуммаИсполненоБезусловно,
	|		Бюджет.Номенклатура КАК Номенклатура,
	|		Бюджет.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|		Бюджет.ОКП КАК ОКП,
	|		Бюджет.ОбъектФАИП КАК ОбъектФАИП,
	|		Бюджет.Подразделение КАК Подразделение,
	|		Бюджет.Ссылка.Дата КАК Период
	|	ИЗ
	|		Документ.РегистрацияОбязательствИСведенийПоДоговорам.РасшифровкаОбязательстваБюджет КАК Бюджет
	|	ГДЕ
	|		Бюджет.Ссылка = &Ссылка
	|	
	|	СГРУППИРОВАТЬ ПО
	|		Бюджет.КПС,
	|		Бюджет.Ссылка.Договор,
	// +АпогейБК
	//|		Бюджет.Ссылка.ИФО,
	|		ВЫБОР
	|			КОГДА Бюджет.ИФО = ЗНАЧЕНИЕ(Справочник.ИсточникиФинансовогоОбеспечения.ПустаяСсылка)
	|				ТОГДА Бюджет.Ссылка.ИФО
	|			ИНАЧЕ Бюджет.ИФО 
	|		КОНЕЦ,
	// -АпогейБК
	|		Бюджет.КФО,
	|		Бюджет.КЭК,
	|		Бюджет.РазделЛицевогоСчета,
	|		Бюджет.КодЦели,
	|		Бюджет.КодМероприятия,
	|		Бюджет.ДопКлассификация,
	|		Бюджет.Номенклатура,
	|		Бюджет.ЕдиницаИзмерения,
	|		Бюджет.ОКП,
	|		Бюджет.ПериодПлана,
	|		Бюджет.ОбъектФАИП,
	|		Бюджет.Подразделение,
	|		Бюджет.Ссылка.Дата
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		Внебюджет.Ссылка.Договор,
	// +АпогейБК
	//|		Внебюджет.Ссылка.ИФО,
	|		ВЫБОР
	|			КОГДА Внебюджет.ИФО = ЗНАЧЕНИЕ(Справочник.ИсточникиФинансовогоОбеспечения.ПустаяСсылка)
	|				ТОГДА Внебюджет.Ссылка.ИФО
	|			ИНАЧЕ Внебюджет.ИФО 
	|		КОНЕЦ КАК ИФО,
	// -АпогейБК
	|		Внебюджет.КФО,
	|		Внебюджет.КПС,
	|		Внебюджет.КЭК,
	|		Внебюджет.РазделЛицевогоСчета,
	|		Внебюджет.ПериодПлана,
	|		Внебюджет.КодЦели,
	|		Внебюджет.КодМероприятия,
	|		Внебюджет.ДопКлассификация,
	|		СУММА(Внебюджет.Количество),
	|		СУММА(Внебюджет.Сумма),
	|		СУММА(Внебюджет.СуммаВВалюте),
	|		СУММА(Внебюджет.СуммаВВалютеБезусловная),
	|		0,
	|		0,
	|		Внебюджет.Номенклатура,
	|		Внебюджет.ЕдиницаИзмерения,
	|		Внебюджет.ОКП,
	|		Внебюджет.ОбъектФАИП,
	|		Внебюджет.Подразделение,
	|		Внебюджет.Ссылка.Дата
	|	ИЗ
	|		Документ.РегистрацияОбязательствИСведенийПоДоговорам.РасшифровкаОбязательстваВнебюджет КАК Внебюджет
	|	ГДЕ
	|		Внебюджет.Ссылка = &Ссылка
	|	
	|	СГРУППИРОВАТЬ ПО
	|		Внебюджет.Ссылка.Договор,
	// +АпогейБК
	//|		Внебюджет.Ссылка.ИФО,
	|		ВЫБОР
	|			КОГДА Внебюджет.ИФО = ЗНАЧЕНИЕ(Справочник.ИсточникиФинансовогоОбеспечения.ПустаяСсылка)
	|				ТОГДА Внебюджет.Ссылка.ИФО
	|			ИНАЧЕ Внебюджет.ИФО 
	|		КОНЕЦ,
	// -АпогейБК
	|		Внебюджет.ПериодПлана,
	|		Внебюджет.КФО,
	|		Внебюджет.КПС,
	|		Внебюджет.КЭК,
	|		Внебюджет.РазделЛицевогоСчета,
	|		Внебюджет.КодЦели,
	|		Внебюджет.КодМероприятия,
	|		Внебюджет.ДопКлассификация,
	|		Внебюджет.Номенклатура,
	|		Внебюджет.ЕдиницаИзмерения,
	|		Внебюджет.ОКП,
	|		Внебюджет.ОбъектФАИП,
	|		Внебюджет.Подразделение,
	|		Внебюджет.Ссылка.Дата) КАК Данные
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаПФО КАК АналитикаПФО
	|		ПО Данные.КФО = АналитикаПФО.КФО
	|			И Данные.КПС = АналитикаПФО.КПС
	|			И Данные.КЭК = АналитикаПФО.КЭК
	|			И Данные.РазделЛицевогоСчета = АналитикаПФО.РазделЛицевогоСчета
	|			И Данные.ПериодПлана = АналитикаПФО.ПериодПлана
	|			И Данные.КодЦели = АналитикаПФО.КодЦели
	|			И Данные.КодМероприятия = АналитикаПФО.КодМероприятия
	|			И Данные.ДопКлассификация = АналитикаПФО.ДопКлассификация
	|			И Данные.Номенклатура = АналитикаПФО.Номенклатура
	|			И Данные.ОбъектФАИП = АналитикаПФО.ОбъектФАИП
	|			И Данные.Подразделение = АналитикаПФО.Подразделение
	|			И Данные.ЕдиницаИзмерения = АналитикаПФО.ЕдиницаИзмерения";
	
	ТаблицыЗапроса.Добавить("ИсполнениеОбязательствПоставщиками");
	Запрос.Текст = Запрос.Текст + ОбщегоНазначения.РазделительПакетаЗапросов() +
	"ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|	ВЫБОР
	|		КОГДА ГрафикИсполнения.ИсполнитьДо = &ПустаяДата
	|			ТОГДА ГрафикИсполнения.Ссылка.Дата
	|		ИНАЧЕ ГрафикИсполнения.ИсполнитьДо
	|	КОНЕЦ КАК Период,
	|	ГрафикИсполнения.КФО КАК КФО,
	|	ГрафикИсполнения.КПС КАК КПС,
	|	ГрафикИсполнения.КЭК КАК КЭК,
	|	ГрафикИсполнения.СчетРасчетов КАК СчетРасчетов,
	|	ГрафикИсполнения.Ссылка.Организация КАК Организация,
	|	ГрафикИсполнения.Ссылка.ИФО КАК ИФО,
	|	ГрафикИсполнения.Ссылка.Контрагент КАК Контрагент,
	|	ГрафикИсполнения.Ссылка.Договор КАК Договор,
	|	ГрафикИсполнения.Ссылка.ВалютаДокумента КАК Валюта,
	|	ГрафикИсполнения.ИсполнитьДо КАК ИсполнитьДо,
	|	ГрафикИсполнения.СуммаВВалюте КАК Сумма
	|ИЗ
	|	Документ.РегистрацияОбязательствИСведенийПоДоговорам.ГрафикИсполнения КАК ГрафикИсполнения
	|ГДЕ
	|	ГрафикИсполнения.Ссылка = &Ссылка
	|	И ГрафикИсполнения.Ссылка.Договор.ВестиПланГрафикИсполнения
	|	И ГрафикИсполнения.Ссылка.Договор.ВидДоговора В(&ВидыДоговоровСПоставщикамиКредиторами)
	|	И ГрафикИсполнения.СуммаВВалюте <> 0";
	
	ТаблицыЗапроса.Добавить("ОплатаПоставщикамКредиторам");
	Запрос.Текст = Запрос.Текст + ОбщегоНазначения.РазделительПакетаЗапросов() +
	"ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|	ВЫБОР
	|		КОГДА ГрафикПлатежей.ОплатитьДо = &ПустаяДата
	|			ТОГДА ГрафикПлатежей.Ссылка.Дата
	|		ИНАЧЕ ГрафикПлатежей.ОплатитьДо
	|	КОНЕЦ КАК Период,
	|	ГрафикПлатежей.ВидРасчетов КАК ВидРасчетов,
	|	ГрафикПлатежей.КФО КАК КФО,
	|	ГрафикПлатежей.КПС КАК КПС,
	|	ГрафикПлатежей.КЭК КАК КЭК,
	|	ГрафикПлатежей.СчетРасчетов КАК СчетРасчетов,
	|	ГрафикПлатежей.ДопКлассификация КАК ДопКлассификация,
	|	ГрафикПлатежей.КодМероприятия КАК КодМероприятия,
	|	ГрафикПлатежей.КодЦели КАК КодЦели,
	|	ГрафикПлатежей.Ссылка.Организация КАК Организация,
	|	ГрафикПлатежей.Ссылка.ИФО КАК ИФО,
	|	ГрафикПлатежей.Ссылка.Контрагент КАК Контрагент,
	|	ГрафикПлатежей.Ссылка.Договор КАК Договор,
	|	ГрафикПлатежей.Ссылка.ВалютаДокумента КАК Валюта,
	|	ГрафикПлатежей.ОплатитьДо КАК ОплатитьДо,
	|	ГрафикПлатежей.СуммаВВалюте КАК Сумма
	|ИЗ
	|	Документ.РегистрацияОбязательствИСведенийПоДоговорам.ГрафикПлатежей КАК ГрафикПлатежей
	|ГДЕ
	|	ГрафикПлатежей.Ссылка = &Ссылка
	|	И ГрафикПлатежей.Ссылка.Договор.ВестиГрафикПлатежей
	|	И ГрафикПлатежей.Ссылка.Договор.ВидДоговора В(&ВидыДоговоровСПоставщикамиКредиторами)
	|	И ГрафикПлатежей.СуммаВВалюте <> 0";
	ВидыДоговоровСПоставщикамиКредиторами = Новый Массив;
	ВидыДоговоровСПоставщикамиКредиторами.Добавить(Перечисления.ВидыДоговоровКонтрагентов.СПоставщиком);
	ВидыДоговоровСПоставщикамиКредиторами.Добавить(Перечисления.ВидыДоговоровКонтрагентов.ПолученныйКредит);
	ВидыДоговоровСПоставщикамиКредиторами.Добавить(Перечисления.ВидыДоговоровКонтрагентов.ВыпускЦенныхБумаг);
	Запрос.УстановитьПараметр("ВидыДоговоровСПоставщикамиКредиторами", ВидыДоговоровСПоставщикамиКредиторами);
	
	Результат = Запрос.ВыполнитьПакет();
	
	ТаблицыДляДвижений = ДополнительныеСвойства.ТаблицыДляДвижений;
	ОбщегоНазначенияБГУ.ВыгрузитьТаблицыЗапроса(ТаблицыДляДвижений, Результат, ТаблицыЗапроса);
	
	Для каждого СтрокаПФО Из ТаблицыДляДвижений.ПланФинансированияОбязательств Цикл
		
		Если СтрокаПФО.КлючАналитики = Неопределено Тогда
			
			//АпогейБК
			//СтрокаПФО.КлючАналитики = Справочники.КлючАналитикиПФО.СоздатьКлючАналитики(СтрокаПФО);
			СтрокаПФО.КлючАналитики = Справочники.КлючАналитикиПФО.ЗначениеКлючаАналитики(СтрокаПФО);
			//АпогейБК
			
		КонецЕсли; 
		
	КонецЦикла;
	
КонецПроцедуры
