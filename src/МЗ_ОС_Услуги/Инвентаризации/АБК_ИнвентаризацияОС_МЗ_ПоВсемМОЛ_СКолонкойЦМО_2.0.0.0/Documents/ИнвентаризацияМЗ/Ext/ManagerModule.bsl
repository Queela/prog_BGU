
&ИзменениеИКонтроль("ОпределитьПараметрыНакладываемогоОтбора")
Функция АБК_ОпределитьПараметрыНакладываемогоОтбора(Форма)

	Перем	ИспользоватьУчетПоИФО,
	ИспользоватьУчетПоПодразделениям,
	НакладыватьОтборПоКФО,
	НакладыватьОтборПоИФО,
	НакладыватьОтборПоКПС,
	НакладыватьОтборПоПодразделению,
	НакладыватьОтборПоДополнительномуСубконто,
	СледуетИспользоватьДополнительноеСубконто,
	СчетаОстатков,
	ИсключенныеСчетаОстатков,
	ЗначенияДляОтбораПоМОЛ,
	ТаблицаРеальныхДополнительныхСубконто;

	ДокументОбъект						= Форма["Объект"];

	ТаблицаАналитикиДопустимыхСчетов	= Форма[ПолучитьИмяРеквизитаФормыТаблицаАналитикиДопустимыхСчетов()];
	ДетальныеСвойства					= Форма[ПолучитьИмяРеквизитаФормыСвойстваВыбраннойГруппыСчетов()]["ДетальныеСвойства"];
	СвойстваДопустимыхСчетов			= ДетальныеСвойства["СвойстваДопустимыхСчетов"];

	ИспользоватьУчетПоИФО				= ПолучитьФункциональнуюОпцию("ИспользоватьУчетПоИФО");
	ИспользоватьУчетПоПодразделениям	= ПолучитьФункциональнуюОпцию("ИспользоватьУчетПоПодразделениям");


	НакладыватьОтборПоКФО						= ЗначениеЗаполнено(ДокументОбъект["КФО"]);

	НакладыватьОтборПоИФО						= ИспользоватьУчетПоИФО
	И НЕ ДокументОбъект["БезУчетаИФО"];

	НакладыватьОтборПоКПС						= СвойстваДопустимыхСчетов["УчетПоКПС"]
	И ЗначениеЗаполнено(ДокументОбъект["КПС"]);

	НакладыватьОтборПоПодразделению				= ИспользоватьУчетПоПодразделениям
	И СвойстваДопустимыхСчетов["УчетПоПодразделениям"]
	И ЗначениеЗаполнено(ДокументОбъект["Подразделение"]);

	СледуетИспользоватьДополнительноеСубконто	= ДетальныеСвойства["КорректностьНастроекДополнительногоСубконто"]["СледуетИспользовать"];

	НакладыватьОтборПоДополнительномуСубконто	= СледуетИспользоватьДополнительноеСубконто
	И ЗначениеЗаполнено(ДокументОбъект["Субконто3"]);


	СчетаОстатков								= Новый Массив();
	ИсключенныеСчетаОстатков					= Новый Массив();

	// Счет отбора сужает список проверяемых счетов
	ЗапросПоиск = Новый Запрос();
	ЗапросПоиск.УстановитьПараметр("Счет", ДокументОбъект["Счет"]);
	ЗапросПоиск.Текст = "
	|ВЫБРАТЬ
	|	Ссылка
	|ИЗ
	|	ПланСчетов.ЕПСБУ
	|ГДЕ
	|	Ссылка В ИЕРАРХИИ(&Счет)
	|;";

	ПотенциальныеСчетаОстатков = ЗапросПоиск.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка");

	// Таблица реальных дополнительных субконто - для Счетов остатков
	// Сведения по Исключаемым счетам в эту таблиццу не включаем
	ТаблицаРеальныхДополнительныхСубконто = ПолучитьПустуюТаблицуАналитикиДопустимыхСчетов();

	Для Каждого СтрокаТаблицы Из ТаблицаАналитикиДопустимыхСчетов Цикл
		ДопустимыйСчет = СтрокаТаблицы["Счет"];

		Если ПотенциальныеСчетаОстатков.Найти(ДопустимыйСчет) = Неопределено Тогда
			Продолжить;
		КонецЕсли;

		Если НакладыватьОтборПоКПС И НЕ СтрокаТаблицы["УчетПоКПС"] Тогда
			ИсключенныеСчетаОстатков.Добавить(ДопустимыйСчет);
			Продолжить;
		КонецЕсли;

		Если НакладыватьОтборПоПодразделению И НЕ СтрокаТаблицы["УчетПоПодразделениям"] Тогда
			ИсключенныеСчетаОстатков.Добавить(ДопустимыйСчет);
			Продолжить;
		КонецЕсли;

		Если НакладыватьОтборПоДополнительномуСубконто Тогда
			ИсключитьЭтотСчет = Ложь;

			Если ЗначениеЗаполнено(СтрокаТаблицы["РеальноеДополнительноеСубконто"]) Тогда
				ПриведенноеЗначение = СтрокаТаблицы["РеальноеДополнительноеСубконто"].ТипЗначения.ПривестиЗначение(ДокументОбъект["Субконто3"]);

				Если НЕ ЗначениеЗаполнено(ПриведенноеЗначение) Тогда
					ИсключитьЭтотСчет = Истина;
				КонецЕсли;
			Иначе
				ИсключитьЭтотСчет = Истина;
			КонецЕсли;

			Если ИсключитьЭтотСчет Тогда
				ИсключенныеСчетаОстатков.Добавить(ДопустимыйСчет);
				Продолжить;
			КонецЕсли;
		КонецЕсли;

		СчетаОстатков.Добавить(ДопустимыйСчет);

		НоваяСтрока = ТаблицаРеальныхДополнительныхСубконто.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТаблицы);
	КонецЦикла;

	// Применяемые значения для отбора по МОЛ
	МОЛ = ДокументОбъект["ЦМО"];
	Если ТипЗнч(МОЛ) = Тип("СправочникСсылка.Сотрудники") Тогда
		// 1 Сотрудник -> N ЦМО,
		// ЦМО определяются для одной указанной организации из шапки документа
		// Может оказаться несколько значений

		ЗапросПоиск = Новый Запрос();
		ЗапросПоиск.УстановитьПараметр("Сотрудник",		МОЛ);
		ЗапросПоиск.УстановитьПараметр("Организация",	ДокументОбъект["Организация"]);
		ЗапросПоиск.Текст = "
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	Ссылка КАК Ссылка
		|ИЗ
		|	Справочник.ЦМО
		|ГДЕ
		|	Сотрудник = &Сотрудник
		|	И Владелец = &Организация
		|;";

		ЗначенияДляОтбораПоМОЛ = ЗапросПоиск.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка");

	Иначе
		// Используем только 1 указанное значение

		ЗначенияДляОтбораПоМОЛ = Новый Массив();
		ЗначенияДляОтбораПоМОЛ.Добавить(МОЛ);
	КонецЕсли;
	#Вставка     
	Если Форма.АБК_ЗаполнитьПоВсемМол Тогда 
		ВсеМОЛСчета = ПолучитьВсеМОЛСчета(ДокументОбъект.Счет, ДокументОбъект.ДатаИнвентаризации);
		Если ВсеМОЛСчета.Количество() Тогда 
			ЗначенияДляОтбораПоМОЛ = ВсеМОЛСчета;	
		КонецЕсли;
	КонецЕсли;
	#КонецВставки

	ПараметрыНакладываемогоОтбора = ПараметрыНакладываемогоОтбора_Пустая();

	ПараметрыНакладываемогоОтбора.ИспользоватьУчетПоИФО                     = ИспользоватьУчетПоИФО;
	ПараметрыНакладываемогоОтбора.ИспользоватьУчетПоПодразделениям          = ИспользоватьУчетПоПодразделениям;
	ПараметрыНакладываемогоОтбора.НакладыватьОтборПоКФО                     = НакладыватьОтборПоКФО;
	ПараметрыНакладываемогоОтбора.НакладыватьОтборПоИФО                     = НакладыватьОтборПоИФО;
	ПараметрыНакладываемогоОтбора.НакладыватьОтборПоКПС                     = НакладыватьОтборПоКПС;
	ПараметрыНакладываемогоОтбора.НакладыватьОтборПоПодразделению           = НакладыватьОтборПоПодразделению;
	ПараметрыНакладываемогоОтбора.НакладыватьОтборПоДополнительномуСубконто = НакладыватьОтборПоДополнительномуСубконто;
	ПараметрыНакладываемогоОтбора.СледуетИспользоватьДополнительноеСубконто = СледуетИспользоватьДополнительноеСубконто;
	ПараметрыНакладываемогоОтбора.СчетаОстатков                             = СчетаОстатков;
	ПараметрыНакладываемогоОтбора.ИсключенныеСчетаОстатков                  = ИсключенныеСчетаОстатков;
	ПараметрыНакладываемогоОтбора.ЗначенияДляОтбораПоМОЛ                    = ЗначенияДляОтбораПоМОЛ;
	ПараметрыНакладываемогоОтбора.ТаблицаРеальныхДополнительныхСубконто     = ТаблицаРеальныхДополнительныхСубконто;

	Возврат ПараметрыНакладываемогоОтбора;

КонецФункции  

Функция ПолучитьВсеМОЛСчета(Счет, ДатаИнвентаризации)
	
	СчетаСКонтрагентами = Новый Массив;
	СчетаСКонтрагентами.Добавить(ПланыСчетов.ЕПСБУ.НайтиПоКоду("25"));
	СчетаСКонтрагентами.Добавить(ПланыСчетов.ЕПСБУ.НайтиПоКоду("26"));
	СчетаСКонтрагентами.Добавить(ПланыСчетов.ЕПСБУ.НайтиПоКоду("27.02"));
	
	ВидСубконто = ПланыВидовХарактеристик.ВидыСубконто.ЦМО;
	
	Если СчетаСКонтрагентами.Найти(Счет)<>Неопределено Тогда 
		ВидСубконто = ПланыВидовХарактеристик.ВидыСубконто.Контрагенты;
	КонецЕсли; 
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Период", ДатаИнвентаризации);
	Запрос.УстановитьПараметр("Счет", Счет);
	Запрос.УстановитьПараметр("ВидСубконто", ВидСубконто);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ЖурналПроводокЕПСБУОстатки.Субконто1 КАК Субконто1
	|ИЗ
	|	РегистрБухгалтерии.ЖурналПроводокЕПСБУ.Остатки(&Период, Счет В ИЕРАРХИИ (&Счет), &ВидСубконто, ) КАК ЖурналПроводокЕПСБУОстатки"; 
	Результат = Запрос.Выполнить().Выгрузить();
	
	Если Результат.Количество() Тогда 
		Возврат Результат.ВыгрузитьКолонку("Субконто1");
	КонецЕсли;
	
	Возврат Новый Массив;
	
КонецФункции // ПолучитьВсеМОЛСчета()

&ИзменениеИКонтроль("ЗаполнитьОбъектПоДаннымУчета")
Процедура АБК_ЗаполнитьОбъектПоДаннымУчета(ДокументОбъект, ПараметрыНакладываемогоОтбора)

	Перем СдвигИндексаТаблицыМатериалы;

	ДатаОпределенияОстатков			= Новый Граница(КонецДня(ДокументОбъект["ДатаИнвентаризации"]), ВидГраницы.Включая);
	ИспользоватьБалансовуюСтоимость = НЕ ЗначениеЗаполнено(ДокументОбъект["ТипЦен"]);

	ПризнакиИспользованияКолонок	= Документы.ИнвентаризацияМЗ.ПолучитьПризнакиИспользованияКолонокТаблицыПорядокСписания();

	УстанавливатьКоличествоПоУмолчанию = ИнвентаризацияСервер.УстанавливатьКоличествоПоУмолчанию(ДокументОбъект);

	КоличествоПоУмолчанию = ИнвентаризацияСервер.ПолучитьКоличествоПоУмолчанию();

	Если УстанавливатьКоличествоПоУмолчанию Тогда
		ВыбиратьНулевоеКоличество = Истина;
		ЗаменятьНулевоеКоличество = Истина;
	Иначе
		ВыбиратьНулевоеКоличество = Ложь;
		ЗаменятьНулевоеКоличество = Ложь;
	КонецЕсли;

	ВариантФормированияОписи = ДокументОбъект["ВариантФормированияОписи"];

	ЗапросОстатки = Новый Запрос();
	ЗапросОстатки.УстановитьПараметр("Организация",								ДокументОбъект["Организация"]);
	ЗапросОстатки.УстановитьПараметр("ДатаЗапроса",								ДатаОпределенияОстатков);
	ЗапросОстатки.УстановитьПараметр("СчетаОстатков",							ПараметрыНакладываемогоОтбора["СчетаОстатков"]);
	ЗапросОстатки.УстановитьПараметр("ИспользоватьУчетПоИФО",					ПараметрыНакладываемогоОтбора["ИспользоватьУчетПоИФО"]);
	ЗапросОстатки.УстановитьПараметр("НакладыватьОтборПоИФО",					ПараметрыНакладываемогоОтбора["НакладыватьОтборПоИФО"]);
	ЗапросОстатки.УстановитьПараметр("ИФО",										ДокументОбъект["ИФО"]);
	ЗапросОстатки.УстановитьПараметр("НакладыватьОтборПоКФО",					ПараметрыНакладываемогоОтбора["НакладыватьОтборПоКФО"]);
	ЗапросОстатки.УстановитьПараметр("КФО",										ДокументОбъект["КФО"]);
	ЗапросОстатки.УстановитьПараметр("НакладыватьОтборПоПодразделению",			ПараметрыНакладываемогоОтбора["НакладыватьОтборПоПодразделению"]);
	ЗапросОстатки.УстановитьПараметр("Подразделение",							ДокументОбъект["Подразделение"]);
	ЗапросОстатки.УстановитьПараметр("НакладыватьОтборПоКПС",					ПараметрыНакладываемогоОтбора["НакладыватьОтборПоКПС"]);
	ЗапросОстатки.УстановитьПараметр("КПС",										ДокументОбъект["КПС"]);
	ЗапросОстатки.УстановитьПараметр("ПустаяНоменклатура",						Справочники.Номенклатура.ПустаяСсылка());
	ЗапросОстатки.УстановитьПараметр("ТипЦен",									ДокументОбъект["ТипЦен"]);

	// Отбор по номенклатуре
	ЗапросОстатки.УстановитьПараметр("НакладыватьОтборПоНоменклатуре",   		ЗначениеЗаполнено(ПараметрыНакладываемогоОтбора["ЗначенияДляОтбораПоНоменклатуре"]));
	ЗапросОстатки.УстановитьПараметр("ЗначенияДляОтбораПоНоменклатуре",			ПараметрыНакладываемогоОтбора["ЗначенияДляОтбораПоНоменклатуре"]);

	// Отбор по МОЛ
	ЗапросОстатки.УстановитьПараметр("ЗначенияДляОтбораПоМОЛ",					ПараметрыНакладываемогоОтбора["ЗначенияДляОтбораПоМОЛ"]);

	// Отбор по дополнительному субконто
	ЗапросОстатки.УстановитьПараметр("СледуетИспользоватьДополнительноеСубконто",	ПараметрыНакладываемогоОтбора["СледуетИспользоватьДополнительноеСубконто"]);
	ЗапросОстатки.УстановитьПараметр("ТаблицаРеальныхДополнительныхСубконто",		ПараметрыНакладываемогоОтбора["ТаблицаРеальныхДополнительныхСубконто"]);
	ЗапросОстатки.УстановитьПараметр("НакладыватьОтборПоДополнительномуСубконто",	ПараметрыНакладываемогоОтбора["НакладыватьОтборПоДополнительномуСубконто"]);
	ЗапросОстатки.УстановитьПараметр("ЗначениеДляОтбораПоДополнительномуСубконто",	ДокументОбъект["Субконто3"]);

	// В ТЧ "Порядок списания" используются не все колонки, а только некоторые
	ЗапросОстатки.УстановитьПараметр("ИспользуетсяКолонкаИФО",					ПризнакиИспользованияКолонок["ИФО"]);
	ЗапросОстатки.УстановитьПараметр("ИспользуетсяКолонкаКФО",					ПризнакиИспользованияКолонок["КФО"]);
	ЗапросОстатки.УстановитьПараметр("ИспользуетсяКолонкаЦМО",					ПризнакиИспользованияКолонок["ЦМО"]);
	ЗапросОстатки.УстановитьПараметр("ИспользуетсяКолонкаКПС",					ПризнакиИспользованияКолонок["КПС"]);
	ЗапросОстатки.УстановитьПараметр("ИспользуетсяКолонкаСчет",					ПризнакиИспользованияКолонок["Счет"]);
	ЗапросОстатки.УстановитьПараметр("ИспользуетсяКолонкаСубконто3",			ПризнакиИспользованияКолонок["Субконто3"]);

	ЗапросОстатки.УстановитьПараметр("СчетГруппа",			ДокументОбъект["СчетГруппа"]);
	ЗапросОстатки.УстановитьПараметр("НеЗаполнятьЦену",		ДокументОбъект["НеЗаполнятьЦену"]);

	ЗапросОстатки.УстановитьПараметр("ЗаменятьНулевоеКоличество",	ЗаменятьНулевоеКоличество);
	ЗапросОстатки.УстановитьПараметр("КоличествоПоУмолчанию",		КоличествоПоУмолчанию);

	ТекстЗапроса = "
	|////////////////////////////////////////////////////////////////////////////////
	|// [0]. Таблица реальных дополнительных субконто для Счетов остатков
	|//
	|ВЫБРАТЬ
	|	ТЗ.Счет										КАК Счет,
	|	ТЗ.РеальноеДополнительноеСубконто			КАК РеальноеДополнительноеСубконто,
	|	ТЗ.НомерРеальногоДополнительногоСубконто	КАК НомерРеальногоДополнительногоСубконто
	|ПОМЕСТИТЬ
	|	ТаблицаРеальныхДополнительныхСубконто
	|ИЗ
	|	&ТаблицаРеальныхДополнительныхСубконто	КАК ТЗ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|// [1]. Все остатки по полным счетам, с отбором по МОЛ.
	|//		Т.к. применяется отбор по МОЛ, то могут быть остатки с нулевыми суммами
	|//		(если на соответствующих счетах не ведется суммовой
	|//		учет по субконто для МОЛ)
	|//
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	Остатки.Счет																			КАК Счет,
	|	Остатки.КФО																				КАК КФО,
	|	ЕСТЬNULL(Остатки.КПС,ЗНАЧЕНИЕ(Справочник.КлассификационныеПризнакиСчетов.ПустаяСсылка))	КАК КПС,
	|	ВЫБОР
	|		КОГДА &ИспользоватьУчетПоИФО ТОГДА
	|			Остатки.ИФО
	|		ИНАЧЕ
	|			ЗНАЧЕНИЕ(Справочник.ИсточникиФинансовогоОбеспечения.ПустаяСсылка)
	|	КОНЕЦ																					КАК ИФО,
	|	ВЫБОР
	|		КОГДА ТИПЗНАЧЕНИЯ(Остатки.Субконто1) = ТИП(Справочник.Номенклатура) ТОГДА
	|			ВЫРАЗИТЬ(Остатки.Субконто1	КАК Справочник.Номенклатура)
	|		КОГДА ТИПЗНАЧЕНИЯ(Остатки.Субконто2) = ТИП(Справочник.Номенклатура) ТОГДА
	|			ВЫРАЗИТЬ(Остатки.Субконто2	КАК Справочник.Номенклатура)
	|		КОГДА ТИПЗНАЧЕНИЯ(Остатки.Субконто3) = ТИП(Справочник.Номенклатура) ТОГДА
	|			ВЫРАЗИТЬ(Остатки.Субконто3	КАК Справочник.Номенклатура)
	|		ИНАЧЕ
	|			ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)
	|	КОНЕЦ																					КАК Номенклатура,
	|	ВЫБОР
	|		КОГДА Субконто1 В (&ЗначенияДляОтбораПоМОЛ) ТОГДА
	|			Субконто1
	|		КОГДА Субконто2 В (&ЗначенияДляОтбораПоМОЛ) ТОГДА
	|			Субконто2
	|		КОГДА Субконто3 В (&ЗначенияДляОтбораПоМОЛ) ТОГДА
	|			Субконто3
	|	КОНЕЦ																					КАК МОЛ,
	|	ВЫБОР
	|		КОГДА &СледуетИспользоватьДополнительноеСубконто И ТаблицаРеальныхДополнительныхСубконто.НомерРеальногоДополнительногоСубконто = 1 ТОГДА
	|			Остатки.Субконто1
	|		КОГДА &СледуетИспользоватьДополнительноеСубконто И ТаблицаРеальныхДополнительныхСубконто.НомерРеальногоДополнительногоСубконто = 2 ТОГДА
	|			Остатки.Субконто2
	|		КОГДА &СледуетИспользоватьДополнительноеСубконто И ТаблицаРеальныхДополнительныхСубконто.НомерРеальногоДополнительногоСубконто = 3 ТОГДА
	|			Остатки.Субконто3
	|		ИНАЧЕ
	|			НЕОПРЕДЕЛЕНО
	|	КОНЕЦ																					КАК Субконто3,
	|	СУММА(Остатки.КоличествоРазвернутыйОстатокДт - Остатки.КоличествоРазвернутыйОстатокКт)	КАК КоличествоУчет,
	|	СУММА(Остатки.СуммаРазвернутыйОстатокДт - Остатки.СуммаРазвернутыйОстатокКт)			КАК СуммаУчет
	|ПОМЕСТИТЬ
	|	ОстаткиСОтборомПоМОЛ
	|ИЗ
	|	РегистрБухгалтерии.ЖурналПроводокЕПСБУ.Остатки(
	|		&ДатаЗапроса,				// 1 Период
	|		Счет В (&СчетаОстатков),	// 2 Условие счета
	|		,							// 3 Виды субконто
	|		Организация = &Организация	// 4 Условие
	|		И ВЫБОР 
	|			КОГДА &НакладыватьОтборПоИФО ТОГДА
	|				ИФО = &ИФО
	|			ИНАЧЕ
	|				ИСТИНА
	|		КОНЕЦ
	|		И ВЫБОР 
	|			КОГДА &НакладыватьОтборПоКФО ТОГДА
	|				КФО = &КФО
	|			ИНАЧЕ
	|				ИСТИНА
	|		КОНЕЦ
	|		И ВЫБОР
	|			КОГДА &НакладыватьОтборПоКПС ТОГДА
	|				КПС = &КПС
	|			ИНАЧЕ
	|				ИСТИНА
	|		КОНЕЦ
	|		И ВЫБОР 
	|			КОГДА &НакладыватьОтборПоПодразделению ТОГДА
	|				Подразделение = &Подразделение
	|			ИНАЧЕ
	|				ИСТИНА
	|		КОНЕЦ
	|		// Отбор по МОЛ
	|		И ВЫБОР
	|			КОГДА Субконто1 В (&ЗначенияДляОтбораПоМОЛ) ТОГДА
	|				ИСТИНА
	|			КОГДА Субконто2 В (&ЗначенияДляОтбораПоМОЛ) ТОГДА
	|				ИСТИНА
	|			КОГДА Субконто3 В (&ЗначенияДляОтбораПоМОЛ) ТОГДА
	|				ИСТИНА
	|			ИНАЧЕ
	|				ЛОЖЬ
	|		КОНЕЦ
	|	)	КАК Остатки
	|	ЛЕВОЕ СОЕДИНЕНИЕ	ТаблицаРеальныхДополнительныхСубконто
	|		ПО	Остатки.Счет = ТаблицаРеальныхДополнительныхСубконто.Счет
	|ГДЕ
	|	// Только заполненная номенклатура
	|	ВЫБОР
	|		КОГДА ТИПЗНАЧЕНИЯ(Остатки.Субконто1) = ТИП(Справочник.Номенклатура) ТОГДА
	|			НЕ Остатки.Субконто1 = &ПустаяНоменклатура
	|		КОГДА ТИПЗНАЧЕНИЯ(Остатки.Субконто2) = ТИП(Справочник.Номенклатура) ТОГДА
	|			НЕ Остатки.Субконто2 = &ПустаяНоменклатура
	|		КОГДА ТИПЗНАЧЕНИЯ(Остатки.Субконто3) = ТИП(Справочник.Номенклатура) ТОГДА
	|			НЕ Остатки.Субконто3 = &ПустаяНоменклатура
	|		ИНАЧЕ
	|			ЛОЖЬ
	|	КОНЕЦ
	|	// Отбор по дополнительному субконто
	|	И ВЫБОР
	|		КОГДА &НакладыватьОтборПоДополнительномуСубконто И ТаблицаРеальныхДополнительныхСубконто.НомерРеальногоДополнительногоСубконто = 1 ТОГДА
	|			Остатки.Субконто1 = &ЗначениеДляОтбораПоДополнительномуСубконто
	|		КОГДА &НакладыватьОтборПоДополнительномуСубконто И ТаблицаРеальныхДополнительныхСубконто.НомерРеальногоДополнительногоСубконто = 2 ТОГДА
	|			Остатки.Субконто2 = &ЗначениеДляОтбораПоДополнительномуСубконто
	|		КОГДА &НакладыватьОтборПоДополнительномуСубконто И ТаблицаРеальныхДополнительныхСубконто.НомерРеальногоДополнительногоСубконто = 3 ТОГДА
	|			Остатки.Субконто3 = &ЗначениеДляОтбораПоДополнительномуСубконто
	|		КОГДА &НакладыватьОтборПоДополнительномуСубконто И ТаблицаРеальныхДополнительныхСубконто.НомерРеальногоДополнительногоСубконто = 0 ТОГДА
	|			ЛОЖЬ
	|		ИНАЧЕ
	|			ИСТИНА
	|	КОНЕЦ
	|	// Отбор по номенклатуре
	|	И НЕ &НакладыватьОтборПоНоменклатуре ИЛИ ВЫБОР
	|		КОГДА ТИПЗНАЧЕНИЯ(Остатки.Субконто1) = ТИП(Справочник.Номенклатура) И НЕ Остатки.Субконто1 В (&ЗначенияДляОтбораПоНоменклатуре) ТОГДА
	|			ЛОЖЬ
	|		КОГДА ТИПЗНАЧЕНИЯ(Остатки.Субконто2) = ТИП(Справочник.Номенклатура) И НЕ Остатки.Субконто2 В (&ЗначенияДляОтбораПоНоменклатуре) ТОГДА
	|			ЛОЖЬ
	|		КОГДА ТИПЗНАЧЕНИЯ(Остатки.Субконто3) = ТИП(Справочник.Номенклатура) И НЕ Остатки.Субконто3 В (&ЗначенияДляОтбораПоНоменклатуре) ТОГДА
	|			ЛОЖЬ
	|		ИНАЧЕ
	|			ИСТИНА
	|	КОНЕЦ
	|СГРУППИРОВАТЬ ПО
	|	Остатки.Счет,
	|	Остатки.КФО,
	|	ЕСТЬNULL(Остатки.КПС,ЗНАЧЕНИЕ(Справочник.КлассификационныеПризнакиСчетов.ПустаяСсылка)),
	|	ВЫБОР
	|		КОГДА &ИспользоватьУчетПоИФО ТОГДА
	|			Остатки.ИФО
	|		ИНАЧЕ
	|			ЗНАЧЕНИЕ(Справочник.ИсточникиФинансовогоОбеспечения.ПустаяСсылка)
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ТИПЗНАЧЕНИЯ(Остатки.Субконто1) = ТИП(Справочник.Номенклатура) ТОГДА
	|			ВЫРАЗИТЬ(Остатки.Субконто1	КАК Справочник.Номенклатура)
	|		КОГДА ТИПЗНАЧЕНИЯ(Остатки.Субконто2) = ТИП(Справочник.Номенклатура) ТОГДА
	|			ВЫРАЗИТЬ(Остатки.Субконто2	КАК Справочник.Номенклатура)
	|		КОГДА ТИПЗНАЧЕНИЯ(Остатки.Субконто3) = ТИП(Справочник.Номенклатура) ТОГДА
	|			ВЫРАЗИТЬ(Остатки.Субконто3	КАК Справочник.Номенклатура)
	|		ИНАЧЕ
	|			ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА Субконто1 В (&ЗначенияДляОтбораПоМОЛ) ТОГДА
	|			Субконто1
	|		КОГДА Субконто2 В (&ЗначенияДляОтбораПоМОЛ) ТОГДА
	|			Субконто2
	|		КОГДА Субконто3 В (&ЗначенияДляОтбораПоМОЛ) ТОГДА
	|			Субконто3
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА &СледуетИспользоватьДополнительноеСубконто И ТаблицаРеальныхДополнительныхСубконто.НомерРеальногоДополнительногоСубконто = 1 ТОГДА
	|			Остатки.Субконто1
	|		КОГДА &СледуетИспользоватьДополнительноеСубконто И ТаблицаРеальныхДополнительныхСубконто.НомерРеальногоДополнительногоСубконто = 2 ТОГДА
	|			Остатки.Субконто2
	|		КОГДА &СледуетИспользоватьДополнительноеСубконто И ТаблицаРеальныхДополнительныхСубконто.НомерРеальногоДополнительногоСубконто = 3 ТОГДА
	|			Остатки.Субконто3
	|		ИНАЧЕ
	|			НЕОПРЕДЕЛЕНО
	|	КОНЕЦ

	|" + ?(ВыбиратьНулевоеКоличество, "", "
	|ИМЕЮЩИЕ
	|	СУММА(Остатки.КоличествоРазвернутыйОстатокДт - Остатки.КоличествоРазвернутыйОстатокКт) <> 0") + "

	|ИНДЕКСИРОВАТЬ ПО
	|	Счет,
	|	КФО,
	|	КПС,
	|	ИФО,
	|	Субконто3,
	|	Номенклатура
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|// [2]. Данные для заполнения таблицы ПорядокСписания
	|//		(Финальная таблица)
	|//
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ВЫБОР КОГДА &ИспользуетсяКолонкаИФО			ТОГДА ИФО		ИНАЧЕ НЕОПРЕДЕЛЕНО КОНЕЦ	КАК ИФО,
	|	ВЫБОР КОГДА &ИспользуетсяКолонкаКФО			ТОГДА КФО		ИНАЧЕ НЕОПРЕДЕЛЕНО КОНЕЦ	КАК КФО,
	|	ВЫБОР КОГДА &ИспользуетсяКолонкаЦМО			ТОГДА МОЛ		ИНАЧЕ НЕОПРЕДЕЛЕНО КОНЕЦ	КАК ЦМО,
	|	ВЫБОР КОГДА &ИспользуетсяКолонкаКПС			ТОГДА КПС		ИНАЧЕ НЕОПРЕДЕЛЕНО КОНЕЦ	КАК КПС,
	|	ВЫБОР КОГДА &ИспользуетсяКолонкаСчет		ТОГДА Счет		ИНАЧЕ НЕОПРЕДЕЛЕНО КОНЕЦ	КАК Счет,
	|	ВЫБОР КОГДА &ИспользуетсяКолонкаСубконто3	ТОГДА Субконто3	ИНАЧЕ НЕОПРЕДЕЛЕНО КОНЕЦ	КАК Субконто3
	|ИЗ
	|	ОстаткиСОтборомПоМОЛ	КАК Остатки
	|;";

	Если ИспользоватьБалансовуюСтоимость Тогда

		Если ВариантФормированияОписи = 3 Тогда
			// 3. Опись с 2018 г. Счет в таблице (5 знаков). Одна строка - один объект

			ТекстЗапросаБлижайшиеОбщиеРодителиСчетов = ИнвентаризацияСервер.ПолучитьТекстЗапросаБлижайшиеОбщиеРодителиСчетов(
			"А_ОстаткиСОтборомПоМОЛ",
			"Номенклатура",
			,
			СдвигИндексаТаблицыМатериалы);
		Иначе
			ТекстЗапросаБлижайшиеОбщиеРодителиСчетов = "";

			СдвигИндексаТаблицыМатериалы = 0;
		КонецЕсли;

		//------------------------------------------------------------------------------
		//	Вариант А.
		//	Заполнение сумм по балансовой стоимости
		//------------------------------------------------------------------------------
		ТекстЗапроса = ТекстЗапроса + "
		|////////////////////////////////////////////////////////////////////////////////
		|// [3А]. Все остатки по полным счетам, без отбора по МОЛ.
		|//		Т.к. отбор по МОЛ не применяется, то в остатках будет заполнена сумма,
		|//		вне зависимости от ведения суммового учета по субконто ЦМО или Контрагенты
		|//
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	Остатки.Счет																			КАК Счет,
		|	Остатки.КФО																				КАК КФО,
		|	ЕСТЬNULL(Остатки.КПС,ЗНАЧЕНИЕ(Справочник.КлассификационныеПризнакиСчетов.ПустаяСсылка))	КАК КПС,
		|	ВЫБОР
		|		КОГДА &ИспользоватьУчетПоИФО ТОГДА
		|			Остатки.ИФО
		|		ИНАЧЕ
		|			ЗНАЧЕНИЕ(Справочник.ИсточникиФинансовогоОбеспечения.ПустаяСсылка)
		|	КОНЕЦ																					КАК ИФО,
		|	ВЫБОР
		|		КОГДА ТИПЗНАЧЕНИЯ(Остатки.Субконто1) = ТИП(Справочник.Номенклатура) ТОГДА
		|			ВЫРАЗИТЬ(Остатки.Субконто1	КАК Справочник.Номенклатура)
		|		КОГДА ТИПЗНАЧЕНИЯ(Остатки.Субконто2) = ТИП(Справочник.Номенклатура) ТОГДА
		|			ВЫРАЗИТЬ(Остатки.Субконто2	КАК Справочник.Номенклатура)
		|		КОГДА ТИПЗНАЧЕНИЯ(Остатки.Субконто3) = ТИП(Справочник.Номенклатура) ТОГДА
		|			ВЫРАЗИТЬ(Остатки.Субконто3	КАК Справочник.Номенклатура)
		|		ИНАЧЕ
		|			ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)
		|	КОНЕЦ																					КАК Номенклатура,
		|	ВЫБОР
		|		КОГДА &СледуетИспользоватьДополнительноеСубконто И ТаблицаРеальныхДополнительныхСубконто.НомерРеальногоДополнительногоСубконто = 1 ТОГДА
		|			Остатки.Субконто1
		|		КОГДА &СледуетИспользоватьДополнительноеСубконто И ТаблицаРеальныхДополнительныхСубконто.НомерРеальногоДополнительногоСубконто = 2 ТОГДА
		|			Остатки.Субконто2
		|		КОГДА &СледуетИспользоватьДополнительноеСубконто И ТаблицаРеальныхДополнительныхСубконто.НомерРеальногоДополнительногоСубконто = 3 ТОГДА
		|			Остатки.Субконто3
		|		ИНАЧЕ
		|			НЕОПРЕДЕЛЕНО
		|	КОНЕЦ																					КАК Субконто3,
		|	СУММА(Остатки.КоличествоРазвернутыйОстатокДт - Остатки.КоличествоРазвернутыйОстатокКт)	КАК КоличествоУчет,
		|	СУММА(Остатки.СуммаРазвернутыйОстатокДт - Остатки.СуммаРазвернутыйОстатокКт)			КАК СуммаУчет
		|ПОМЕСТИТЬ
		|	А_ОстаткиБезОтбораПоМОЛ
		|ИЗ
		|	РегистрБухгалтерии.ЖурналПроводокЕПСБУ.Остатки(
		|		&ДатаЗапроса,				// 1 Период
		|		Счет В (&СчетаОстатков),	// 2 Условие счета
		|		,							// 3 Виды субконто
		|		Организация = &Организация	// 4 Условие
		|		И ВЫБОР 
		|			КОГДА &НакладыватьОтборПоИФО ТОГДА
		|				ИФО = &ИФО
		|			ИНАЧЕ
		|				ИСТИНА
		|		КОНЕЦ
		|		И ВЫБОР 
		|			КОГДА &НакладыватьОтборПоКФО ТОГДА
		|				КФО = &КФО
		|			ИНАЧЕ
		|				ИСТИНА
		|		КОНЕЦ
		|		И ВЫБОР
		|			КОГДА &НакладыватьОтборПоКПС ТОГДА
		|				КПС = &КПС
		|			ИНАЧЕ
		|				ИСТИНА
		|		КОНЕЦ
		|		И ВЫБОР 
		|			КОГДА &НакладыватьОтборПоПодразделению ТОГДА
		|				Подразделение = &Подразделение
		|			ИНАЧЕ
		|				ИСТИНА
		|		КОНЕЦ
		|	)	КАК Остатки
		|	ЛЕВОЕ СОЕДИНЕНИЕ	ТаблицаРеальныхДополнительныхСубконто
		|		ПО	Остатки.Счет = ТаблицаРеальныхДополнительныхСубконто.Счет
		|ГДЕ
		|	// Только заполненная номенклатура
		|	ВЫБОР
		|		КОГДА ТИПЗНАЧЕНИЯ(Остатки.Субконто1) = ТИП(Справочник.Номенклатура) ТОГДА
		|			НЕ Остатки.Субконто1 = &ПустаяНоменклатура
		|		КОГДА ТИПЗНАЧЕНИЯ(Остатки.Субконто2) = ТИП(Справочник.Номенклатура) ТОГДА
		|			НЕ Остатки.Субконто2 = &ПустаяНоменклатура
		|		КОГДА ТИПЗНАЧЕНИЯ(Остатки.Субконто3) = ТИП(Справочник.Номенклатура) ТОГДА
		|			НЕ Остатки.Субконто3 = &ПустаяНоменклатура
		|		ИНАЧЕ
		|			ЛОЖЬ
		|	КОНЕЦ
		|	// Отбор по дополнительному субконто
		|	И ВЫБОР
		|		КОГДА &НакладыватьОтборПоДополнительномуСубконто И ТаблицаРеальныхДополнительныхСубконто.НомерРеальногоДополнительногоСубконто = 1 ТОГДА
		|			Остатки.Субконто1 = &ЗначениеДляОтбораПоДополнительномуСубконто
		|		КОГДА &НакладыватьОтборПоДополнительномуСубконто И ТаблицаРеальныхДополнительныхСубконто.НомерРеальногоДополнительногоСубконто = 2 ТОГДА
		|			Остатки.Субконто2 = &ЗначениеДляОтбораПоДополнительномуСубконто
		|		КОГДА &НакладыватьОтборПоДополнительномуСубконто И ТаблицаРеальныхДополнительныхСубконто.НомерРеальногоДополнительногоСубконто = 3 ТОГДА
		|			Остатки.Субконто3 = &ЗначениеДляОтбораПоДополнительномуСубконто
		|		КОГДА &НакладыватьОтборПоДополнительномуСубконто И ТаблицаРеальныхДополнительныхСубконто.НомерРеальногоДополнительногоСубконто = 0 ТОГДА
		|			ЛОЖЬ
		|		ИНАЧЕ
		|			ИСТИНА
		|	КОНЕЦ
		|СГРУППИРОВАТЬ ПО
		|	Остатки.Счет,
		|	Остатки.КФО,
		|	ЕСТЬNULL(Остатки.КПС,ЗНАЧЕНИЕ(Справочник.КлассификационныеПризнакиСчетов.ПустаяСсылка)),
		|	ВЫБОР
		|		КОГДА &ИспользоватьУчетПоИФО ТОГДА
		|			Остатки.ИФО
		|		ИНАЧЕ
		|			ЗНАЧЕНИЕ(Справочник.ИсточникиФинансовогоОбеспечения.ПустаяСсылка)
		|	КОНЕЦ,
		|	ВЫБОР
		|		КОГДА ТИПЗНАЧЕНИЯ(Остатки.Субконто1) = ТИП(Справочник.Номенклатура) ТОГДА
		|			ВЫРАЗИТЬ(Остатки.Субконто1	КАК Справочник.Номенклатура)
		|		КОГДА ТИПЗНАЧЕНИЯ(Остатки.Субконто2) = ТИП(Справочник.Номенклатура) ТОГДА
		|			ВЫРАЗИТЬ(Остатки.Субконто2	КАК Справочник.Номенклатура)
		|		КОГДА ТИПЗНАЧЕНИЯ(Остатки.Субконто3) = ТИП(Справочник.Номенклатура) ТОГДА
		|			ВЫРАЗИТЬ(Остатки.Субконто3	КАК Справочник.Номенклатура)
		|		ИНАЧЕ
		|			ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)
		|	КОНЕЦ,
		|	ВЫБОР
		|		КОГДА &СледуетИспользоватьДополнительноеСубконто И ТаблицаРеальныхДополнительныхСубконто.НомерРеальногоДополнительногоСубконто = 1 ТОГДА
		|			Остатки.Субконто1
		|		КОГДА &СледуетИспользоватьДополнительноеСубконто И ТаблицаРеальныхДополнительныхСубконто.НомерРеальногоДополнительногоСубконто = 2 ТОГДА
		|			Остатки.Субконто2
		|		КОГДА &СледуетИспользоватьДополнительноеСубконто И ТаблицаРеальныхДополнительныхСубконто.НомерРеальногоДополнительногоСубконто = 3 ТОГДА
		|			Остатки.Субконто3
		|		ИНАЧЕ
		|			НЕОПРЕДЕЛЕНО
		|	КОНЕЦ

		|" + ?(ВыбиратьНулевоеКоличество, "", "
		|ИМЕЮЩИЕ
		|	СУММА(Остатки.КоличествоРазвернутыйОстатокДт - Остатки.КоличествоРазвернутыйОстатокКт) <> 0") + "

		|ИНДЕКСИРОВАТЬ ПО
		|	Счет,
		|	КФО,
		|	КПС,
		|	ИФО,
		|	Субконто3,
		|	Номенклатура
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|// [4А]. Суммы остатков номенклатуры (балансовая стоимость) по выбранному МОЛ
		|//
		|ВЫБРАТЬ
		|	ОстаткиБезОтбораПоМОЛ.Счет					КАК Счет,
		|	ОстаткиБезОтбораПоМОЛ.КФО					КАК КФО,
		|	ОстаткиБезОтбораПоМОЛ.КПС					КАК КПС,
		|	ОстаткиБезОтбораПоМОЛ.ИФО					КАК ИФО,
		|	ОстаткиБезОтбораПоМОЛ.Субконто3				КАК Субконто3,
		|	ОстаткиБезОтбораПоМОЛ.Номенклатура			КАК Номенклатура,
		|	ОстаткиСОтборомПоМОЛ.МОЛ					КАК МОЛ,
		|	СУММА(ОстаткиСОтборомПоМОЛ.КоличествоУчет)	КАК КоличествоУчет,
		|	
		|	// Балансовая стоимость
		|	СУММА(ВЫБОР
		|		КОГДА ОстаткиСОтборомПоМОЛ.СуммаУчет <> 0 ТОГДА
		|			ОстаткиСОтборомПоМОЛ.СуммаУчет
		|		КОГДА ОстаткиБезОтбораПоМОЛ.КоличествоУчет = 0 ТОГДА
		|			0
		|		ИНАЧЕ
		|			ОстаткиБезОтбораПоМОЛ.СуммаУчет * ОстаткиСОтборомПоМОЛ.КоличествоУчет / ОстаткиБезОтбораПоМОЛ.КоличествоУчет
		|	КОНЕЦ)										КАК СуммаУчетБалансовая
		|ПОМЕСТИТЬ
		|	А_ОстаткиСОтборомПоМОЛ
		|ИЗ
		|	А_ОстаткиБезОтбораПоМОЛ	КАК ОстаткиБезОтбораПоМОЛ
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ	ОстаткиСОтборомПоМОЛ	КАК ОстаткиСОтборомПоМОЛ
		|		ПО	ОстаткиБезОтбораПоМОЛ.Счет			= ОстаткиСОтборомПоМОЛ.Счет
		|		И	ОстаткиБезОтбораПоМОЛ.КФО			= ОстаткиСОтборомПоМОЛ.КФО
		|		И	ОстаткиБезОтбораПоМОЛ.КПС			= ОстаткиСОтборомПоМОЛ.КПС
		|		И	ОстаткиБезОтбораПоМОЛ.ИФО			= ОстаткиСОтборомПоМОЛ.ИФО
		|		И	ОстаткиБезОтбораПоМОЛ.Субконто3		= ОстаткиСОтборомПоМОЛ.Субконто3
		|		И	ОстаткиБезОтбораПоМОЛ.Номенклатура	= ОстаткиСОтборомПоМОЛ.Номенклатура
		|СГРУППИРОВАТЬ ПО
		|	ОстаткиБезОтбораПоМОЛ.Счет,
		|	ОстаткиБезОтбораПоМОЛ.КФО,
		|	ОстаткиБезОтбораПоМОЛ.КПС,
		|	ОстаткиБезОтбораПоМОЛ.ИФО,
		|	ОстаткиБезОтбораПоМОЛ.Субконто3,
		|	ОстаткиБезОтбораПоМОЛ.Номенклатура,
		|	ОстаткиСОтборомПоМОЛ.МОЛ
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|// [5А]. Данные для заполнения таблицы Остатки (балансовая стоимость)
		|//		(Финальная таблица)
		|//
		|ВЫБРАТЬ
		|	Счет						КАК Счет,
		|	ИФО							КАК ИФО,
		|	КФО							КАК КФО,
		|	КПС							КАК КПС,
		|	МОЛ							КАК ЦМО,
		|	Субконто3					КАК Субконто3,
		|	Номенклатура				КАК Номенклатура,
		|	СУММА(КоличествоУчет)		КАК КоличествоУчет,
		|	СУММА(СуммаУчетБалансовая)	КАК СуммаУчет
		|ИЗ
		|	А_ОстаткиСОтборомПоМОЛ
		|СГРУППИРОВАТЬ ПО
		|	Счет,
		|	ИФО,
		|	КФО,
		|	КПС,
		|	МОЛ,
		|	Субконто3,
		|	Номенклатура
		|УПОРЯДОЧИТЬ ПО
		|	СуммаУчет УБЫВ
		|;
		|"

		+ ТекстЗапросаБлижайшиеОбщиеРодителиСчетов

		+ "
		|////////////////////////////////////////////////////////////////////////////////
		|// [6А]. Данные для заполнения таблицы Материалы (балансовая стоимость)
		|//		(Финальная таблица)
		|//
		|ВЫБРАТЬ
		|	Субконто3									КАК Субконто3, 
		#Вставка     
		|	МОЛ											КАК ЦМО,
		#КонецВставки
		|	Номенклатура								КАК Номенклатура,
		|	КоличествоУчет								КАК КоличествоУчет,
		|	ВЫБОР
		|		КОГДА &НеЗаполнятьЦену ТОГДА
		|			0
		|		КОГДА КоличествоУчет = 0 ТОГДА
		|			0
		|		ИНАЧЕ
		|			СуммаУчетБалансовая / КоличествоУчет
		|	КОНЕЦ										КАК Цена,
		|	СуммаУчетБалансовая							КАК СуммаУчет,
		|		
		|	Счет										КАК Счет,
		|	КПС											КАК КПС,
		|	КФО											КАК КФО
		|ИЗ
		|	("

		+ ПолучитьТекстЗапросаСвернутыеОстаткиДляТаблицыМатериалы(
		ВариантФормированияОписи,
		ИспользоватьБалансовуюСтоимость)

		+ ")	КАК СвернутыеОстатки
		|УПОРЯДОЧИТЬ ПО
		|	Номенклатура.Наименование
		|;";

		// [5А]. Данные для заполнения таблицы Остатки (балансовая стоимость)
		ИндексТаблицыОстатки = 5;

		// [6А]. Данные для заполнения таблицы Материалы (балансовая стоимость)
		ИндексТаблицыМатериалы = 6 + СдвигИндексаТаблицыМатериалы;

	Иначе
		//------------------------------------------------------------------------------
		//	Вариант Б.
		//	Заполнение сумм по указанному типу цен номенклатуры
		//------------------------------------------------------------------------------

		Если ВариантФормированияОписи = 3 Тогда
			// 3. Опись с 2018 г. Счет в таблице (5 знаков). Одна строка - один объект

			ТекстЗапросаБлижайшиеОбщиеРодителиСчетов = ИнвентаризацияСервер.ПолучитьТекстЗапросаБлижайшиеОбщиеРодителиСчетов(
			"ОстаткиСОтборомПоМОЛ",
			"Номенклатура",
			,
			СдвигИндексаТаблицыМатериалы);
		Иначе
			ТекстЗапросаБлижайшиеОбщиеРодителиСчетов = "";

			СдвигИндексаТаблицыМатериалы = 0;
		КонецЕсли;

		ТекстЗапроса = ТекстЗапроса + "
		|////////////////////////////////////////////////////////////////////////////////
		|// [3Б]. Установленные цены номенклатуры
		|//
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	Номенклатура	КАК Номенклатура,
		|	Цена			КАК Цена
		|ПОМЕСТИТЬ
		|	Б_ЦеныНоменклатуры
		|ИЗ
		|	РегистрСведений.ЦеныНоменклатуры.СрезПоследних(
		|		&ДатаЗапроса,
		|		Организация = &Организация
		|		И	ТипЦен = &ТипЦен
		|		И	Номенклатура В
		|			(ВЫБРАТЬ
		|				Номенклатура
		|			ИЗ
		|				ОстаткиСОтборомПоМОЛ)
		|	)	КАК ЦеныНоменклатурыСрезПоследних
		|ИНДЕКСИРОВАТЬ ПО
		|	Номенклатура
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|// [4Б]. Данные для заполнения таблицы Остатки (указанный тип цен)
		|//		(Финальная таблица)
		|//
		|ВЫБРАТЬ
		|	Остатки.Счет								КАК Счет,
		|	Остатки.ИФО									КАК ИФО,
		|	Остатки.КФО									КАК КФО,
		|	Остатки.КПС									КАК КПС,
		|	Остатки.МОЛ									КАК ЦМО,
		|	Остатки.Субконто3							КАК Субконто3,
		|	Остатки.Номенклатура						КАК Номенклатура,
		|	СУММА(Остатки.КоличествоУчет)				КАК КоличествоУчет,
		|	СУММА(ВЫБОР
		|		КОГДА Остатки.КоличествоУчет = 0 И &ЗаменятьНулевоеКоличество ТОГДА
		|			ЕСТЬNULL(Цены.Цена, 0) * &КоличествоПоУмолчанию
		|		ИНАЧЕ
		|			ЕСТЬNULL(Цены.Цена, 0) * Остатки.КоличествоУчет
		|	КОНЕЦ)										КАК СуммаУчет
		|ИЗ
		|	ОстаткиСОтборомПоМОЛ	КАК Остатки
		|	ЛЕВОЕ СОЕДИНЕНИЕ	Б_ЦеныНоменклатуры	КАК Цены
		|		ПО	Остатки.Номенклатура = Цены.Номенклатура
		|СГРУППИРОВАТЬ ПО
		|	Остатки.Счет,
		|	Остатки.ИФО,
		|	Остатки.КФО,
		|	Остатки.КПС,
		|	Остатки.МОЛ,
		|	Остатки.Субконто3,
		|	Остатки.Номенклатура
		|УПОРЯДОЧИТЬ ПО
		|	Остатки.Номенклатура.Наименование
		|;
		|"

		+ ТекстЗапросаБлижайшиеОбщиеРодителиСчетов

		+ "
		|////////////////////////////////////////////////////////////////////////////////
		|// [5Б]. Данные для заполнения таблицы Материалы (указанный тип цен)
		|//		(Финальная таблица)
		|//
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	Субконто3									КАК Субконто3,
		|	Номенклатура								КАК Номенклатура,
		|	КоличествоУчет								КАК КоличествоУчет,
		|	ВЫБОР
		|		КОГДА &НеЗаполнятьЦену ТОГДА
		|			0
		|		ИНАЧЕ
		|			Цена
		|	КОНЕЦ										КАК Цена,
		|	СуммаУчет									КАК СуммаУчет,
		|	
		|	Счет										КАК Счет,
		|	КПС											КАК КПС,
		|	КФО											КАК КФО
		|ИЗ
		|	("

		+ ПолучитьТекстЗапросаСвернутыеОстаткиДляТаблицыМатериалы(
		ВариантФормированияОписи,
		ИспользоватьБалансовуюСтоимость)

		+ ")	КАК СвернутыеОстаткиПОВыбраннымЦМО
		|УПОРЯДОЧИТЬ ПО
		|	Номенклатура.Наименование
		|;";

		// [4Б]. Данные для заполнения таблицы Остатки (указанный тип цен)
		ИндексТаблицыОстатки = 4;

		// [5Б]. Данные для заполнения таблицы Материалы (указанный тип цен)
		ИндексТаблицыМатериалы = 5 + СдвигИндексаТаблицыМатериалы;

	КонецЕсли;

	ЗапросОстатки.Текст = ТекстЗапроса;

	Результаты = ЗапросОстатки.ВыполнитьПакет();

	// [2]. Данные для заполнения таблицы ПорядокСписания
	ДокументОбъект["ПорядокСписания"].Загрузить(Результаты[2].Выгрузить());

	// [Х1]. Данные для заполнения таблицы Материалы
	ТаблицаМатериалы = Результаты[ИндексТаблицыМатериалы].Выгрузить();

	ИменаКолонок = Неопределено;
	ИнвентаризацияСервер.Редактирование0504087_СоздатьКолонкиЧисловыхПоказателей(ТаблицаМатериалы, ДокументОбъект.Ссылка, ИменаКолонок);

	Если ВыбиратьНулевоеКоличество И ЗаменятьНулевоеКоличество Тогда
		ИнвентаризацияСервер.УстановитьКоличествоПоУмолчанию(
		ТаблицаМатериалы,
		ИменаКолонок,
		ИспользоватьБалансовуюСтоимость,
		ДокументОбъект["НеЗаполнятьЦену"],
		Истина);
	КонецЕсли;

	ИнвентаризацияСервер.Редактирование0504087_РассчитатьРасхождениеВТаблице(ТаблицаМатериалы, ИменаКолонок);

	ДокументОбъект["Материалы"].Загрузить(ТаблицаМатериалы);

	// [Х2]. Данные для заполнения таблицы Остатки
	ТаблицаОстатки = Результаты[ИндексТаблицыОстатки].Выгрузить();

	Если ВыбиратьНулевоеКоличество И ЗаменятьНулевоеКоличество Тогда
		ИнвентаризацияСервер.УстановитьКоличествоПоУмолчанию(
		ТаблицаОстатки,
		ИменаКолонок);
	КонецЕсли;

	ДокументОбъект["Остатки"].Загрузить(ТаблицаОстатки);

КонецПроцедуры

&ИзменениеИКонтроль("ПолучитьТекстЗапросаСвернутыеОстаткиДляТаблицыМатериалы")
Функция АБК_ПолучитьТекстЗапросаСвернутыеОстаткиДляТаблицыМатериалы(ВариантФормированияОписи, ИспользоватьБалансовуюСтоимость)

	// Заполненность полей счета определяется значением реквизита ВариантФормированияОписи

	Если ИспользоватьБалансовуюСтоимость Тогда
		// Балансовая стоимость

		Если ВариантФормированияОписи = 0 Тогда
			// Балансовая стоимость
			// 0. Опись до 2018 г. Счет в шапке
			// Выбираем: ничего

			Возврат "
			|ВЫБРАТЬ
			|	Субконто3					КАК Субконто3,
			|	Номенклатура				КАК Номенклатура,
			|	НЕОПРЕДЕЛЕНО				КАК Счет,
			|	НЕОПРЕДЕЛЕНО				КАК КПС,
			|	НЕОПРЕДЕЛЕНО				КАК КФО,
			|	СУММА(КоличествоУчет)		КАК КоличествоУчет,
			|	СУММА(СуммаУчетБалансовая)	КАК СуммаУчетБалансовая
			|ИЗ
			|	А_ОстаткиСОтборомПоМОЛ
			|СГРУППИРОВАТЬ ПО
			|	Субконто3,
			|	Номенклатура";

		ИначеЕсли ВариантФормированияОписи = 1 Тогда
			// Балансовая стоимость
			// 1. Опись с 2018 г. Счет в таблице (26 знаков)
			// Выбираем: Счет, КПС, КФО

			Возврат "
			|ВЫБРАТЬ
			|	Субконто3					КАК Субконто3,
			#Вставка     
			|	МОЛ							КАК МОЛ,
			#КонецВставки
			|	Номенклатура				КАК Номенклатура,
			|	Счет						КАК Счет,
			|	КПС							КАК КПС,
			|	КФО							КАК КФО,
			|	СУММА(КоличествоУчет)		КАК КоличествоУчет,
			|	СУММА(СуммаУчетБалансовая)	КАК СуммаУчетБалансовая
			|ИЗ
			|	А_ОстаткиСОтборомПоМОЛ
			|СГРУППИРОВАТЬ ПО
			|	Субконто3,
			#Вставка     
			|	МОЛ,
			#КонецВставки
			|	Номенклатура,
			|	Счет,
			|	КПС,
			|	КФО";

		ИначеЕсли ВариантФормированияОписи = 2 Тогда
			// Балансовая стоимость
			// 2. Опись с 2018 г. Счет в таблице (5 знаков)
			// Выбираем: Счет

			Возврат "
			|ВЫБРАТЬ
			|	Субконто3					КАК Субконто3,
			|	Номенклатура				КАК Номенклатура,
			|	Счет						КАК Счет,
			|	НЕОПРЕДЕЛЕНО				КАК КПС,
			|	НЕОПРЕДЕЛЕНО				КАК КФО,
			|	СУММА(КоличествоУчет)		КАК КоличествоУчет,
			|	СУММА(СуммаУчетБалансовая)	КАК СуммаУчетБалансовая
			|ИЗ
			|	А_ОстаткиСОтборомПоМОЛ
			|СГРУППИРОВАТЬ ПО
			|	Субконто3,
			|	Номенклатура,
			|	Счет";

		Иначе
			// Балансовая стоимость
			// 3. Опись с 2018 г. Счет в таблице (5 знаков). Одна строка - один объект
			// Выбираем: Счет

			Возврат "
			|ВЫБРАТЬ
			|	Субконто3							КАК Субконто3,
			|	А_ОстаткиСОтборомПоМОЛ.Номенклатура	КАК Номенклатура,
			|	БлижайшиеОбщиеРодителиСчетов.Счет	КАК Счет,
			|	НЕОПРЕДЕЛЕНО						КАК КПС,
			|	НЕОПРЕДЕЛЕНО						КАК КФО,
			|	СУММА(КоличествоУчет)				КАК КоличествоУчет,
			|	СУММА(СуммаУчетБалансовая)			КАК СуммаУчетБалансовая
			|ИЗ
			|	А_ОстаткиСОтборомПоМОЛ
			|	ЛЕВОЕ СОЕДИНЕНИЕ БлижайшиеОбщиеРодителиСчетов
			|		ПО	А_ОстаткиСОтборомПоМОЛ.Номенклатура = БлижайшиеОбщиеРодителиСчетов.НФА
			|СГРУППИРОВАТЬ ПО
			|	Субконто3,
			|	А_ОстаткиСОтборомПоМОЛ.Номенклатура,
			|	БлижайшиеОбщиеРодителиСчетов.Счет";

		КонецЕсли;

	Иначе
		// Указанный тип цен

		Если ВариантФормированияОписи = 0 Тогда
			// Указанный тип цен
			// 0. Опись до 2018 г. Счет в шапке
			// Выбираем: ничего

			Возврат "
			|ВЫБРАТЬ
			|	Остатки.Субконто3									КАК Субконто3,
			|	Остатки.Номенклатура								КАК Номенклатура,
			|	НЕОПРЕДЕЛЕНО										КАК Счет,
			|	НЕОПРЕДЕЛЕНО										КАК КПС,
			|	НЕОПРЕДЕЛЕНО										КАК КФО,
			|	СУММА(Остатки.КоличествоУчет)						КАК КоличествоУчет,
			|	ЕСТЬNULL(СРЕДНЕЕ(Цены.Цена), 0)						КАК Цена,
			|	СУММА(Остатки.КоличествоУчет) * ЕСТЬNULL(СРЕДНЕЕ(Цены.Цена),0)	КАК СуммаУчет
			|ИЗ
			|	ОстаткиСОтборомПоМОЛ	КАК Остатки
			|	ЛЕВОЕ СОЕДИНЕНИЕ	Б_ЦеныНоменклатуры	КАК Цены
			|		ПО	Остатки.Номенклатура = Цены.Номенклатура
			|СГРУППИРОВАТЬ ПО
			|	Остатки.Субконто3,
			|	Остатки.Номенклатура";

		ИначеЕсли ВариантФормированияОписи = 1 Тогда
			// Указанный тип цен
			// 1. Опись с 2018 г. Счет в таблице (26 знаков)
			// Выбираем: Счет, КПС, КФО

			Возврат "
			|ВЫБРАТЬ
			|	Остатки.Субконто3									КАК Субконто3,
			|	Остатки.Номенклатура								КАК Номенклатура,
			|	Остатки.Счет										КАК Счет,
			|	Остатки.КПС											КАК КПС,
			|	Остатки.КФО											КАК КФО,
			|	СУММА(Остатки.КоличествоУчет)						КАК КоличествоУчет,
			|	ЕСТЬNULL(СРЕДНЕЕ(Цены.Цена), 0)						КАК Цена,
			|	СУММА(Остатки.КоличествоУчет) * ЕСТЬNULL(СРЕДНЕЕ(Цены.Цена),0)	КАК СуммаУчет
			|ИЗ
			|	ОстаткиСОтборомПоМОЛ	КАК Остатки
			|	ЛЕВОЕ СОЕДИНЕНИЕ	Б_ЦеныНоменклатуры	КАК Цены
			|		ПО	Остатки.Номенклатура = Цены.Номенклатура
			|СГРУППИРОВАТЬ ПО
			|	Остатки.Субконто3,
			|	Остатки.Номенклатура,
			|	Остатки.Счет,
			|	Остатки.КПС,
			|	Остатки.КФО";

		ИначеЕсли ВариантФормированияОписи = 2 Тогда
			// Указанный тип цен
			// 2. Опись с 2018 г. Счет в таблице (5 знаков)
			// Выбираем: Счет

			Возврат "
			|ВЫБРАТЬ
			|	Остатки.Субконто3									КАК Субконто3,
			|	Остатки.Номенклатура								КАК Номенклатура,
			|	Остатки.Счет										КАК Счет,
			|	НЕОПРЕДЕЛЕНО										КАК КПС,
			|	НЕОПРЕДЕЛЕНО										КАК КФО,
			|	СУММА(Остатки.КоличествоУчет)						КАК КоличествоУчет,
			|	ЕСТЬNULL(СРЕДНЕЕ(Цены.Цена), 0)						КАК Цена,
			|	СУММА(Остатки.КоличествоУчет) * ЕСТЬNULL(СРЕДНЕЕ(Цены.Цена),0)	КАК СуммаУчет
			|ИЗ
			|	ОстаткиСОтборомПоМОЛ	КАК Остатки
			|	ЛЕВОЕ СОЕДИНЕНИЕ	Б_ЦеныНоменклатуры	КАК Цены
			|		ПО	Остатки.Номенклатура = Цены.Номенклатура
			|СГРУППИРОВАТЬ ПО
			|	Остатки.Субконто3,
			|	Остатки.Номенклатура,
			|	Остатки.Счет";

		Иначе
			// Указанный тип цен
			// 3. Опись с 2018 г. Счет в таблице (5 знаков). Одна строка - один объект
			// Выбираем: Счет

			Возврат "
			|ВЫБРАТЬ
			|	Остатки.Субконто3									КАК Субконто3,
			|	Остатки.Номенклатура								КАК Номенклатура,
			|	БлижайшиеОбщиеРодителиСчетов.Счет					КАК Счет,
			|	НЕОПРЕДЕЛЕНО										КАК КПС,
			|	НЕОПРЕДЕЛЕНО										КАК КФО,
			|	СУММА(Остатки.КоличествоУчет)						КАК КоличествоУчет,
			|	ЕСТЬNULL(СРЕДНЕЕ(Цены.Цена), 0)						КАК Цена,
			|	СУММА(Остатки.КоличествоУчет) * ЕСТЬNULL(СРЕДНЕЕ(Цены.Цена),0)	КАК СуммаУчет
			|ИЗ
			|	ОстаткиСОтборомПоМОЛ	КАК Остатки
			|	ЛЕВОЕ СОЕДИНЕНИЕ	Б_ЦеныНоменклатуры	КАК Цены
			|		ПО	Остатки.Номенклатура = Цены.Номенклатура
			|	ЛЕВОЕ СОЕДИНЕНИЕ БлижайшиеОбщиеРодителиСчетов
			|		ПО	Остатки.Номенклатура = БлижайшиеОбщиеРодителиСчетов.НФА
			|СГРУППИРОВАТЬ ПО
			|	Остатки.Субконто3,
			|	Остатки.Номенклатура,
			|	БлижайшиеОбщиеРодителиСчетов.Счет";

		КонецЕсли;

	КонецЕсли;

КонецФункции

&ИзменениеИКонтроль("Печать0504087_194н_ПолучитьВыборкиДляПечати")
Функция АБК_Печать0504087_194н_ПолучитьВыборкиДляПечати(МассивОбъектов)

	ЗапросПечать = Новый Запрос;
	ЗапросПечать.УстановитьПараметр("МассивСсылок", МассивОбъектов);
	ЗапросПечать.Текст = "
	|////////////////////////////////////////////////////////////////////////////////
	|// [0]. Шапки документов
	|//
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	Ссылка							КАК ДокументСсылка,
	|	Номер							КАК Номер,
	|	Дата 							КАК Дата,
	|	Организация						КАК Организация,
	|	Организация.НаименованиеПолное	КАК НаименованиеОрганизации,
	|	Подразделение.Наименование		КАК НаименованиеПодразделения,
	|	Организация.КодОКПО				КАК КодОКПО,
	|	ВЫБОР
	|		КОГДА ТИПЗНАЧЕНИЯ(ЦМО) = ТИП(Справочник.ЦМО) ТОГДА
	|			ЦМО.Сотрудник
	|		ИНАЧЕ
	|			ЦМО
	|	КОНЕЦ							КАК МОЛ,
	|	Счет.Забалансовый				КАК СчетЗабалансовый,
	|
	|	НомерПриказаИнвентаризации		КАК ДокументОснованиеНомер,
	|	ДатаПриказаИнвентаризации		КАК ДокументОснованиеДата,
	|	МестоИнвентаризации				КАК МестоИнвентаризации,
	|	ДатаНачала						КАК ДатаНачалаИнвентаризации,
	|	ДатаОкончания					КАК ДатаОкончанияИнвентаризации,
	|	ДатаИнвентаризации				КАК ДатаИнвентаризации,
	|
	|	ДолжностьПредседателяКомиссии	КАК ДолжностьПредседателяКомиссии,
	|	ФамилияПредседателяКомиссии		КАК ФамилияПредседателяКомиссии,
	|	ИмяПредседателяКомиссии			КАК ИмяПредседателяКомиссии,
	|	ОтчествоПредседателяКомиссии	КАК ОтчествоПредседателяКомиссии,
	|	ОбъяснениеПричинРасхождений		КАК ОбъяснениеПричинРасхождений,
	|	ЗаключениеКомиссии				КАК ЗаключениеКомиссии,
	|	ВариантФормированияОписи		КАК ВариантФормированияОписи,
	|	ДатаПодписиМОЛ					КАК ДатаПодписиМОЛ,
	|	ДатаПодписиКомиссии				КАК ДатаПодписиКомиссии
	|ИЗ
	|	Документ.ИнвентаризацияМЗ
	|ГДЕ
	|	Ссылка В (&МассивСсылок)
	|УПОРЯДОЧИТЬ ПО
	|	Дата,
	|	Номер
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|// [1]. Инвентаризационные комиссии
	|//
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	Ссылка					КАК ДокументСсылка,
	|	ДолжностьЧленаКомиссии	КАК ДолжностьЧленаКомиссии,
	|	ФамилияЧленаКомиссии	КАК ФамилияЧленаКомиссии,
	|	ИмяЧленаКомиссии		КАК ИмяЧленаКомиссии,
	|	ОтчествоЧленаКомиссии	КАК ОтчествоЧленаКомиссии
	|ИЗ
	|	Документ.ИнвентаризацияМЗ.СоставКомиссии
	|ГДЕ
	|	Ссылка В (&МассивСсылок)
	|ИТОГИ ПО
	|	// Обязательно первые итоги по ссылке на документ
	|	Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|// [2]. Таблицы НФА
	|//
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	Ссылка											КАК ДокументСсылка,
	|	НомерСтроки										КАК Ном,  
	#Вставка    
	|	ЦМО										КАК ЦМО,  
	#КонецВставки
	|	ЕСТЬNULL(ВЫБОР
	|		КОГДА Номенклатура.НаименованиеПолное <> """" ТОГДА
	|			Номенклатура.НаименованиеПолное
	|		ИНАЧЕ
	|			Номенклатура.Наименование
	|	КОНЕЦ, """")										КАК НаименованиеНФА,
	|		
	|	ЕСТЬNULL(Номенклатура.НоменклатурныйНомер, """")	КАК НоменклатурныйНомер,
	|	ЕСТЬNULL(Номенклатура.Код, """")					КАК КодЭлемента,
	|	ЛОЖЬ											КАК НФАЭтоОС,
	|	ПРЕДСТАВЛЕНИЕ(Субконто3)						КАК Субконто3Представление,
	|	ВЫБОР
	|		КОГДА ЕдИзм <> ЗНАЧЕНИЕ(Справочник.КлассификаторЕдиницИзмерения.ПустаяСсылка) ТОГДА
	|			ЕдИзм
	|		ИНАЧЕ
	|			ЕСТЬNULL(Номенклатура.ЕдиницаИзмерения.Представление, """")
	|	КОНЕЦ											КАК ЕдиницаИзмерения,
	|	Цена											КАК Цена,
	|	
	|	КоличествоУчет									КАК КоличествоБ,
	|	СуммаУчет										КАК СуммаБ,
	|	
	|	КоличествоФакт									КАК КоличествоФ,
	|	СуммаФакт										КАК СуммаФ,
	|	
	|	НедостачаКоличество								КАК КоличествоН,
	|	НедостачаСумма									КАК СуммаН,
	|	
	|	ИзлишекКоличество								КАК КоличествоИ,
	|	ИзлишекСумма									КАК СуммаИ,
	|	
	|	СтатусОбъекта									КАК СтатусОбъекта,
	|	ЦелеваяФункцияАктива							КАК ЦелеваяФункция,
	|	
	|	ПРЕДСТАВЛЕНИЕ(КФО)								КАК КФО,
	|	КПС.Представление								КАК КПС,
	|	Счет.Представление								КАК Счет,
	|	
	|	КоличествоНеСоответствует						КАК КоличествоНА,
	|	СуммаНеСоответствует							КАК СуммаНА,
	|	Примечание										КАК Примечание
	|ИЗ
	|	Документ.ИнвентаризацияМЗ.Материалы
	|ГДЕ
	|	Ссылка В (&МассивСсылок)
	|УПОРЯДОЧИТЬ ПО
	|	ДокументСсылка,
	|	Ном
	|ИТОГИ
	|	СУММА(СуммаБ)		КАК СуммаБ,
	|	СУММА(СуммаФ)		КАК СуммаФ,
	|	СУММА(СуммаН)		КАК СуммаН,
	|	СУММА(СуммаИ)		КАК СуммаИ,
	|	СУММА(СуммаНА)		КАК СуммаНА,
	|	СУММА(КоличествоБ)	КАК КоличествоБ,
	|	СУММА(КоличествоФ)	КАК КоличествоФ,
	|	СУММА(КоличествоН)	КАК КоличествоН,
	|	СУММА(КоличествоИ)	КАК КоличествоИ,
	|	СУММА(КоличествоНА)	КАК КоличествоНА
	|ПО
	|	// Обязательно первые итоги по ссылке на документ
	| 	ДокументСсылка
	|;";

	Результаты = ЗапросПечать.ВыполнитьПакет();

	ВыборкиДляПечати = Новый Структура();

	// [0]. Шапки документов	
	ВыборкиДляПечати.Вставить("Шапка", Результаты[0].Выбрать());

	// [1]. Инвентаризационные комиссии
	ВыборкиДляПечати.Вставить("ДокументКомиссия", Результаты[1].Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам));

	// [2]. Таблицы НФА
	ВыборкиДляПечати.Вставить("ДокументТаблицаНФА", Результаты[2].Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам));

	Возврат ВыборкиДляПечати;

КонецФункции

&ИзменениеИКонтроль("ПечатьФормыИНВ3")
Функция АБК_ПечатьФормыИНВ3(МассивСсылок, ОбъектыПечати, ВыводитьФактическоеНаличие)

	// Параметры печати
	ПараметрыПечати = Новый Структура();
	ПараметрыПечати.Вставить("РазбиватьНаСтраницы",				Истина);
	ПараметрыПечати.Вставить("ПовторятьШапкуТаблицы",			Истина);
	ПараметрыПечати.Вставить("ВыводитьФактическоеНаличие",		ВыводитьФактическоеНаличие);
	ПараметрыПечати.Вставить("ФорматнаяСтрокаКоличествоФакт",	?(ВыводитьФактическоеНаличие, "ЧДЦ=3; ЧН=-;", "ЧДЦ=3;"));
	ПараметрыПечати.Вставить("ФорматнаяСтрокаКоличествоУчет",	"ЧДЦ=3; ЧН=-;");
	ПараметрыПечати.Вставить("ФорматнаяСтрокаСуммаФакт",		?(ВыводитьФактическоеНаличие, "ЧДЦ=2; ЧН=-;", "ЧДЦ=2;"));
	ПараметрыПечати.Вставить("ФорматнаяСтрокаСуммаУчет",		"ЧДЦ=2; ЧН=-;");

	// Используемый макет
	Макет = УправлениеПечатью.МакетПечатнойФормы("ОбщийМакет.ПФ_MXL_ИнвентаризационнаяОписьТМЦ_ИНВ3");

	// Возвращаемый табличный документ
	ТабДокумент = Новый ТабличныйДокумент();
	ТабДокумент.ОриентацияСтраницы = ОриентацияСтраницы.Ландшафт;
	ТабДокумент.АвтоМасштаб = Истина;
	ТабДокумент.КлючПараметровПечати = "ИнвентаризацияМЗ_ПечатьФормыИНВ3";

	// Предопределенные группы счетов - одинаковые для всех организаций
	// Важно - должны отсутствовать дубли строк по полю ОсновнойСчетГруппы!
	ТаблицаПредопределенных = ПолучитьПустуюТаблицуНастроекГруппСчетов();
	ДобавитьНастройкиПредопределенныхГруппСчетов(ТаблицаПредопределенных);

	// Пользовательские группы счетов - могут различаться для каждой организации
	// Важно - должны отсутствовать дубли строк по полям Организация, ОсновнойСчетГруппы!
	ТаблицаПользовательских = ИНВ3_ПолучитьТаблицуПользовательскихГруппСчетов(МассивСсылок);

	ЗапросПечать = Новый Запрос;
	ЗапросПечать.УстановитьПараметр("МассивСсылок",					МассивСсылок);
	ЗапросПечать.УстановитьПараметр("ВыводитьФактическоеНаличие",	ВыводитьФактическоеНаличие);
	ЗапросПечать.УстановитьПараметр("ТаблицаПредопределенных",		ТаблицаПредопределенных);
	ЗапросПечать.УстановитьПараметр("ТаблицаПользовательских",		ТаблицаПользовательских);
	ЗапросПечать.Текст = "
	|////////////////////////////////////////////////////////////////////////////////
	|// [0]. Свойства предопределенных групп счетов, одинаковые для всех указанных организаций
	|//		Важно - должны отсутствовать дубли строк по полю ОсновнойСчетГруппы!
	|//
	|ВЫБРАТЬ
	|	ТЗ.ОсновнойСчетГруппы					КАК ОсновнойСчетГруппы,
	|	ТЗ.ИспользуетсяДополнительноеСубконто	КАК ИспользуетсяДополнительноеСубконто
	|ПОМЕСТИТЬ
	|	ТаблицаПредопределенных
	|ИЗ
	|	&ТаблицаПредопределенных	КАК ТЗ
	|ИНДЕКСИРОВАТЬ ПО
	|	ОсновнойСчетГруппы
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|// [1]. Свойства пользовательских групп счетов, различные для указанных организаций
	|//		Важно - должны отсутствовать дубли строк по полям Организация, ОсновнойСчетГруппы!
	|//
	|ВЫБРАТЬ
	|	ТЗ.Организация							КАК Организация,
	|	ТЗ.ОсновнойСчетГруппы					КАК ОсновнойСчетГруппы,
	|	ТЗ.ИспользуетсяДополнительноеСубконто	КАК ИспользуетсяДополнительноеСубконто
	|ПОМЕСТИТЬ
	|	ТаблицаПользовательских
	|ИЗ
	|	&ТаблицаПользовательских	КАК ТЗ
	|ИНДЕКСИРОВАТЬ ПО
	|	Организация,
	|	ОсновнойСчетГруппы
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|// [2]. Шапки документов
	|//		(подготовка)
	|//
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	Док.Ссылка											КАК ДокСсылка,
	|	Док.Номер											КАК Номер,
	|	Док.ДатаИнвентаризации								КАК Дата,
	|	Док.ДатаИнвентаризации								КАК ДатаИнвентаризации,
	|	Док.СчетГруппа										КАК СчетГруппа,
	|	Док.Счет											КАК СчетБУСсылка,
	|	Док.Счет.Представление								КАК СчетБУ,
	|	Док.Организация										КАК Организация,
	|	Док.Организация.НаименованиеПолное					КАК НаименованиеОрганизации,
	|	Док.Подразделение.Наименование						КАК НаименованиеПодразделения,
	|	Док.Организация.КодОКПО								КАК КодОКПО,
	|	ВЫБОР
	|		КОГДА ТИПЗНАЧЕНИЯ(Док.ЦМО) = ТИП(Справочник.ЦМО) ТОГДА
	|			Док.ЦМО.Сотрудник
	|		ИНАЧЕ
	|			// Тип=Сотрудник,Контрагент
	|			Док.ЦМО
	|	КОНЕЦ												КАК МОЛ,
	|	Док.КФО												КАК КФО,
	|	
	|	Док.НомерПриказаИнвентаризации						КАК ДокументОснованиеНомер,
	|	Док.ДатаПриказаИнвентаризации						КАК ДокументОснованиеДата,
	|	Док.МестоИнвентаризации								КАК МестоИнвентаризации,
	|	Док.ДатаНачала										КАК ДатаНачалаИнвентаризации,
	|	Док.ДатаОкончания									КАК ДатаОкончанияИнвентаризации,
	|	
	|	Док.ДолжностьПредседателяКомиссии					КАК ДолжностьПредседателяКомиссии,
	|	Док.ФамилияПредседателяКомиссии						КАК ФамилияПредседателяКомиссии,
	|	Док.ИмяПредседателяКомиссии							КАК ИмяПредседателяКомиссии,
	|	Док.ОтчествоПредседателяКомиссии					КАК ОтчествоПредседателяКомиссии,
	|	
	|	// Рассчитываемые поля
	|	ЕСТЬNULL(ТаблицаПользовательских.ИспользуетсяДополнительноеСубконто, 
	|		ЕСТЬNULL(ТаблицаПредопределенных.ИспользуетсяДополнительноеСубконто,
	|			ЛОЖЬ))										КАК ИспользуетсяДополнительноеСубконто
	|ПОМЕСТИТЬ
	|	ШапкиДокументов
	|ИЗ
	|	Документ.ИнвентаризацияМЗ	КАК Док
	|	ЛЕВОЕ СОЕДИНЕНИЕ	ТаблицаПредопределенных
	|		ПО	Док.СчетГруппа	= ТаблицаПредопределенных.ОсновнойСчетГруппы
	|	ЛЕВОЕ СОЕДИНЕНИЕ	ТаблицаПользовательских
	|		ПО	Док.Организация	= ТаблицаПользовательских.Организация
	|		И	Док.СчетГруппа	= ТаблицаПользовательских.ОсновнойСчетГруппы
	|ГДЕ
	|	Док.Ссылка В (&МассивСсылок)
	|ИНДЕКСИРОВАТЬ ПО
	|	ДокСсылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|// [3]. Табличные части инвентаризационных комиссий
	|//
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ТЧ.Ссылка					КАК ДокСсылка,
	|	ТЧ.ДолжностьЧленаКомиссии	КАК ДолжностьЧленаКомиссии,
	|	ТЧ.ФамилияЧленаКомиссии		КАК ФамилияЧленаКомиссии,
	|	ТЧ.ИмяЧленаКомиссии			КАК ИмяЧленаКомиссии,
	|	ТЧ.ОтчествоЧленаКомиссии	КАК ОтчествоЧленаКомиссии
	|ИЗ
	|	Документ.ИнвентаризацияМЗ.СоставКомиссии	КАК ТЧ
	|ГДЕ
	|	ТЧ.Ссылка В (&МассивСсылок)
	|ИТОГИ ПО
	|	// Обязательно первые итоги по ссылке на документ
	|	ТЧ.Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|// [4]. Табличные части Материалы
	|//
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	Материалы.Ссылка													КАК ДокСсылка,
	|	Материалы.НомерСтроки												КАК Ном,  
	#Вставка
	|	Материалы.ЦМО														КАК ЦМО,  
	#КонецВставки
	|	Материалы.Номенклатура												КАК Номенклатура,
	|	ВЫБОР
	|		КОГДА ШапкиДокументов.ИспользуетсяДополнительноеСубконто ТОГДА
	|			Материалы.Субконто3
	|		ИНАЧЕ
	|			НЕОПРЕДЕЛЕНО
	|	КОНЕЦ																КАК Субконто3,
	|	ЕСТЬNULL(ВЫБОР
	|		КОГДА Материалы.Номенклатура.НаименованиеПолное = """" ТОГДА
	|			Материалы.Номенклатура.Наименование
	|		ИНАЧЕ
	|			Материалы.Номенклатура.НаименованиеПолное
	|	КОНЕЦ, """")															КАК НаименованиеНФА,
	|	ЕСТЬNULL(ВЫБОР
	|		КОГДА Материалы.Номенклатура.НоменклатурныйНомер <> """" ТОГДА
	|			Материалы.Номенклатура.НоменклатурныйНомер
	|		ИНАЧЕ
	|			Материалы.Номенклатура.Код
	|	КОНЕЦ, """")															КАК НомНомер,
	|	ЕСТЬNULL(Материалы.Номенклатура.ЕдиницаИзмерения,
	|		ЗНАЧЕНИЕ(Справочник.КлассификаторЕдиницИзмерения.ПустаяСсылка))	КАК ЕдиницаИзмеренияСсылка,
	|	ЕСТЬNULL(Материалы.Номенклатура.ЕдиницаИзмерения.Наименование, """")	КАК ЕдиницаИзмерения,
	|	ЕСТЬNULL(Материалы.Номенклатура.ЕдиницаИзмерения.Код, """")			КАК КодЕдиницыИзмерения,
	|	Материалы.Цена														КАК Цена,
	|	
	|	// По учету
	|	Материалы.КоличествоУчет											КАК Количество,
	|	Материалы.СуммаУчет													КАК Сумма,
	|	
	|	// По факту
	|	ВЫБОР
	|		КОГДА &ВыводитьФактическоеНаличие ТОГДА
	|			ЕСТЬNULL(Материалы.КоличествоФакт, 0)
	|		ИНАЧЕ
	|			0
	|	КОНЕЦ																КАК КоличествоФ,
	|	ВЫБОР
	|		КОГДА &ВыводитьФактическоеНаличие ТОГДА
	|			ЕСТЬNULL(Материалы.СуммаФакт, 0)
	|		ИНАЧЕ
	|			0
	|	КОНЕЦ																КАК СуммаФ
	|ПОМЕСТИТЬ
	|	ТабличныеЧастиМатериалы
	|ИЗ
	|	Документ.ИнвентаризацияМЗ.Материалы	КАК Материалы
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ	ШапкиДокументов
	|		ПО	Материалы.Ссылка = ШапкиДокументов.ДокСсылка
	|ИНДЕКСИРОВАТЬ ПО
	|	ДокСсылка,
	|	Номенклатура,
	|	Субконто3
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|// [5]. Табличные части Остатки
	|//
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	Остатки.Ссылка					КАК ДокСсылка,
	|	Остатки.Номенклатура			КАК Номенклатура,
	|	ВЫБОР
	|		КОГДА ШапкиДокументов.ИспользуетсяДополнительноеСубконто ТОГДА
	|			Остатки.Субконто3
	|		ИНАЧЕ
	|			НЕОПРЕДЕЛЕНО
	|	КОНЕЦ							КАК Субконто3,
	|	Остатки.Счет					КАК Счет
	|ПОМЕСТИТЬ
	|	ТабличныеЧастиОстатки
	|ИЗ
	|	Документ.ИнвентаризацияМЗ.Остатки	КАК Остатки
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ	ШапкиДокументов
	|		ПО	Остатки.Ссылка	= ШапкиДокументов.ДокСсылка
	|ИНДЕКСИРОВАТЬ ПО
	|	ДокСсылка,
	|	Номенклатура,
	|	Субконто3
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|// [6]. Счета учета номенклатуры определяем по табличным частям Остатки
	|//		(Финальная таблица)
	|//
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ТабличныеЧастиМатериалы.ДокСсылка			КАК ДокСсылка,
	|	ТабличныеЧастиМатериалы.Номенклатура		КАК Номенклатура,
	|	ТабличныеЧастиОстатки.Счет.Представление	КАК СчетПредставление
	|ИЗ
	|	ТабличныеЧастиМатериалы
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ	ТабличныеЧастиОстатки
	|		ПО	ТабличныеЧастиМатериалы.ДокСсылка		= ТабличныеЧастиОстатки.ДокСсылка
	|		И	ТабличныеЧастиМатериалы.Номенклатура	= ТабличныеЧастиОстатки.Номенклатура
	|		И	ТабличныеЧастиМатериалы.Субконто3		= ТабличныеЧастиОстатки.Субконто3
	|ИТОГИ
	|ПО
	|	// Обязательно первые итоги по ссылке на документ
	|	ДокСсылка,
	|	Номенклатура
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|// [7]. Шапки документов
	|//		(Финальная таблица)
	|//
	|ВЫБРАТЬ
	|	*
	|ИЗ
	|	ШапкиДокументов
	|УПОРЯДОЧИТЬ ПО
	|	Дата,
	|	Номер
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|// [8]. Искомая таблица для вывода в печатную форму
	|//		(Финальная таблица)
	|//
	|ВЫБРАТЬ
	|	*,
	|	ПРЕДСТАВЛЕНИЕ(Субконто3)	КАК Субконто3Представление,
	|	
	|	// Рассчитываемые колонки
	|	// Признаки наличия различных единиц измерения в строках таблицы
	|	// Предназначены для использования только в итогах
	|	ЛОЖЬ	КАК ЕдиницыИзмеренияРазличные
	|ИЗ
	|	ТабличныеЧастиМатериалы
	|УПОРЯДОЧИТЬ ПО
	|	ДокСсылка,
	|	Ном
	|ИТОГИ
	|	СУММА(Сумма)		КАК Сумма,
	|	СУММА(СуммаФ)		КАК СуммаФ,
	|	СУММА(Количество)	КАК Количество,
	|	СУММА(КоличествоФ)	КАК КоличествоФ,
	|	ВЫБОР
	|		КОГДА МИНИМУМ(ЕдиницаИзмеренияСсылка) = МАКСИМУМ(ЕдиницаИзмеренияСсылка) ТОГДА
	|			ЛОЖЬ
	|		ИНАЧЕ
	|			ИСТИНА
	|	КОНЕЦ				КАК ЕдиницыИзмеренияРазличные
	|ПО
	|	// Обязательно первые итоги по ссылке на документ
	| 	ДокСсылка
	|;";

	Результаты = ЗапросПечать.ВыполнитьПакет();

	// [3]. Табличные части инвентаризационных комиссий
	ВыборкаКомиссияГруппировкаПоДокСсылка = Результаты[3].Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);

	// [6]. Счета учета номенклатуры определяем по табличным частям Остатки
	ВыборкаСчетаУчетаГруппировкаПоДокСсылка = Результаты[6].Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);

	// [7]. Шапки документов
	ВыборкаШапка = Результаты[7].Выбрать();

	// [8]. Искомая таблица для вывода в печатную форму
	ВыборкаДанныеГруппировкаПоДокСсылка = Результаты[8].Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);

	ЭтоПервыйДокумент = Истина;

	Пока ВыборкаШапка.Следующий() Цикл

		Если НЕ ЭтоПервыйДокумент Тогда
			ТабДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;

		ЭтоПервыйДокумент = Ложь;

		// Продвигаемся в остальных выборках до текущего документа
		ПоискТекущегоДокумента = Новый Структура("ДокСсылка", ВыборкаШапка["ДокСсылка"]);
		ВыборкаКомиссияГруппировкаПоДокСсылка.Сбросить();
		ВыборкаСчетаУчетаГруппировкаПоДокСсылка.Сбросить();
		ВыборкаДанныеГруппировкаПоДокСсылка.Сбросить();
		ЕстьВыборкаКомиссияГруппировкаПоДокСсылка	= ВыборкаКомиссияГруппировкаПоДокСсылка.НайтиСледующий(ПоискТекущегоДокумента);
		ЕстьВыборкаСчетаУчетаГруппировкаПоДокСсылка	= ВыборкаСчетаУчетаГруппировкаПоДокСсылка.НайтиСледующий(ПоискТекущегоДокумента);
		ЕстьВыборкаДанныеГруппировкаПоДокСсылка		= ВыборкаДанныеГруппировкаПоДокСсылка.НайтиСледующий(ПоискТекущегоДокумента);

		// Используемые области макета
		ИспользуемыеОбласти = Новый Структура();
		ИспользуемыеОбласти.Вставить("Титул",							Макет.ПолучитьОбласть("Титул"));
		ИспользуемыеОбласти.Вставить("Шапка",							Макет.ПолучитьОбласть("Шапка"));
		ИспользуемыеОбласти.Вставить("Строка",							Макет.ПолучитьОбласть("Строка"));
		ИспользуемыеОбласти.Вставить("ПустаяСтрока",					Макет.ПолучитьОбласть("Строка"));
		ИспользуемыеОбласти.Вставить("СтрокаИтогоВТаблицеПоСтранице",	Макет.ПолучитьОбласть("ИтСтрЧ"));
		ИспользуемыеОбласти.Вставить("СтрокаИтогоВТаблицеПоОписи",		Макет.ПолучитьОбласть("ИтОписьЧ"));
		ИспользуемыеОбласти.Вставить("ИтогоПрописьюПоСтранице",			Макет.ПолучитьОбласть("ИтСтрП"));
		ИспользуемыеОбласти.Вставить("ИтогоПрописьюПоОписи",			Макет.ПолучитьОбласть("Итог1"));
		ИспользуемыеОбласти.Вставить("Итог2",							Макет.ПолучитьОбласть("Итог2"));
		ИспользуемыеОбласти.Вставить("Комиссия",						Макет.ПолучитьОбласть("Комиссия"));

		// Статистика показывает данные, фактически выведенные в табличный документ
		// Невыведенные данные в статистике не отражаются
		Статистика = ИНВ3_Статистика_ПолучитьПустую();

		НомерСтрокиНачало = ТабДокумент.ВысотаТаблицы + 1;
		ОбщееКоличествоПорядковыхНомеров = 0;

		//---
		// Титул
		//---

		СведенияМОЛ = ИнвентаризацияСервер.ПолучитьОписаниеМОЛ(ВыборкаШапка);

		ИспользуемыеОбласти["Титул"].Параметры.Заполнить(ВыборкаШапка);
		ИспользуемыеОбласти["Титул"].Параметры.Заполнить(СведенияМОЛ);

		ТабДокумент.Вывести(ИспользуемыеОбласти["Титул"]);

		ТабДокумент.ВывестиГоризонтальныйРазделительСтраниц();

		//---
		// Основная таблица
		//---
		ТабДокумент.Вывести(ИспользуемыеОбласти["Шапка"]);

		Если ЕстьВыборкаДанныеГруппировкаПоДокСсылка Тогда
			ВыборкаДанные = ВыборкаДанныеГруппировкаПоДокСсылка.Выбрать();

			// Статистику по всей описи мы уже знаем частично
			Статистика["ПоОписи"]["ОбщееКоличествоСтрок"]	= ВыборкаДанные.Количество();
			Статистика["ПоОписи"]["ИтогоКоличествоФакт"]	= ВыборкаДанныеГруппировкаПоДокСсылка["КоличествоФ"];
			Статистика["ПоОписи"]["ИтогоКоличествоУчет"]	= ВыборкаДанныеГруппировкаПоДокСсылка["Количество"];
			Статистика["ПоОписи"]["ИтогоСуммаФакт"]			= ВыборкаДанныеГруппировкаПоДокСсылка["СуммаФ"];
			Статистика["ПоОписи"]["ИтогоСуммаУчет"]			= ВыборкаДанныеГруппировкаПоДокСсылка["Сумма"];

			Пока ВыборкаДанные.Следующий() Цикл
				//---
				// Строка таблицы
				//---
				ВставляемыеЗначения = ИНВ3_ПолучитьПредставлениеЧиселСтроки(ВыборкаДанные, ПараметрыПечати);
				// Счета учета
				Если ЕстьВыборкаСчетаУчетаГруппировкаПоДокСсылка Тогда
					СтрокаСчетовУчета = ИНВ3_ПолучитьСтрокуСчетовУчетаНоменклатуры(ВыборкаДанные, ВыборкаСчетаУчетаГруппировкаПоДокСсылка);
				Иначе
					СтрокаСчетовУчета = "";
				КонецЕсли;
				ВставляемыеЗначения.Вставить("СчетУчета", СтрокаСчетовУчета);
				// Наименование номенклатуры с учетом дополнительного субконто
				Если ВыборкаШапка["ИспользуетсяДополнительноеСубконто"]
					И ЗначениеЗаполнено(ВыборкаДанные["Субконто3Представление"]) Тогда
					НаименованиеНФА = ВыборкаДанные["НаименованиеНФА"] + " (" + ВыборкаДанные["Субконто3Представление"] + ")";
					ВставляемыеЗначения.Вставить("НаименованиеНФА", НаименованиеНФА);
				КонецЕсли;
				ИспользуемыеОбласти["Строка"].Параметры.Заполнить(ВыборкаДанные);
				ИспользуемыеОбласти["Строка"].Параметры.Заполнить(ВставляемыеЗначения);


				// Проверяем, не пора ли начинать новую страницу
				Если ПараметрыПечати["РазбиватьНаСтраницы"] Тогда
					ИНВ3_ЗаполнитьИтогиПоСтранице(ИспользуемыеОбласти, Статистика, ПараметрыПечати);

					ПроверяемыеОбласти = Новый Массив();
					ПроверяемыеОбласти.Добавить(ИспользуемыеОбласти["Строка"]);
					ПроверяемыеОбласти.Добавить(ИспользуемыеОбласти["Строка"]);
					ПроверяемыеОбласти.Добавить(ИспользуемыеОбласти["СтрокаИтогоВТаблицеПоСтранице"]);
					ПроверяемыеОбласти.Добавить(ИспользуемыеОбласти["ИтогоПрописьюПоСтранице"]);

					Если НЕ ТабДокумент.ПроверитьВывод(ПроверяемыеОбласти) Тогда
						//---
						// Итого по странице
						//---
						ТабДокумент.Вывести(ИспользуемыеОбласти["СтрокаИтогоВТаблицеПоСтранице"]);
						ТабДокумент.Вывести(ИспользуемыеОбласти["ИтогоПрописьюПоСтранице"]);

						// Статистика
						ИНВ3_Статистика_ОтразитьЗавершениеВыводаТекущейСтраницы(Статистика);

						ТабДокумент.ВывестиГоризонтальныйРазделительСтраниц();

						Если ПараметрыПечати["ПовторятьШапкуТаблицы"] Тогда
							ТабДокумент.Вывести(ИспользуемыеОбласти["Шапка"]);
						КонецЕсли;
					КонецЕсли;

				КонецЕсли;

				//---
				// Строка таблицы
				//---
				ТабДокумент.Вывести(ИспользуемыеОбласти["Строка"]);

				// Статистика
				ИНВ3_Статистика_ОтразитьЗавершениеВыводаТекущейСтроки(Статистика, ВыборкаДанные);
			КонецЦикла;	
		КонецЕсли;

		// Обновляем итоги
		ИНВ3_ЗаполнитьИтогиПоСтранице(ИспользуемыеОбласти, Статистика, ПараметрыПечати);
		ИНВ3_ЗаполнитьИтогиПоОписи(ИспользуемыеОбласти, Статистика, ПараметрыПечати, СведенияМОЛ);

		// Выводим пустые строки таблицы до конца страницы
		ПроверяемыеОбласти = Новый Массив;
		ПроверяемыеОбласти.Добавить(ИспользуемыеОбласти["ПустаяСтрока"]);
		ПроверяемыеОбласти.Добавить(ИспользуемыеОбласти["СтрокаИтогоВТаблицеПоСтранице"]);
		ПроверяемыеОбласти.Добавить(ИспользуемыеОбласти["СтрокаИтогоВТаблицеПоОписи"]);
		ПроверяемыеОбласти.Добавить(ИспользуемыеОбласти["ИтогоПрописьюПоСтранице"]);
		ПроверяемыеОбласти.Добавить(ИспользуемыеОбласти["Итог2"]);
		Пока ТабДокумент.ПроверитьВывод(ПроверяемыеОбласти) Цикл
			ТабДокумент.Вывести(ИспользуемыеОбласти["ПустаяСтрока"]);
		КонецЦикла;

		//---
		// Итого по странице
		//---
		ТабДокумент.Вывести(ИспользуемыеОбласти["СтрокаИтогоВТаблицеПоСтранице"]);
		ТабДокумент.Вывести(ИспользуемыеОбласти["СтрокаИтогоВТаблицеПоОписи"]);
		ТабДокумент.Вывести(ИспользуемыеОбласти["ИтогоПрописьюПоСтранице"]);

		ТабДокумент.ВывестиГоризонтальныйРазделительСтраниц();

		//---
		// Итоги по всей описи
		//---
		ТабДокумент.Вывести(ИспользуемыеОбласти["ИтогоПрописьюПоОписи"]);

		//---
		// Инвентаризационная комиссия
		//---
		ОбластьВсяКомиссия = УправлениеОсновнымиСредствами.СформироватьОбластьКомиссия(
		ВыборкаШапка,
		ВыборкаКомиссияГруппировкаПоДокСсылка.Выбрать(), 
		ИспользуемыеОбласти["Комиссия"], 
		3);
		ТабДокумент.Вывести(ОбластьВсяКомиссия);

		//---
		// Повторная подпись МОЛ
		//---
		ТабДокумент.Вывести(ИспользуемыеОбласти["Итог2"]);


		// В табличном документе зададим имя области, в которую был 
		// выведен объект. Нужно для возможности печати покомплектно.
		УправлениеПечатью.ЗадатьОбластьПечатиДокумента(
		ТабДокумент, 
		НомерСтрокиНачало,
		ОбъектыПечати,
		ВыборкаШапка.ДокСсылка);
	КонецЦикла;

	Возврат ТабДокумент;

КонецФункции

