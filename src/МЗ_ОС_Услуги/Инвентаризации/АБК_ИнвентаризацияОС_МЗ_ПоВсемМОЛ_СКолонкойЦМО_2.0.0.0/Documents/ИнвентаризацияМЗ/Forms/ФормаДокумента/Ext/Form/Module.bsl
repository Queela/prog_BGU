
&НаСервере
&После("ПриСозданииНаСервере")
Процедура АБК_ПриСозданииНаСервере(Отказ, СтандартнаяОбработка) 
	
	ДобРеквизиты = Новый Массив;
	нРеквизит = Новый РеквизитФормы("АБК_ЗаполнитьПоВсемМол", Новый ОписаниеТипов("Булево"),,"Заполнить по всем МОЛ"); 
	
	ДобРеквизиты.Добавить(нРеквизит);
	ИзменитьРеквизиты(ДобРеквизиты);
	
	нЭлемент = Элементы.Вставить("АБК_ЗаполнитьПоВсемМол", Тип("ПолеФормы"),, Элементы.Страницы);
	нЭлемент.ПутьКДанным = "АБК_ЗаполнитьПоВсемМол";
	нЭлемент.Вид = ВидПоляФормы.ПолеФлажка;
	
	нЭлемент = Элементы.Вставить("МатериалыЦМО",Тип("ПолеФормы"),Элементы.Материалы,Элементы.МатериалыНоменклатура);
	нЭлемент.ПутьКДанным = "Объект.Материалы.ЦМО";
	нЭлемент.Вид = ВидПоляФормы.ПолеВвода;
	
КонецПроцедуры

&НаКлиенте
&ИзменениеИКонтроль("НеобходимыеРеквизитыЗаполнены")
Функция АБК_НеобходимыеРеквизитыЗаполнены()

	Результат = Истина;

	Если НЕ ЗначениеЗаполнено(Объект.ДатаИнвентаризации) Тогда
		Результат = Ложь;
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
		НСтр("ru='Не указана дата проведения инвентаризации'"),
		Объект["Ссылка"],
		"Объект.ДатаИнвентаризации");
	КонецЕсли;

	Если НЕ ЗначениеЗаполнено(Объект.Организация) Тогда
		Результат = Ложь;
		ТекстСообщения = НСтр("ru='Поле Организация не заполнено'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,
		Объект.Ссылка,
		"Объект.Организация");
	КонецЕсли;
    #Удаление
	Если НЕ ЗначениеЗаполнено(Объект.ЦМО) Тогда
	#КонецУдаления
	#Вставка     
	Если НЕ ЗначениеЗаполнено(Объект.ЦМО) И НЕ АБК_ЗаполнитьПоВсемМол Тогда
	#КонецВставки
		Результат = Ложь;
		ТекстСообщения = НСтр("ru='Поле МОЛ не заполнено'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,
		Объект.Ссылка,
		"Объект.ЦМО");
	КонецЕсли;

	Возврат Результат;

КонецФункции
