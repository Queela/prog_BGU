
&НаСервере
&После("ПриСозданииНаСервере")
Процедура АБК_ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	нЭлемент = Элементы.Вставить("ИнвентаризацияЦМО",Тип("ПолеФормы"),Элементы.Инвентаризация,Элементы.ИнвентаризацияОсновноеСредство);
	нЭлемент.ПутьКДанным = "Объект.Инвентаризация.ЦМО";
	нЭлемент.Вид = ВидПоляФормы.ПолеВвода;  
	
	нЭлемент = Элементы.Вставить("Инвентаризация194нЦМО",Тип("ПолеФормы"),Элементы.Инвентаризация194нГруппаОС,Элементы.Инвентаризация194нОсновноеСредство);
	нЭлемент.ПутьКДанным = "Объект.Инвентаризация.ЦМО";
	нЭлемент.Вид = ВидПоляФормы.ПолеВвода;
	
	нЭлемент = Элементы.Вставить("АБК_ЗаполнитьПоВсемМОЛ", Тип("ПолеФормы"),, Элементы.Страницы);
	нЭлемент.ПутьКДанным = "Объект.АБК_ЗаполнитьПоВсемМОЛ";
	нЭлемент.Вид = ВидПоляФормы.ПолеФлажка;
	
КонецПроцедуры

&НаКлиенте
&ИзменениеИКонтроль("НельзяДобавитьВТЧИнвентаризация")
Функция АБК_НельзяДобавитьВТЧИнвентаризация()
	
	Отказ = Ложь;
	Если Не ЗначениеЗаполнено(Объект.Организация) Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
		НСтр("ru = 'Не заполнен обязательный реквизит ""Организация""'"),
		,
		"Объект.Организация",,Отказ);
	КонецЕсли;
	#Удаление
	Если Не ЗначениеЗаполнено(Объект.ЦМО) Тогда 
	#КонецУдаления
	#Вставка
	Если Не ЗначениеЗаполнено(Объект.ЦМО) И НЕ Объект.АБК_ЗаполнитьПоВсемМОЛ Тогда 
	#КонецВставки
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
		НСтр("ru = 'Не заполнен обязательный реквизит ""МОЛ / Место хранения""'"),
		,
		"Объект.ЦМО",,Отказ);
	КонецЕсли;
	Если Не ЗначениеЗаполнено(Объект.СчетУчета) Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
		НСтр("ru = 'Не заполнен обязательный реквизит ""Счет учета""'"),
		,
		"Объект.СчетУчета",,Отказ);
	КонецЕсли;
	Возврат Отказ;							
		
КонецФункции
