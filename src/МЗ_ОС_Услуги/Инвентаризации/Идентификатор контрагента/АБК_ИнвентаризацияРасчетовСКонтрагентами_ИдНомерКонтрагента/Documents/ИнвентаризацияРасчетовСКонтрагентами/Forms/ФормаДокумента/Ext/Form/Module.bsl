
&НаСервере
Процедура АБК_ПриСозданииНаСервереПосле(Отказ, СтандартнаяОбработка)   
	
	МассивРеквизитов = Новый Массив;
	МассивРеквизитов.Добавить(Новый РеквизитФормы("ИдентификаторКонтрагента", Новый ОписаниеТипов("Строка"), "Объект.ДебиторскаяЗадолженность", "Идентификатор контрагента",Ложь));
	МассивРеквизитов.Добавить(Новый РеквизитФормы("ИдентификаторКонтрагента", Новый ОписаниеТипов("Строка"), "Объект.КредиторскаяЗадолженность", "Идентификатор контрагента",Ложь));
	ИзменитьРеквизиты(МассивРеквизитов);
	
	НовЭл = Элементы.Вставить("ДебиторскаяЗадолженностьИдентификаторКонтрагента", Тип("ПолеФормы"), Элементы.ДебиторскаяЗадолженностьГруппаКонтрагентДоговор,Элементы.ДебиторскаяЗадолженностьДоговор); 
	НовЭл.Вид = ВидПоляФормы.ПолеВвода; 
	НовЭл.Заголовок = "Идентификатор контрагента"; 
	НовЭл.ПутьКДанным = "Объект.ДебиторскаяЗадолженность.ИдентификаторКонтрагента";
	
	НовЭл = Элементы.Вставить("КредиторскаяЗадолженностьИдентификаторКонтрагента", Тип("ПолеФормы"), Элементы.КредиторскаяЗадолженностьГруппаКонтрагентДоговор, Элементы.КредиторскаяЗадолженностьДоговор); 
	НовЭл.Вид = ВидПоляФормы.ПолеВвода;          
	НовЭл.Заголовок = "Идентификатор контрагента"; 
	НовЭл.ПутьКДанным = "Объект.КредиторскаяЗадолженность.ИдентификаторКонтрагента";  
	
	ЗаполнитьИдентификаторы();
	
КонецПроцедуры                   

&НаСервере
Процедура ЗаполнитьИдентификаторы()
	Для Каждого Стр из Объект.ДебиторскаяЗадолженность Цикл    
		Стр.ИдентификаторКонтрагента = ВычислитьИдентификатор(Стр.Контрагент);
	КонецЦикла;   
	
	Для Каждого Стр из Объект.КредиторскаяЗадолженность Цикл    
		Стр.ИдентификаторКонтрагента = ВычислитьИдентификатор(Стр.Контрагент);
	КонецЦикла;
КонецПроцедуры

&НаСервере
Функция ВычислитьИдентификатор(Контрагент) 
	
	Если ЗначениеЗаполнено(Контрагент)
		И ТипЗнч(Контрагент) = Тип("СправочникСсылка.Контрагенты") Тогда
		
		Возврат БухгалтерскийУчет.ИдентификаторКонтрагента(Контрагент);   
	КонецЕсли;
КонецФункции

&НаСервере
Функция УдалитьНедопустимыеСимволы(Строка)
	НедопустимыеСимволы = ",.+-=_N\/<> "+Символы.НПП+Символы.ПС;
	Возврат СтрСоединить(СтрРазделить(Строка,НедопустимыеСимволы));
КонецФункции

&НаСервере
Процедура АБК_ПослеЗаписиНаСервереПосле(ТекущийОбъект, ПараметрыЗаписи)
	ЗаполнитьИдентификаторы();
КонецПроцедуры

&НаСервере
&После("ЗавершениеЗаполненияПоДаннымБухУчетаСервер")
Процедура АБК_ЗавершениеЗаполненияПоДаннымБухУчетаСервер(Знач АдресРезультата, ЗаполнитьДебиторскуюЗадолженность, ЗаполнитьКредиторскуюЗадолженность)
	ЗаполнитьИдентификаторы();
КонецПроцедуры
