
&ИзменениеИКонтроль("ОпределитьПараметрыНакладываемогоОтбора")
Функция АБК_ОпределитьПараметрыНакладываемогоОтбора(Форма)

	Перем	ИспользоватьУчетПоИФО,
	ИспользоватьУчетПоПодразделениям,
	НакладыватьОтборПоКФО,
	НакладыватьОтборПоИФО,
	НакладыватьОтборПоКПС,
	НакладыватьОтборПоПодразделению,
	НакладыватьОтборПоДополнительномуСубконто,
	СледуетИспользоватьДополнительноеСубконто,
	СчетаОстатков,
	ИсключенныеСчетаОстатков,
	ЗначенияДляОтбораПоМОЛ,
	ТаблицаРеальныхДополнительныхСубконто;

	ДокументОбъект						= Форма["Объект"];

	ТаблицаАналитикиДопустимыхСчетов	= Форма[ПолучитьИмяРеквизитаФормыТаблицаАналитикиДопустимыхСчетов()];
	ДетальныеСвойства					= Форма[ПолучитьИмяРеквизитаФормыСвойстваВыбраннойГруппыСчетов()]["ДетальныеСвойства"];
	СвойстваДопустимыхСчетов			= ДетальныеСвойства["СвойстваДопустимыхСчетов"];

	ИспользоватьУчетПоИФО				= ПолучитьФункциональнуюОпцию("ИспользоватьУчетПоИФО");
	ИспользоватьУчетПоПодразделениям	= ПолучитьФункциональнуюОпцию("ИспользоватьУчетПоПодразделениям");


	НакладыватьОтборПоКФО						= ЗначениеЗаполнено(ДокументОбъект["КФО"]);

	НакладыватьОтборПоИФО						= ИспользоватьУчетПоИФО
	И НЕ ДокументОбъект["БезУчетаИФО"];

	НакладыватьОтборПоКПС						= СвойстваДопустимыхСчетов["УчетПоКПС"]
	И ЗначениеЗаполнено(ДокументОбъект["КПС"]);

	НакладыватьОтборПоПодразделению				= ИспользоватьУчетПоПодразделениям
	И СвойстваДопустимыхСчетов["УчетПоПодразделениям"]
	И ЗначениеЗаполнено(ДокументОбъект["Подразделение"]);

	СледуетИспользоватьДополнительноеСубконто	= ДетальныеСвойства["КорректностьНастроекДополнительногоСубконто"]["СледуетИспользовать"];

	НакладыватьОтборПоДополнительномуСубконто	= СледуетИспользоватьДополнительноеСубконто
	И ЗначениеЗаполнено(ДокументОбъект["Субконто3"]);


	СчетаОстатков								= Новый Массив();
	ИсключенныеСчетаОстатков					= Новый Массив();

	// Счет отбора сужает список проверяемых счетов
	ЗапросПоиск = Новый Запрос();
	ЗапросПоиск.УстановитьПараметр("Счет", ДокументОбъект["Счет"]);
	ЗапросПоиск.Текст = "
	|ВЫБРАТЬ
	|	Ссылка
	|ИЗ
	|	ПланСчетов.ЕПСБУ
	|ГДЕ
	|	Ссылка В ИЕРАРХИИ(&Счет)
	|;";

	ПотенциальныеСчетаОстатков = ЗапросПоиск.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка");

	// Таблица реальных дополнительных субконто - для Счетов остатков
	// Сведения по Исключаемым счетам в эту таблиццу не включаем
	ТаблицаРеальныхДополнительныхСубконто = ПолучитьПустуюТаблицуАналитикиДопустимыхСчетов();

	Для Каждого СтрокаТаблицы Из ТаблицаАналитикиДопустимыхСчетов Цикл
		ДопустимыйСчет = СтрокаТаблицы["Счет"];

		Если ПотенциальныеСчетаОстатков.Найти(ДопустимыйСчет) = Неопределено Тогда
			Продолжить;
		КонецЕсли;

		Если НакладыватьОтборПоКПС И НЕ СтрокаТаблицы["УчетПоКПС"] Тогда
			ИсключенныеСчетаОстатков.Добавить(ДопустимыйСчет);
			Продолжить;
		КонецЕсли;

		Если НакладыватьОтборПоПодразделению И НЕ СтрокаТаблицы["УчетПоПодразделениям"] Тогда
			ИсключенныеСчетаОстатков.Добавить(ДопустимыйСчет);
			Продолжить;
		КонецЕсли;

		Если НакладыватьОтборПоДополнительномуСубконто Тогда
			ИсключитьЭтотСчет = Ложь;

			Если ЗначениеЗаполнено(СтрокаТаблицы["РеальноеДополнительноеСубконто"]) Тогда
				ПриведенноеЗначение = СтрокаТаблицы["РеальноеДополнительноеСубконто"].ТипЗначения.ПривестиЗначение(ДокументОбъект["Субконто3"]);

				Если НЕ ЗначениеЗаполнено(ПриведенноеЗначение) Тогда
					ИсключитьЭтотСчет = Истина;
				КонецЕсли;
			Иначе
				ИсключитьЭтотСчет = Истина;
			КонецЕсли;

			Если ИсключитьЭтотСчет Тогда
				ИсключенныеСчетаОстатков.Добавить(ДопустимыйСчет);
				Продолжить;
			КонецЕсли;
		КонецЕсли;

		СчетаОстатков.Добавить(ДопустимыйСчет);

		НоваяСтрока = ТаблицаРеальныхДополнительныхСубконто.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТаблицы);
	КонецЦикла;

	// Применяемые значения для отбора по МОЛ
	МОЛ = ДокументОбъект["ЦМО"];
	Если ТипЗнч(МОЛ) = Тип("СправочникСсылка.Сотрудники") Тогда
		// 1 Сотрудник -> N ЦМО,
		// ЦМО определяются для одной указанной организации из шапки документа
		// Может оказаться несколько значений

		ЗапросПоиск = Новый Запрос();
		ЗапросПоиск.УстановитьПараметр("Сотрудник",		МОЛ);
		ЗапросПоиск.УстановитьПараметр("Организация",	ДокументОбъект["Организация"]);
		ЗапросПоиск.Текст = "
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	Ссылка КАК Ссылка
		|ИЗ
		|	Справочник.ЦМО
		|ГДЕ
		|	Сотрудник = &Сотрудник
		|	И Владелец = &Организация
		|;";

		ЗначенияДляОтбораПоМОЛ = ЗапросПоиск.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка");

	Иначе
		// Используем только 1 указанное значение

		ЗначенияДляОтбораПоМОЛ = Новый Массив();
		ЗначенияДляОтбораПоМОЛ.Добавить(МОЛ);
	КонецЕсли;


	ПараметрыНакладываемогоОтбора = ПараметрыНакладываемогоОтбора_Пустая();

	ПараметрыНакладываемогоОтбора.ИспользоватьУчетПоИФО                     = ИспользоватьУчетПоИФО;
	ПараметрыНакладываемогоОтбора.ИспользоватьУчетПоПодразделениям          = ИспользоватьУчетПоПодразделениям;
	ПараметрыНакладываемогоОтбора.НакладыватьОтборПоКФО                     = НакладыватьОтборПоКФО;
	ПараметрыНакладываемогоОтбора.НакладыватьОтборПоИФО                     = НакладыватьОтборПоИФО;
	ПараметрыНакладываемогоОтбора.НакладыватьОтборПоКПС                     = НакладыватьОтборПоКПС;
	ПараметрыНакладываемогоОтбора.НакладыватьОтборПоПодразделению           = НакладыватьОтборПоПодразделению;
	ПараметрыНакладываемогоОтбора.НакладыватьОтборПоДополнительномуСубконто = НакладыватьОтборПоДополнительномуСубконто;
	ПараметрыНакладываемогоОтбора.СледуетИспользоватьДополнительноеСубконто = СледуетИспользоватьДополнительноеСубконто;
	ПараметрыНакладываемогоОтбора.СчетаОстатков                             = СчетаОстатков;
	ПараметрыНакладываемогоОтбора.ИсключенныеСчетаОстатков                  = ИсключенныеСчетаОстатков;
	ПараметрыНакладываемогоОтбора.ЗначенияДляОтбораПоМОЛ                    = ЗначенияДляОтбораПоМОЛ;
	ПараметрыНакладываемогоОтбора.ТаблицаРеальныхДополнительныхСубконто     = ТаблицаРеальныхДополнительныхСубконто;
    #Вставка     
	МассивНоменклатур = Новый Массив;
	
	Для Каждого Стр Из Форма["АБК_ОтборНоменклатуры"] Цикл 
		Если Стр.АБК_Использовать Тогда 
			МассивНоменклатур.Добавить(Стр.АБК_Номенклатура);
		КонецЕсли;
	КонецЦикла; 
	
	Если МассивНоменклатур.Количество() Тогда 
	ПараметрыНакладываемогоОтбора.Вставить("МассивНоменклатур",МассивНоменклатур);	
	КонецЕсли;
	
	#КонецВставки
	Возврат ПараметрыНакладываемогоОтбора;

КонецФункции

&ИзменениеИКонтроль("ЗаполнитьОбъектПоДаннымУчета")
Процедура АБК_ЗаполнитьОбъектПоДаннымУчета(ДокументОбъект, ПараметрыНакладываемогоОтбора)

	Перем СдвигИндексаТаблицыМатериалы;

	ДатаОпределенияОстатков			= Новый Граница(КонецДня(ДокументОбъект["ДатаИнвентаризации"]), ВидГраницы.Включая);
	ИспользоватьБалансовуюСтоимость = НЕ ЗначениеЗаполнено(ДокументОбъект["ТипЦен"]);

	ПризнакиИспользованияКолонок	= Документы.ИнвентаризацияМЗ.ПолучитьПризнакиИспользованияКолонокТаблицыПорядокСписания();

	УстанавливатьКоличествоПоУмолчанию = ИнвентаризацияСервер.УстанавливатьКоличествоПоУмолчанию(ДокументОбъект);

	КоличествоПоУмолчанию = ИнвентаризацияСервер.ПолучитьКоличествоПоУмолчанию();

	Если УстанавливатьКоличествоПоУмолчанию Тогда
		ВыбиратьНулевоеКоличество = Истина;
		ЗаменятьНулевоеКоличество = Истина;
	Иначе
		ВыбиратьНулевоеКоличество = Ложь;
		ЗаменятьНулевоеКоличество = Ложь;
	КонецЕсли;

	ВариантФормированияОписи = ДокументОбъект["ВариантФормированияОписи"];

	ЗапросОстатки = Новый Запрос();
	ЗапросОстатки.УстановитьПараметр("Организация",								ДокументОбъект["Организация"]);
	ЗапросОстатки.УстановитьПараметр("ДатаЗапроса",								ДатаОпределенияОстатков);
	ЗапросОстатки.УстановитьПараметр("СчетаОстатков",							ПараметрыНакладываемогоОтбора["СчетаОстатков"]);
	ЗапросОстатки.УстановитьПараметр("ИспользоватьУчетПоИФО",					ПараметрыНакладываемогоОтбора["ИспользоватьУчетПоИФО"]);
	ЗапросОстатки.УстановитьПараметр("НакладыватьОтборПоИФО",					ПараметрыНакладываемогоОтбора["НакладыватьОтборПоИФО"]);
	ЗапросОстатки.УстановитьПараметр("ИФО",										ДокументОбъект["ИФО"]);
	ЗапросОстатки.УстановитьПараметр("НакладыватьОтборПоКФО",					ПараметрыНакладываемогоОтбора["НакладыватьОтборПоКФО"]);
	ЗапросОстатки.УстановитьПараметр("КФО",										ДокументОбъект["КФО"]);
	ЗапросОстатки.УстановитьПараметр("НакладыватьОтборПоПодразделению",			ПараметрыНакладываемогоОтбора["НакладыватьОтборПоПодразделению"]);
	ЗапросОстатки.УстановитьПараметр("Подразделение",							ДокументОбъект["Подразделение"]);
	ЗапросОстатки.УстановитьПараметр("НакладыватьОтборПоКПС",					ПараметрыНакладываемогоОтбора["НакладыватьОтборПоКПС"]);
	ЗапросОстатки.УстановитьПараметр("КПС",										ДокументОбъект["КПС"]);
	ЗапросОстатки.УстановитьПараметр("ПустаяНоменклатура",						Справочники.Номенклатура.ПустаяСсылка());
	ЗапросОстатки.УстановитьПараметр("ТипЦен",									ДокументОбъект["ТипЦен"]);

	// Отбор по номенклатуре
	ЗапросОстатки.УстановитьПараметр("НакладыватьОтборПоНоменклатуре",   		ЗначениеЗаполнено(ПараметрыНакладываемогоОтбора["ЗначенияДляОтбораПоНоменклатуре"]));
	ЗапросОстатки.УстановитьПараметр("ЗначенияДляОтбораПоНоменклатуре",			ПараметрыНакладываемогоОтбора["ЗначенияДляОтбораПоНоменклатуре"]);

	// Отбор по МОЛ
	ЗапросОстатки.УстановитьПараметр("ЗначенияДляОтбораПоМОЛ",					ПараметрыНакладываемогоОтбора["ЗначенияДляОтбораПоМОЛ"]);

	// Отбор по дополнительному субконто
	ЗапросОстатки.УстановитьПараметр("СледуетИспользоватьДополнительноеСубконто",	ПараметрыНакладываемогоОтбора["СледуетИспользоватьДополнительноеСубконто"]);
	ЗапросОстатки.УстановитьПараметр("ТаблицаРеальныхДополнительныхСубконто",		ПараметрыНакладываемогоОтбора["ТаблицаРеальныхДополнительныхСубконто"]);
	ЗапросОстатки.УстановитьПараметр("НакладыватьОтборПоДополнительномуСубконто",	ПараметрыНакладываемогоОтбора["НакладыватьОтборПоДополнительномуСубконто"]);
	ЗапросОстатки.УстановитьПараметр("ЗначениеДляОтбораПоДополнительномуСубконто",	ДокументОбъект["Субконто3"]);

	// В ТЧ "Порядок списания" используются не все колонки, а только некоторые
	ЗапросОстатки.УстановитьПараметр("ИспользуетсяКолонкаИФО",					ПризнакиИспользованияКолонок["ИФО"]);
	ЗапросОстатки.УстановитьПараметр("ИспользуетсяКолонкаКФО",					ПризнакиИспользованияКолонок["КФО"]);
	ЗапросОстатки.УстановитьПараметр("ИспользуетсяКолонкаЦМО",					ПризнакиИспользованияКолонок["ЦМО"]);
	ЗапросОстатки.УстановитьПараметр("ИспользуетсяКолонкаКПС",					ПризнакиИспользованияКолонок["КПС"]);
	ЗапросОстатки.УстановитьПараметр("ИспользуетсяКолонкаСчет",					ПризнакиИспользованияКолонок["Счет"]);
	ЗапросОстатки.УстановитьПараметр("ИспользуетсяКолонкаСубконто3",			ПризнакиИспользованияКолонок["Субконто3"]);

	ЗапросОстатки.УстановитьПараметр("СчетГруппа",			ДокументОбъект["СчетГруппа"]);
	ЗапросОстатки.УстановитьПараметр("НеЗаполнятьЦену",		ДокументОбъект["НеЗаполнятьЦену"]);

	ЗапросОстатки.УстановитьПараметр("ЗаменятьНулевоеКоличество",	ЗаменятьНулевоеКоличество);
	ЗапросОстатки.УстановитьПараметр("КоличествоПоУмолчанию",		КоличествоПоУмолчанию);

	ТекстЗапроса = "
	|////////////////////////////////////////////////////////////////////////////////
	|// [0]. Таблица реальных дополнительных субконто для Счетов остатков
	|//
	|ВЫБРАТЬ
	|	ТЗ.Счет										КАК Счет,
	|	ТЗ.РеальноеДополнительноеСубконто			КАК РеальноеДополнительноеСубконто,
	|	ТЗ.НомерРеальногоДополнительногоСубконто	КАК НомерРеальногоДополнительногоСубконто
	|ПОМЕСТИТЬ
	|	ТаблицаРеальныхДополнительныхСубконто
	|ИЗ
	|	&ТаблицаРеальныхДополнительныхСубконто	КАК ТЗ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|// [1]. Все остатки по полным счетам, с отбором по МОЛ.
	|//		Т.к. применяется отбор по МОЛ, то могут быть остатки с нулевыми суммами
	|//		(если на соответствующих счетах не ведется суммовой
	|//		учет по субконто для МОЛ)
	|//
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	Остатки.Счет																			КАК Счет,
	|	Остатки.КФО																				КАК КФО,
	|	ЕСТЬNULL(Остатки.КПС,ЗНАЧЕНИЕ(Справочник.КлассификационныеПризнакиСчетов.ПустаяСсылка))	КАК КПС,
	|	ВЫБОР
	|		КОГДА &ИспользоватьУчетПоИФО ТОГДА
	|			Остатки.ИФО
	|		ИНАЧЕ
	|			ЗНАЧЕНИЕ(Справочник.ИсточникиФинансовогоОбеспечения.ПустаяСсылка)
	|	КОНЕЦ																					КАК ИФО,
	|	ВЫБОР
	|		КОГДА ТИПЗНАЧЕНИЯ(Остатки.Субконто1) = ТИП(Справочник.Номенклатура) ТОГДА
	|			ВЫРАЗИТЬ(Остатки.Субконто1	КАК Справочник.Номенклатура)
	|		КОГДА ТИПЗНАЧЕНИЯ(Остатки.Субконто2) = ТИП(Справочник.Номенклатура) ТОГДА
	|			ВЫРАЗИТЬ(Остатки.Субконто2	КАК Справочник.Номенклатура)
	|		КОГДА ТИПЗНАЧЕНИЯ(Остатки.Субконто3) = ТИП(Справочник.Номенклатура) ТОГДА
	|			ВЫРАЗИТЬ(Остатки.Субконто3	КАК Справочник.Номенклатура)
	|		ИНАЧЕ
	|			ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)
	|	КОНЕЦ																					КАК Номенклатура,
	|	ВЫБОР
	|		КОГДА Субконто1 В (&ЗначенияДляОтбораПоМОЛ) ТОГДА
	|			Субконто1
	|		КОГДА Субконто2 В (&ЗначенияДляОтбораПоМОЛ) ТОГДА
	|			Субконто2
	|		КОГДА Субконто3 В (&ЗначенияДляОтбораПоМОЛ) ТОГДА
	|			Субконто3
	|	КОНЕЦ																					КАК МОЛ,
	|	ВЫБОР
	|		КОГДА &СледуетИспользоватьДополнительноеСубконто И ТаблицаРеальныхДополнительныхСубконто.НомерРеальногоДополнительногоСубконто = 1 ТОГДА
	|			Остатки.Субконто1
	|		КОГДА &СледуетИспользоватьДополнительноеСубконто И ТаблицаРеальныхДополнительныхСубконто.НомерРеальногоДополнительногоСубконто = 2 ТОГДА
	|			Остатки.Субконто2
	|		КОГДА &СледуетИспользоватьДополнительноеСубконто И ТаблицаРеальныхДополнительныхСубконто.НомерРеальногоДополнительногоСубконто = 3 ТОГДА
	|			Остатки.Субконто3
	|		ИНАЧЕ
	|			НЕОПРЕДЕЛЕНО
	|	КОНЕЦ																					КАК Субконто3,
	|	СУММА(Остатки.КоличествоРазвернутыйОстатокДт - Остатки.КоличествоРазвернутыйОстатокКт)	КАК КоличествоУчет,
	|	СУММА(Остатки.СуммаРазвернутыйОстатокДт - Остатки.СуммаРазвернутыйОстатокКт)			КАК СуммаУчет
	|ПОМЕСТИТЬ
	|	ОстаткиСОтборомПоМОЛ
	|ИЗ
	|	РегистрБухгалтерии.ЖурналПроводокЕПСБУ.Остатки(
	|		&ДатаЗапроса,				// 1 Период
	|		Счет В (&СчетаОстатков),	// 2 Условие счета
	|		,							// 3 Виды субконто
	|		Организация = &Организация	// 4 Условие
	|		И ВЫБОР 
	|			КОГДА &НакладыватьОтборПоИФО ТОГДА
	|				ИФО = &ИФО
	|			ИНАЧЕ
	|				ИСТИНА
	|		КОНЕЦ
	|		И ВЫБОР 
	|			КОГДА &НакладыватьОтборПоКФО ТОГДА
	|				КФО = &КФО
	|			ИНАЧЕ
	|				ИСТИНА
	|		КОНЕЦ
	|		И ВЫБОР
	|			КОГДА &НакладыватьОтборПоКПС ТОГДА
	|				КПС = &КПС
	|			ИНАЧЕ
	|				ИСТИНА
	|		КОНЕЦ
	|		И ВЫБОР 
	|			КОГДА &НакладыватьОтборПоПодразделению ТОГДА
	|				Подразделение = &Подразделение
	|			ИНАЧЕ
	|				ИСТИНА
	|		КОНЕЦ
	|		// Отбор по МОЛ
	|		И ВЫБОР
	|			КОГДА Субконто1 В (&ЗначенияДляОтбораПоМОЛ) ТОГДА
	|				ИСТИНА
	|			КОГДА Субконто2 В (&ЗначенияДляОтбораПоМОЛ) ТОГДА
	|				ИСТИНА
	|			КОГДА Субконто3 В (&ЗначенияДляОтбораПоМОЛ) ТОГДА
	|				ИСТИНА
	|			ИНАЧЕ
	|				ЛОЖЬ
	|		КОНЕЦ
	#Вставка     
	|		%УсловиеНоменклатуры%
	#КонецВставки
	|	)	КАК Остатки
	|	ЛЕВОЕ СОЕДИНЕНИЕ	ТаблицаРеальныхДополнительныхСубконто
	|		ПО	Остатки.Счет = ТаблицаРеальныхДополнительныхСубконто.Счет
	|ГДЕ
	|	// Только заполненная номенклатура
	|	ВЫБОР
	|		КОГДА ТИПЗНАЧЕНИЯ(Остатки.Субконто1) = ТИП(Справочник.Номенклатура) ТОГДА
	|			НЕ Остатки.Субконто1 = &ПустаяНоменклатура
	|		КОГДА ТИПЗНАЧЕНИЯ(Остатки.Субконто2) = ТИП(Справочник.Номенклатура) ТОГДА
	|			НЕ Остатки.Субконто2 = &ПустаяНоменклатура
	|		КОГДА ТИПЗНАЧЕНИЯ(Остатки.Субконто3) = ТИП(Справочник.Номенклатура) ТОГДА
	|			НЕ Остатки.Субконто3 = &ПустаяНоменклатура
	|		ИНАЧЕ
	|			ЛОЖЬ
	|	КОНЕЦ
	|	// Отбор по дополнительному субконто
	|	И ВЫБОР
	|		КОГДА &НакладыватьОтборПоДополнительномуСубконто И ТаблицаРеальныхДополнительныхСубконто.НомерРеальногоДополнительногоСубконто = 1 ТОГДА
	|			Остатки.Субконто1 = &ЗначениеДляОтбораПоДополнительномуСубконто
	|		КОГДА &НакладыватьОтборПоДополнительномуСубконто И ТаблицаРеальныхДополнительныхСубконто.НомерРеальногоДополнительногоСубконто = 2 ТОГДА
	|			Остатки.Субконто2 = &ЗначениеДляОтбораПоДополнительномуСубконто
	|		КОГДА &НакладыватьОтборПоДополнительномуСубконто И ТаблицаРеальныхДополнительныхСубконто.НомерРеальногоДополнительногоСубконто = 3 ТОГДА
	|			Остатки.Субконто3 = &ЗначениеДляОтбораПоДополнительномуСубконто
	|		КОГДА &НакладыватьОтборПоДополнительномуСубконто И ТаблицаРеальныхДополнительныхСубконто.НомерРеальногоДополнительногоСубконто = 0 ТОГДА
	|			ЛОЖЬ
	|		ИНАЧЕ
	|			ИСТИНА
	|	КОНЕЦ
	|	// Отбор по номенклатуре
	|	И НЕ &НакладыватьОтборПоНоменклатуре ИЛИ ВЫБОР
	|		КОГДА ТИПЗНАЧЕНИЯ(Остатки.Субконто1) = ТИП(Справочник.Номенклатура) И НЕ Остатки.Субконто1 В (&ЗначенияДляОтбораПоНоменклатуре) ТОГДА
	|			ЛОЖЬ
	|		КОГДА ТИПЗНАЧЕНИЯ(Остатки.Субконто2) = ТИП(Справочник.Номенклатура) И НЕ Остатки.Субконто2 В (&ЗначенияДляОтбораПоНоменклатуре) ТОГДА
	|			ЛОЖЬ
	|		КОГДА ТИПЗНАЧЕНИЯ(Остатки.Субконто3) = ТИП(Справочник.Номенклатура) И НЕ Остатки.Субконто3 В (&ЗначенияДляОтбораПоНоменклатуре) ТОГДА
	|			ЛОЖЬ
	|		ИНАЧЕ
	|			ИСТИНА
	|	КОНЕЦ
	|СГРУППИРОВАТЬ ПО
	|	Остатки.Счет,
	|	Остатки.КФО,
	|	ЕСТЬNULL(Остатки.КПС,ЗНАЧЕНИЕ(Справочник.КлассификационныеПризнакиСчетов.ПустаяСсылка)),
	|	ВЫБОР
	|		КОГДА &ИспользоватьУчетПоИФО ТОГДА
	|			Остатки.ИФО
	|		ИНАЧЕ
	|			ЗНАЧЕНИЕ(Справочник.ИсточникиФинансовогоОбеспечения.ПустаяСсылка)
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ТИПЗНАЧЕНИЯ(Остатки.Субконто1) = ТИП(Справочник.Номенклатура) ТОГДА
	|			ВЫРАЗИТЬ(Остатки.Субконто1	КАК Справочник.Номенклатура)
	|		КОГДА ТИПЗНАЧЕНИЯ(Остатки.Субконто2) = ТИП(Справочник.Номенклатура) ТОГДА
	|			ВЫРАЗИТЬ(Остатки.Субконто2	КАК Справочник.Номенклатура)
	|		КОГДА ТИПЗНАЧЕНИЯ(Остатки.Субконто3) = ТИП(Справочник.Номенклатура) ТОГДА
	|			ВЫРАЗИТЬ(Остатки.Субконто3	КАК Справочник.Номенклатура)
	|		ИНАЧЕ
	|			ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА Субконто1 В (&ЗначенияДляОтбораПоМОЛ) ТОГДА
	|			Субконто1
	|		КОГДА Субконто2 В (&ЗначенияДляОтбораПоМОЛ) ТОГДА
	|			Субконто2
	|		КОГДА Субконто3 В (&ЗначенияДляОтбораПоМОЛ) ТОГДА
	|			Субконто3
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА &СледуетИспользоватьДополнительноеСубконто И ТаблицаРеальныхДополнительныхСубконто.НомерРеальногоДополнительногоСубконто = 1 ТОГДА
	|			Остатки.Субконто1
	|		КОГДА &СледуетИспользоватьДополнительноеСубконто И ТаблицаРеальныхДополнительныхСубконто.НомерРеальногоДополнительногоСубконто = 2 ТОГДА
	|			Остатки.Субконто2
	|		КОГДА &СледуетИспользоватьДополнительноеСубконто И ТаблицаРеальныхДополнительныхСубконто.НомерРеальногоДополнительногоСубконто = 3 ТОГДА
	|			Остатки.Субконто3
	|		ИНАЧЕ
	|			НЕОПРЕДЕЛЕНО
	|	КОНЕЦ

	|" + ?(ВыбиратьНулевоеКоличество, "", "
	|ИМЕЮЩИЕ
	|	СУММА(Остатки.КоличествоРазвернутыйОстатокДт - Остатки.КоличествоРазвернутыйОстатокКт) <> 0") + "

	|ИНДЕКСИРОВАТЬ ПО
	|	Счет,
	|	КФО,
	|	КПС,
	|	ИФО,
	|	Субконто3,
	|	Номенклатура
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|// [2]. Данные для заполнения таблицы ПорядокСписания
	|//		(Финальная таблица)
	|//
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ВЫБОР КОГДА &ИспользуетсяКолонкаИФО			ТОГДА ИФО		ИНАЧЕ НЕОПРЕДЕЛЕНО КОНЕЦ	КАК ИФО,
	|	ВЫБОР КОГДА &ИспользуетсяКолонкаКФО			ТОГДА КФО		ИНАЧЕ НЕОПРЕДЕЛЕНО КОНЕЦ	КАК КФО,
	|	ВЫБОР КОГДА &ИспользуетсяКолонкаЦМО			ТОГДА МОЛ		ИНАЧЕ НЕОПРЕДЕЛЕНО КОНЕЦ	КАК ЦМО,
	|	ВЫБОР КОГДА &ИспользуетсяКолонкаКПС			ТОГДА КПС		ИНАЧЕ НЕОПРЕДЕЛЕНО КОНЕЦ	КАК КПС,
	|	ВЫБОР КОГДА &ИспользуетсяКолонкаСчет		ТОГДА Счет		ИНАЧЕ НЕОПРЕДЕЛЕНО КОНЕЦ	КАК Счет,
	|	ВЫБОР КОГДА &ИспользуетсяКолонкаСубконто3	ТОГДА Субконто3	ИНАЧЕ НЕОПРЕДЕЛЕНО КОНЕЦ	КАК Субконто3
	|ИЗ
	|	ОстаткиСОтборомПоМОЛ	КАК Остатки
	|;";
    #Вставка     
	Если ПараметрыНакладываемогоОтбора.Свойство("МассивНоменклатур") Тогда
		
		УсловиеНоменклатуры = 	"		И ВЫБОР
		|			КОГДА Субконто1 В (&МассивНоменклатур) ТОГДА
		|				ИСТИНА
		|			КОГДА Субконто2 В (&МассивНоменклатур) ТОГДА
		|				ИСТИНА
		|			КОГДА Субконто3 В (&МассивНоменклатур) ТОГДА
		|				ИСТИНА
		|			ИНАЧЕ
		|				ЛОЖЬ
		|		КОНЕЦ";
		
		
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса,"%УсловиеНоменклатуры%",УсловиеНоменклатуры);
		ЗапросОстатки.УстановитьПараметр("МассивНоменклатур",ПараметрыНакладываемогоОтбора.МассивНоменклатур);
	Иначе 
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса,"%УсловиеНоменклатуры%","");
	КонецЕсли;
	#КонецВставки
	Если ИспользоватьБалансовуюСтоимость Тогда

		Если ВариантФормированияОписи = 3 Тогда
			// 3. Опись с 2018 г. Счет в таблице (5 знаков). Одна строка - один объект

			ТекстЗапросаБлижайшиеОбщиеРодителиСчетов = ИнвентаризацияСервер.ПолучитьТекстЗапросаБлижайшиеОбщиеРодителиСчетов(
			"А_ОстаткиСОтборомПоМОЛ",
			"Номенклатура",
			,
			СдвигИндексаТаблицыМатериалы);
		Иначе
			ТекстЗапросаБлижайшиеОбщиеРодителиСчетов = "";

			СдвигИндексаТаблицыМатериалы = 0;
		КонецЕсли;

		//------------------------------------------------------------------------------
		//	Вариант А.
		//	Заполнение сумм по балансовой стоимости
		//------------------------------------------------------------------------------
		ТекстЗапроса = ТекстЗапроса + "
		|////////////////////////////////////////////////////////////////////////////////
		|// [3А]. Все остатки по полным счетам, без отбора по МОЛ.
		|//		Т.к. отбор по МОЛ не применяется, то в остатках будет заполнена сумма,
		|//		вне зависимости от ведения суммового учета по субконто ЦМО или Контрагенты
		|//
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	Остатки.Счет																			КАК Счет,
		|	Остатки.КФО																				КАК КФО,
		|	ЕСТЬNULL(Остатки.КПС,ЗНАЧЕНИЕ(Справочник.КлассификационныеПризнакиСчетов.ПустаяСсылка))	КАК КПС,
		|	ВЫБОР
		|		КОГДА &ИспользоватьУчетПоИФО ТОГДА
		|			Остатки.ИФО
		|		ИНАЧЕ
		|			ЗНАЧЕНИЕ(Справочник.ИсточникиФинансовогоОбеспечения.ПустаяСсылка)
		|	КОНЕЦ																					КАК ИФО,
		|	ВЫБОР
		|		КОГДА ТИПЗНАЧЕНИЯ(Остатки.Субконто1) = ТИП(Справочник.Номенклатура) ТОГДА
		|			ВЫРАЗИТЬ(Остатки.Субконто1	КАК Справочник.Номенклатура)
		|		КОГДА ТИПЗНАЧЕНИЯ(Остатки.Субконто2) = ТИП(Справочник.Номенклатура) ТОГДА
		|			ВЫРАЗИТЬ(Остатки.Субконто2	КАК Справочник.Номенклатура)
		|		КОГДА ТИПЗНАЧЕНИЯ(Остатки.Субконто3) = ТИП(Справочник.Номенклатура) ТОГДА
		|			ВЫРАЗИТЬ(Остатки.Субконто3	КАК Справочник.Номенклатура)
		|		ИНАЧЕ
		|			ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)
		|	КОНЕЦ																					КАК Номенклатура,
		|	ВЫБОР
		|		КОГДА &СледуетИспользоватьДополнительноеСубконто И ТаблицаРеальныхДополнительныхСубконто.НомерРеальногоДополнительногоСубконто = 1 ТОГДА
		|			Остатки.Субконто1
		|		КОГДА &СледуетИспользоватьДополнительноеСубконто И ТаблицаРеальныхДополнительныхСубконто.НомерРеальногоДополнительногоСубконто = 2 ТОГДА
		|			Остатки.Субконто2
		|		КОГДА &СледуетИспользоватьДополнительноеСубконто И ТаблицаРеальныхДополнительныхСубконто.НомерРеальногоДополнительногоСубконто = 3 ТОГДА
		|			Остатки.Субконто3
		|		ИНАЧЕ
		|			НЕОПРЕДЕЛЕНО
		|	КОНЕЦ																					КАК Субконто3,
		|	СУММА(Остатки.КоличествоРазвернутыйОстатокДт - Остатки.КоличествоРазвернутыйОстатокКт)	КАК КоличествоУчет,
		|	СУММА(Остатки.СуммаРазвернутыйОстатокДт - Остатки.СуммаРазвернутыйОстатокКт)			КАК СуммаУчет
		|ПОМЕСТИТЬ
		|	А_ОстаткиБезОтбораПоМОЛ
		|ИЗ
		|	РегистрБухгалтерии.ЖурналПроводокЕПСБУ.Остатки(
		|		&ДатаЗапроса,				// 1 Период
		|		Счет В (&СчетаОстатков),	// 2 Условие счета
		|		,							// 3 Виды субконто
		|		Организация = &Организация	// 4 Условие
		|		И ВЫБОР 
		|			КОГДА &НакладыватьОтборПоИФО ТОГДА
		|				ИФО = &ИФО
		|			ИНАЧЕ
		|				ИСТИНА
		|		КОНЕЦ
		|		И ВЫБОР 
		|			КОГДА &НакладыватьОтборПоКФО ТОГДА
		|				КФО = &КФО
		|			ИНАЧЕ
		|				ИСТИНА
		|		КОНЕЦ
		|		И ВЫБОР
		|			КОГДА &НакладыватьОтборПоКПС ТОГДА
		|				КПС = &КПС
		|			ИНАЧЕ
		|				ИСТИНА
		|		КОНЕЦ
		|		И ВЫБОР 
		|			КОГДА &НакладыватьОтборПоПодразделению ТОГДА
		|				Подразделение = &Подразделение
		|			ИНАЧЕ
		|				ИСТИНА
		|		КОНЕЦ
		|	)	КАК Остатки
		|	ЛЕВОЕ СОЕДИНЕНИЕ	ТаблицаРеальныхДополнительныхСубконто
		|		ПО	Остатки.Счет = ТаблицаРеальныхДополнительныхСубконто.Счет
		|ГДЕ
		|	// Только заполненная номенклатура
		|	ВЫБОР
		|		КОГДА ТИПЗНАЧЕНИЯ(Остатки.Субконто1) = ТИП(Справочник.Номенклатура) ТОГДА
		|			НЕ Остатки.Субконто1 = &ПустаяНоменклатура
		|		КОГДА ТИПЗНАЧЕНИЯ(Остатки.Субконто2) = ТИП(Справочник.Номенклатура) ТОГДА
		|			НЕ Остатки.Субконто2 = &ПустаяНоменклатура
		|		КОГДА ТИПЗНАЧЕНИЯ(Остатки.Субконто3) = ТИП(Справочник.Номенклатура) ТОГДА
		|			НЕ Остатки.Субконто3 = &ПустаяНоменклатура
		|		ИНАЧЕ
		|			ЛОЖЬ
		|	КОНЕЦ
		|	// Отбор по дополнительному субконто
		|	И ВЫБОР
		|		КОГДА &НакладыватьОтборПоДополнительномуСубконто И ТаблицаРеальныхДополнительныхСубконто.НомерРеальногоДополнительногоСубконто = 1 ТОГДА
		|			Остатки.Субконто1 = &ЗначениеДляОтбораПоДополнительномуСубконто
		|		КОГДА &НакладыватьОтборПоДополнительномуСубконто И ТаблицаРеальныхДополнительныхСубконто.НомерРеальногоДополнительногоСубконто = 2 ТОГДА
		|			Остатки.Субконто2 = &ЗначениеДляОтбораПоДополнительномуСубконто
		|		КОГДА &НакладыватьОтборПоДополнительномуСубконто И ТаблицаРеальныхДополнительныхСубконто.НомерРеальногоДополнительногоСубконто = 3 ТОГДА
		|			Остатки.Субконто3 = &ЗначениеДляОтбораПоДополнительномуСубконто
		|		КОГДА &НакладыватьОтборПоДополнительномуСубконто И ТаблицаРеальныхДополнительныхСубконто.НомерРеальногоДополнительногоСубконто = 0 ТОГДА
		|			ЛОЖЬ
		|		ИНАЧЕ
		|			ИСТИНА
		|	КОНЕЦ
		|СГРУППИРОВАТЬ ПО
		|	Остатки.Счет,
		|	Остатки.КФО,
		|	ЕСТЬNULL(Остатки.КПС,ЗНАЧЕНИЕ(Справочник.КлассификационныеПризнакиСчетов.ПустаяСсылка)),
		|	ВЫБОР
		|		КОГДА &ИспользоватьУчетПоИФО ТОГДА
		|			Остатки.ИФО
		|		ИНАЧЕ
		|			ЗНАЧЕНИЕ(Справочник.ИсточникиФинансовогоОбеспечения.ПустаяСсылка)
		|	КОНЕЦ,
		|	ВЫБОР
		|		КОГДА ТИПЗНАЧЕНИЯ(Остатки.Субконто1) = ТИП(Справочник.Номенклатура) ТОГДА
		|			ВЫРАЗИТЬ(Остатки.Субконто1	КАК Справочник.Номенклатура)
		|		КОГДА ТИПЗНАЧЕНИЯ(Остатки.Субконто2) = ТИП(Справочник.Номенклатура) ТОГДА
		|			ВЫРАЗИТЬ(Остатки.Субконто2	КАК Справочник.Номенклатура)
		|		КОГДА ТИПЗНАЧЕНИЯ(Остатки.Субконто3) = ТИП(Справочник.Номенклатура) ТОГДА
		|			ВЫРАЗИТЬ(Остатки.Субконто3	КАК Справочник.Номенклатура)
		|		ИНАЧЕ
		|			ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)
		|	КОНЕЦ,
		|	ВЫБОР
		|		КОГДА &СледуетИспользоватьДополнительноеСубконто И ТаблицаРеальныхДополнительныхСубконто.НомерРеальногоДополнительногоСубконто = 1 ТОГДА
		|			Остатки.Субконто1
		|		КОГДА &СледуетИспользоватьДополнительноеСубконто И ТаблицаРеальныхДополнительныхСубконто.НомерРеальногоДополнительногоСубконто = 2 ТОГДА
		|			Остатки.Субконто2
		|		КОГДА &СледуетИспользоватьДополнительноеСубконто И ТаблицаРеальныхДополнительныхСубконто.НомерРеальногоДополнительногоСубконто = 3 ТОГДА
		|			Остатки.Субконто3
		|		ИНАЧЕ
		|			НЕОПРЕДЕЛЕНО
		|	КОНЕЦ

		|" + ?(ВыбиратьНулевоеКоличество, "", "
		|ИМЕЮЩИЕ
		|	СУММА(Остатки.КоличествоРазвернутыйОстатокДт - Остатки.КоличествоРазвернутыйОстатокКт) <> 0") + "

		|ИНДЕКСИРОВАТЬ ПО
		|	Счет,
		|	КФО,
		|	КПС,
		|	ИФО,
		|	Субконто3,
		|	Номенклатура
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|// [4А]. Суммы остатков номенклатуры (балансовая стоимость) по выбранному МОЛ
		|//
		|ВЫБРАТЬ
		|	ОстаткиБезОтбораПоМОЛ.Счет					КАК Счет,
		|	ОстаткиБезОтбораПоМОЛ.КФО					КАК КФО,
		|	ОстаткиБезОтбораПоМОЛ.КПС					КАК КПС,
		|	ОстаткиБезОтбораПоМОЛ.ИФО					КАК ИФО,
		|	ОстаткиБезОтбораПоМОЛ.Субконто3				КАК Субконто3,
		|	ОстаткиБезОтбораПоМОЛ.Номенклатура			КАК Номенклатура,
		|	ОстаткиСОтборомПоМОЛ.МОЛ					КАК МОЛ,
		|	СУММА(ОстаткиСОтборомПоМОЛ.КоличествоУчет)	КАК КоличествоУчет,
		|	
		|	// Балансовая стоимость
		|	СУММА(ВЫБОР
		|		КОГДА ОстаткиСОтборомПоМОЛ.СуммаУчет <> 0 ТОГДА
		|			ОстаткиСОтборомПоМОЛ.СуммаУчет
		|		КОГДА ОстаткиБезОтбораПоМОЛ.КоличествоУчет = 0 ТОГДА
		|			0
		|		ИНАЧЕ
		|			ОстаткиБезОтбораПоМОЛ.СуммаУчет * ОстаткиСОтборомПоМОЛ.КоличествоУчет / ОстаткиБезОтбораПоМОЛ.КоличествоУчет
		|	КОНЕЦ)										КАК СуммаУчетБалансовая
		|ПОМЕСТИТЬ
		|	А_ОстаткиСОтборомПоМОЛ
		|ИЗ
		|	А_ОстаткиБезОтбораПоМОЛ	КАК ОстаткиБезОтбораПоМОЛ
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ	ОстаткиСОтборомПоМОЛ	КАК ОстаткиСОтборомПоМОЛ
		|		ПО	ОстаткиБезОтбораПоМОЛ.Счет			= ОстаткиСОтборомПоМОЛ.Счет
		|		И	ОстаткиБезОтбораПоМОЛ.КФО			= ОстаткиСОтборомПоМОЛ.КФО
		|		И	ОстаткиБезОтбораПоМОЛ.КПС			= ОстаткиСОтборомПоМОЛ.КПС
		|		И	ОстаткиБезОтбораПоМОЛ.ИФО			= ОстаткиСОтборомПоМОЛ.ИФО
		|		И	ОстаткиБезОтбораПоМОЛ.Субконто3		= ОстаткиСОтборомПоМОЛ.Субконто3
		|		И	ОстаткиБезОтбораПоМОЛ.Номенклатура	= ОстаткиСОтборомПоМОЛ.Номенклатура
		|СГРУППИРОВАТЬ ПО
		|	ОстаткиБезОтбораПоМОЛ.Счет,
		|	ОстаткиБезОтбораПоМОЛ.КФО,
		|	ОстаткиБезОтбораПоМОЛ.КПС,
		|	ОстаткиБезОтбораПоМОЛ.ИФО,
		|	ОстаткиБезОтбораПоМОЛ.Субконто3,
		|	ОстаткиБезОтбораПоМОЛ.Номенклатура,
		|	ОстаткиСОтборомПоМОЛ.МОЛ
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|// [5А]. Данные для заполнения таблицы Остатки (балансовая стоимость)
		|//		(Финальная таблица)
		|//
		|ВЫБРАТЬ
		|	Счет						КАК Счет,
		|	ИФО							КАК ИФО,
		|	КФО							КАК КФО,
		|	КПС							КАК КПС,
		|	МОЛ							КАК ЦМО,
		|	Субконто3					КАК Субконто3,
		|	Номенклатура				КАК Номенклатура,
		|	СУММА(КоличествоУчет)		КАК КоличествоУчет,
		|	СУММА(СуммаУчетБалансовая)	КАК СуммаУчет
		|ИЗ
		|	А_ОстаткиСОтборомПоМОЛ
		|СГРУППИРОВАТЬ ПО
		|	Счет,
		|	ИФО,
		|	КФО,
		|	КПС,
		|	МОЛ,
		|	Субконто3,
		|	Номенклатура
		|УПОРЯДОЧИТЬ ПО
		|	СуммаУчет УБЫВ
		|;
		|"

		+ ТекстЗапросаБлижайшиеОбщиеРодителиСчетов

		+ "
		|////////////////////////////////////////////////////////////////////////////////
		|// [6А]. Данные для заполнения таблицы Материалы (балансовая стоимость)
		|//		(Финальная таблица)
		|//
		|ВЫБРАТЬ
		|	Субконто3									КАК Субконто3,
		|	Номенклатура								КАК Номенклатура,
		|	КоличествоУчет								КАК КоличествоУчет,
		|	ВЫБОР
		|		КОГДА &НеЗаполнятьЦену ТОГДА
		|			0
		|		КОГДА КоличествоУчет = 0 ТОГДА
		|			0
		|		ИНАЧЕ
		|			СуммаУчетБалансовая / КоличествоУчет
		|	КОНЕЦ										КАК Цена,
		|	СуммаУчетБалансовая							КАК СуммаУчет,
		|		
		|	Счет										КАК Счет,
		|	КПС											КАК КПС,
		|	КФО											КАК КФО
		|ИЗ
		|	("

		+ ПолучитьТекстЗапросаСвернутыеОстаткиДляТаблицыМатериалы(
		ВариантФормированияОписи,
		ИспользоватьБалансовуюСтоимость)

		+ ")	КАК СвернутыеОстатки
		|УПОРЯДОЧИТЬ ПО
		|	Номенклатура.Наименование
		|;";

		// [5А]. Данные для заполнения таблицы Остатки (балансовая стоимость)
		ИндексТаблицыОстатки = 5;

		// [6А]. Данные для заполнения таблицы Материалы (балансовая стоимость)
		ИндексТаблицыМатериалы = 6 + СдвигИндексаТаблицыМатериалы;

	Иначе
		//------------------------------------------------------------------------------
		//	Вариант Б.
		//	Заполнение сумм по указанному типу цен номенклатуры
		//------------------------------------------------------------------------------

		Если ВариантФормированияОписи = 3 Тогда
			// 3. Опись с 2018 г. Счет в таблице (5 знаков). Одна строка - один объект

			ТекстЗапросаБлижайшиеОбщиеРодителиСчетов = ИнвентаризацияСервер.ПолучитьТекстЗапросаБлижайшиеОбщиеРодителиСчетов(
			"ОстаткиСОтборомПоМОЛ",
			"Номенклатура",
			,
			СдвигИндексаТаблицыМатериалы);
		Иначе
			ТекстЗапросаБлижайшиеОбщиеРодителиСчетов = "";

			СдвигИндексаТаблицыМатериалы = 0;
		КонецЕсли;

		ТекстЗапроса = ТекстЗапроса + "
		|////////////////////////////////////////////////////////////////////////////////
		|// [3Б]. Установленные цены номенклатуры
		|//
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	Номенклатура	КАК Номенклатура,
		|	Цена			КАК Цена
		|ПОМЕСТИТЬ
		|	Б_ЦеныНоменклатуры
		|ИЗ
		|	РегистрСведений.ЦеныНоменклатуры.СрезПоследних(
		|		&ДатаЗапроса,
		|		Организация = &Организация
		|		И	ТипЦен = &ТипЦен
		|		И	Номенклатура В
		|			(ВЫБРАТЬ
		|				Номенклатура
		|			ИЗ
		|				ОстаткиСОтборомПоМОЛ)
		|	)	КАК ЦеныНоменклатурыСрезПоследних
		|ИНДЕКСИРОВАТЬ ПО
		|	Номенклатура
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|// [4Б]. Данные для заполнения таблицы Остатки (указанный тип цен)
		|//		(Финальная таблица)
		|//
		|ВЫБРАТЬ
		|	Остатки.Счет								КАК Счет,
		|	Остатки.ИФО									КАК ИФО,
		|	Остатки.КФО									КАК КФО,
		|	Остатки.КПС									КАК КПС,
		|	Остатки.МОЛ									КАК ЦМО,
		|	Остатки.Субконто3							КАК Субконто3,
		|	Остатки.Номенклатура						КАК Номенклатура,
		|	СУММА(Остатки.КоличествоУчет)				КАК КоличествоУчет,
		|	СУММА(ВЫБОР
		|		КОГДА Остатки.КоличествоУчет = 0 И &ЗаменятьНулевоеКоличество ТОГДА
		|			ЕСТЬNULL(Цены.Цена, 0) * &КоличествоПоУмолчанию
		|		ИНАЧЕ
		|			ЕСТЬNULL(Цены.Цена, 0) * Остатки.КоличествоУчет
		|	КОНЕЦ)										КАК СуммаУчет
		|ИЗ
		|	ОстаткиСОтборомПоМОЛ	КАК Остатки
		|	ЛЕВОЕ СОЕДИНЕНИЕ	Б_ЦеныНоменклатуры	КАК Цены
		|		ПО	Остатки.Номенклатура = Цены.Номенклатура
		|СГРУППИРОВАТЬ ПО
		|	Остатки.Счет,
		|	Остатки.ИФО,
		|	Остатки.КФО,
		|	Остатки.КПС,
		|	Остатки.МОЛ,
		|	Остатки.Субконто3,
		|	Остатки.Номенклатура
		|УПОРЯДОЧИТЬ ПО
		|	Остатки.Номенклатура.Наименование
		|;
		|"

		+ ТекстЗапросаБлижайшиеОбщиеРодителиСчетов

		+ "
		|////////////////////////////////////////////////////////////////////////////////
		|// [5Б]. Данные для заполнения таблицы Материалы (указанный тип цен)
		|//		(Финальная таблица)
		|//
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	Субконто3									КАК Субконто3,
		|	Номенклатура								КАК Номенклатура,
		|	КоличествоУчет								КАК КоличествоУчет,
		|	ВЫБОР
		|		КОГДА &НеЗаполнятьЦену ТОГДА
		|			0
		|		ИНАЧЕ
		|			Цена
		|	КОНЕЦ										КАК Цена,
		|	СуммаУчет									КАК СуммаУчет,
		|	
		|	Счет										КАК Счет,
		|	КПС											КАК КПС,
		|	КФО											КАК КФО
		|ИЗ
		|	("

		+ ПолучитьТекстЗапросаСвернутыеОстаткиДляТаблицыМатериалы(
		ВариантФормированияОписи,
		ИспользоватьБалансовуюСтоимость)

		+ ")	КАК СвернутыеОстаткиПОВыбраннымЦМО
		|УПОРЯДОЧИТЬ ПО
		|	Номенклатура.Наименование
		|;";

		// [4Б]. Данные для заполнения таблицы Остатки (указанный тип цен)
		ИндексТаблицыОстатки = 4;

		// [5Б]. Данные для заполнения таблицы Материалы (указанный тип цен)
		ИндексТаблицыМатериалы = 5 + СдвигИндексаТаблицыМатериалы;

	КонецЕсли;

	ЗапросОстатки.Текст = ТекстЗапроса;

	Результаты = ЗапросОстатки.ВыполнитьПакет();

	// [2]. Данные для заполнения таблицы ПорядокСписания
	ДокументОбъект["ПорядокСписания"].Загрузить(Результаты[2].Выгрузить());

	// [Х1]. Данные для заполнения таблицы Материалы
	ТаблицаМатериалы = Результаты[ИндексТаблицыМатериалы].Выгрузить();

	ИменаКолонок = Неопределено;
	ИнвентаризацияСервер.Редактирование0504087_СоздатьКолонкиЧисловыхПоказателей(ТаблицаМатериалы, ДокументОбъект.Ссылка, ИменаКолонок);

	Если ВыбиратьНулевоеКоличество И ЗаменятьНулевоеКоличество Тогда
		ИнвентаризацияСервер.УстановитьКоличествоПоУмолчанию(
		ТаблицаМатериалы,
		ИменаКолонок,
		ИспользоватьБалансовуюСтоимость,
		ДокументОбъект["НеЗаполнятьЦену"],
		Истина);
	КонецЕсли;

	ИнвентаризацияСервер.Редактирование0504087_РассчитатьРасхождениеВТаблице(ТаблицаМатериалы, ИменаКолонок);

	ДокументОбъект["Материалы"].Загрузить(ТаблицаМатериалы);

	// [Х2]. Данные для заполнения таблицы Остатки
	ТаблицаОстатки = Результаты[ИндексТаблицыОстатки].Выгрузить();

	Если ВыбиратьНулевоеКоличество И ЗаменятьНулевоеКоличество Тогда
		ИнвентаризацияСервер.УстановитьКоличествоПоУмолчанию(
		ТаблицаОстатки,
		ИменаКолонок);
	КонецЕсли;

	ДокументОбъект["Остатки"].Загрузить(ТаблицаОстатки);

КонецПроцедуры
