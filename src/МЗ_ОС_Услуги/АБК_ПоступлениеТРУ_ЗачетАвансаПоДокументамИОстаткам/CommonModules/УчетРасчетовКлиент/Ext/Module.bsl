
&ИзменениеИКонтроль("СпособЗачетаАвансаПриИзменении")
Процедура АБК_СпособЗачетаАвансаПриИзменении(Элемент, Форма)

	Объект = Форма.Объект;
	Если Объект.СпособЗачетаАванса = ПредопределенноеЗначение("Перечисление.СпособыЗачетаАвансов.НеЗачитывать")
		ИЛИ Объект.СпособЗачетаАванса = ПредопределенноеЗначение("Перечисление.СпособыЗачетаАвансов.Автоматически") Тогда
		Объект.ЗачетАвансов.Очистить();  
		#Вставка
		Объект.ЗачетАвансовПоДокументамИОстаткам.Очистить();
	ИначеЕсли Объект.СпособЗачетаАванса = ПредопределенноеЗначение("Перечисление.СпособыЗачетаАвансов.ПоДокументу") Тогда
		Объект.ЗачетАвансовПоДокументамИОстаткам.Очистить();
	ИначеЕсли Объект.СпособЗачетаАванса = ПредопределенноеЗначение("Перечисление.СпособыЗачетаАвансов.ПоДокументамИОстаткам") Тогда
		Объект.ЗачетАвансов.Очистить();  
		#КонецВставки
	КонецЕсли;

	УчетРасчетовКлиентСервер.УправлениеЭлементамиЗачетаАвансов(Форма);

КонецПроцедуры

Процедура АБК_ДокументАвансаПоДокументамИОстаткамНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка, Форма, Объект) Экспорт
	
	СтандартнаяОбработка = Ложь;
	
	Параметры = Новый Структура;
	Параметры.Вставить("Организация", Объект.Организация);
	Параметры.Вставить("Контрагент", Объект.Контрагент);
	Параметры.Вставить("Договор", Объект.Договор);
	Параметры.Вставить("РежимВыбора", Истина);
	Если Форма.Модифицированность ИЛИ Объект.Ссылка.Пустая() Тогда
		Параметры.Вставить("Период", КонецДня(Объект.Дата));
	Иначе
		Параметры.Вставить("Период", Объект.Ссылка);
	КонецЕсли;   
	
	//АпогейБК
	Параметры.Вставить("ПодборПоДокументамИОстаткам", Истина);
	//АпогейБК
	
	ОткрытьФорму("Обработка.ПодборДокументовАвансов.Форма", Параметры, Элемент);
	
КонецПроцедуры

Процедура АБК_ДокументАвансаПоДокументамИОстаткамОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка, Форма, Объект) Экспорт

	Если ТипЗнч(ВыбранноеЗначение) = Тип("Структура") И Форма.Элементы.ЗачетАвансовПоДокументамИОстаткам.ТекущиеДанные <> Неопределено Тогда
		ЗаполнитьЗначенияСвойств(Форма.Элементы.ЗачетАвансовПоДокументамИОстаткам.ТекущиеДанные, ВыбранноеЗначение);
		ВыбранноеЗначение = ВыбранноеЗначение.ДокументАванса;
	КонецЕсли;

КонецПроцедуры
