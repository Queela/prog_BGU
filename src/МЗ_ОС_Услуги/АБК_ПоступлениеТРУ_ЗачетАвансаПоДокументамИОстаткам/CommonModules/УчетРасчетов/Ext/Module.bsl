&ИзменениеИКонтроль("ПустаяТаблицаЗачетАвансовПоДокументу")
Функция АБК_ПустаяТаблицаЗачетАвансовПоДокументу()

	ОписаниеТипаСуммы = Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(18, 2, ДопустимыйЗнак.Любой));
	#Вставка
	ОписаниеТиповСубконто = Метаданные.ПланыВидовХарактеристик.ВидыСубконто.Тип;
	#КонецВставки

	ТаблицаЗачетаАвансов = Новый ТаблицаЗначений;
	// Идентификация строки.
	ТаблицаЗачетаАвансов.Колонки.Добавить("НомерСтроки",    Новый ОписаниеТипов("Число"));
	// Признак поступления ценностей: материальных запасов, услуг и пр.
	ТаблицаЗачетаАвансов.Колонки.Добавить("ЭтоПоступление", Новый ОписаниеТипов("Булево"));
	// Общие разрезы учета.
	ТаблицаЗачетаАвансов.Колонки.Добавить("ИФО",            Новый ОписаниеТипов("СправочникСсылка.ИсточникиФинансовогоОбеспечения"));
	ТаблицаЗачетаАвансов.Колонки.Добавить("КФО",            Новый ОписаниеТипов("ПеречислениеСсылка.КВД"));
	ТаблицаЗачетаАвансов.Колонки.Добавить("КПС",            Новый ОписаниеТипов("СправочникСсылка.КлассификационныеПризнакиСчетов"));
	ТаблицаЗачетаАвансов.Колонки.Добавить("Подразделение",  Новый ОписаниеТипов("СправочникСсылка.Подразделения"));
	// Счет и аналитика учета расчетов с контрагентами.
	ТаблицаЗачетаАвансов.Колонки.Добавить("КЭК",            Новый ОписаниеТипов("СправочникСсылка.КОСГУ"));
	ТаблицаЗачетаАвансов.Колонки.Добавить("СчетРасчетов",   Новый ОписаниеТипов("ПланСчетовСсылка.ЕПСБУ"));
	ТаблицаЗачетаАвансов.Колонки.Добавить("СчетАвансов",    Новый ОписаниеТипов("ПланСчетовСсылка.ЕПСБУ"));
	ТаблицаЗачетаАвансов.Колонки.Добавить("Контрагент",     Новый ОписаниеТипов("СправочникСсылка.Контрагенты"));
	ТаблицаЗачетаАвансов.Колонки.Добавить("Договор",        Новый ОписаниеТипов("СправочникСсылка.Договоры"));
	ТаблицаЗачетаАвансов.Колонки.Добавить("Валюта",         Новый ОписаниеТипов("СправочникСсылка.Валюты"));
	ТаблицаЗачетаАвансов.Колонки.Добавить("ДокументАванса", ПланыВидовХарактеристик.ВидыСубконто.ДокументыРасчетов.ТипЗначения);
	// Сумма расчетов для зачета авансов.
	ТаблицаЗачетаАвансов.Колонки.Добавить("СуммаРасчетов",  ОписаниеТипаСуммы);    
	#Вставка
	ТаблицаЗачетаАвансов.Колонки.Добавить("ДопСубконтоАвансов",  ОписаниеТиповСубконто);    
	ТаблицаЗачетаАвансов.Колонки.Добавить("ВидДопСубконтоАвансов",  Новый ОписаниеТипов("ПланВидовХарактеристикСсылка.ВидыСубконто"));    
	#КонецВставки

	Возврат ТаблицаЗачетаАвансов;

КонецФункции

&ИзменениеИКонтроль("ПодготовитьТаблицуЗачетАвансовПоДокументу")
Функция АБК_ПодготовитьТаблицуЗачетАвансовПоДокументу(Документ, Параметры)

	ТаблицаЗачетаАвансов = ПустаяТаблицаЗачетАвансовПоДокументу();

	МетаданныеДокумента = Документ.Метаданные();
	Если НЕ ОбщегоНазначенияБГУ.ЕстьРеквизитДокумента("СпособЗачетаАванса", МетаданныеДокумента)
		#Вставка
		ИЛИ НЕ (ОбщегоНазначенияБГУ.ЕстьТабЧастьДокумента("ЗачетАвансовПоДокументамИОстаткам", МетаданныеДокумента)
		ИЛИ ОбщегоНазначенияБГУ.ЕстьТабЧастьДокумента("ЗачетАвансов", МетаданныеДокумента)) Тогда  
		#КонецВставки    
		#Удаление
		ИЛИ НЕ ОбщегоНазначенияБГУ.ЕстьТабЧастьДокумента("ЗачетАвансов", МетаданныеДокумента) Тогда  
		#КонецУдаления
		Возврат ТаблицаЗачетаАвансов;      
		#Удаление
	ИначеЕсли Документ.СпособЗачетаАванса <> Перечисления.СпособыЗачетаАвансов.ПоДокументу Тогда   
		#КонецУдаления  
		#Вставка
	ИначеЕсли Документ.СпособЗачетаАванса <> Перечисления.СпособыЗачетаАвансов.ПоДокументу
		И Документ.СпособЗачетаАванса <> Перечисления.СпособыЗачетаАвансов.ПоДокументамИОстаткам Тогда   
		#КонецВставки    
		Возврат ТаблицаЗачетаАвансов;
	КонецЕсли;

	Если Параметры = Неопределено Тогда
		Параметры = Новый Структура;
	КонецЕсли;

	Запрос = Новый Запрос;

	ОбщегоНазначенияБГУ.ПоместитьТабЧастьВВиртуальнуюТаблицу(Запрос, "ЗачетАвансов",
	"НомерСтроки,ИФО,КФО,КПС,КЭК,Подразделение,Контрагент,Договор,ВалютаДокумента,ДокументАванса,СуммаЗачета",
	Документ, Параметры);

	Запрос.Текст = Запрос.Текст +
	"ВЫБРАТЬ
	|	ЗачетАвансов.НомерСтроки КАК НомерСтроки,
	|	ЗачетАвансов.ИФО КАК ИФО,
	|	ЗачетАвансов.КФО КАК КФО,
	|	ЗачетАвансов.КПС КАК КПС,
	|	ЗачетАвансов.КЭК КАК КЭК,
	|	ЗачетАвансов.Подразделение КАК Подразделение,
	|	ЗачетАвансов.Контрагент КАК Контрагент,
	|	ЗачетАвансов.Договор КАК Договор,
	|	ЗачетАвансов.ВалютаДокумента КАК Валюта,
	|	ЗачетАвансов.ДокументАванса КАК ДокументАванса,
	|	ЗачетАвансов.СуммаЗачета КАК СуммаРасчетов
	|ИЗ
	|	ТаблицаЗачетАвансов КАК ЗачетАвансов
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки";

	Если НЕ ОбщегоНазначенияБГУ.ЕстьРеквизитДокумента("ВалютаДокумента", МетаданныеДокумента) Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "ЗачетАвансов.ВалютаДокумента", "&ВалютаРеглУчета");
		Запрос.УстановитьПараметр("ВалютаРеглУчета", БухгалтерскийУчетПовтИсп.ВалютаРегламентированногоУчета());
	КонецЕсли;

	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл

		НоваяСтрока = ТаблицаЗачетаАвансов.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);

		НоваяСтрока.СчетРасчетов = БухгалтерскийУчет.ПолучитьАналитическийСчетПоКЭК(Выборка.КЭК,
		"302.00", , Документ.Дата, Документ.Организация, Выборка.ИФО); // Пока зачет авансов по документам (ручной) предусмотрен только для авансов, выданных поставщикам.

		// Счет авансов определим по счету расчетов.
		НоваяСтрока.СчетАвансов = УчетРасчетов.ОпределитьСчетАвансов(НоваяСтрока.СчетРасчетов, Документ.Дата, Документ.Организация);

		НоваяСтрока.ЭтоПоступление = БухгалтерскийУчетКлиентСервер.СчетВИерархии(НоваяСтрока.СчетРасчетов, "302.00");

	КонецЦикла;   
	
	#Вставка   
	Если ОбщегоНазначенияБГУ.ЕстьТабЧастьДокумента("ЗачетАвансовПоДокументамИОстаткам", МетаданныеДокумента) Тогда 
		
		Запрос = Новый Запрос;
		
		ОбщегоНазначенияБГУ.ПоместитьТабЧастьВВиртуальнуюТаблицу(Запрос, "ЗачетАвансовПоДокументамИОстаткам",
		"НомерСтроки,ИФО,КФО,КПС,КЭК,Подразделение,Контрагент,Договор,ВалютаДокумента,ДокументАванса,ДопСубконтоАвансов,СуммаЗачета",
		Документ, Параметры);
		
		Запрос.Текст = Запрос.Текст +
		"ВЫБРАТЬ
		|	ЗачетАвансов.НомерСтроки КАК НомерСтроки,
		|	ЗачетАвансов.ИФО КАК ИФО,
		|	ЗачетАвансов.КФО КАК КФО,
		|	ЗачетАвансов.КПС КАК КПС,
		|	ЗачетАвансов.КЭК КАК КЭК,
		|	ЗачетАвансов.Подразделение КАК Подразделение,
		|	ЗачетАвансов.Контрагент КАК Контрагент,
		|	ЗачетАвансов.Договор КАК Договор,
		|	ЗачетАвансов.ВалютаДокумента КАК Валюта,
		|	ЗачетАвансов.ДокументАванса КАК ДокументАванса,
		|	ЗачетАвансов.ДопСубконтоАвансов КАК ДопСубконтоАвансов,
		|	ЗачетАвансов.СуммаЗачета КАК СуммаРасчетов
		|ИЗ
		|	ТаблицаЗачетАвансовПоДокументамИОстаткам КАК ЗачетАвансов
		|
		|УПОРЯДОЧИТЬ ПО
		|	НомерСтроки";
		
		Если НЕ ОбщегоНазначенияБГУ.ЕстьРеквизитДокумента("ВалютаДокумента", МетаданныеДокумента) Тогда
			Запрос.Текст = СтрЗаменить(Запрос.Текст, "ЗачетАвансов.ВалютаДокумента", "&ВалютаРеглУчета");
			Запрос.УстановитьПараметр("ВалютаРеглУчета", БухгалтерскийУчетПовтИсп.ВалютаРегламентированногоУчета());
		КонецЕсли;
		
		Выборка = Запрос.Выполнить().Выбрать();
		Пока Выборка.Следующий() Цикл
			
			НоваяСтрока = ТаблицаЗачетаАвансов.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);
			
			НоваяСтрока.СчетРасчетов = БухгалтерскийУчет.ПолучитьАналитическийСчетПоКЭК(Выборка.КЭК,
			"302.00", , Документ.Дата, Документ.Организация, Выборка.ИФО); // Пока зачет авансов по документам (ручной) предусмотрен только для авансов, выданных поставщикам.
			
			// Счет авансов определим по счету расчетов.
			НоваяСтрока.СчетАвансов = УчетРасчетов.ОпределитьСчетАвансов(НоваяСтрока.СчетРасчетов, Документ.Дата, Документ.Организация);
			
			НоваяСтрока.ЭтоПоступление = БухгалтерскийУчетКлиентСервер.СчетВИерархии(НоваяСтрока.СчетРасчетов, "302.00"); 
			
			НоваяСтрока.ВидДопСубконтоАвансов = ПолучитьНетиповоеСубконтоСчета(НоваяСтрока.СчетАвансов);
			
		КонецЦикла;       
	КонецЕсли;
	#КонецВставки

	Возврат ТаблицаЗачетаАвансов;

КонецФункции

&ИзменениеИКонтроль("ЗачетАвансовВыданныхОбработкаПроверкиЗаполнения")
Процедура АБК_ЗачетАвансовВыданныхОбработкаПроверкиЗаполнения(Объект, НепроверяемыеРеквизиты, Отказ)
	
	Если Объект.СпособЗачетаАванса <> Перечисления.СпособыЗачетаАвансов.ПоДокументу Тогда
		НепроверяемыеРеквизиты.Добавить("ЗачетАвансов"); 
		#Вставка         
	ИначеЕсли Объект.СпособЗачетаАванса <> Перечисления.СпособыЗачетаАвансов.ПоДокументамИОстаткам Тогда  
		НепроверяемыеРеквизиты.Добавить("ЗачетАвансовПоДокументамИОстаткам"); 
		#КонецВставки
	КонецЕсли;

	ИмяТЧ = "";
	ПредставлениеТЧ = "";
	ИмяРеквизита = "";
	СуммаДокумента = 0;
	Если ТипЗнч(Объект) = Тип("ДокументОбъект.ПоступлениеУслугРабот") Тогда
		ИмяТЧ = "УслугиИРаботы";
		ПредставлениеТЧ = "Услуги и работы";
	ИначеЕсли ТипЗнч(Объект) = Тип("ДокументОбъект.ПоступлениеОС") 
		ИЛИ ТипЗнч(Объект) = Тип("ДокументОбъект.ПоступлениеОСВПути")
		Или ТипЗнч(Объект) = Тип("ДокументОбъект.ПоступлениеОСИК") Тогда
		ИмяТЧ = "КапВложения";
		ПредставлениеТЧ = "Кап. вложения";
	ИначеЕсли ТипЗнч(Объект) = Тип("ДокументОбъект.ПоступлениеМЗ") Тогда
		ИмяРеквизита = "СуммаДокумента";
	ИначеЕсли ТипЗнч(Объект) = Тип("ДокументОбъект.ПоступлениеМЗВПути") Тогда
		ИмяРеквизита = "ВсегоПоДокументу";
	ИначеЕсли ТипЗнч(Объект) = Тип("ДокументОбъект.ПриходныйОрдерФондовый") Тогда
		ИмяРеквизита = "СуммаДокумента";
	ИначеЕсли ТипЗнч(Объект) = Тип("ДокументОбъект.ФормированиеВложенийВФинансовыеАктивы") Тогда
		ИмяРеквизита = "СуммаДокумента";
	ИначеЕсли ТипЗнч(Объект) = Тип("ДокументОбъект.ГТДИмпорт") Тогда
		Возврат; // пока не проверяется
	КонецЕсли;

	Если ИмяТЧ <> "" Тогда
		СуммаДокумента = Объект[ИмяТЧ].Итог("Всего");
	ИначеЕсли ИмяРеквизита <> "" Тогда
		СуммаДокумента = Объект[ИмяРеквизита];
	КонецЕсли;

	ВалютаДокумента = ?(ОбщегоНазначенияБГУ.ЕстьРеквизитДокумента("ВалютаДокумента", Объект.Метаданные()),
	Объект.ВалютаДокумента, БухгалтерскийУчетПовтИсп.ВалютаРегламентированногоУчета());

	СуммаАванса = Объект.ЗачетАвансов.Итог("СуммаЗачета");
	#Вставка                          
	ЕстьТЧЗачетАвансовПоДокументамИОстаткам = ОбщегоНазначенияБГУ.ЕстьТабЧастьДокумента("ЗачетАвансовПоДокументамИОстаткам", Объект.Метаданные());
	Если ЕстьТЧЗачетАвансовПоДокументамИОстаткам Тогда
		СуммаАванса = СуммаАванса + Объект.ЗачетАвансовПоДокументамИОстаткам.Итог("СуммаЗачета");
	КонецЕсли;
	#КонецВставки
	Если Не СуммаАванса = 0 И СуммаАванса > СуммаДокумента Тогда
		ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		НСтр("ru = 'Сумма зачета аванса (%1) превышает сумму документа (%2).'"),
		ОбщегоНазначенияБГУ.ФорматСумм(СуммаАванса, ВалютаДокумента),
		ОбщегоНазначенияБГУ.ФорматСумм(СуммаДокумента, ВалютаДокумента));
		ОбщегоНазначения.СообщитьПользователю(ТекстОшибки, Объект.Ссылка, "ЗачетАвансов[0].СуммаЗачета", "Объект", Отказ);
	КонецЕсли;

КонецПроцедуры

&ИзменениеИКонтроль("ТаблицаРасчетовСЗачетомАвансов")
Функция АБК_ТаблицаРасчетовСЗачетомАвансов(ТаблицаДокумента, ТаблицаЗачетАвансовПоДокументу, Реквизиты, Отказ)

	ТаблицаРасчетов = ПустаяТаблицаРасчетовСЗачетомАвансов();

	УчетнаяПолитика = БухгалтерскиеОперацииПовтИсп.УчетнаяПолитикаОрганизации(Реквизиты.Организация, Реквизиты.Дата);
	Реквизиты.Вставить("ИспользоватьУчетПоИФО",            ПолучитьФункциональнуюОпцию("ИспользоватьУчетПоИФО"));
	Реквизиты.Вставить("ИспользоватьУчетПоПодразделениям", ПолучитьФункциональнуюОпцию("ИспользоватьУчетПоПодразделениям"));
	Реквизиты.Вставить("ИспользоватьВалютныйУчет",         ПолучитьФункциональнуюОпцию("ИспользоватьВалютныйУчет"));
	Реквизиты.Вставить("УчитыватьЗадолженностьЕНВД",       УчетнаяПолитика.ПрименяетсяУСН И УчетнаяПолитика.ПлательщикЕНВД);

	Если НЕ Реквизиты.Свойство("СпособЗачетаАванса") ИЛИ НЕ ЗначениеЗаполнено(Реквизиты.СпособЗачетаАванса) Тогда
		Реквизиты.Вставить("СпособЗачетаАванса", Перечисления.СпособыЗачетаАвансов.Автоматически);
	КонецЕсли;
	Если НЕ Реквизиты.Свойство("ПоВсемКФО") Тогда
		Реквизиты.Вставить("ПоВсемКФО", Ложь);
	КонецЕсли;
	Если НЕ Реквизиты.Свойство("ПоВсемКПС") Тогда
		Реквизиты.Вставить("ПоВсемКПС", Ложь);
	КонецЕсли;
	Если НЕ Реквизиты.Свойство("ВидДеятельностиУСН") Тогда
		Реквизиты.Вставить("ВидДеятельностиУСН", Перечисления.ВидыДеятельностиДляНалоговогоУчетаЗатрат.ОсновнаяСистемаНалогообложения);
	КонецЕсли;

	ДобавитьСлужебныеКолонкиРасчетовСКонтрагентами(ТаблицаДокумента, Реквизиты);
	ДобавитьСлужебныеКолонкиРасчетовСКонтрагентами(ТаблицаЗачетАвансовПоДокументу, Реквизиты);

	ПроверитьКорректностьНастройкиСчетовРасчетов(ТаблицаДокумента, Реквизиты);

	Для Каждого СтрокаДокумента Из ТаблицаДокумента Цикл

		// При поступлении ценностей по договору, по которому организация выступает налоговым агентом по НДС,
		// сумма расчетов не включает НДС, т.к. сумма НДС оплачивается в налоговую инспекцию, а не поставщику.
		Если СтрокаДокумента.ЭтоПоступление И СтрокаДокумента.УчетАгентскогоНДС Тогда

			СтрокаДокумента.СуммаРасчетов = СтрокаДокумента.СуммаРасчетов - СтрокаДокумента.СуммаНДС;

			КурсДокумента = ?(СтрокаДокумента.Кратность = 0, СтрокаДокумента.Курс,
			СтрокаДокумента.Курс / СтрокаДокумента.Кратность);
			СтрокаДокумента.СуммаРуб = СтрокаДокумента.СуммаРуб - Окр(СтрокаДокумента.СуммаНДС * КурсДокумента, 2);

		КонецЕсли;

	КонецЦикла;

	// Подготовим таблицу остатков авансов.
	Если Реквизиты.Свойство("ПериодОстатков") Тогда // Период остатков указан явно.
		ПериодОстатков = Реквизиты.ПериодОстатков;
	ИначеЕсли Не ЗначениеЗаполнено(Реквизиты.Ссылка) Тогда // Новый документ.
		ПериодОстатков = Новый Граница(КонецДня(Реквизиты.Дата), ВидГраницы.Включая);
	Иначе
		ПериодОстатков = Новый Граница(Реквизиты.Ссылка.МоментВремени(), ВидГраницы.Исключая);
	КонецЕсли;
	ОстаткиАвансов = ОстаткиАвансов(ПериодОстатков, ТаблицаДокумента, Реквизиты);

	Если Реквизиты.СпособЗачетаАванса = Перечисления.СпособыЗачетаАвансов.ПоДокументу Тогда

		Для Каждого СтрокаДокумента Из ТаблицаЗачетАвансовПоДокументу Цикл

			НераспределенныйОстаток = СтрокаДокумента.СуммаРасчетов;

			Если ОстаткиАвансов.Количество() > 0 Тогда

				Отбор = НовыйОтборАвансов(СтрокаДокумента.СчетАвансов, СтрокаДокумента, Реквизиты);  
				
				Отбор.Вставить("ВидДопСубконтоАвансов", ПланыВидовХарактеристик.ВидыСубконто.ДокументыРасчетов);
				Отбор.Вставить("ДопСубконтоАвансов",    СтрокаДокумента.ДокументАванса);        
				
				НайденныеОстаткиАвансов = ОстаткиАвансов.НайтиСтроки(Отбор);

				ДобавитьСтрокиЗачтенныхАвансов(
				ТаблицаРасчетов,
				НераспределенныйОстаток,
				НайденныеОстаткиАвансов,
				СтрокаДокумента,
				Реквизиты);   
				

				Если СтрокаДокумента.СчетАвансов <> СтрокаДокумента.СчетРасчетов
					И Не БухгалтерскийУчет.СчетВИерархии(СтрокаДокумента.СчетАвансов, "АП") Тогда

					// Зачет остатков долга ("аванса"), образовавшихся на счете расчетов, например, при возвратах.
					Отбор = НовыйОтборАвансов(СтрокаДокумента.СчетРасчетов, СтрокаДокумента, Реквизиты);
					НайденныеОстаткиАвансов = ОстаткиАвансов.НайтиСтроки(Отбор);

					ДобавитьСтрокиЗачтенныхАвансов(
					ТаблицаРасчетов,
					НераспределенныйОстаток,
					НайденныеОстаткиАвансов,
					СтрокаДокумента,
					Реквизиты);

				КонецЕсли;

			КонецЕсли;

			// При зачете авансов "по документу" нераспределенный остаток означает,
			// что указанная сумма зачета превышает остаток аванса.
			Если НераспределенныйОстаток > 0 Тогда

				ТекстОшибки = НСтр("ru='Указана сумма зачета, превышающая остаток незачтенного аванса.
				|
				|Документ аванса: ""%1""
				|Указана сумма зачета: %2 %3; Остаток аванса: %4 %3; Превышение: %5 %3'");
				ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстОшибки,
				СтрокаДокумента.ДокументАванса,
				СтрокаДокумента.СуммаРасчетов,
				СтрокаДокумента.Валюта,
				СтрокаДокумента.СуммаРасчетов - НераспределенныйОстаток,
				НераспределенныйОстаток);

				ПолеСуммаЗачета = "ЗачетАвансов[" + Формат(СтрокаДокумента.НомерСтроки - 1, "ЧН=0; ЧГ=") + "].СуммаЗачета";
				ОбщегоНазначения.СообщитьПользователю(ТекстОшибки, Реквизиты.Ссылка, ПолеСуммаЗачета, "Объект", Отказ);

			КонецЕсли;

		КонецЦикла;

		// Сумму расчетов, оставшуюся после зачета всех авансов, добавляем отдельной строкой.
		Для Каждого СтрокаДокумента Из ТаблицаДокумента Цикл

			Отбор = Новый Структура("ЭтоПоступление,ИФО,КФО,КПС,Подразделение,СчетРасчетов,СчетАвансов,Контрагент,Договор,Валюта");
			ЗаполнитьЗначенияСвойств(Отбор, СтрокаДокумента);
			ЗачетноАвансов = ТаблицаЗачетАвансовПоДокументу.Скопировать(Отбор);

			ДобавитьСтрокуЗадолженности(
			ТаблицаРасчетов,
			СтрокаДокумента.СуммаРасчетов - ЗачетноАвансов.Итог("СуммаРасчетов"),
			СтрокаДокумента,
			Реквизиты);

		КонецЦикла;     
		
	#Вставка
	ИначеЕсли Реквизиты.СпособЗачетаАванса = Перечисления.СпособыЗачетаАвансов.ПоДокументамИОстаткам Тогда
		
		Для Каждого СтрокаДокумента из ТаблицаЗачетАвансовПоДокументу Цикл
			Отбор = Новый Структура("ЭтоПоступление,ИФО,КФО,КПС,Подразделение,СчетРасчетов,СчетАвансов,Контрагент,Договор,Валюта");
			ЗаполнитьЗначенияСвойств(Отбор, СтрокаДокумента);  
			
			Если ЗначениеЗаполнено(СтрокаДокумента.ДопСубконтоАвансов) Тогда
				Отбор.Вставить("ДопСубконтоАвансов", СтрокаДокумента.ДопСубконтоАвансов);	
			Иначе
				Отбор.Вставить("ДопСубконтоАвансов", Неопределено);	
			КонецЕсли;
			
			НайдСтрокиТаблицыДокумента = ТаблицаДокумента.НайтиСтроки(Отбор);   
			
			НераспределенныйОстаток = СтрокаДокумента.СуммаРасчетов; 
			
			НераспределенныйОстатокДляКонтроля = СтрокаДокумента.СуммаРасчетов;
			
			Для Каждого НайдСтрокаТаблицыДокумента  из НайдСтрокиТаблицыДокумента Цикл  
				
				Если НераспределенныйОстаток = 0 Тогда
					Прервать;	
				КонецЕсли;
				
				Если ОстаткиАвансов.Количество() > 0 Тогда
					
					Отбор = НовыйОтборАвансов(СтрокаДокумента.СчетАвансов, СтрокаДокумента, Реквизиты);  
					
					Отбор.Вставить("ВидДопСубконтоАвансов", СтрокаДокумента.ВидДопСубконтоАвансов);
					Отбор.Вставить("ДопСубконтоАвансов",    СтрокаДокумента.ДопСубконтоАвансов);        
					
					НайденныеОстаткиАвансов = ОстаткиАвансов.НайтиСтроки(Отбор);
					
					ДобавитьСтрокиЗачтенныхАвансов(
					ТаблицаРасчетов,
					НераспределенныйОстаток,
					НайденныеОстаткиАвансов,
					СтрокаДокумента,
					Реквизиты); 
					
					НераспределенныйОстатокДляКонтроля = Макс(НераспределенныйОстатокДляКонтроля - НайдСтрокаТаблицыДокумента.СуммаРасчетов, 0);
					
				КонецЕсли;  
				
			КонецЦикла;  
			
			Если НераспределенныйОстатокДляКонтроля > 0 Тогда
				
				ТекстОшибки = НСтр("ru='Указана сумма зачета, превышающая остаток незачтенного аванса.
				|
				|Документ аванса: ""%1"", доп. субконто аванса: %6
				|Указана сумма зачета: %2 %3; Остаток аванса: %4 %3; Превышение: %5 %3'");
				ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстОшибки,
				СтрокаДокумента.ДокументАванса,
				СтрокаДокумента.СуммаРасчетов,
				СтрокаДокумента.Валюта,
				НераспределенныйОстатокДляКонтроля,  
				СтрокаДокумента.СуммаРасчетов - (СтрокаДокумента.СуммаРасчетов - НераспределенныйОстатокДляКонтроля),
				СтрокаДокумента.ДопСубконтоАвансов);
				
				ПолеСуммаЗачета = "ЗачетАвансовПоДокументамИОстаткам[" + Формат(СтрокаДокумента.НомерСтроки - 1, "ЧН=0; ЧГ=") + "].СуммаЗачета";
				ОбщегоНазначения.СообщитьПользователю(ТекстОшибки, Реквизиты.Ссылка, ПолеСуммаЗачета, "Объект", Отказ);
				
			КонецЕсли;
			
		КонецЦикла;     
		
		// Сумму расчетов, оставшуюся после зачета всех авансов, добавляем отдельной строкой.
		Для Каждого СтрокаДокумента Из ТаблицаДокумента Цикл

			Отбор = Новый Структура("ЭтоПоступление,ИФО,КФО,КПС,Подразделение,СчетРасчетов,СчетАвансов,Контрагент,Договор,Валюта,ДопСубконтоАвансов");
			ЗаполнитьЗначенияСвойств(Отбор, СтрокаДокумента);
			ЗачетноАвансов = ТаблицаЗачетАвансовПоДокументу.Скопировать(Отбор);

			ДобавитьСтрокуЗадолженности(
			ТаблицаРасчетов,
			СтрокаДокумента.СуммаРасчетов - ЗачетноАвансов.Итог("СуммаРасчетов"),
			СтрокаДокумента,
			Реквизиты);

		КонецЦикла;     
	#КонецВставки

	Иначе // Автоматически или НеЗачитывать

		ДетальныеКЭК440 = ПолучитьДетальныеКЭК440();

		Для Каждого СтрокаДокумента Из ТаблицаДокумента Цикл

			НераспределенныйОстаток = СтрокаДокумента.СуммаРасчетов;

			Если Реквизиты.СпособЗачетаАванса = Перечисления.СпособыЗачетаАвансов.Автоматически
				И ОстаткиАвансов.Количество() > 0 Тогда

				Отбор = НовыйОтборАвансов(СтрокаДокумента.СчетАвансов, СтрокаДокумента, Реквизиты);
				НайденныеОстаткиАвансов = ОстаткиАвансов.НайтиСтроки(Отбор);

				Если НайденныеОстаткиАвансов.Количество() = 0
					И ДетальныеКЭК440.Получить(
					ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(Отбор, "КЭК")) <> Неопределено Тогда
					// Если не найдены авансы по детальному КЭК, нужно проверить авансы по групповому КЭК
					Отбор.КЭК = БухгалтерскийУчетПовтИсп.КЭКпоКоду("440");
					НайденныеОстаткиАвансов = ОстаткиАвансов.НайтиСтроки(Отбор);
				КонецЕсли;

				ДобавитьСтрокиЗачтенныхАвансов(
				ТаблицаРасчетов,
				НераспределенныйОстаток,
				НайденныеОстаткиАвансов,
				СтрокаДокумента,
				Реквизиты);

				Если СтрокаДокумента.СчетАвансов <> СтрокаДокумента.СчетРасчетов
					И Не БухгалтерскийУчет.СчетВИерархии(СтрокаДокумента.СчетАвансов, "АП") Тогда

					// Зачет остатков долга ("аванса"), образовавшихся на счете расчетов, например, при возвратах.
					Отбор = НовыйОтборАвансов(СтрокаДокумента.СчетРасчетов, СтрокаДокумента, Реквизиты);
					НайденныеОстаткиАвансов = ОстаткиАвансов.НайтиСтроки(Отбор);

					ДобавитьСтрокиЗачтенныхАвансов(
					ТаблицаРасчетов,
					НераспределенныйОстаток,
					НайденныеОстаткиАвансов,
					СтрокаДокумента,
					Реквизиты);

				КонецЕсли;

			КонецЕсли;

			// Сумму расчетов, оставшуюся после зачета всех авансов, добавляем отдельной строкой.
			ДобавитьСтрокуЗадолженности(
			ТаблицаРасчетов,
			НераспределенныйОстаток,
			СтрокаДокумента,
			Реквизиты);

		КонецЦикла;

	КонецЕсли;

	Возврат ТаблицаРасчетов;

КонецФункции

&ИзменениеИКонтроль("ПустаяТаблицаРасчетовПоДокументу")
Функция АБК_ПустаяТаблицаРасчетовПоДокументу()

	ОписаниеТипаСуммы = Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(18, 2, ДопустимыйЗнак.Любой));
	#Вставка
	ОписаниеТиповСубконто = Метаданные.ПланыВидовХарактеристик.ВидыСубконто.Тип;
	#КонецВставки

	ТаблицаРасчетов = Новый ТаблицаЗначений;
	// Признак поступления ценностей: материальных запасов, услуг и пр.
	ТаблицаРасчетов.Колонки.Добавить("ЭтоПоступление", Новый ОписаниеТипов("Булево"));
	// Общие разрезы учета.
	ТаблицаРасчетов.Колонки.Добавить("ИФО",            Новый ОписаниеТипов("СправочникСсылка.ИсточникиФинансовогоОбеспечения"));
	ТаблицаРасчетов.Колонки.Добавить("КФО",            Новый ОписаниеТипов("ПеречислениеСсылка.КВД"));
	ТаблицаРасчетов.Колонки.Добавить("КПС",            Новый ОписаниеТипов("СправочникСсылка.КлассификационныеПризнакиСчетов"));
	ТаблицаРасчетов.Колонки.Добавить("Подразделение",  Новый ОписаниеТипов("СправочникСсылка.Подразделения"));
	// Счет и аналитика учета расчетов с контрагентами.
	ТаблицаРасчетов.Колонки.Добавить("КЭК",            Новый ОписаниеТипов("СправочникСсылка.КОСГУ"));
	ТаблицаРасчетов.Колонки.Добавить("СчетРасчетов",   Новый ОписаниеТипов("ПланСчетовСсылка.ЕПСБУ"));
	ТаблицаРасчетов.Колонки.Добавить("Контрагент",     Новый ОписаниеТипов("СправочникСсылка.Контрагенты"));
	ТаблицаРасчетов.Колонки.Добавить("Договор",        Новый ОписаниеТипов("СправочникСсылка.Договоры"));
	ТаблицаРасчетов.Колонки.Добавить("Валюта",         Новый ОписаниеТипов("СправочникСсылка.Валюты"));
	// Суммовые показатели операции.
	ТаблицаРасчетов.Колонки.Добавить("СуммаНДС",       ОписаниеТипаСуммы);
	ТаблицаРасчетов.Колонки.Добавить("СуммаРасчетов",  ОписаниеТипаСуммы); 
	
	#Вставка
	ТаблицаРасчетов.Колонки.Добавить("ДопСубконтоАвансов",  ОписаниеТиповСубконто); 
	#КонецВставки

	Возврат ТаблицаРасчетов;
КонецФункции

&ИзменениеИКонтроль("ПодготовитьТаблицуРасчетовПоДокументу")
Функция АБК_ПодготовитьТаблицуРасчетовПоДокументу(Документ, Параметры, Отказ)

	ТаблицаРасчетов = ПустаяТаблицаРасчетовПоДокументу();

	Если Параметры = Неопределено Тогда
		Параметры = Новый Структура;
	КонецЕсли;
	Если НЕ Параметры.Свойство("ПоВсемКФО") Тогда
		Параметры.Вставить("ПоВсемКФО", Ложь);
	КонецЕсли;
	Если НЕ Параметры.Свойство("ПоВсемКПС") Тогда
		Параметры.Вставить("ПоВсемКПС", Ложь);
	КонецЕсли;

	МетаданныеДокумента = Документ.Метаданные();

	Если МетаданныеДокумента.Имя = "ГТДИмпорт" Тогда // Особый случай. Существенно отличается структура данных.
		Возврат ПодготовитьТаблицуРасчетовПоДокументуГТДИмпорт(ТаблицаРасчетов, Документ, Параметры, Отказ);
	КонецЕсли;

	Если МетаданныеДокумента.Имя = "ИзвещениеИсходящее" Тогда 
		// Особый случай. Зачет авансов будет реализован позднее
		Возврат Документы.ИзвещениеИсходящее.ТаблицаПередаваемыхРасчетов(ТаблицаРасчетов, Документ, Параметры, Отказ);
	КонецЕсли;

	Если ТипЗнч(Документ) = Тип("ДокументОбъект.АктПриемкиТоваровРаботУслуг") Тогда
		Возврат Документы.АктПриемкиТоваровРаботУслуг.ПодготовитьТаблицуРасчетовПоДокументу(Документ, Параметры, Отказ);
	КонецЕсли;

	ЕстьВалютаДокумента = ОбщегоНазначения.ЕстьРеквизитОбъекта("ВалютаДокумента", МетаданныеДокумента);
	Если Не ЕстьВалютаДокумента И НЕ Параметры.Свойство("ВалютаДокумента") Тогда
		Параметры.Вставить("ВалютаДокумента", Константы.ВалютаРегламентированногоУчета.Получить());
	КонецЕсли;

	Запрос = Новый Запрос;

	// 1. Расчеты по материальным запасам (поступившим и отгруженным).
	ЕстьТабЧастьМатериалы      = ОбщегоНазначенияБГУ.ЕстьТабЧастьДокумента("Материалы", МетаданныеДокумента);
	ЕстьТабЧастьНоменклатура   = ОбщегоНазначенияБГУ.ЕстьТабЧастьДокумента("Номенклатура", МетаданныеДокумента);
	ЕстьТабЧастьВозвратнаяТара = ОбщегоНазначенияБГУ.ЕстьТабЧастьДокумента("ВозвратнаяТара", МетаданныеДокумента);

	Если ЕстьТабЧастьМатериалы Тогда
		Если НЕ ОбщегоНазначения.ЕстьРеквизитОбъекта("КЭК", МетаданныеДокумента.ТабличныеЧасти.Материалы)
			И НЕ Параметры.Свойство("КЭК") Тогда
			Параметры.Вставить("КЭК", Справочники.КОСГУ.ПустаяСсылка());
		КонецЕсли;
		ОбщегоНазначенияБГУ.ПоместитьТабЧастьВВиртуальнуюТаблицу(Запрос, "Материалы", 
		#Вставка
		"НомерСтроки,"+
		#КонецВставки
		"ИФО,КФО,КПС,Подразделение,Контрагент,Договор,ВалютаДокумента,СуммаНДС,Всего,КЭК",
		Документ, Параметры);
	ИначеЕсли ЕстьТабЧастьНоменклатура Тогда
		Если НЕ ОбщегоНазначения.ЕстьРеквизитОбъекта("КЭК", МетаданныеДокумента.ТабличныеЧасти.Номенклатура)
			И НЕ Параметры.Свойство("КЭК") Тогда
			Параметры.Вставить("КЭК", Справочники.КОСГУ.ПустаяСсылка());
		КонецЕсли;
		ОбщегоНазначенияБГУ.ПоместитьТабЧастьВВиртуальнуюТаблицу(Запрос, "Номенклатура",  
		#Вставка
		"НомерСтроки,"+
		#КонецВставки
		"ИФО,КФО,КПС,Подразделение,Контрагент,Договор,ВалютаДокумента,СуммаНДС,Всего,КЭК",
		Документ, Параметры);
		// Переименуем таблицу, т.к. таб.часть называется так же, как реквизит - "Номенклатура".
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "Номенклатура.", "ТабНоменклатура.");
		Запрос.Текст = СтрЗаменить(Запрос.Текст,
		"ТаблицаНоменклатура КАК Номенклатура", "ТаблицаНоменклатура КАК ТабНоменклатура");
	КонецЕсли;
	Если ЕстьТабЧастьВозвратнаяТара Тогда
		Если НЕ ОбщегоНазначения.ЕстьРеквизитОбъекта("КЭК", МетаданныеДокумента.ТабличныеЧасти.ВозвратнаяТара)
			И НЕ Параметры.Свойство("КЭК") Тогда
			Параметры.Вставить("КЭК", Справочники.КОСГУ.ПустаяСсылка());
		КонецЕсли;
		ОбщегоНазначенияБГУ.ПоместитьТабЧастьВВиртуальнуюТаблицу(Запрос, "ВозвратнаяТара",  
		#Вставка
		"НомерСтроки,"+
		#КонецВставки
		"ИФО,КФО,КПС,Подразделение,Контрагент,Договор,ВалютаДокумента,Сумма,КЭК",
		Документ, Параметры);
	КонецЕсли;

	Если ЕстьТабЧастьМатериалы ИЛИ ЕстьТабЧастьНоменклатура Тогда

		Если ЕстьТабЧастьМатериалы Тогда

			Запрос.Текст = Запрос.Текст +
			"ВЫБРАТЬ
			|	Материалы.ИФО КАК ИФО,   
			#Вставка 
			|	Материалы.НомерСтроки КАК НомерСтроки,
			|	ЛОЖЬ КАК ЭтоВозвратнаяТара, 
			|	НЕОПРЕДЕЛЕНО КАК ДопСубконтоАвансов,
			#КонецВставки
			|	Материалы.КФО КАК КФО,
			|	Материалы.КПС КАК КПС,
			|	Материалы.Подразделение КАК Подразделение,
			|	Материалы.Контрагент КАК Контрагент,
			|	Материалы.Договор КАК Договор,
			|	Материалы.ВалютаДокумента КАК Валюта,
			|	Материалы.СуммаНДС КАК СуммаНДС,
			|	Материалы.Всего КАК СуммаРасчетов,
			|	Материалы.КЭК КАК КЭК
			|ИЗ
			|	ТаблицаМатериалы КАК Материалы";

		ИначеЕсли ЕстьТабЧастьНоменклатура Тогда

			Запрос.Текст = Запрос.Текст +
			"ВЫБРАТЬ
			|	ТабНоменклатура.ИФО КАК ИФО,
			#Вставка 
			|	ТабНоменклатура.НомерСтроки КАК НомерСтроки,  
			|	ЛОЖЬ КАК ЭтоВозвратнаяТара,      
			|	НЕОПРЕДЕЛЕНО КАК ДопСубконтоАвансов,
			#КонецВставки
			|	ТабНоменклатура.КФО КАК КФО,
			|	ТабНоменклатура.КПС КАК КПС,
			|	ТабНоменклатура.Подразделение КАК Подразделение,
			|	ТабНоменклатура.Контрагент КАК Контрагент,
			|	ТабНоменклатура.Договор КАК Договор,
			|	ТабНоменклатура.ВалютаДокумента КАК Валюта,
			|	ТабНоменклатура.СуммаНДС КАК СуммаНДС,
			|	ТабНоменклатура.Всего КАК СуммаРасчетов,
			|	ТабНоменклатура.КЭК КАК КЭК
			|ИЗ
			|	ТаблицаНоменклатура КАК ТабНоменклатура";

		КонецЕсли;

		Если ЕстьТабЧастьВозвратнаяТара Тогда

			Запрос.Текст = Запрос.Текст + ОбщегоНазначенияБГУ.ТекстРазделителяОбъединенияЗапросов("ВСЕ");
			Запрос.Текст = Запрос.Текст + 
			"ВЫБРАТЬ
			|	ВозвратнаяТара.ИФО, 
			#Вставка 
			|	ВозвратнаяТара.НомерСтроки, 
			|	ИСТИНА КАК ЭтоВозвратнаяТара,
			|	НЕОПРЕДЕЛЕНО КАК ДопСубконтоАвансов,
			#КонецВставки
			|	ВозвратнаяТара.КФО,
			|	ВозвратнаяТара.КПС,
			|	ВозвратнаяТара.Подразделение,
			|	ВозвратнаяТара.Контрагент,
			|	ВозвратнаяТара.Договор,
			|	ВозвратнаяТара.ВалютаДокумента,
			|	0,
			|	ВозвратнаяТара.Сумма,
			|	ВозвратнаяТара.КЭК КАК КЭК
			|ИЗ
			|	ТаблицаВозвратнаяТара КАК ВозвратнаяТара";

		КонецЕсли;

		Если Параметры.ПоВсемКФО Тогда
			Запрос.Текст = СтрЗаменить(Запрос.Текст, "Материалы.КФО",
			"ЗНАЧЕНИЕ(Перечисление.КВД.ПустаяСсылка)");
			Запрос.Текст = СтрЗаменить(Запрос.Текст, "ТабНоменклатура.КФО",
			"ЗНАЧЕНИЕ(Перечисление.КВД.ПустаяСсылка)");
			Запрос.Текст = СтрЗаменить(Запрос.Текст, "ВозвратнаяТара.КФО",
			"ЗНАЧЕНИЕ(Перечисление.КВД.ПустаяСсылка)");
		КонецЕсли;
		Если Параметры.ПоВсемКПС Тогда
			Запрос.Текст = СтрЗаменить(Запрос.Текст, "Материалы.КПС",
			"ЗНАЧЕНИЕ(Справочник.КлассификационныеПризнакиСчетов.ПустаяСсылка)");
			Запрос.Текст = СтрЗаменить(Запрос.Текст, "ТабНоменклатура.КПС",
			"ЗНАЧЕНИЕ(Справочник.КлассификационныеПризнакиСчетов.ПустаяСсылка)");
			Запрос.Текст = СтрЗаменить(Запрос.Текст, "ВозвратнаяТара.КПС",
			"ЗНАЧЕНИЕ(Справочник.КлассификационныеПризнакиСчетов.ПустаяСсылка)");
		КонецЕсли;

		РасчетыПоМатериальнымЗапасам = Запрос.Выполнить().Выгрузить();        
		
		#Вставка 
		Если ЕстьТабЧастьМатериалы Тогда
			ЗаполнитьДопКолонку("КодМероприятия", "Материалы", Документ.Ссылка, РасчетыПоМатериальнымЗапасам, "ДопСубконтоАвансов");
		ИначеЕсли ЕстьТабЧастьНоменклатура Тогда
			ЗаполнитьДопКолонку("КодМероприятия", "Номенклатура", Документ.Ссылка, РасчетыПоМатериальнымЗапасам, "ДопСубконтоАвансов");
		КонецЕсли;        
		
		Если ЕстьТабЧастьВозвратнаяТара Тогда
			ЗаполнитьДопКолонку("КодМероприятия", "ВозвратнаяТара", Документ.Ссылка, РасчетыПоМатериальнымЗапасам, "ДопСубконтоАвансов", Истина);
		КонецЕсли;
		#КонецВставки

		ВидОперации = ?(Найти(МетаданныеДокумента.Имя, "Поступление") > 0, "Поступление", "Реализация");
		СчетРасчетов = ОпределитьСчетРасчетовПоПараметрам(Параметры, ВидОперации,
		Документ.Дата, Документ.Организация, Документ.ИФО);

		ИспользоватьКПСРасчетов = Ложь;
		Если ВидОперации = "Реализация" И НЕ Параметры.ПоВсемКПС Тогда
			// В документах реализации в таб.части указан КПС расходов.
			// Для расчетов должен использоваться КПС доходов.
			ИспользоватьКПСРасчетов = Истина;
		ИначеЕсли ВидОперации = "Поступление" И НЕ Параметры.ПоВсемКПС Тогда
			// В документах поступления материальных запасов может быть указан КПС счета расчетов (302.ХХ),
			// отличный от КПС счета учета материальных запасов (105.ХХ).
			ИспользоватьКПСРасчетов = (Параметры.Свойство("КПС") И ЗначениеЗаполнено(Параметры.КПС));
		КонецЕсли;

		ГрупповойКЭК440 = БухгалтерскийУчетПовтИсп.КЭКпоКоду("440");
		КЭК360 = БухгалтерскийУчетПовтИсп.КЭКпоКоду("360");

		Для Каждого СтрокаДокумента Из РасчетыПоМатериальнымЗапасам Цикл

			НоваяСтрока = ТаблицаРасчетов.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаДокумента);

			Если ЗначениеЗаполнено(СчетРасчетов) Тогда
				НоваяСтрока.СчетРасчетов = СчетРасчетов;
			Иначе
				Если ВидОперации = "Поступление" Тогда
					Если НоваяСтрока.КЭК = КЭК360 Тогда
						НоваяСтрока.СчетРасчетов = БухгалтерскийУчет.СчетПоКоду("302.36", Документ.Дата, Документ.Организация);
					Иначе
						НоваяСтрока.СчетРасчетов = БухгалтерскийУчет.СчетПоКоду("302.34", Документ.Дата, Документ.Организация);
					КонецЕсли;
				Иначе
					НоваяСтрока.СчетРасчетов = БухгалтерскийУчет.СчетПоКоду("205.74", Документ.Дата, Документ.Организация);
				КонецЕсли;
			КонецЕсли;

			Если НоваяСтрока.КЭК <> КЭК360 Тогда
				КЭКПоСчету = БухгалтерскийУчетПовтИсп.АналитическийКЭКпоСчету(НоваяСтрока.СчетРасчетов);
				Если КЭКПоСчету <> ГрупповойКЭК440 ИЛИ НЕ ЗначениеЗаполнено(НоваяСтрока.КЭК) Тогда
					НоваяСтрока.КЭК = КЭКПоСчету;
				КонецЕсли;
			КонецЕсли;

			Если ИспользоватьКПСРасчетов Тогда
				НоваяСтрока.КПС = Параметры.КПС;
			КонецЕсли;

			НоваяСтрока.ЭтоПоступление = БухгалтерскийУчет.СчетВИерархии(НоваяСтрока.СчетРасчетов, "302.00");

		КонецЦикла;

	КонецЕсли;


	// 2. Расчеты по поступившим услугам, работам.
	ЕстьТабЧастьУслугиИРаботы = ОбщегоНазначенияБГУ.ЕстьТабЧастьДокумента("УслугиИРаботы", МетаданныеДокумента);
	Если ЕстьТабЧастьУслугиИРаботы Тогда

		ОбщегоНазначенияБГУ.ПоместитьТабЧастьВВиртуальнуюТаблицу(Запрос, "УслугиИРаботы",
		"НомерСтроки,ИФО,КФО,КПС,КЭК,Подразделение,Контрагент,Договор,ВалютаДокумента,СуммаНДС,Всего",
		Документ, Параметры);

		Запрос.Текст = Запрос.Текст +
		"ВЫБРАТЬ
		|	УслугиИРаботы.НомерСтроки КАК НомерСтроки,
		|	УслугиИРаботы.ИФО КАК ИФО,  
		#Вставка
		|	НЕОПРЕДЕЛЕНО КАК ДопСубконтоАвансов,
		#КонецВставки
		|	УслугиИРаботы.КФО КАК КФО,
		|	УслугиИРаботы.КПС КАК КПС,
		|	УслугиИРаботы.КЭК КАК КЭК,
		|	УслугиИРаботы.Подразделение КАК Подразделение,
		|	УслугиИРаботы.Контрагент КАК Контрагент,
		|	УслугиИРаботы.Договор КАК Договор,
		|	УслугиИРаботы.ВалютаДокумента КАК Валюта,
		|	УслугиИРаботы.СуммаНДС КАК СуммаНДС,
		|	УслугиИРаботы.Всего КАК СуммаРасчетов
		|ИЗ
		|	ТаблицаУслугиИРаботы КАК УслугиИРаботы";

		Если Параметры.ПоВсемКФО Тогда
			Запрос.Текст = СтрЗаменить(Запрос.Текст, "УслугиИРаботы.КФО",
			"ЗНАЧЕНИЕ(Перечисление.КВД.ПустаяСсылка)");
		КонецЕсли;
		Если Параметры.ПоВсемКПС Тогда
			Запрос.Текст = СтрЗаменить(Запрос.Текст, "УслугиИРаботы.КПС",
			"ЗНАЧЕНИЕ(Справочник.КлассификационныеПризнакиСчетов.ПустаяСсылка)");
		КонецЕсли;

		РасчетыПоУслугамРаботам = Запрос.Выполнить().Выгрузить(); 
		
		#Вставка 
		ЗаполнитьДопКолонку("КодМероприятия", "УслугиИРаботы", Документ.Ссылка, РасчетыПоУслугамРаботам, "ДопСубконтоАвансов");
		#КонецВставки

		СчетРасчетов = ОпределитьСчетРасчетовПоПараметрам(Параметры, "Поступление",
		Документ.Дата, Документ.Организация, Документ.ИФО);

		Для Каждого СтрокаДокумента Из РасчетыПоУслугамРаботам Цикл

			НоваяСтрока = ТаблицаРасчетов.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаДокумента);

			НоваяСтрока.ЭтоПоступление = Истина;

			// Счет расчетов.
			Если ЗначениеЗаполнено(СчетРасчетов) Тогда
				НоваяСтрока.СчетРасчетов = СчетРасчетов;
			Иначе
				НоваяСтрока.СчетРасчетов = БухгалтерскийУчет.ПолучитьАналитическийСчетПоКЭК(
				СтрокаДокумента.КЭК, "302.00", , Документ.Дата, Документ.Организация, СтрокаДокумента.ИФО);
			КонецЕсли;

			Если НЕ ЗначениеЗаполнено(НоваяСтрока.СчетРасчетов) Тогда
				ТекстОшибки = НСтр("ru='Невозможно определить счет расчетов с контрагентом по КЭК ""%1"".
				|Проверьте настройки соответствия аналитических счетов КЭК.'");
				ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстОшибки,
				СтрокаДокумента.КЭК);
				ОбщегоНазначения.СообщитьПользователю(ТекстОшибки,
				Документ.Ссылка, "УслугиИРаботы[" + Формат(СтрокаДокумента.НомерСтроки - 1, "ЧН=0; ЧГ=") + "].КЭК", "Объект", Отказ);
			КонецЕсли;

		КонецЦикла;

	КонецЕсли;


	// 2b. Расчеты по аренде (арендатор) - начисление амортизации прав пользования.
	ЕстьТабЧастьПраваПользования = ОбщегоНазначенияБГУ.ЕстьТабЧастьДокумента("ПраваПользования", МетаданныеДокумента);
	Если ЕстьТабЧастьПраваПользования Тогда

		ОбщегоНазначенияБГУ.ПоместитьТабЧастьВВиртуальнуюТаблицу(Запрос, "ПраваПользования",
		"ВидОперации,ИФО,КФО,КПС,Подразделение,ОсновноеСредство,СчетУчета,Контрагент,Договор,ВалютаДокумента,СуммаНДС,Всего",
		Документ, Параметры);

		Запрос.Текст = Запрос.Текст +
		"ВЫБРАТЬ
		|	ПраваПользования.ВидОперации КАК ВидОперации,
		|	ПраваПользования.ИФО КАК ИФО,
		|	ПраваПользования.КФО КАК КФО,
		|	ПраваПользования.КПС КАК КПС,
		|	ПраваПользования.Подразделение КАК Подразделение,
		|	ПраваПользования.ОсновноеСредство КАК ОсновноеСредство,
		|	ПраваПользования.СчетУчета КАК СчетУчета,
		|	ПраваПользования.Контрагент КАК Контрагент,
		|	ПраваПользования.Договор КАК Договор,
		|	ПраваПользования.ВалютаДокумента КАК Валюта,
		|	ПраваПользования.СуммаНДС КАК СуммаНДС,
		|	ПраваПользования.Всего КАК СуммаРасчетов
		|ПОМЕСТИТЬ АрендныеПлатежи
		|ИЗ
		|	ТаблицаПраваПользования КАК ПраваПользования
		|ГДЕ
		|	ПраваПользования.Всего > 0
		|	И ПраваПользования.ВидОперации <> ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПринятияКУчетуПравПользованияОС.ПоступлениеВБезвозмездноеПользование)
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Договор,
		|	ОсновноеСредство
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ПараметрыАмортизацииПравПользованияСрезПоследних.Договор КАК Договор,
		|	ПараметрыАмортизацииПравПользованияСрезПоследних.ОсновноеСредство КАК ОсновноеСредство,
		|	ПараметрыАмортизацииПравПользованияСрезПоследних.СчетРасчетов КАК СчетРасчетов,
		|	ПараметрыАмортизацииПравПользованияСрезПоследних.КПСРасчетов КАК КПСРасчетов
		|ПОМЕСТИТЬ ПараметрыАмортизацииПравПользования
		|ИЗ
		|	РегистрСведений.ПараметрыАмортизацииПравПользованияОС.СрезПоследних(
		|			&ГраницаСреза,
		|			Договор В
		|					(ВЫБРАТЬ РАЗЛИЧНЫЕ
		|						АрендныеПлатежи.Договор
		|					ИЗ
		|						АрендныеПлатежи)
		|				И Организация = &Организация
		|				И ОсновноеСредство В
		|					(ВЫБРАТЬ РАЗЛИЧНЫЕ
		|						АрендныеПлатежи.ОсновноеСредство
		|					ИЗ
		|						АрендныеПлатежи)) КАК ПараметрыАмортизацииПравПользованияСрезПоследних
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Договор,
		|	ОсновноеСредство
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	АрендныеПлатежи.ВидОперации КАК ВидОперации,
		|	АрендныеПлатежи.ИФО КАК ИФО,
		|	АрендныеПлатежи.КФО КАК КФО,
		|	АрендныеПлатежи.КПС КАК КПС,
		|	АрендныеПлатежи.Подразделение КАК Подразделение,
		|	АрендныеПлатежи.ОсновноеСредство КАК ОсновноеСредство,
		|	АрендныеПлатежи.СчетУчета КАК СчетУчета,
		|	АрендныеПлатежи.Контрагент КАК Контрагент,
		|	АрендныеПлатежи.Договор КАК Договор,
		|	АрендныеПлатежи.Валюта КАК Валюта,
		|	АрендныеПлатежи.СуммаНДС КАК СуммаНДС,
		|	АрендныеПлатежи.СуммаРасчетов КАК СуммаРасчетов,
		|	ЕСТЬNULL(ПараметрыАмортизацииПравПользования.СчетРасчетов, ЗНАЧЕНИЕ(ПланСчетов.ЕПСБУ.ПустаяСсылка)) КАК СчетРасчетов,
		|	ЕСТЬNULL(ПараметрыАмортизацииПравПользования.КПСРасчетов, ЗНАЧЕНИЕ(Справочник.КлассификационныеПризнакиСчетов.ПустаяСсылка)) КАК КПСРасчетов
		|ИЗ
		|	АрендныеПлатежи КАК АрендныеПлатежи
		|		ЛЕВОЕ СОЕДИНЕНИЕ ПараметрыАмортизацииПравПользования КАК ПараметрыАмортизацииПравПользования
		|		ПО АрендныеПлатежи.Договор = ПараметрыАмортизацииПравПользования.Договор
		|			И АрендныеПлатежи.ОсновноеСредство = ПараметрыАмортизацииПравПользования.ОсновноеСредство";
		Запрос.УстановитьПараметр("ГраницаСреза", КонецМесяца(Документ.Дата));
		Запрос.УстановитьПараметр("Организация",  Документ.Организация);

		Если Параметры.ПоВсемКФО Тогда
			Запрос.Текст = СтрЗаменить(Запрос.Текст, "ПраваПользования.КФО",
			"ЗНАЧЕНИЕ(Перечисление.КВД.ПустаяСсылка)");
		КонецЕсли;
		Если Параметры.ПоВсемКПС Тогда
			Запрос.Текст = СтрЗаменить(Запрос.Текст, "ПраваПользования.КПС",
			"ЗНАЧЕНИЕ(Справочник.КлассификационныеПризнакиСчетов.ПустаяСсылка)");
			Запрос.Текст = СтрЗаменить(Запрос.Текст, "ПараметрыАмортизацииПравПользованияСрезПоследних.КПСРасчетов",
			"ЗНАЧЕНИЕ(Справочник.КлассификационныеПризнакиСчетов.ПустаяСсылка)");
		КонецЕсли;

		РасчетыПоАренднойПлате = Запрос.Выполнить().Выгрузить();

		Для Каждого СтрокаДокумента Из РасчетыПоАренднойПлате Цикл

			НоваяСтрока = ТаблицаРасчетов.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаДокумента);

			НоваяСтрока.ЭтоПоступление = Истина;

			Если ЗначениеЗаполнено(СтрокаДокумента.СчетРасчетов) Тогда
				// Счет расчетов указан в регистре сведений, берем его.
				// Если КПС расчетов не указан, значит совпадает с КПС учета.
				НоваяСтрока.СчетРасчетов = СтрокаДокумента.СчетРасчетов;
				НоваяСтрока.КПС = ?(ЗначениеЗаполнено(СтрокаДокумента.КПСРасчетов),
				СтрокаДокумента.КПСРасчетов, СтрокаДокумента.КПС);
			Иначе
				// Счет расчетов не указан в регистре сведений, определяем его по счету учета.
				// КПС совпадает с КПС учета.
				НоваяСтрока.СчетРасчетов = УчетРасчетов.СчетРасчетовПоАренднойПлате(
				СтрокаДокумента.СчетУчета, Документ.Дата, Документ.Организация);
				НоваяСтрока.КПС = СтрокаДокумента.КПС;
			КонецЕсли;

			НоваяСтрока.КЭК = БухгалтерскийУчетПовтИсп.АналитическийКЭКпоСчету(НоваяСтрока.СчетРасчетов);

		КонецЦикла;

	КонецЕсли;


	// 3. Расчеты по реализованным услугам, работам.
	ЕстьТабЧастьУслугиОказанные  = ОбщегоНазначенияБГУ.ЕстьТабЧастьДокумента("УслугиОказанные", МетаданныеДокумента);
	ЕстьТабЧастьСоставЭтапаРабот = ОбщегоНазначенияБГУ.ЕстьТабЧастьДокумента("СоставЭтапаРабот", МетаданныеДокумента);
	ЕстьТабЧастьНачисления       = ОбщегоНазначенияБГУ.ЕстьТабЧастьДокумента("Начисления", МетаданныеДокумента);

	Если ЕстьТабЧастьУслугиОказанные Тогда

		ОбщегоНазначенияБГУ.ПоместитьТабЧастьВВиртуальнуюТаблицу(Запрос, "УслугиОказанные",
		"ИФО,КФО,КПС,Подразделение,Контрагент,Договор,ВалютаДокумента,СуммаНДС,Всего",
		Документ, Параметры);

		Запрос.Текст = Запрос.Текст +
		"ВЫБРАТЬ
		|	УслугиОказанные.ИФО КАК ИФО,
		|	УслугиОказанные.КФО КАК КФО,
		|	УслугиОказанные.КПС КАК КПС,
		|	УслугиОказанные.Подразделение КАК Подразделение,
		|	УслугиОказанные.Контрагент КАК Контрагент,
		|	УслугиОказанные.Договор КАК Договор,
		|	УслугиОказанные.ВалютаДокумента КАК Валюта,
		|	УслугиОказанные.СуммаНДС КАК СуммаНДС,
		|	УслугиОказанные.Всего КАК СуммаРасчетов
		|ИЗ
		|	ТаблицаУслугиОказанные КАК УслугиОказанные";

		Если Параметры.ПоВсемКФО Тогда
			Запрос.Текст = СтрЗаменить(Запрос.Текст, "УслугиОказанные.КФО",
			"ЗНАЧЕНИЕ(Перечисление.КВД.ПустаяСсылка)");
		КонецЕсли;
		Если Параметры.ПоВсемКПС Тогда
			Запрос.Текст = СтрЗаменить(Запрос.Текст, "УслугиОказанные.КПС",
			"ЗНАЧЕНИЕ(Справочник.КлассификационныеПризнакиСчетов.ПустаяСсылка)");
		КонецЕсли;

	ИначеЕсли ЕстьТабЧастьСоставЭтапаРабот Тогда

		ОбщегоНазначенияБГУ.ПоместитьТабЧастьВВиртуальнуюТаблицу(Запрос, "СоставЭтапаРабот",
		"ИФО,КФО,КПС,Подразделение,Контрагент,Договор,ВалютаДокумента,СуммаНДС,Всего",
		Документ, Параметры);

		Запрос.Текст = Запрос.Текст +
		"ВЫБРАТЬ
		|	СоставЭтапаРабот.ИФО КАК ИФО,
		|	СоставЭтапаРабот.КФО КАК КФО,
		|	СоставЭтапаРабот.КПС КАК КПС,
		|	СоставЭтапаРабот.Подразделение КАК Подразделение,
		|	СоставЭтапаРабот.Контрагент КАК Контрагент,
		|	СоставЭтапаРабот.Договор КАК Договор,
		|	СоставЭтапаРабот.ВалютаДокумента КАК Валюта,
		|	СоставЭтапаРабот.СуммаНДС КАК СуммаНДС,
		|	СоставЭтапаРабот.Всего КАК СуммаРасчетов
		|ИЗ
		|	ТаблицаСоставЭтапаРабот КАК СоставЭтапаРабот";

		Если Параметры.ПоВсемКФО Тогда
			Запрос.Текст = СтрЗаменить(Запрос.Текст, "СоставЭтапаРабот.КФО",
			"ЗНАЧЕНИЕ(Перечисление.КВД.ПустаяСсылка)");
		КонецЕсли;
		Если Параметры.ПоВсемКПС Тогда
			Запрос.Текст = СтрЗаменить(Запрос.Текст, "СоставЭтапаРабот.КПС",
			"ЗНАЧЕНИЕ(Справочник.КлассификационныеПризнакиСчетов.ПустаяСсылка)");
		КонецЕсли;

	ИначеЕсли МетаданныеДокумента.Имя = "НачислениеЗаУслугиОбразования" Или МетаданныеДокумента.Имя = "НачислениеСтудентамЗаПрочиеУслуги" Тогда

		ОбщегоНазначенияБГУ.ПоместитьТабЧастьВВиртуальнуюТаблицу(Запрос, "Начисления",
		"ИФО,КФО,КПС,КЭК,Факультет,Контрагент,ДоговорКонтрагента,
		|ВалютаДокумента,УчитыватьНДС,СуммаВключаетНДС,СуммаНДС,Сумма",
		Документ, Параметры);

		Если Документ.ВидОперации = Перечисления.ВидыОперацийНачисленияУчащимся.ПоКраткосрочнымДоговорам Тогда
			// Исключение из общего принципа - для начислений по краткосрочным договорам (образование),
			// КФО, КПС и КЭК берем из параметров источника данных (реквизитов типовой операции),
			// несмотря на то, что одноименные реквизиты есть в табличной части.
			Если Параметры.Свойство("КФО") И ЗначениеЗаполнено(Параметры.КФО) Тогда
				Запрос.Текст = СтрЗаменить(Запрос.Текст, "Начисления.КФО", "&ПараметрКФО");
				Запрос.УстановитьПараметр("ПараметрКФО", Параметры.КФО);
			КонецЕсли;
			Если Параметры.Свойство("КПС") И ЗначениеЗаполнено(Параметры.КПС) Тогда
				Запрос.Текст = СтрЗаменить(Запрос.Текст, "Начисления.КПС", "&ПараметрКПС");
				Запрос.УстановитьПараметр("ПараметрКПС", Параметры.КПС);
			КонецЕсли;
			Если Параметры.Свойство("КЭКРасчетов") И ЗначениеЗаполнено(Параметры.КЭКРасчетов) Тогда
				Запрос.Текст = СтрЗаменить(Запрос.Текст, "Начисления.КЭК", "&ПараметрКЭК");
				Запрос.УстановитьПараметр("ПараметрКЭК", Параметры.КЭКРасчетов);
			КонецЕсли;
		КонецЕсли;

		Запрос.Текст = Запрос.Текст +
		"ВЫБРАТЬ
		|	Начисления.ИФО КАК ИФО,
		|	Начисления.КФО КАК КФО,
		|	Начисления.КПС КАК КПС,
		|	Начисления.КЭК КАК КЭК,
		|	Начисления.Факультет КАК Подразделение,
		|	Начисления.Контрагент КАК Контрагент,
		|	Начисления.ДоговорКонтрагента КАК Договор,
		|	Начисления.ВалютаДокумента КАК Валюта,
		|	Начисления.СуммаНДС КАК СуммаНДС,
		|	ВЫБОР
		|		КОГДА Начисления.УчитыватьНДС
		|				И НЕ Начисления.СуммаВключаетНДС
		|			ТОГДА Начисления.Сумма + Начисления.СуммаНДС
		|		ИНАЧЕ Начисления.Сумма
		|	КОНЕЦ КАК СуммаРасчетов
		|ИЗ
		|	ТаблицаНачисления КАК Начисления";

		Если НЕ ПолучитьФункциональнуюОпцию("ИспользоватьУчетПоПодразделениям") Тогда
			Запрос.Текст = СтрЗаменить(Запрос.Текст, "Начисления.Факультет", "ЗНАЧЕНИЕ(Справочник.Подразделения.ПустаяСсылка)");
		КонецЕсли;

		Если Параметры.ПоВсемКФО Тогда
			Запрос.Текст = СтрЗаменить(Запрос.Текст, "Начисления.КФО",
			"ЗНАЧЕНИЕ(Перечисление.КВД.ПустаяСсылка)");
		КонецЕсли;
		Если Параметры.ПоВсемКПС Тогда
			Запрос.Текст = СтрЗаменить(Запрос.Текст, "Начисления.КПС",
			"ЗНАЧЕНИЕ(Справочник.КлассификационныеПризнакиСчетов.ПустаяСсылка)");
		КонецЕсли;

	ИначеЕсли ЕстьТабЧастьНачисления Тогда

		ОбщегоНазначенияБГУ.ПоместитьТабЧастьВВиртуальнуюТаблицу(Запрос, "Начисления",
		"ИФО,КФО,КПС,Подразделение,Контрагент,ДоговорКонтрагента,ВалютаДокумента,УчитыватьНДС,СуммаВключаетНДС,СуммаНДС,Сумма",
		Документ, Параметры);

		Запрос.Текст = Запрос.Текст +
		"ВЫБРАТЬ
		|	Начисления.ИФО КАК ИФО,
		|	Начисления.КФО КАК КФО,
		|	Начисления.КПС КАК КПС,
		|	Начисления.Подразделение КАК Подразделение,
		|	Начисления.Контрагент КАК Контрагент,
		|	Начисления.ДоговорКонтрагента КАК Договор,
		|	Начисления.ВалютаДокумента КАК Валюта,
		|	Начисления.СуммаНДС КАК СуммаНДС,
		|	ВЫБОР
		|		КОГДА Начисления.УчитыватьНДС
		|				И НЕ Начисления.СуммаВключаетНДС
		|			ТОГДА Начисления.Сумма + Начисления.СуммаНДС
		|		ИНАЧЕ Начисления.Сумма
		|	КОНЕЦ КАК СуммаРасчетов
		|ИЗ
		|	ТаблицаНачисления КАК Начисления";

		Если Параметры.ПоВсемКФО Тогда
			Запрос.Текст = СтрЗаменить(Запрос.Текст, "Начисления.КФО",
			"ЗНАЧЕНИЕ(Перечисление.КВД.ПустаяСсылка)");
		КонецЕсли;
		Если Параметры.ПоВсемКПС Тогда
			Запрос.Текст = СтрЗаменить(Запрос.Текст, "Начисления.КПС",
			"ЗНАЧЕНИЕ(Справочник.КлассификационныеПризнакиСчетов.ПустаяСсылка)");
		КонецЕсли;

	КонецЕсли;

	Если ЕстьТабЧастьУслугиОказанные ИЛИ ЕстьТабЧастьСоставЭтапаРабот ИЛИ ЕстьТабЧастьНачисления Тогда

		РасчетыПоУслугамРаботам = Запрос.Выполнить().Выгрузить();

		Если Параметры.Свойство("КЭКРасчетов") Тогда
			КЭКРасчетов = Параметры.КЭКРасчетов;
		ИначеЕсли ОбщегоНазначенияБГУ.ЕстьРеквизитДокумента("КЭК", МетаданныеДокумента) Тогда
			КЭКРасчетов = Документ.КЭК;
			Параметры.Вставить("КЭКРасчетов", Документ.КЭК);
		Иначе
			КЭКРасчетов = Справочники.КОСГУ.ПустаяСсылка();
		КонецЕсли;

		СчетРасчетов = ОпределитьСчетРасчетовПоПараметрам(Параметры, "Реализация",
		Документ.Дата, Документ.Организация, Документ.ИФО);

		Для Каждого СтрокаДокумента Из РасчетыПоУслугамРаботам Цикл

			НоваяСтрока = ТаблицаРасчетов.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаДокумента);

			НоваяСтрока.ЭтоПоступление = Ложь;

			Если ЗначениеЗаполнено(СчетРасчетов) Тогда
				НоваяСтрока.СчетРасчетов = СчетРасчетов;
			ИначеЕсли ЗначениеЗаполнено(НоваяСтрока.КЭК) Тогда
				НоваяСтрока.СчетРасчетов = БухгалтерскийУчет.ПолучитьАналитическийСчетПоКЭК(НоваяСтрока.КЭК,
				?(Параметры.Свойство("СчетРасчетов") И ЗначениеЗаполнено(Параметры.СчетРасчетов), Параметры.СчетРасчетов, "205.00"), ,
				Документ.Дата, Документ.Организация, СтрокаДокумента.ИФО);
			КонецЕсли;

			Если ЗначениеЗаполнено(КЭКРасчетов) Тогда
				НоваяСтрока.КЭК = КЭКРасчетов;
			Иначе
				НоваяСтрока.КЭК = БухгалтерскийУчетПовтИсп.АналитическийКЭКпоСчету(НоваяСтрока.СчетРасчетов);
			КонецЕсли;

			Если НЕ ЗначениеЗаполнено(НоваяСтрока.СчетРасчетов) Тогда
				ТекстОшибки = НСтр("ru='Невозможно определить счет расчетов с контрагентом по КЭК ""%1"".
				|Проверьте настройки соответствия аналитических счетов КЭК.'");
				ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстОшибки,
				НоваяСтрока.КЭК);
				ОбщегоНазначения.СообщитьПользователю(ТекстОшибки,
				Документ.Ссылка, , "Объект", Отказ);
			КонецЕсли;

		КонецЦикла;

	КонецЕсли;


	// 3b. Расчеты по реализованным услугам, работам - списание доходов будущих периодов.
	ЕстьТабЧастьДБП = ОбщегоНазначенияБГУ.ЕстьТабЧастьДокумента("ДоходыБудущихПериодов", МетаданныеДокумента);
	Если ЕстьТабЧастьДБП Тогда

		ОбщегоНазначенияБГУ.ПоместитьТабЧастьВВиртуальнуюТаблицу(Запрос, "ДоходыБудущихПериодов",
		"ИФО,КФО,КПС,КЭК,Подразделение,Контрагент,Договор,ВалютаДокумента,СуммаНДС,Всего",
		Документ, Параметры);

		Запрос.Текст = Запрос.Текст +
		"ВЫБРАТЬ
		|	ДоходыБудущихПериодов.ИФО КАК ИФО,
		|	ДоходыБудущихПериодов.КФО КАК КФО,
		|	ДоходыБудущихПериодов.КПС КАК КПС,
		|	ДоходыБудущихПериодов.КЭК КАК КЭК,
		|	ДоходыБудущихПериодов.Подразделение КАК Подразделение,
		|	ДоходыБудущихПериодов.Контрагент КАК Контрагент,
		|	ДоходыБудущихПериодов.Договор КАК Договор,
		|	ДоходыБудущихПериодов.ВалютаДокумента КАК Валюта,
		|	ДоходыБудущихПериодов.СуммаНДС КАК СуммаНДС,
		|	ДоходыБудущихПериодов.Всего КАК СуммаРасчетов
		|ИЗ
		|	ТаблицаДоходыБудущихПериодов КАК ДоходыБудущихПериодов
		|ГДЕ
		|	ДоходыБудущихПериодов.Всего > 0";

		Если Параметры.ПоВсемКФО Тогда
			Запрос.Текст = СтрЗаменить(Запрос.Текст, "ДоходыБудущихПериодов.КФО",
			"ЗНАЧЕНИЕ(Перечисление.КВД.ПустаяСсылка)");
		КонецЕсли;
		Если Параметры.ПоВсемКПС Тогда
			Запрос.Текст = СтрЗаменить(Запрос.Текст, "ДоходыБудущихПериодов.КПС",
			"ЗНАЧЕНИЕ(Справочник.КлассификационныеПризнакиСчетов.ПустаяСсылка)");
		КонецЕсли;

		РасчетыПоУслугамРаботам = Запрос.Выполнить().Выгрузить();

		Для Каждого СтрокаДокумента Из РасчетыПоУслугамРаботам Цикл

			НоваяСтрока = ТаблицаРасчетов.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаДокумента);

			НоваяСтрока.ЭтоПоступление = Ложь;
			НоваяСтрока.СчетРасчетов   = БухгалтерскийУчет.ПолучитьАналитическийСчетПоКЭК(
			НоваяСтрока.КЭК, "205.00", , Документ.Дата, Документ.Организация, СтрокаДокумента.ИФО);

		КонецЦикла;

	КонецЕсли;


	// 4. Расчеты по поступлению ОС, НМА, НПА.
	ЕстьТабЧастьКапВложения = ОбщегоНазначенияБГУ.ЕстьТабЧастьДокумента("КапВложения", МетаданныеДокумента);
	Если ЕстьТабЧастьКапВложения Тогда

		ОбщегоНазначенияБГУ.ПоместитьТабЧастьВВиртуальнуюТаблицу(Запрос, "КапВложения",
		"НомерСтроки,ИФО,КФО,КПС,Подразделение,Контрагент,Договор,ВалютаДокумента,
		|ВнеоборотныйАктив,СчетУчета,СуммаНДС,Всего",
		Документ, Параметры);

		Запрос.Текст = Запрос.Текст +
		"ВЫБРАТЬ
		|	КапВложения.НомерСтроки КАК НомерСтроки,
		|	КапВложения.ИФО КАК ИФО,   
		#Вставка
		|	НЕОПРЕДЕЛЕНО КАК ДопСубконтоАвансов,
		#КонецВставки
		|	КапВложения.КФО КАК КФО,
		|	КапВложения.КПС КАК КПС,
		|	КапВложения.Подразделение КАК Подразделение,
		|	КапВложения.Контрагент КАК Контрагент,
		|	КапВложения.Договор КАК Договор,
		|	КапВложения.ВалютаДокумента КАК Валюта,
		|	КапВложения.ВнеоборотныйАктив КАК ВнеоборотныйАктив,
		|	КапВложения.СчетУчета КАК СчетУчета,
		|	КапВложения.СуммаНДС КАК СуммаНДС,
		|	КапВложения.Всего КАК СуммаРасчетов
		|ИЗ
		|	ТаблицаКапВложения КАК КапВложения";

		Если Параметры.ПоВсемКФО Тогда
			Запрос.Текст = СтрЗаменить(Запрос.Текст, "КапВложения.КФО",
			"ЗНАЧЕНИЕ(Перечисление.КВД.ПустаяСсылка)");
		КонецЕсли;
		Если Параметры.ПоВсемКПС Тогда
			Запрос.Текст = СтрЗаменить(Запрос.Текст, "КапВложения.КПС",
			"ЗНАЧЕНИЕ(Справочник.КлассификационныеПризнакиСчетов.ПустаяСсылка)");
		КонецЕсли;

		РасчетыПоОС = Запрос.Выполнить().Выгрузить(); 
		
		#Вставка 
		ЗаполнитьДопКолонку("КодМероприятия", "КапВложения", Документ.Ссылка, РасчетыПоОС, "ДопСубконтоАвансов");
		#КонецВставки

		СчетРасчетов = ОпределитьСчетРасчетовПоПараметрам(Параметры, "Поступление",
		Документ.Дата, Документ.Организация, Документ.ИФО);

		Для Каждого СтрокаДокумента Из РасчетыПоОС Цикл

			// СчетУчета.
			Если НЕ ЗначениеЗаполнено(СтрокаДокумента.СчетУчета) Тогда
				ТекстСообщения = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения("Колонка",, НСтр("ru = 'Счет учета'"),
				СтрокаДокумента.НомерСтроки, "Капитальные вложения");
				ПолеКолонки = "Объект.КапВложения[" + Формат(СтрокаДокумента.НомерСтроки-1, "ЧН=0; ЧГ=") + "].СчетУчета";
				БухгалтерскиеОперацииСервер.ДобавитьСообщениеОбОшибке(ТекстСообщения, ПолеКолонки, Отказ);
			КонецЕсли;

			Если Отказ Тогда
				Продолжить;
			КонецЕсли;

			НоваяСтрока = ТаблицаРасчетов.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаДокумента);

			НоваяСтрока.ЭтоПоступление = Истина;

			// Счет расчетов.
			Если ЗначениеЗаполнено(СчетРасчетов) Тогда
				НоваяСтрока.СчетРасчетов = СчетРасчетов;
			ИначеЕсли МетаданныеДокумента.Имя = "ПоступлениеОСВПути" Тогда
				НоваяСтрока.СчетРасчетов = БухгалтерскийУчет.СчетПоКоду("302.31", Документ.Дата, Документ.Организация);
			Иначе
				НоваяСтрока.СчетРасчетов = УчетРасчетов.СчетРасчетовПоПриобретениюВНА(
				СтрокаДокумента.СчетУчета, "302.00", Документ.Дата, Документ.Организация);
			КонецЕсли;

			НоваяСтрока.КЭК = БухгалтерскийУчетПовтИсп.АналитическийКЭКпоСчету(НоваяСтрока.СчетРасчетов);

			Если НЕ ЗначениеЗаполнено(НоваяСтрока.СчетРасчетов) Тогда
				ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru='Невозможно определить счет расчетов с контрагентом для внеоборотного актива ""%1"".
				|Возможно, неверно указан счет учета в строке поступления внеоборотного актива.'"),
				СтрокаДокумента.ВнеоборотныйАктив);
				ПолеКолонки = "Объект.КапВложения[" + Формат(СтрокаДокумента.НомерСтроки-1, "ЧН=0; ЧГ=") + "].СчетУчета";
				БухгалтерскиеОперацииСервер.ДобавитьСообщениеОбОшибке(ТекстСообщения, ПолеКолонки, Отказ);
			КонецЕсли;

		КонецЦикла;

	КонецЕсли;


	// 5. Расчеты по реализованным ОС, НМА, НПА.
	ЕстьТабЧастьОсновныеСредства  = ОбщегоНазначенияБГУ.ЕстьТабЧастьДокумента("ОсновныеСредства", МетаданныеДокумента);
	ЕстьРеализацияОсновныхСредств = (МетаданныеДокумента.Имя = "ПередачаОбъектаОС"
	ИЛИ МетаданныеДокумента.Имя = "ПередачаЗдания");

	Если ЕстьТабЧастьОсновныеСредства Тогда

		ОбщегоНазначенияБГУ.ПоместитьТабЧастьВВиртуальнуюТаблицу(Запрос, "ОсновныеСредства",
		"ИФО,КФО,КПС,Подразделение,Контрагент,Договор,ВалютаДокумента,ОсновноеСредство,СуммаНДС,Всего",
		Документ, Параметры);

	ИначеЕсли ЕстьРеализацияОсновныхСредств Тогда

		ОбщегоНазначенияБГУ.ПоместитьТабЧастьВВиртуальнуюТаблицу(Запрос, "Документ",
		"ИФО,КФО,КПС,Подразделение,Контрагент,Договор,ВалютаДокумента,ОсновноеСредство,СуммаНДС,Всего",
		Документ, Параметры);

	КонецЕсли;

	Если ЕстьТабЧастьОсновныеСредства ИЛИ ЕстьРеализацияОсновныхСредств Тогда

		Запрос.Текст = Запрос.Текст +
		"ВЫБРАТЬ
		|	ОсновныеСредства.ИФО КАК ИФО,
		|	ОсновныеСредства.КФО КАК КФО,
		|	ОсновныеСредства.КПС КАК КПС,
		|	ОсновныеСредства.Подразделение КАК Подразделение,
		|	ОсновныеСредства.Контрагент КАК Контрагент,
		|	ОсновныеСредства.Договор КАК Договор,
		|	ОсновныеСредства.ВалютаДокумента КАК Валюта,
		|	ОсновныеСредства.ОсновноеСредство КАК ОсновноеСредство,
		|	ОсновныеСредства.ОсновноеСредство.ВидНФА КАК ВидНФА,
		|	ОсновныеСредства.СуммаНДС КАК СуммаНДС,
		|	ОсновныеСредства.Всего КАК СуммаРасчетов
		|ИЗ
		|	ТаблицаОсновныеСредства КАК ОсновныеСредства";
		Если НЕ ЕстьТабЧастьОсновныеСредства Тогда
			Запрос.Текст = СтрЗаменить(Запрос.Текст, "ТаблицаОсновныеСредства КАК ОсновныеСредства",
			"Документ КАК ОсновныеСредства");
		КонецЕсли;

		Если Параметры.ПоВсемКФО Тогда
			Запрос.Текст = СтрЗаменить(Запрос.Текст, "ОсновныеСредства.КФО",
			"ЗНАЧЕНИЕ(Перечисление.КВД.ПустаяСсылка)");
		КонецЕсли;
		Если Параметры.ПоВсемКПС Тогда
			Запрос.Текст = СтрЗаменить(Запрос.Текст, "ОсновныеСредства.КПС",
			"ЗНАЧЕНИЕ(Справочник.КлассификационныеПризнакиСчетов.ПустаяСсылка)");
		КонецЕсли;

		РасчетыПоРеализованнымОС = Запрос.Выполнить().Выгрузить();

		Для Каждого СтрокаДокумента Из РасчетыПоРеализованнымОС Цикл

			НоваяСтрока = ТаблицаРасчетов.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаДокумента);

			НоваяСтрока.ЭтоПоступление = Ложь;

			Если СтрокаДокумента.ВидНФА = Перечисления.ВидыНФА.НПА Тогда
				НоваяСтрока.СчетРасчетов = БухгалтерскийУчетКлиентСервер.СчетПоКоду("205.73", Документ.Дата, Документ.Организация);
			ИначеЕсли СтрокаДокумента.ВидНФА = Перечисления.ВидыНФА.НМА Тогда
				НоваяСтрока.СчетРасчетов = БухгалтерскийУчетКлиентСервер.СчетПоКоду("205.72", Документ.Дата, Документ.Организация);
			Иначе
				НоваяСтрока.СчетРасчетов = БухгалтерскийУчетКлиентСервер.СчетПоКоду("205.71", Документ.Дата, Документ.Организация);
			КонецЕсли;

			НоваяСтрока.КЭК = БухгалтерскийУчетПовтИсп.АналитическийКЭКпоСчету(НоваяСтрока.СчетРасчетов);

			Если НЕ Параметры.ПоВсемКПС Тогда
				// В документах реализации в таб.части указан КПС расходов.
				// Для расчетов должен использоваться КПС доходов.
				НоваяСтрока.КПС = Параметры.КПС;
			КонецЕсли;

		КонецЦикла;

	КонецЕсли;


	// 6. Расчеты по поступлению денежных документов.
	Если МетаданныеДокумента.Имя = "ПриходныйОрдерФондовый" Тогда

		ОбщегоНазначенияБГУ.ПоместитьТабЧастьВВиртуальнуюТаблицу(Запрос, "Расшифровка",
		"НомерСтроки,ИФО,КФО,КПС,ПодразделениеКор,КорСчет,Контрагент,Договор,Сумма",
		Документ, Параметры);

		Запрос.Текст = Запрос.Текст +
		"ВЫБРАТЬ
		|	Расшифровка.НомерСтроки КАК НомерСтроки,
		|	Расшифровка.ИФО КАК ИФО,
		|	Расшифровка.КФО КАК КФО,
		|	Расшифровка.КПС КАК КПС,
		|	Расшифровка.ПодразделениеКор КАК Подразделение,
		|	Расшифровка.КорСчет КАК СчетРасчетов,
		|	Расшифровка.Контрагент КАК Контрагент,
		|	Расшифровка.Договор КАК Договор,
		|	&ВалютаРеглУчета КАК Валюта,
		|	0 КАК СуммаНДС,
		|	Расшифровка.Сумма КАК СуммаРасчетов
		|ИЗ
		|	ТаблицаРасшифровка КАК Расшифровка";
		Запрос.УстановитьПараметр("ВалютаРеглУчета", БухгалтерскийУчетПовтИсп.ВалютаРегламентированногоУчета());

		Если Параметры.ПоВсемКФО Тогда
			Запрос.Текст = СтрЗаменить(Запрос.Текст, "Расшифровка.КФО",
			"ЗНАЧЕНИЕ(Перечисление.КВД.ПустаяСсылка)");
		КонецЕсли;
		Если Параметры.ПоВсемКПС Тогда
			Запрос.Текст = СтрЗаменить(Запрос.Текст, "Расшифровка.КПС",
			"ЗНАЧЕНИЕ(Справочник.КлассификационныеПризнакиСчетов.ПустаяСсылка)");
		КонецЕсли;

		РасчетыПоДенежнымДокументам = Запрос.Выполнить().Выгрузить();

		Для Каждого СтрокаДокумента Из РасчетыПоДенежнымДокументам Цикл

			НоваяСтрока = ТаблицаРасчетов.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаДокумента);

			НоваяСтрока.ЭтоПоступление = Истина;

			НоваяСтрока.КЭК = БухгалтерскийУчетПовтИсп.АналитическийКЭКпоСчету(НоваяСтрока.СчетРасчетов);

		КонецЦикла;

	КонецЕсли;


	// 7. Расчеты по перечислению средств в финансовые вложения.
	Если МетаданныеДокумента.Имя = "ФормированиеВложенийВФинансовыеАктивы" Тогда

		ОбщегоНазначенияБГУ.ПоместитьТабЧастьВВиртуальнуюТаблицу(Запрос, "ВложенияВФинансовыеАктивы",
		"НомерСтроки,ИФО,КФО,КПС,Подразделение,СчетУчета,Контрагент,Договор,ВалютаДокумента,Сумма",
		Документ, Параметры);

		Запрос.Текст = Запрос.Текст +
		"ВЫБРАТЬ
		|	ВложенияВФинансовыеАктивы.НомерСтроки КАК НомерСтроки,
		|	ВложенияВФинансовыеАктивы.ИФО КАК ИФО,
		|	ВложенияВФинансовыеАктивы.КФО КАК КФО,
		|	ВложенияВФинансовыеАктивы.КПС КАК КПС,
		|	ВложенияВФинансовыеАктивы.Подразделение КАК Подразделение,
		|	ВложенияВФинансовыеАктивы.СчетУчета КАК СчетУчета,
		|	ВложенияВФинансовыеАктивы.Контрагент КАК Контрагент,
		|	ВложенияВФинансовыеАктивы.Договор КАК Договор,
		|	ВложенияВФинансовыеАктивы.ВалютаДокумента КАК Валюта,
		|	0 КАК СуммаНДС,
		|	ВложенияВФинансовыеАктивы.Сумма КАК СуммаРасчетов
		|ИЗ
		|	ТаблицаВложенияВФинансовыеАктивы КАК ВложенияВФинансовыеАктивы";

		Если Параметры.ПоВсемКФО Тогда
			Запрос.Текст = СтрЗаменить(Запрос.Текст, "Расшифровка.КФО",
			"ЗНАЧЕНИЕ(Перечисление.КВД.ПустаяСсылка)");
		КонецЕсли;
		Если Параметры.ПоВсемКПС Тогда
			Запрос.Текст = СтрЗаменить(Запрос.Текст, "Расшифровка.КПС",
			"ЗНАЧЕНИЕ(Справочник.КлассификационныеПризнакиСчетов.ПустаяСсылка)");
		КонецЕсли;

		Выборка = Запрос.Выполнить().Выбрать();
		Пока Выборка.Следующий() Цикл

			НоваяСтрока = ТаблицаРасчетов.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);

			НоваяСтрока.ЭтоПоступление = Истина;

			НоваяСтрока.СчетРасчетов = УчетФинансовыхВложений.СчетРасчетовПоСчетуУчетаВложенийВФинансовыеАктивы(
			Выборка.СчетУчета, Документ.Дата, Документ.Организация);
			НоваяСтрока.КЭК = БухгалтерскийУчетПовтИсп.АналитическийКЭКпоСчету(НоваяСтрока.СчетРасчетов);

		КонецЦикла;

	КонецЕсли;

	// 8. Расчеты по реализации финансовых вложений.
	Если МетаданныеДокумента.Имя = "ВыбытиеФинансовыхВложений" Тогда

		ОбщегоНазначенияБГУ.ПоместитьТабЧастьВВиртуальнуюТаблицу(Запрос, "Документ",
		"ИФО,КФО,КПС,Подразделение,СчетУчета,Контрагент,Договор,ВалютаДокумента,СуммаРеализации",
		Документ, Параметры);

		Запрос.Текст = Запрос.Текст +
		"ВЫБРАТЬ
		|	1 КАК НомерСтроки,
		|	ВыбытиеФинансовыхВложений.ИФО КАК ИФО,
		|	ВыбытиеФинансовыхВложений.КФО КАК КФО,
		|	ВыбытиеФинансовыхВложений.КПС КАК КПС,
		|	ВыбытиеФинансовыхВложений.Подразделение КАК Подразделение,
		|	ВыбытиеФинансовыхВложений.СчетУчета КАК СчетУчета,
		|	ВыбытиеФинансовыхВложений.Контрагент КАК Контрагент,
		|	ВыбытиеФинансовыхВложений.Договор КАК Договор,
		|	ВыбытиеФинансовыхВложений.ВалютаДокумента КАК Валюта,
		|	0 КАК СуммаНДС,
		|	ВыбытиеФинансовыхВложений.СуммаРеализации КАК СуммаРасчетов
		|ИЗ
		|	Документ КАК ВыбытиеФинансовыхВложений";

		Если Параметры.ПоВсемКФО Тогда
			Запрос.Текст = СтрЗаменить(Запрос.Текст, "ВыбытиеФинансовыхВложений.КФО",
			"ЗНАЧЕНИЕ(Перечисление.КВД.ПустаяСсылка)");
		КонецЕсли;
		Если Параметры.ПоВсемКПС Тогда
			Запрос.Текст = СтрЗаменить(Запрос.Текст, "ВыбытиеФинансовыхВложений.КПС",
			"ЗНАЧЕНИЕ(Справочник.КлассификационныеПризнакиСчетов.ПустаяСсылка)");
		КонецЕсли;

		Выборка = Запрос.Выполнить().Выбрать();
		Пока Выборка.Следующий() Цикл

			НоваяСтрока = ТаблицаРасчетов.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);

			НоваяСтрока.ЭтоПоступление = Ложь;

			НоваяСтрока.СчетРасчетов = БухгалтерскийУчет.СчетПоКоду("205.75", Документ.Дата, Документ.Организация);

			Если Параметры.Свойство("КЭКРасчетов") И ЗначениеЗаполнено(Параметры.КЭКРасчетов) Тогда
				НоваяСтрока.КЭК = Параметры.КЭКРасчетов;
			Иначе
				НоваяСтрока.КЭК = БухгалтерскийУчетПовтИсп.АналитическийКЭКпоСчету(НоваяСтрока.СчетРасчетов);
			КонецЕсли;

			НоваяСтрока.КПС = Параметры.КПС;

		КонецЦикла;

	КонецЕсли;        

	
	#Удаление
	ТаблицаРасчетов.Свернуть("ЭтоПоступление,ИФО,КФО,КПС,КЭК,Подразделение,СчетРасчетов,Контрагент,Договор,Валюта",
	"СуммаНДС,СуммаРасчетов"); 
	#КонецУдаления
	#Вставка
	ТаблицаРасчетов.Свернуть("ЭтоПоступление,ИФО,КФО,КПС,КЭК,Подразделение,СчетРасчетов,Контрагент,Договор,Валюта,ДопСубконтоАвансов",
	"СуммаНДС,СуммаРасчетов"); 
	#КонецВставки

	Если МетаданныеДокумента.Имя = "НачислениеЗаУслугиОбразования" Тогда
		ИспользуетсяСчетАП = (Документ.Дата >= ДатаПереходаНаЗабалансовыйСчетАП(Документ.Организация));
		Если НЕ ИспользуетсяСчетАП Тогда
			// Не нужно зачитывать аванс, только долги по документам расчетов - до перевода авансов на счет АП.
			// Авансы по счету АП зачитываются в обычном порядке.
			ТаблицаРасчетов.Колонки.Добавить("СчетАвансов", Новый ОписаниеТипов("ПланСчетовСсылка.ЕПСБУ"));
			Для Каждого СтрокаРасчетов Из ТаблицаРасчетов Цикл
				СтрокаРасчетов.СчетАвансов = СтрокаРасчетов.СчетРасчетов;
			КонецЦикла;
		КонецЕсли;
	КонецЕсли;

	Возврат ТаблицаРасчетов;

КонецФункции       

Функция ПолучитьДопТабЧастьДокумента(Наименование,Документ)
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ДополнительныеТабличныеЧастиДокументов.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.ДополнительныеТабличныеЧастиДокументов КАК ДополнительныеТабличныеЧастиДокументов
	|ГДЕ
	|	ДополнительныеТабличныеЧастиДокументов.ИмяТабличнойЧасти = &Наименование
	|	И ДополнительныеТабличныеЧастиДокументов.ИдентификаторДокумента = &ИдентификаторДокумента
	|	И НЕ ДополнительныеТабличныеЧастиДокументов.ПометкаУдаления"); 
	
	Запрос.УстановитьПараметр("Наименование",Наименование);
	Запрос.УстановитьПараметр("ИдентификаторДокумента",Справочники.ИдентификаторыОбъектовМетаданных.ИдентификаторОбъектаМетаданных(Документ.Метаданные(),Ложь));
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Возврат	Выборка.Ссылка;
	КонецЕсли;
	
	Возврат Справочники.ДополнительныеТабличныеЧастиДокументов.ПустаяСсылка();
КонецФункции

Процедура ЗаполнитьДопКолонку(ИмяДопКолонки, ИмяДопТабЧасти, ДокументСсылка, ТаблицаДокумента, ИмяКолонкиВТаблице, ОтборВозвратнаяТара = Неопределено) Экспорт
	ДопТабЧасть = ПолучитьДопТабЧастьДокумента(ИмяДопТабЧасти, ДокументСсылка);
	ДопКолонка = Справочники.КолонкиДополнительныхТабличныхЧастей.НайтиПоРеквизиту("ИмяКолонки", ИмяДопКолонки,,ДопТабЧасть);
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ЗначенияКолонокДополнительныхТабличныхЧастей.НомерСтрокиДокумента КАК НомерСтроки,
	|	ЗначенияКолонокДополнительныхТабличныхЧастей.Значение КАК Значение
	|ИЗ
	|	РегистрСведений.ЗначенияКолонокДополнительныхТабличныхЧастей КАК ЗначенияКолонокДополнительныхТабличныхЧастей
	|ГДЕ
	|	ЗначенияКолонокДополнительныхТабличныхЧастей.Объект = &Объект
	|	И ЗначенияКолонокДополнительныхТабличныхЧастей.ТабличнаяЧасть = &ТабличнаяЧасть
	|	И ЗначенияКолонокДополнительныхТабличныхЧастей.КолонкаТабличнойЧасти = &КолонкаТабличнойЧасти");
	Запрос.УстановитьПараметр("Объект", ДокументСсылка);
	Запрос.УстановитьПараметр("ТабличнаяЧасть", ДопТабЧасть);
	Запрос.УстановитьПараметр("КолонкаТабличнойЧасти", ДопКолонка);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл  
		Отбор = Новый Структура("НомерСтроки", Выборка.НомерСтроки);   
		
		Если ОтборВозвратнаяТара <> Неопределено Тогда
			Отбор.Вставить("ЭтоВозвратнаяТара", ОтборВозвратнаяТара);		
		КонецЕсли;
		
		НайдСтроки = ТаблицаДокумента.НайтиСтроки(Отбор);
		
		Если НайдСтроки.Количество() Тогда
			НайдСтроки[0][ИмяКолонкиВТаблице] = Выборка.Значение;	
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры

&ИзменениеИКонтроль("ДенежныеОбязательстваПоПоступлениюТРУ")
Функция АБК_ДенежныеОбязательстваПоПоступлениюТРУ(Объект, ПериодОстатков, Отказ)

	ТаблицаОбязательств = Новый ТаблицаЗначений;
	ТаблицаОбязательств.Колонки.Добавить("КФО",   			Новый ОписаниеТипов("ПеречислениеСсылка.КВД"));
	ТаблицаОбязательств.Колонки.Добавить("КПС",   			Новый ОписаниеТипов("СправочникСсылка.КлассификационныеПризнакиСчетов"));
	ТаблицаОбязательств.Колонки.Добавить("КЭК",   			Новый ОписаниеТипов("СправочникСсылка.КОСГУ"));
	ТаблицаОбязательств.Колонки.Добавить("Контрагент",		Новый ОписаниеТипов("СправочникСсылка.Контрагенты"));
	ТаблицаОбязательств.Колонки.Добавить("Договор",   		Новый ОписаниеТипов("СправочникСсылка.Договоры"));
	ТаблицаОбязательств.Колонки.Добавить("Подразделение", 	Новый ОписаниеТипов("СправочникСсылка.Подразделения"));
	ТаблицаОбязательств.Колонки.Добавить("Сумма", 			Новый ОписаниеТипов("Число"));

	ПараметрыЗаполнения = Новый Структура("ПериодОстатков, Организация, Дата, Ссылка, КЭКРасчетов, СчетРасчетов, КПС, СпособЗачетаАванса");

	ПараметрыЗаполнения.ПериодОстатков = ПериодОстатков;
	Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(Объект, "СчетРасчетов") Тогда
		ПараметрыЗаполнения.СчетРасчетов = Объект.СчетРасчетов;
	ИначеЕсли ТипЗнч(Объект) = Тип("ДокументОбъект.ПоступлениеМЗВПути") Тогда
		ПараметрыЗаполнения.СчетРасчетов = БухгалтерскийУчет.СчетПоКоду("302.34", Объект.Дата, Объект.Организация);
	КонецЕсли;
	Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(Объект, "КПСРасчетов") Тогда
		ПараметрыЗаполнения.КПС = Объект.КПСРасчетов;
		КПСРасчетовОбъекта = Объект.КПСРасчетов;
	Иначе
		КПСРасчетовОбъекта = Неопределено;
	КонецЕсли;
	Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(Объект, "СпособЗачетаАванса") Тогда
		ПараметрыЗаполнения.СпособЗачетаАванса = Объект.СпособЗачетаАванса;
	КонецЕсли;

	ЗаполнитьЗначенияСвойств(ПараметрыЗаполнения, Объект, "Организация, Дата, Ссылка");

	РасчетыПоДокументу = ПодготовитьТаблицуРасчетовПоДокументу(Объект, ПараметрыЗаполнения, Отказ);

	Если Отказ Тогда
		Возврат ТаблицаОбязательств;
	КонецЕсли;

	Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(Объект, "НомерКорректировки") И Объект.НомерКорректировки > 0 Тогда
		ПараметрыЗаполнения.Вставить("НомерКорректировки", Объект.НомерКорректировки);
		ПараметрыЗаполнения.Вставить("КорректируемыйДокумент", Объект.ДокументОснование);
	КонецЕсли;

	Если ПараметрыЗаполнения.СпособЗачетаАванса = Перечисления.СпособыЗачетаАвансов.ПоДокументу Тогда

		ВалютаРубль = Константы.ВалютаРегламентированногоУчета.Получить();

		ЗачетАвансовПоДокументу = ПустаяТаблицаЗачетАвансовПоДокументу();
		Для Каждого СтрокаАванса Из Объект.ЗачетАвансов Цикл
			НоваяСтрокаЗачета = ЗачетАвансовПоДокументу.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрокаЗачета, Объект);
			ЗаполнитьЗначенияСвойств(НоваяСтрокаЗачета, СтрокаАванса);
			НоваяСтрокаЗачета.СчетАвансов = ОпределитьСчетАвансов(НоваяСтрокаЗачета.СчетРасчетов,
			Объект.Дата, Объект.Организация);
			НоваяСтрокаЗачета.СуммаРасчетов = СтрокаАванса.СуммаЗачета;
			НоваяСтрокаЗачета.ЭтоПоступление = Истина;
			НоваяСтрокаЗачета.Валюта = ?(ЗначениеЗаполнено(НоваяСтрокаЗачета.Валюта), НоваяСтрокаЗачета.Валюта, ВалютаРубль);
		КонецЦикла;  
		
		#Вставка
	ИначеЕсли ПараметрыЗаполнения.СпособЗачетаАванса = Перечисления.СпособыЗачетаАвансов.ПоДокументамИОстаткам Тогда    
		
		МетаданныеДокумента = Объект.Метаданные();  
		ВидОперации = ?(Найти(МетаданныеДокумента.Имя, "Поступление") > 0, "Поступление", "Реализация");

		ВалютаРубль = Константы.ВалютаРегламентированногоУчета.Получить();
		
		ЗачетАвансовПоДокументу = ПустаяТаблицаЗачетАвансовПоДокументу();
		Для Каждого СтрокаАванса Из Объект.ЗачетАвансовПоДокументамИОстаткам Цикл    
			ИзначальныйКЭКРасчетов = ПараметрыЗаполнения.КЭКРасчетов;
			
			НоваяСтрокаЗачета = ЗачетАвансовПоДокументу.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрокаЗачета, Объект);
			ЗаполнитьЗначенияСвойств(НоваяСтрокаЗачета, СтрокаАванса);   
			
			ПараметрыЗаполнения.КЭКРасчетов = СтрокаАванса.КЭК;
			
			НоваяСтрокаЗачета.СчетРасчетов = ОпределитьСчетРасчетовПоПараметрам(ПараметрыЗаполнения, ВидОперации,
			Объект.Дата, Объект.Организация, Объект.ИФО); 
			НоваяСтрокаЗачета.СчетАвансов = ОпределитьСчетАвансов(НоваяСтрокаЗачета.СчетРасчетов,
			Объект.Дата, Объект.Организация);
			
			НоваяСтрокаЗачета.ВидДопСубконтоАвансов = ПолучитьНетиповоеСубконтоСчета(НоваяСтрокаЗачета.СчетАвансов);
			
			НоваяСтрокаЗачета.СуммаРасчетов = СтрокаАванса.СуммаЗачета;
			НоваяСтрокаЗачета.ЭтоПоступление = Истина;
			НоваяСтрокаЗачета.Валюта = ?(ЗначениеЗаполнено(НоваяСтрокаЗачета.Валюта), НоваяСтрокаЗачета.Валюта, ВалютаРубль); 
			
			ПараметрыЗаполнения.КЭКРасчетов = ИзначальныйКЭКРасчетов;
		КонецЦикла;  
		#КонецВставки

	Иначе
		ЗачетАвансовПоДокументу = ПустаяТаблицаЗачетАвансовПоДокументу();
	КонецЕсли;
	РасчетыСЗачетомАвансов = ТаблицаРасчетовСЗачетомАвансов(РасчетыПоДокументу, ЗачетАвансовПоДокументу, ПараметрыЗаполнения, Отказ);

	Если Отказ Тогда
		Возврат ТаблицаОбязательств;
	КонецЕсли;

	ТаблицыПоступления = Новый Структура;

	РассчитыватьКЭКВКаждойСтроке = ТипЗнч(Объект) = Тип("ДокументОбъект.АктПриемкиТоваровРаботУслуг");
	Если ТипЗнч(Объект) = Тип("ДокументОбъект.АктПриемкиТоваровРаботУслуг") Тогда
		ТаблицаПоступления = УправлениеМатериальнымиЗапасами.ПустаяТаблицаПоступлениеМатериальныхЗапасов();

		МатериальныеЗапасыДокумента = УправлениеМатериальнымиЗапасами.ПодготовитьТаблицуПоступлениеМатериальныхЗапасовПоДокументу(Объект,, Отказ);
		ТаблицаПоступленияМЗ = УправлениеМатериальнымиЗапасами.ТаблицаПоступлениеМатериальныхЗапасов(МатериальныеЗапасыДокумента, РасчетыСЗачетомАвансов);
		Для каждого СтрокаПоступленияМЗ Из ТаблицаПоступленияМЗ Цикл
			ЗаполнитьЗначенияСвойств(ТаблицаПоступления.Добавить(), СтрокаПоступленияМЗ);
		КонецЦикла;

		ПараметрыЗаполнения.Вставить("ВидЗатрат", ПредопределенноеЗначение("Справочник.ВидыЗатрат.ПустаяСсылка"));
		ОСДокумента = УправлениеОсновнымиСредствами.ПодготовитьТаблицуПоступлениеОсновныхСредствПоДокументу(Объект, ПараметрыЗаполнения, Отказ);
		ТаблицаПоступленияОС = УправлениеОсновнымиСредствами.ТаблицаПоступлениеОсновныхСредств(ОСДокумента, РасчетыСЗачетомАвансов, ПараметрыЗаполнения, Отказ);
		Для каждого СтрокаПоступленияОС Из ТаблицаПоступленияОС Цикл
			СтрокаПоступления = ТаблицаПоступления.Добавить();
			ЗаполнитьЗначенияСвойств(СтрокаПоступления, СтрокаПоступленияОС);
			СтрокаПоступления.КЭК = БухгалтерскийУчетПовтИсп.АналитическийКЭКпоСчету(СтрокаПоступленияОС.СчетРасчетов)
		КонецЦикла;

		ТабДокумента = ПодготовитьТаблицуПоступлениеУслугРаботПоДокументу(Объект, ПараметрыЗаполнения); 
		ТаблицаПоступленияУслуг = ТаблицаПоступлениеУслугРабот(ТабДокумента, РасчетыСЗачетомАвансов, ПараметрыЗаполнения, Отказ);
		Для каждого СтрокаПоступленияУслуг Из ТаблицаПоступленияУслуг Цикл
			ЗаполнитьЗначенияСвойств(ТаблицаПоступления.Добавить(), СтрокаПоступленияУслуг);
		КонецЦикла;
		ТаблицыПоступления.Вставить("ПоступлениеМатериальныхЗапасов", ТаблицаПоступления);
	ИначеЕсли ОбщегоНазначенияБГУ.ЕстьТабЧастьДокумента("Материалы", Объект.Метаданные()) Тогда
		МатериальныеЗапасыДокумента = УправлениеМатериальнымиЗапасами.ПодготовитьТаблицуПоступлениеМатериальныхЗапасовПоДокументу(Объект,, Отказ);
		Если Отказ Тогда
			Возврат ТаблицаОбязательств;
		КонецЕсли;
		ТаблицаПоступления = УправлениеМатериальнымиЗапасами.ТаблицаПоступлениеМатериальныхЗапасов(МатериальныеЗапасыДокумента, РасчетыСЗачетомАвансов);
		ВозвратнаяТараДокумента = УправлениеМатериальнымиЗапасами.ПодготовитьТаблицуПоступлениеВозвратнойТарыПоДокументу(Объект,, Отказ);
		Если Отказ Тогда
			Возврат ТаблицаОбязательств;
		КонецЕсли;
		Для каждого СтрокаВозвратнойТары Из ВозвратнаяТараДокумента Цикл
			НоваяСтрока = ТаблицаПоступления.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаВозвратнойТары);
			НоваяСтрока.СуммаРуб = СтрокаВозвратнойТары.Сумма;
		КонецЦикла;
		ТаблицыПоступления.Вставить("ПоступлениеМатериальныхЗапасов", ТаблицаПоступления);

	ИначеЕсли ОбщегоНазначенияБГУ.ЕстьТабЧастьДокумента("КапВложения", Объект.Метаданные()) Тогда
		ПараметрыЗаполнения.Вставить("ВидЗатрат", ПредопределенноеЗначение("Справочник.ВидыЗатрат.ПустаяСсылка"));
		ОСДокумента = УправлениеОсновнымиСредствами.ПодготовитьТаблицуПоступлениеОсновныхСредствПоДокументу(Объект, ПараметрыЗаполнения, Отказ);
		Если Отказ Тогда
			Возврат ТаблицаОбязательств;
		КонецЕсли;
		ТаблицаПоступления = УправлениеОсновнымиСредствами.ТаблицаПоступлениеОсновныхСредств(ОСДокумента, РасчетыСЗачетомАвансов, ПараметрыЗаполнения, Отказ);
		Если Отказ Тогда
			Возврат ТаблицаОбязательств;
		КонецЕсли;
		ТаблицыПоступления.Вставить("ПоступлениеОсновныхСредств", ТаблицаПоступления);

	ИначеЕсли ОбщегоНазначенияБГУ.ЕстьТабЧастьДокумента("ПраваПользования", Объект.Метаданные())
		Или ОбщегоНазначенияБГУ.ЕстьТабЧастьДокумента("УслугиИРаботы", Объект.Метаданные()) Тогда

		ТабДокумента = ПодготовитьТаблицуПоступлениеУслугРаботПоДокументу(Объект, ПараметрыЗаполнения); 
		ТаблицаПоступления = ТаблицаПоступлениеУслугРабот(ТабДокумента, РасчетыСЗачетомАвансов, ПараметрыЗаполнения, Отказ);
		Если Отказ Тогда
			Возврат ТаблицаОбязательств;
		КонецЕсли;
		ТаблицыПоступления.Вставить("ПоступлениеУслугРабот", ТаблицаПоступления);
	КонецЕсли;

	ПараметрыРасчетаДО = Новый Структура;
	Если ТипЗнч(Объект) = Тип("ДокументОбъект.АктПриемкиТоваровРаботУслуг") Тогда
		ПараметрыРасчетаДО.Вставить("ЗаполнятьКЭК", Истина);
	КонецЕсли;
	ТаблицаДО = ТаблицаПринятыеДенежныеОбязательства(РасчетыСЗачетомАвансов, ТаблицыПоступления, ПараметрыРасчетаДО);

	КЭК340 = БухгалтерскийУчетПовтИсп.КЭКПоКоду("340");
	Если ТипЗнч(Объект) = Тип("ДокументОбъект.ПоступлениеУслугРабот")
		Или ТипЗнч(Объект) = Тип("ДокументОбъект.АктПриемкиТоваровРаботУслуг") Тогда
		ИспользоватьДетальныйКЭК = Истина;
	Иначе
		Если ЗначениеЗаполнено(ПараметрыЗаполнения.СчетРасчетов) Тогда
			КЭКРасчетов = БухгалтерскийУчетПовтИсп.АналитическийКЭКпоСчетуСУчетомПоступленияМЗ(ПараметрыЗаполнения.СчетРасчетов);
		Иначе
			КЭКРасчетов = Справочники.КОСГУ.ПустаяСсылка();
		КонецЕсли;
		ИспользоватьДетальныйКЭК = КЭКРасчетов = КЭК340;
	КонецЕсли;

	ПринятьВсеОбязательства = (ТипЗнч(ТаблицаПоступления) = Тип("ТаблицаЗначений"))  
	И (ТаблицаПоступления.Итог("СуммаРуб") = ТаблицаДО.Итог("СуммаРуб"));

	Если ПринятьВсеОбязательства И ИспользоватьДетальныйКЭК  Тогда

		Для Каждого СтрМатериалы Из ТаблицаПоступления Цикл
			НовСтрДО = ТаблицаОбязательств.Добавить();
			НовСтрДО.КФО   = СтрМатериалы.КФО;
			НовСтрДО.КПС   = ?(ЗначениеЗаполнено(КПСРасчетовОбъекта), КПСРасчетовОбъекта, СтрМатериалы.КПС);
			НовСтрДО.КЭК   = СтрМатериалы.КЭК;
			НовСтрДО.Сумма = СтрМатериалы.СуммаРуб;
		КонецЦикла;

	Иначе

		ДетальныеКЭК340 = ДетальныеКЭК340();
		ПоступлениеМЗПоРазнымКЭК = ЕстьРазныеДетальныеКЭК340(ТаблицаПоступления, ДетальныеКЭК340);
		Если ИспользоватьДетальныйКЭК Тогда
			ИспользуемыеКОСГУ = ОбщегоНазначения.ВыгрузитьКолонку(ТаблицаПоступления, "КЭК", Истина);
			Если ИспользуемыеКОСГУ.Количество() = 1 И ЗначениеЗаполнено(ИспользуемыеКОСГУ[0]) Тогда
				КОСГУ = ИспользуемыеКОСГУ[0];
			Иначе
				КОСГУ = Справочники.КОСГУ.ПустаяСсылка();
			КонецЕсли;
		Иначе
			КОСГУ = КЭКРасчетов;
		КонецЕсли;

		Для Каждого СтрТаблицаДО Из ТаблицаДО Цикл
			НовСтрДО = ТаблицаОбязательств.Добавить();
			НовСтрДО.Контрагент= СтрТаблицаДО.Контрагент;
			НовСтрДО.Договор= СтрТаблицаДО.Договор;
			НовСтрДО.Подразделение= СтрТаблицаДО.Подразделение;
			НовСтрДО.КФО   = СтрТаблицаДО.КФО;
			НовСтрДО.КПС   = СтрТаблицаДО.КПС;
			Если РассчитыватьКЭКВКаждойСтроке Тогда
				Если ДетальныеКЭК340.Получить(СтрТаблицаДО.КЭК) <> Неопределено
					И ПоступлениеМЗПоРазнымКЭК Тогда
					НовСтрДО.КЭК = КЭК340;
				ИначеЕсли ЗначениеЗаполнено(СтрТаблицаДО.КЭК) Тогда
					НовСтрДО.КЭК = СтрТаблицаДО.КЭК;
				Иначе
					НовСтрДО.КЭК = БухгалтерскийУчетПовтИсп.АналитическийКЭКпоСчетуСУчетомПоступленияМЗ(СтрТаблицаДО.СчетРасчетов);
				КонецЕсли;
			Иначе
				НовСтрДО.КЭК   = ?(ЗначениеЗаполнено(КОСГУ), КОСГУ, БухгалтерскийУчетПовтИсп.АналитическийКЭКпоСчетуСУчетомПоступленияМЗ(СтрТаблицаДО.СчетРасчетов));
			КонецЕсли;
			НовСтрДО.Сумма = СтрТаблицаДО.СуммаРуб;
		КонецЦикла;

	КонецЕсли;

	ТаблицаОбязательств.Свернуть("Контрагент,Договор,Подразделение,КФО,КПС,КЭК", "Сумма");

	Возврат ТаблицаОбязательств;

КонецФункции                         

Функция ПолучитьНетиповоеСубконтоСчета(Счет)       
	ВидДопСубконтоАвансов = Неопределено;
	
	МассивТиповыхСубконто = Новый Массив;
	МассивТиповыхСубконто.Добавить(ПланыВидовХарактеристик.ВидыСубконто.Контрагенты);
	МассивТиповыхСубконто.Добавить(ПланыВидовХарактеристик.ВидыСубконто.Договоры); 
	
	Для Каждого ВидСубконтоСчета из Счет.ВидыСубконто Цикл 
		
		ВидСубконто = ВидСубконтоСчета.ВидСубконто;
		
		Если МассивТиповыхСубконто.Найти(ВидСубконто) = Неопределено Тогда     
			
			ВидДопСубконтоАвансов = ВидСубконто; 
			
		КонецЕсли;
	КонецЦикла; 
	
	Возврат ВидДопСубконтоАвансов;
КонецФункции

