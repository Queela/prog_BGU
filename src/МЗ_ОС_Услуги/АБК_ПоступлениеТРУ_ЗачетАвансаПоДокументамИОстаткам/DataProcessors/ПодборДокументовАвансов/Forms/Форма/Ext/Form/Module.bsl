
&НаСервере
Процедура АБК_ПриСозданииНаСервереПеред(Отказ, СтандартнаяОбработка) 
	
	Если Параметры.Свойство("ПодборПоДокументамИОстаткам") Тогда
		ЭтаФорма.ПодборПоДокументамИОстаткам = Параметры.ПодборПоДокументамИОстаткам;	
	КонецЕсли;
	
	Если ЭтаФорма.ПодборПоДокументамИОстаткам Тогда
		
		ДокументыАвансов.ТекстЗапроса = 
		"ВЫБРАТЬ
		|	ЖурналПроводокЕПСБУОбороты.Регистратор КАК Регистратор,
		|	ВЫБОР
		|		КОГДА ЖурналПроводокЕПСБУОбороты.Субконто1 ССЫЛКА Справочник.Контрагенты
		|			ТОГДА ВЫРАЗИТЬ(ЖурналПроводокЕПСБУОбороты.Субконто1 КАК Справочник.Контрагенты)
		|		КОГДА ЖурналПроводокЕПСБУОбороты.Субконто2 ССЫЛКА Справочник.Контрагенты
		|			ТОГДА ВЫРАЗИТЬ(ЖурналПроводокЕПСБУОбороты.Субконто2 КАК Справочник.Контрагенты)
		|		КОГДА ЖурналПроводокЕПСБУОбороты.Субконто3 ССЫЛКА Справочник.Контрагенты
		|			ТОГДА ВЫРАЗИТЬ(ЖурналПроводокЕПСБУОбороты.Субконто3 КАК Справочник.Контрагенты)
		|	КОНЕЦ КАК Контрагент,
		|	ВЫБОР
		|		КОГДА ЖурналПроводокЕПСБУОбороты.Субконто1 ССЫЛКА Справочник.Договоры
		|			ТОГДА ВЫРАЗИТЬ(ЖурналПроводокЕПСБУОбороты.Субконто1 КАК Справочник.Договоры)
		|		КОГДА ЖурналПроводокЕПСБУОбороты.Субконто2 ССЫЛКА Справочник.Договоры
		|			ТОГДА ВЫРАЗИТЬ(ЖурналПроводокЕПСБУОбороты.Субконто2 КАК Справочник.Договоры)
		|		КОГДА ЖурналПроводокЕПСБУОбороты.Субконто3 ССЫЛКА Справочник.Договоры
		|			ТОГДА ВЫРАЗИТЬ(ЖурналПроводокЕПСБУОбороты.Субконто3 КАК Справочник.Договоры)
		|	КОНЕЦ КАК Договор,
		|	ВЫБОР
		|		КОГДА НЕ (ЖурналПроводокЕПСБУОбороты.Субконто1 ССЫЛКА Справочник.Контрагенты
		|			ИЛИ ЖурналПроводокЕПСБУОбороты.Субконто1 ССЫЛКА Справочник.Договоры)
		|			ТОГДА ЖурналПроводокЕПСБУОбороты.Субконто1
		|		КОГДА НЕ (ЖурналПроводокЕПСБУОбороты.Субконто2 ССЫЛКА Справочник.Контрагенты
		|			ИЛИ ЖурналПроводокЕПСБУОбороты.Субконто2 ССЫЛКА Справочник.Договоры)
		|			ТОГДА ЖурналПроводокЕПСБУОбороты.Субконто2
		|		КОГДА НЕ (ЖурналПроводокЕПСБУОбороты.Субконто3 ССЫЛКА Справочник.Контрагенты
		|			ИЛИ ЖурналПроводокЕПСБУОбороты.Субконто3 ССЫЛКА Справочник.Договоры)
		|			ТОГДА ЖурналПроводокЕПСБУОбороты.Субконто3
		|	КОНЕЦ КАК ДопСубконтоАвансов,
		|	ЖурналПроводокЕПСБУОбороты.КПС КАК КПС,
		|	ЖурналПроводокЕПСБУОбороты.ИФО КАК ИФО,
		|	ЖурналПроводокЕПСБУОбороты.КФО КАК КФО,
		|	ЖурналПроводокЕПСБУОбороты.Организация КАК Организация,
		|	ЖурналПроводокЕПСБУОбороты.Подразделение КАК Подразделение,
		|	ЖурналПроводокЕПСБУОбороты.СуммаОборот КАК СуммаОборот,
		|	ЖурналПроводокЕПСБУОбороты.Счет КАК Счет
		|ПОМЕСТИТЬ ВТ_ДокументыВыдачиАванса
		|ИЗ
		|	РегистрБухгалтерии.ЖурналПроводокЕПСБУ.Обороты(, {(&ПериодЗапроса)}, Регистратор, Счет В (&Счет206), , , , ) КАК ЖурналПроводокЕПСБУОбороты
		|ГДЕ
		|	ЖурналПроводокЕПСБУОбороты.СуммаОборот > 0
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ПоступлениеУслугРаботЗачетАвансовПоДокументамИОстаткам.Ссылка КАК Ссылка,
		|	ПоступлениеУслугРаботЗачетАвансовПоДокументамИОстаткам.НомерСтроки КАК НомерСтроки,
		|	ПоступлениеУслугРаботЗачетАвансовПоДокументамИОстаткам.ДокументАванса КАК ДокументАванса,
		|	ПоступлениеУслугРаботЗачетАвансовПоДокументамИОстаткам.КФО КАК КФО,
		|	ПоступлениеУслугРаботЗачетАвансовПоДокументамИОстаткам.КПС КАК КПС,
		|	ПоступлениеУслугРаботЗачетАвансовПоДокументамИОстаткам.КЭК КАК КЭК,
		|	ПоступлениеУслугРаботЗачетАвансовПоДокументамИОстаткам.ДопСубконтоАвансов КАК ДопСубконтоАвансов,
		|	ПоступлениеУслугРаботЗачетАвансовПоДокументамИОстаткам.СуммаЗачета КАК СуммаЗачета
		|ПОМЕСТИТЬ ВТ_ЗачетАвансовПоДокументам
		|ИЗ
		|	Документ.ПоступлениеУслугРабот.ЗачетАвансовПоДокументамИОстаткам КАК ПоступлениеУслугРаботЗачетАвансовПоДокументамИОстаткам
		|ГДЕ
		|	ПоступлениеУслугРаботЗачетАвансовПоДокументамИОстаткам.Ссылка.Проведен
		|	И ПоступлениеУслугРаботЗачетАвансовПоДокументамИОстаткам.СуммаЗачета <> 0
		|{ГДЕ
		|	(ПоступлениеУслугРаботЗачетАвансовПоДокументамИОстаткам.Ссылка.Дата <= &ПериодЗапроса) КАК ДатаПоступленияУслуг}
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТ_ДокументыВыдачиАванса.Регистратор КАК Регистратор,
		|	ВТ_ДокументыВыдачиАванса.Контрагент КАК Контрагент,
		|	ВТ_ДокументыВыдачиАванса.Договор КАК Договор,
		|	ВТ_ДокументыВыдачиАванса.КПС КАК КПС,
		|	ВТ_ДокументыВыдачиАванса.ИФО КАК ИФО,
		|	ВТ_ДокументыВыдачиАванса.КФО КАК КФО,
		|	ВТ_ДокументыВыдачиАванса.Организация КАК Организация,
		|	ВТ_ДокументыВыдачиАванса.ДопСубконтоАвансов КАК ДопСубконтоАвансов,
		|	ВТ_ДокументыВыдачиАванса.Подразделение КАК Подразделение,
		|	ВТ_ДокументыВыдачиАванса.СуммаОборот - ЕСТЬNULL(ВТ_ЗачетАвансовПоДокументам.СуммаЗачета, 0) КАК СуммаОстаткаАвансаПоДокументу,
		|	ВТ_ДокументыВыдачиАванса.Счет КАК Счет
		|ПОМЕСТИТЬ ВТ_Итоговая
		|ИЗ
		|	ВТ_ДокументыВыдачиАванса КАК ВТ_ДокументыВыдачиАванса
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ЗачетАвансовПоДокументам КАК ВТ_ЗачетАвансовПоДокументам
		|		ПО ВТ_ДокументыВыдачиАванса.Регистратор = ВТ_ЗачетАвансовПоДокументам.ДокументАванса
		|			И ВТ_ДокументыВыдачиАванса.КФО = ВТ_ЗачетАвансовПоДокументам.КФО
		|			И ВТ_ДокументыВыдачиАванса.КПС = ВТ_ЗачетАвансовПоДокументам.КПС
		|			И ВТ_ДокументыВыдачиАванса.ДопСубконтоАвансов = ВТ_ЗачетАвансовПоДокументам.ДопСубконтоАвансов
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ЖурналПроводокЕПСБУОстатки.Счет КАК Счет,
		|	ВЫБОР
		|		КОГДА ЕПСБУКонтрагенты.НомерСтроки = 1
		|			ТОГДА ВЫРАЗИТЬ(ЖурналПроводокЕПСБУОстатки.Субконто1 КАК Справочник.Контрагенты)
		|		КОГДА ЕПСБУКонтрагенты.НомерСтроки = 2
		|			ТОГДА ВЫРАЗИТЬ(ЖурналПроводокЕПСБУОстатки.Субконто2 КАК Справочник.Контрагенты)
		|		КОГДА ЕПСБУКонтрагенты.НомерСтроки = 3
		|			ТОГДА ВЫРАЗИТЬ(ЖурналПроводокЕПСБУОстатки.Субконто3 КАК Справочник.Контрагенты)
		|	КОНЕЦ КАК Контрагент,
		|	ВЫБОР
		|		КОГДА ЕПСБУДоговоры.НомерСтроки = 1
		|			ТОГДА ВЫРАЗИТЬ(ЖурналПроводокЕПСБУОстатки.Субконто1 КАК Справочник.Договоры)
		|		КОГДА ЕПСБУДоговоры.НомерСтроки = 2
		|			ТОГДА ВЫРАЗИТЬ(ЖурналПроводокЕПСБУОстатки.Субконто2 КАК Справочник.Договоры)
		|		КОГДА ЕПСБУДоговоры.НомерСтроки = 3
		|			ТОГДА ВЫРАЗИТЬ(ЖурналПроводокЕПСБУОстатки.Субконто3 КАК Справочник.Договоры)
		|	КОНЕЦ КАК Договор,
		|	ВЫБОР
		|		КОГДА НЕ (ЖурналПроводокЕПСБУОстатки.Субконто1 ССЫЛКА Справочник.Контрагенты
		|			ИЛИ ЖурналПроводокЕПСБУОстатки.Субконто1 ССЫЛКА Справочник.Договоры)
		|			ТОГДА ЖурналПроводокЕПСБУОстатки.Субконто1
		|		КОГДА НЕ (ЖурналПроводокЕПСБУОстатки.Субконто2 ССЫЛКА Справочник.Контрагенты
		|			ИЛИ ЖурналПроводокЕПСБУОстатки.Субконто2 ССЫЛКА Справочник.Договоры)
		|			ТОГДА ЖурналПроводокЕПСБУОстатки.Субконто2
		|		КОГДА НЕ (ЖурналПроводокЕПСБУОстатки.Субконто3 ССЫЛКА Справочник.Контрагенты
		|			ИЛИ ЖурналПроводокЕПСБУОстатки.Субконто3 ССЫЛКА Справочник.Договоры)
		|			ТОГДА ЖурналПроводокЕПСБУОстатки.Субконто3
		|	КОНЕЦ КАК ДопСубконтоАвансов,
		|	ЖурналПроводокЕПСБУОстатки.СуммаОстаток КАК СуммаОстаток,
		|	ЖурналПроводокЕПСБУОстатки.ВалютнаяСуммаОстаток КАК ВалютнаяСуммаОстаток,
		|	ЖурналПроводокЕПСБУОстатки.КПС КАК КПС,
		|	ЖурналПроводокЕПСБУОстатки.ИФО КАК ИФО,
		|	ВЫБОР
		|		КОГДА ЕПСБУ.Валютный
		|			ТОГДА ЖурналПроводокЕПСБУОстатки.Валюта
		|		ИНАЧЕ ВалютаРегламентированногоУчета.Значение
		|	КОНЕЦ КАК Валюта,
		|	ВЫБОР
		|		КОГДА ЕПСБУ.УчетПоПодразделениям
		|			ТОГДА ЖурналПроводокЕПСБУОстатки.Подразделение
		|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.Подразделения.ПустаяСсылка)
		|	КОНЕЦ КАК Подразделение,
		|	ЖурналПроводокЕПСБУОстатки.КФО КАК КФО,
		|	ВложенныйЗапрос.КЭКАванса КАК КЭК
		|ПОМЕСТИТЬ ВТ_ОстаткиАвансов
		|ИЗ
		|	РегистрБухгалтерии.ЖурналПроводокЕПСБУ.Остатки({(&ПериодЗапроса)}, Счет В (&Счет206) {(Счет) КАК СчетАванса}, , {(Валюта), (ИФО), (КПС), (КФО), (Организация), (Подразделение), (КЭК) КАК КЭКОстатков}) КАК ЖурналПроводокЕПСБУОстатки
		|		ЛЕВОЕ СОЕДИНЕНИЕ ПланСчетов.ЕПСБУ.ВидыСубконто КАК ЕПСБУКонтрагенты
		|		ПО ЖурналПроводокЕПСБУОстатки.Счет = ЕПСБУКонтрагенты.Ссылка
		|			И (ЕПСБУКонтрагенты.ВидСубконто = ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконто.Контрагенты))
		|		ЛЕВОЕ СОЕДИНЕНИЕ ПланСчетов.ЕПСБУ.ВидыСубконто КАК ЕПСБУДоговоры
		|		ПО ЖурналПроводокЕПСБУОстатки.Счет = ЕПСБУДоговоры.Ссылка
		|			И (ЕПСБУДоговоры.ВидСубконто = ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконто.Договоры))
		//|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПланСчетов.ЕПСБУ.ВидыСубконто КАК ЕПСБУАвансы
		//|		ПО ЖурналПроводокЕПСБУОстатки.Счет = ЕПСБУАвансы.Ссылка
		//|			И (ЕПСБУАвансы.ВидСубконто = ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконто.ДокументыРасчетов))
		|		ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
		|			СоответствияАналитическихСчетовКЭК.Счет КАК Счет,
		|			МАКСИМУМ(СоответствияАналитическихСчетовКЭК.КЭК) КАК КЭКАванса
		|		ИЗ
		|			РегистрСведений.СоответствияАналитическихСчетовКЭК КАК СоответствияАналитическихСчетовКЭК
		|		ГДЕ
		|			СоответствияАналитическихСчетовКЭК.Счет В(&Счет206)
		|		{ГДЕ
		|			СоответствияАналитическихСчетовКЭК.Счет.* КАК СчетАванса}
		|		
		|		СГРУППИРОВАТЬ ПО
		|			СоответствияАналитическихСчетовКЭК.Счет) КАК ВложенныйЗапрос
		|		ПО ЖурналПроводокЕПСБУОстатки.Счет = ВложенныйЗапрос.Счет
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПланСчетов.ЕПСБУ КАК ЕПСБУ
		|		ПО ЖурналПроводокЕПСБУОстатки.Счет = ЕПСБУ.Ссылка,
		|	Константа.ВалютаРегламентированногоУчета КАК ВалютаРегламентированногоУчета
		|{ГДЕ
		|	ЖурналПроводокЕПСБУОстатки.КЭК.* КАК КЭКОстатков}
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТ_ОстаткиАвансов.Счет КАК Счет,
		|	ВТ_ОстаткиАвансов.Контрагент КАК Контрагент,
		|	ВТ_ОстаткиАвансов.Договор КАК Договор,
		|	ВТ_Итоговая.Регистратор КАК ДокументАванса,
		|	ВТ_ОстаткиАвансов.СуммаОстаток КАК СуммаОстаток,
		|	ВТ_Итоговая.СуммаОстаткаАвансаПоДокументу КАК СуммаОстаткаАвансаПоДокументу,
		|	ВТ_ОстаткиАвансов.ВалютнаяСуммаОстаток КАК ВалютнаяСуммаОстаток,
		|	ВТ_ОстаткиАвансов.КПС КАК КПС,
		|	ВТ_ОстаткиАвансов.ИФО КАК ИФО,
		|	ВТ_ОстаткиАвансов.Валюта КАК Валюта,
		|	ВТ_ОстаткиАвансов.Подразделение КАК Подразделение,
		|	ВТ_ОстаткиАвансов.КФО КАК КФО,
		|	ВТ_ОстаткиАвансов.ДопСубконтоАвансов КАК ДопСубконтоАвансов,
		|	ВТ_ОстаткиАвансов.КЭК КАК КЭК
		|ИЗ
		|	ВТ_ОстаткиАвансов КАК ВТ_ОстаткиАвансов
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_Итоговая КАК ВТ_Итоговая
		|		ПО (ВТ_ОстаткиАвансов.Счет = ВТ_Итоговая.Счет)
		|			И (ВТ_ОстаткиАвансов.Контрагент = ВТ_Итоговая.Контрагент)
		|			И (ВТ_ОстаткиАвансов.Договор = ВТ_Итоговая.Договор)
		|			И (ВТ_ОстаткиАвансов.КПС = ВТ_Итоговая.КПС)
		|			И (ВТ_ОстаткиАвансов.КФО = ВТ_Итоговая.КФО)
		|			И (ВТ_ОстаткиАвансов.ИФО = ВТ_Итоговая.ИФО)
		|			И (ВТ_ОстаткиАвансов.ДопСубконтоАвансов = ВТ_Итоговая.ДопСубконтоАвансов)
		|ГДЕ
		|	ВТ_Итоговая.СуммаОстаткаАвансаПоДокументу <> 0";
		
		НовЭл = Элементы.Вставить("ДокументыАвансовСуммаОстаткаАвансаПоДокументу", Тип("ПолеФормы"), Элементы.ДокументыАвансов, Элементы.ДокументыАвансовСуммаОстаток);
		НовЭл.ПутьКДанным = "ДокументыАвансов.СуммаОстаткаАвансаПоДокументу";
		НовЭл.Вид = ВидПоляФормы.ПолеВвода;
		
		НовЭл = Элементы.Вставить("ДокументыАвансовДополнительнаяАналитикаСчета", Тип("ПолеФормы"), Элементы.ДокументыАвансов, НовЭл);
		НовЭл.ПутьКДанным = "ДокументыАвансов.ДопСубконтоАвансов";
		НовЭл.Вид = ВидПоляФормы.ПолеВвода;
		
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
&ИзменениеИКонтроль("ОбработатьВыборДокумента")
Процедура АБК_ОбработатьВыборДокумента(СтандартнаяОбработка)

	СтандартнаяОбработка = Ложь;
	//	РезультатВыбора = Новый Массив;
	Для каждого ТекИдентификаторСтроки Из Элементы.ДокументыАвансов.ВыделенныеСтроки Цикл

		ДанныеВозврата = Новый Структура("ДокументАванса, КФО, КПС, КЭК");
		ДанныеТекущейСтроки = Элементы.ДокументыАвансов.ДанныеСтроки(ТекИдентификаторСтроки);
		ЗаполнитьЗначенияСвойств(ДанныеВозврата, ДанныеТекущейСтроки);
		ДанныеВозврата.Вставить("СуммаЗачета", ДанныеТекущейСтроки.СуммаОстаток);  
		
		#Вставка   
		Если ЭтаФорма.ПодборПоДокументамИОстаткам Тогда
			ДанныеВозврата.Вставить("СуммаЗачета", ДанныеТекущейСтроки.СуммаОстаткаАвансаПоДокументу); 
			ДанныеВозврата.Вставить("ДопСубконтоАвансов", ДанныеТекущейСтроки.ДопСубконтоАвансов); 
		КонецЕсли;
		#КонецВставки
		
		//		РезультатВыбора.Добавить(ДанныеВозврата);

	КонецЦикла; 

	ОповеститьОВыборе(ДанныеВозврата);


КонецПроцедуры
