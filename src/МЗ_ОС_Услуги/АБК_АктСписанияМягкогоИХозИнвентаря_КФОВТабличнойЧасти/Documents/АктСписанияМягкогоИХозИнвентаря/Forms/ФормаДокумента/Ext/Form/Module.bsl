#Область ОбработчикиСобытийФормы

&НаСервере
Процедура АБК_ПриСозданииНаСервереПеред(Отказ, СтандартнаяОбработка)
	
	АБК_ДопКолонкиДокументов.НайтиСоздатьДопКолонку("АктСписанияМягкогоИХозИнвентаря",
		"Материалы",
		"АБК_КФО",
		"КФО",
		Новый ОписаниеТипов("ПеречислениеСсылка.КВД"));	       
		
КонецПроцедуры

&НаСервере
Процедура АБК_ПриСозданииНаСервереПосле(Отказ, СтандартнаяОбработка)
	 
	ПолеКФОВТабличнойЧасти = Элементы.Добавить("АБК_КФОВТабличнойЧасти", Тип("ПолеФормы"), Элементы.ГруппаКФОКПС); 
	ПолеКФОВТабличнойЧасти.Вид = ВидПоляФормы.ПолеФлажка; 
	ПолеКФОВТабличнойЧасти.Заголовок = "Указывается в табличной части";
	ПолеКФОВТабличнойЧасти.ПутьКДанным = "АБК_КФОВТабличнойЧасти";
	ПолеКФОВТабличнойЧасти.УстановитьДействие("ПриИзменении", "АБК_ПриИзмененииКФОВТабличнойЧасти");  
	ПолеКФОВТабличнойЧасти.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Право;
	
	// Чтение допРеквизита формы
	Если ЗначениеЗаполнено(Объект.Ссылка) Тогда
		
		Свойство = АБК_ДопКолонкиДокументов.НайтиСоздатьДополнительноеСведение("АБК_КФОВТабличнойЧасти");
		Если ЗначениеЗаполнено(Свойство) Тогда
			НЗ = РегистрыСведений.ДополнительныеСведения.СоздатьНаборЗаписей();
			НЗ.Отбор.Объект.Установить(Объект.Ссылка);
			НЗ.Отбор.Свойство.Установить(Свойство);
			НЗ.Прочитать();
			ТЧРеквизитов = НЗ.Выгрузить();
			
			Если ТЧРеквизитов.Количество() И ЗначениеЗаполнено(ТЧРеквизитов[0].Значение) Тогда
				АБК_КФОВТабличнойЧасти = ТЧРеквизитов[0].Значение;  
			КонецЕсли;
		КонецЕсли;  
		
	КонецЕсли; 
	
	ДопРеквизитНаФорме = ЭтаФорма.БухгалтерскиеОперации_ДобавленныеРеквизитыОперации.НайтиСтроки(Новый Структура("ИмяДляФормулы,ИмяТаблицы","АБК_КФО","Материалы"));
	Если ДопРеквизитНаФорме.Количество() Тогда
		АБК_ИмяРеквизитаКФО = ДопРеквизитНаФорме[0].ИмяРеквизита; 
		АБК_ИмяЭлементаКФО = ДопРеквизитНаФорме[0].ИмяЭлемента; 
		Элементы.Переместить(Элементы[АБК_ИмяЭлементаКФО],Элементы.Материалы,Элементы.МатериалыКЭК);
	КонецЕсли;                
	УстановитьВидимостьДопКолонокКФО(ЭтотОбъект);
	
	// Переопределение предыдущего события (очистка таблицы)
	Элементы.КФО.УстановитьДействие("ПриИзменении", "АБК_ПриИзмененииКФОВТабличнойЧасти");
	Команды.Найти("Подбор").Действие = "АБК_Подбор";
	
КонецПроцедуры

&НаСервере
Процедура АБК_ПриЧтенииНаСервереПосле(ТекущийОбъект)
	
	Если Элементы.Найти("АБК_КФОВТабличнойЧасти") <> Неопределено Тогда
		Свойство = АБК_ДопКолонкиДокументов.НайтиСоздатьДополнительноеСведение("АБК_КФОВТабличнойЧасти");
		Если ЗначениеЗаполнено(Свойство) Тогда
			НЗ = РегистрыСведений.ДополнительныеСведения.СоздатьНаборЗаписей();
			НЗ.Отбор.Объект.Установить(ТекущийОбъект.Ссылка);
			НЗ.Отбор.Свойство.Установить(Свойство);
			НЗ.Прочитать();
			ТЧРеквизитов = НЗ.Выгрузить();
			
			Если ТЧРеквизитов.Количество() И ЗначениеЗаполнено(ТЧРеквизитов[0].Значение) Тогда
				КФОВТабличнойЧасти = ТЧРеквизитов[0].Значение;  
			КонецЕсли;
		КонецЕсли;	
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура АБК_ПередЗаписьюНаСервереПосле(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	ТекущийОбъект.ДополнительныеСвойства.Вставить("АБК_КФОВТабличнойЧасти", АБК_КФОВТабличнойЧасти);
КонецПроцедуры

&НаСервере
Процедура АБК_ПослеЗаписиНаСервереПосле(ТекущийОбъект, ПараметрыЗаписи)
  
  ЗаписатьЗначениеДопРеквизитаСФормы(Объект.Ссылка, АБК_КФОВТабличнойЧасти, "АБК_КФОВТабличнойЧасти");
  
КонецПроцедуры

#КонецОбласти


#Область ОбработчикиКомандФорм

&НаКлиенте
Процедура АБК_Подбор(Команда)
	ПараметрыПодбора = УправлениеМатериальнымиЗапасамиФормыКлиент.ПараметрыОткрытияПодбора();
	ПроверяемыеРеквизиты = Новый Массив;
	ПроверяемыеРеквизиты.Добавить(НСтр("ru = 'Организация'"));
	Если Не СчетУчета27(ЭтотОбъект) Тогда
		ПроверяемыеРеквизиты.Добавить(НСтр("ru = 'ЦМООтправитель, МОЛ/Место хранения'"));
	КонецЕсли;
	ПроверяемыеРеквизиты.Добавить(НСтр("ru = 'СчетУчета, Счет учета'"));
	// АБК
	//ПроверяемыеРеквизиты.Добавить(НСтр("ru = 'КФО'"));
	Если Не АБК_КФОВТабличнойЧасти Тогда
		ПроверяемыеРеквизиты.Добавить(НСтр("ru = 'КФО'"));
	КонецЕсли;
	// АБК
	ПараметрыПодбора.ПроверяемыеРеквизиты = СтрСоединить(ПроверяемыеРеквизиты, ";");
	УправлениеМатериальнымиЗапасамиФормыКлиент.ОткрытьПодборМатериалов(ЭтотОбъект, Объект, ПараметрыПодбора);
КонецПроцедуры

#КонецОбласти
   
 
#Область ОбработчикиСобытийЭлементов

&НаКлиенте
Процедура АБК_ПриИзмененииКФОВТабличнойЧасти(Элемент)
	
	УстановитьВидимостьДопКолонокКФО(ЭтотОбъект);
	
	Если ЗначениеЗаполнено(Объект.КФО) И ЗначениеЗаполнено(АБК_ИмяРеквизитаКФО) Тогда
		Для Каждого Строка ИЗ Объект.Материалы Цикл
			Строка[АБК_ИмяРеквизитаКФО] = Объект.КФО;
		КонецЦИкла;
	КонецЕсли;
	
КонецПроцедуры 

#КонецОбласти


#Область СлужебныеПроцедурыИФункции

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьВидимостьДопКолонокКФО(Форма)
	
	ИмяЭлементаКФО = Форма.АБК_ИмяЭлементаКФО;
	ИмяРеквизитаКФО = Форма.АБК_ИмяРеквизитаКФО;  
	КФОВТабличнойЧасти = Форма.АБК_КФОВТабличнойЧасти;
	Объект = Форма.Объект;
	
	Если ЗначениеЗаполнено(ИмяЭлементаКФО) Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Форма.Элементы,ИмяЭлементаКФО, "Видимость", КФОВТабличнойЧасти);
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Форма.Элементы,"КФО", "Доступность", Не КФОВТабличнойЧасти);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаписатьЗначениеДопРеквизитаСФормы(СсылкаНаВладельца,ЗначениеРеквизита,ИдентификаторДляФормул)
	
	Свойство = АБК_ДопКолонкиДокументов.НайтиСоздатьДополнительноеСведение(ИдентификаторДляФормул);
	
	Если ЗначениеЗаполнено(Свойство) И ЗначениеЗаполнено(СсылкаНаВладельца) Тогда
		
		МЗ = РегистрыСведений.ДополнительныеСведения.СоздатьМенеджерЗаписи();
		МЗ.Объект   	= СсылкаНаВладельца;
		МЗ.Свойство   = Свойство;
		МЗ.Значение   = ЗначениеРеквизита;
		МЗ.Записать();
		
	КонецЕсли; 
	
КонецПроцедуры

&НаСервере
&ИзменениеИКонтроль("ОбработатьВыборПодбораНаСервере")
Процедура АБК_ОбработатьВыборПодбораНаСервере(ВыбранноеЗначение)
	СписокПодбора = ПолучитьИзВременногоХранилища(ВыбранноеЗначение.АдресПодобраннойНоменклатурыВХранилище);
	Для Каждого ЭлементПодбора Из СписокПодбора Цикл
		НоваяСтрока = Объект.Материалы.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока,ЭлементПодбора);
		Если Не ЗначениеЗаполнено(НоваяСтрока.КоличествоНорма) Тогда
			НоваяСтрока.КоличествоНорма = НоваяСтрока.Количество;
		КонецЕсли;      
		#Вставка
		Если ЗначениеЗаполнено(АБК_ИмяРеквизитаКФО) И АБК_КФОВТабличнойЧасти = Истина Тогда
			НоваяСтрока[АБК_ИмяРеквизитаКФО] = ЭлементПодбора.КФО;	
		КонецЕсли;
		#КонецВставки
		//УправлениеМатериальнымиЗапасамиКлиентСервер.УстановитьДанныеДинамическихКолонокТекущейСтрокиМатериалов(Объект, НоваяСтрока);
		ЗаполнитьДанныеСчетаВСтроке(ЭтотОбъект, НоваяСтрока, Ложь);
	КонецЦикла;
	РассчитатьПрослеживаемыеТовары();
	УстановитьВидимостьЭлементов(ЭтотОбъект);

КонецПроцедуры

#КонецОбласти



