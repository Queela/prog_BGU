
// Процедура - Поместить таблица материалов
//
//	Помещает таблицу материалов из документа в вр. хранилище для заполенения корзины при открытии подбора
//
// Параметры:
//  ПараметрыПодбора		 - 	 - 
//  ДополнительныйПараметр	 - 	 - 
//  ДанныеФормы				 - 	 - 
//
Процедура ПоместитьТаблицаМатериалов(ПараметрыПодбора, ДополнительныйПараметр, ДанныеФормы) Экспорт
	
	МетаданныеДокумента = ДанныеФормы.Ссылка.Метаданные();
	
	Материалы = Новый ТаблицаЗначений;
	
	Для Каждого Колонка Из Метаданные.Обработки.ПодборНоменклатуры.ТабличныеЧасти.ПодобраннаяНоменклатура.Реквизиты Цикл
		Материалы.Колонки.Добавить(Колонка.Имя, Колонка.Тип);
	КонецЦикла;
	
	ИмяТабличнойЧасти = "";
	
	Если Не ПолучитьИмяТабличнойЧастиМатериалы(МетаданныеДокумента, ИмяТабличнойЧасти) Тогда
		Возврат;
	КонецЕсли;
	
	ИсключаемыеСвойства = "";
	
	Для Каждого СтрокаДокумента Из ДанныеФормы[ИмяТабличнойЧасти] Цикл
		
		НоваяСтрока = Материалы.Добавить();
		
		Если ПараметрыПодбора.Свойство("ПодборОстатковВПользовании")
			И ПараметрыПодбора.ПодборОстатковВПользовании
			И ТипЗнч(СтрокаДокумента.ДополнительнаяАналитикаСчета) = Тип("СправочникСсылка.Контрагенты") Тогда
			
			ИсключаемыеСвойства = "ДополнительнаяАналитикаСчета";
			НоваяСтрока.КонтрагентВПользовании = СтрокаДокумента.ДополнительнаяАналитикаСчета;
			
		КонецЕсли;
		
		ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаДокумента,, ИсключаемыеСвойства);
		
	КонецЦикла;    
	
	Если Не ОбщегоНазначенияБГУ.ЕстьРеквизитТабЧастиДокумента("КФО", МетаданныеДокумента, ИмяТабличнойЧасти)
		И ОбщегоНазначенияБГУ.ЕстьРеквизитДокумента("КФО", МетаданныеДокумента) Тогда
		Материалы.ЗаполнитьЗначения(ДанныеФормы.КФО, "КФО");
	КонецЕсли;
	
	Если Не ОбщегоНазначенияБГУ.ЕстьРеквизитТабЧастиДокумента("ИФО", МетаданныеДокумента, ИмяТабличнойЧасти)
		И ОбщегоНазначенияБГУ.ЕстьРеквизитДокумента("ИФО", МетаданныеДокумента)
		И ПолучитьФункциональнуюОпцию("ИспользоватьУчетПоИФО") Тогда
		Материалы.ЗаполнитьЗначения(ДанныеФормы.ИФО, "ИФО");
	КонецЕсли;
		
	Адрес = ПоместитьВоВременноеХранилище(Материалы, Новый УникальныйИдентификатор);
	
	ПараметрыПодбора.Вставить("АдресТабличнойЧасти", Адрес);
	
КонецПроцедуры

Функция ПолучитьИмяТабличнойЧастиМатериалы(Знач МетаданныеДокумента, ИмяТабличнойЧасти) Экспорт 
	
	Если ТипЗнч(МетаданныеДокумента) = Тип("ДанныеФормыСтруктура") Тогда
		МетаданныеДокумента = МетаданныеДокумента.Ссылка.Метаданные();
	КонецЕсли;
	
	Если ОбщегоНазначенияБГУ.ЕстьТабЧастьДокумента("Материалы", МетаданныеДокумента) Тогда
		ИмяТабличнойЧасти = "Материалы";
	КонецЕсли;
	
	Если ОбщегоНазначенияБГУ.ЕстьТабЧастьДокумента("МатериальныеЗапасы", МетаданныеДокумента) Тогда
		ИмяТабличнойЧасти = "МатериальныеЗапасы";
	КонецЕсли;
	
	Возврат Не ПустаяСтрока(ИмяТабличнойЧасти);
	
КонецФункции









