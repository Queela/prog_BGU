Функция ПодготовитьТаблицуРасчетовПоДокументуРешениеОПризнанииОС(Документ, Параметры, Отказ) Экспорт
	
	ОписаниеТипаСуммы = Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(18, 2, ДопустимыйЗнак.Любой));
	
	ТаблицаРасчетов = Новый ТаблицаЗначений;
	// Признак поступления ценностей: материальных запасов, услуг и пр.
	ТаблицаРасчетов.Колонки.Добавить("ЭтоПоступление", Новый ОписаниеТипов("Булево"));
	// Общие разрезы учета.
	ТаблицаРасчетов.Колонки.Добавить("ИФО",            Новый ОписаниеТипов("СправочникСсылка.ИсточникиФинансовогоОбеспечения"));
	ТаблицаРасчетов.Колонки.Добавить("КФО",            Новый ОписаниеТипов("ПеречислениеСсылка.КВД"));
	ТаблицаРасчетов.Колонки.Добавить("КПС",            Новый ОписаниеТипов("СправочникСсылка.КлассификационныеПризнакиСчетов"));
	ТаблицаРасчетов.Колонки.Добавить("Подразделение",  Новый ОписаниеТипов("СправочникСсылка.Подразделения"));
	// Счет и аналитика учета расчетов с контрагентами.
	ТаблицаРасчетов.Колонки.Добавить("КЭК",            Новый ОписаниеТипов("СправочникСсылка.КОСГУ"));
	ТаблицаРасчетов.Колонки.Добавить("СчетРасчетов",   Новый ОписаниеТипов("ПланСчетовСсылка.ЕПСБУ"));
	ТаблицаРасчетов.Колонки.Добавить("Контрагент",     Новый ОписаниеТипов("СправочникСсылка.Контрагенты"));
	ТаблицаРасчетов.Колонки.Добавить("Договор",        Новый ОписаниеТипов("СправочникСсылка.Договоры"));
	ТаблицаРасчетов.Колонки.Добавить("Валюта",         Новый ОписаниеТипов("СправочникСсылка.Валюты"));
	// Суммовые показатели операции.
	ТаблицаРасчетов.Колонки.Добавить("СуммаНДС",       ОписаниеТипаСуммы);
	ТаблицаРасчетов.Колонки.Добавить("СуммаРасчетов",  ОписаниеТипаСуммы);
	
	Если Параметры = Неопределено Тогда
		Параметры = Новый Структура;
	КонецЕсли;
	Если НЕ Параметры.Свойство("ПоВсемКФО") Тогда
		Параметры.Вставить("ПоВсемКФО", Ложь);
	КонецЕсли;
	Если НЕ Параметры.Свойство("ПоВсемКПС") Тогда
		Параметры.Вставить("ПоВсемКПС", Ложь);
	КонецЕсли;       
	
	Договор = Справочники.Договоры.ПустаяСсылка();
	Если НЕ Параметры.Свойство("Договор",Договор)
		ИЛИ НЕ ЗначениеЗаполнено(Договор) Тогда
		Отказ = Истина;
	КонецЕсли;      
	
	КПС = Справочники.КлассификационныеПризнакиСчетов.ПустаяСсылка();
	Если НЕ Параметры.Свойство("КПС",КПС)
		ИЛИ НЕ ЗначениеЗаполнено(КПС) Тогда
		Отказ = Истина;
	КонецЕсли;
	
	Если Отказ Тогда
		Возврат ТаблицаРасчетов;	
	КонецЕсли;
	
	МетаданныеДокумента = Документ.Метаданные();
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	РешениеОПризнанииОбъектовОСОсновныеСредства.Ссылка.ИФО КАК ИФО,
	|	РешениеОПризнанииОбъектовОСОсновныеСредства.Ссылка.КФО КАК КФО,
	|	РешениеОПризнанииОбъектовОСОсновныеСредства.Ссылка.Подразделение КАК Подразделение,
	|	РешениеОПризнанииОбъектовОСОсновныеСредства.Ссылка.Контрагент КАК Контрагент,
	|	РешениеОПризнанииОбъектовОСОсновныеСредства.ОсновноеСредство КАК ОсновноеСредство,
	|	РешениеОПризнанииОбъектовОСОсновныеСредства.ОсновноеСредство.ВидНФА КАК ВидНФА,
	|	РешениеОПризнанииОбъектовОСОсновныеСредства.Сумма КАК СуммаРасчетов,
	|	0 КАК СуммаНДС,
	|	ЗНАЧЕНИЕ(Справочник.КлассификационныеПризнакиСчетов.ПустаяСсылка) КАК КПС,
	|	ЗНАЧЕНИЕ(Справочник.Договоры.ПустаяСсылка) КАК Договор,
	|	ЗНАЧЕНИЕ(Справочник.КОСГУ.ПустаяСсылка) КАК КЭК
	|ИЗ
	|	Документ.РешениеОПризнанииОбъектовОС.ОсновныеСредства КАК РешениеОПризнанииОбъектовОСОсновныеСредства");
	
	РасчетыПоОС = Запрос.Выполнить().Выгрузить();
	
	СчетРасчетов = БухгалтерскиеОперации.СчетПоКоду("302.31",Документ.Дата); 
	КЭКРасчетов = БухгалтерскиеОперации.КЭКПоКоду("310");
	
	Для Каждого СтрокаДокумента Из РасчетыПоОС Цикл
		
		Если Отказ Тогда
			Продолжить;
		КонецЕсли;
		
		НоваяСтрока = ТаблицаРасчетов.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаДокумента);
		
		НоваяСтрока.ЭтоПоступление = Истина;
		НоваяСтрока.СчетРасчетов = СчетРасчетов;
		НоваяСтрока.Договор = Договор;
		НоваяСтрока.КПС = КПС;
		НоваяСтрока.КЭК = КЭКРасчетов;
		
	КонецЦикла;
		
	ТаблицаРасчетов.Свернуть("ЭтоПоступление,ИФО,КФО,КПС,КЭК,Подразделение,СчетРасчетов,Контрагент,Договор,Валюта",
		"СуммаНДС,СуммаРасчетов");
	
	Возврат ТаблицаРасчетов;
	
КонецФункции

