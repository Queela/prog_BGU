
&ИзменениеИКонтроль("ТаблицаПринятыеОбязательства")
Функция АБК_ТаблицаПринятыеОбязательства(ИмяТаблицы, Реквизиты)

	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.Текст = "
	|ВЫБРАТЬ
	|	СуммыИзменений.КФО,
	#Вставка
	|	СуммыИзменений.ИФО,
	#КонецВставки
	|	СуммыИзменений.КПС,
	|	СуммыИзменений.КЭК,
	|	СуммыИзменений.ПериодПлана,
	|	СуммыИзменений.РазделЛицевогоСчета,
	|	СуммыИзменений.КодЦели,
	|	СуммыИзменений.КодМероприятия,
	|	СуммыИзменений.ДопКлассификация,
	|	СуммыИзменений.Подразделение,
	|	СуммыИзменений.Сумма КАК Сумма,
	|	СуммыИзменений.СуммаВВалютеБезусловная КАК СуммаВВалютеБезусловная
	|ПОМЕСТИТЬ СуммыИзмененийПредв
	|ИЗ
	|	&СуммыИзмененийПредв КАК СуммыИзменений";

	Запрос.УстановитьПараметр("СуммыИзмененийПредв", РаботаСОбязательствамиПовтИспНаВремяВызова.ПолучитьТаблицуИзмененийОбязательства(Реквизиты.Договор, Реквизиты.Ссылка));
	Запрос.Выполнить();

	Запрос.Текст ="ВЫБРАТЬ
	|	СуммыИзменений.КФО,
	#Вставка
	|	СуммыИзменений.ИФО,
	#КонецВставки
	|	СуммыИзменений.КПС,
	|	СуммыИзменений.КЭК,
	|	СуммыИзменений.ПериодПлана,
	|	СуммыИзменений.РазделЛицевогоСчета,
	|	СуммыИзменений.КодЦели,
	|	СуммыИзменений.КодМероприятия,
	|	СуммыИзменений.ДопКлассификация,
	|	СуммыИзменений.Подразделение,
	|	СУММА(СуммыИзменений.Сумма) КАК Сумма,
	|	СУММА(СуммыИзменений.СуммаВВалютеБезусловная) КАК СуммаВВалютеБезусловная
	|ПОМЕСТИТЬ СуммыИзменений
	|ИЗ
	|	СуммыИзмененийПредв КАК СуммыИзменений
	|
	|СГРУППИРОВАТЬ ПО
	|	СуммыИзменений.КодМероприятия,
	|	СуммыИзменений.КодЦели,
	|	СуммыИзменений.РазделЛицевогоСчета,
	|	СуммыИзменений.ДопКлассификация,
	|	СуммыИзменений.Подразделение,
	|	СуммыИзменений.КФО,
	#Вставка
	|	СуммыИзменений.ИФО,
	#КонецВставки
	|	СуммыИзменений.ПериодПлана,
	|	СуммыИзменений.КПС,
	|	СуммыИзменений.КЭК
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	РегистрацияОбязательствИСведенийПоДоговорамРасшифровкаОбязательства.ПериодПлана.Представление КАК ПериодПлана,
	|	РегистрацияОбязательствИСведенийПоДоговорамРасшифровкаОбязательства.ПериодПлана КАК ПериодСсылка,
	|	ВЫБОР
	|		КОГДА ГОД(РегистрацияОбязательствИСведенийПоДоговорамРасшифровкаОбязательства.ПериодПлана.ДатаОкончания) - ГОД(РегистрацияОбязательствИСведенийПоДоговорамРасшифровкаОбязательства.Ссылка.Дата) = 0
	|			ТОГДА ""1""
	|		КОГДА ГОД(РегистрацияОбязательствИСведенийПоДоговорамРасшифровкаОбязательства.ПериодПлана.ДатаОкончания) - ГОД(РегистрацияОбязательствИСведенийПоДоговорамРасшифровкаОбязательства.Ссылка.Дата) = 1
	|			ТОГДА ""2""
	|		КОГДА ГОД(РегистрацияОбязательствИСведенийПоДоговорамРасшифровкаОбязательства.ПериодПлана.ДатаОкончания) - ГОД(РегистрацияОбязательствИСведенийПоДоговорамРасшифровкаОбязательства.Ссылка.Дата) = 2
	|			ТОГДА ""3""
	|		КОГДА ГОД(РегистрацияОбязательствИСведенийПоДоговорамРасшифровкаОбязательства.ПериодПлана.ДатаОкончания) - ГОД(РегистрацияОбязательствИСведенийПоДоговорамРасшифровкаОбязательства.Ссылка.Дата) = 3
	|			ТОГДА ""4""
	|		ИНАЧЕ ""9""
	|	КОНЕЦ КАК КодПлановогоПериода,
	|	РегистрацияОбязательствИСведенийПоДоговорамРасшифровкаОбязательства.КФО,
	#Вставка
	|	РегистрацияОбязательствИСведенийПоДоговорамРасшифровкаОбязательства.ИФО,
	#КонецВставки
	|	РегистрацияОбязательствИСведенийПоДоговорамРасшифровкаОбязательства.КПС,
	|	РегистрацияОбязательствИСведенийПоДоговорамРасшифровкаОбязательства.КЭК,
	|	РегистрацияОбязательствИСведенийПоДоговорамРасшифровкаОбязательства.РазделЛицевогоСчета,
	|	РегистрацияОбязательствИСведенийПоДоговорамРасшифровкаОбязательства.КодЦели,
	|	РегистрацияОбязательствИСведенийПоДоговорамРасшифровкаОбязательства.КодМероприятия,
	|	РегистрацияОбязательствИСведенийПоДоговорамРасшифровкаОбязательства.ДопКлассификация,
	|	РегистрацияОбязательствИСведенийПоДоговорамРасшифровкаОбязательства.Подразделение,
	|	СУММА(РегистрацияОбязательствИСведенийПоДоговорамРасшифровкаОбязательства.Сумма) КАК Сумма,
	|	СУММА(РегистрацияОбязательствИСведенийПоДоговорамРасшифровкаОбязательства.СуммаВВалютеБезусловная) КАК СуммаВВалютеБезусловная
	|ПОМЕСТИТЬ ТаблицаДокумента
	|ИЗ
	|	Документ.РегистрацияОбязательствИСведенийПоДоговорам." + ИмяТаблицы + " КАК РегистрацияОбязательствИСведенийПоДоговорамРасшифровкаОбязательства
	|ГДЕ
	|	РегистрацияОбязательствИСведенийПоДоговорамРасшифровкаОбязательства.Ссылка = &Ссылка
	|   И  НЕ ГОД(РегистрацияОбязательствИСведенийПоДоговорамРасшифровкаОбязательства.ПериодПлана.ДатаОкончания) - ГОД(РегистрацияОбязательствИСведенийПоДоговорамРасшифровкаОбязательства.Ссылка.Дата) < 0
	|
	|СГРУППИРОВАТЬ ПО
	|	РегистрацияОбязательствИСведенийПоДоговорамРасшифровкаОбязательства.ПериодПлана.Представление,
	|	РегистрацияОбязательствИСведенийПоДоговорамРасшифровкаОбязательства.ПериодПлана,
	|	ВЫБОР
	|		КОГДА ГОД(РегистрацияОбязательствИСведенийПоДоговорамРасшифровкаОбязательства.ПериодПлана.ДатаОкончания) - ГОД(РегистрацияОбязательствИСведенийПоДоговорамРасшифровкаОбязательства.Ссылка.Дата) = 0
	|			ТОГДА ""1""
	|		КОГДА ГОД(РегистрацияОбязательствИСведенийПоДоговорамРасшифровкаОбязательства.ПериодПлана.ДатаОкончания) - ГОД(РегистрацияОбязательствИСведенийПоДоговорамРасшифровкаОбязательства.Ссылка.Дата) = 1
	|			ТОГДА ""2""
	|		КОГДА ГОД(РегистрацияОбязательствИСведенийПоДоговорамРасшифровкаОбязательства.ПериодПлана.ДатаОкончания) - ГОД(РегистрацияОбязательствИСведенийПоДоговорамРасшифровкаОбязательства.Ссылка.Дата) = 2
	|			ТОГДА ""3""
	|		КОГДА ГОД(РегистрацияОбязательствИСведенийПоДоговорамРасшифровкаОбязательства.ПериодПлана.ДатаОкончания) - ГОД(РегистрацияОбязательствИСведенийПоДоговорамРасшифровкаОбязательства.Ссылка.Дата) = 3
	|			ТОГДА ""4""
	|		ИНАЧЕ ""9""
	|	КОНЕЦ,
	|	РегистрацияОбязательствИСведенийПоДоговорамРасшифровкаОбязательства.КФО,
	#Вставка
	|	РегистрацияОбязательствИСведенийПоДоговорамРасшифровкаОбязательства.ИФО,
	#КонецВставки
	|	РегистрацияОбязательствИСведенийПоДоговорамРасшифровкаОбязательства.КПС,
	|	РегистрацияОбязательствИСведенийПоДоговорамРасшифровкаОбязательства.КЭК,
	|	РегистрацияОбязательствИСведенийПоДоговорамРасшифровкаОбязательства.РазделЛицевогоСчета,
	|	РегистрацияОбязательствИСведенийПоДоговорамРасшифровкаОбязательства.КодЦели,
	|	РегистрацияОбязательствИСведенийПоДоговорамРасшифровкаОбязательства.КодМероприятия,
	|	РегистрацияОбязательствИСведенийПоДоговорамРасшифровкаОбязательства.ДопКлассификация,
	|	РегистрацияОбязательствИСведенийПоДоговорамРасшифровкаОбязательства.Подразделение
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаДокумента.ПериодПлана КАК ПериодПлана,
	|	ТаблицаДокумента.ПериодСсылка КАК ПериодСсылка,
	|	ТаблицаДокумента.КодПлановогоПериода КАК КодПлановогоПериода,
	|	ТаблицаДокумента.КФО,
	#Вставка
	|	ТаблицаДокумента.ИФО,
	#КонецВставки
	|	ТаблицаДокумента.КПС,
	|	ТаблицаДокумента.КЭК,
	|	ТаблицаДокумента.РазделЛицевогоСчета,
	|	ТаблицаДокумента.КодЦели,
	|	ТаблицаДокумента.КодМероприятия,
	|	ТаблицаДокумента.ДопКлассификация,
	|	ТаблицаДокумента.Подразделение,
	|	ТаблицаДокумента.Сумма,
	|	ТаблицаДокумента.Сумма - ЕСТЬNULL(СуммыИзменений.Сумма, 0) КАК СуммаИзменений,
	|	ТаблицаДокумента.СуммаВВалютеБезусловная,
	|	ТаблицаДокумента.СуммаВВалютеБезусловная - ЕСТЬNULL(СуммыИзменений.СуммаВВалютеБезусловная, 0) КАК СуммаИзмененийВВалютеБезусловная,
	|	ЛОЖЬ КАК УменьшениеЦены,
	|	ЛОЖЬ КАК Расторжение,
	|	0 КАК УчтеноЭкономии
	|ИЗ
	|	ТаблицаДокумента КАК ТаблицаДокумента
	|		ЛЕВОЕ СОЕДИНЕНИЕ СуммыИзменений КАК СуммыИзменений
	|		ПО ТаблицаДокумента.КФО = СуммыИзменений.КФО
	|			И ТаблицаДокумента.КПС = СуммыИзменений.КПС
	#Вставка
	|			И ТаблицаДокумента.ИФО = СуммыИзменений.ИФО
	#КонецВставки
	|			И ТаблицаДокумента.КЭК = СуммыИзменений.КЭК
	|			И ТаблицаДокумента.РазделЛицевогоСчета = СуммыИзменений.РазделЛицевогоСчета
	|			И ТаблицаДокумента.ПериодСсылка = СуммыИзменений.ПериодПлана
	|			И ТаблицаДокумента.КодЦели = СуммыИзменений.КодЦели
	|			И ТаблицаДокумента.КодМероприятия = СуммыИзменений.КодМероприятия
	|			И ТаблицаДокумента.ДопКлассификация = СуммыИзменений.ДопКлассификация
	|			И ТаблицаДокумента.Подразделение = СуммыИзменений.Подразделение";


	Запрос.УстановитьПараметр("Ссылка", Реквизиты.Ссылка);

	Таб = Запрос.Выполнить().Выгрузить();

	Если Таб.Итог("СуммаИзменений") < 0 Тогда

		Запрос = Новый Запрос("ВЫБРАТЬ
		|	ЖурналПроводокЕПСБУОборотыДтКт.СуммаОборот КАК СуммаОборот
		|ИЗ
		|	РегистрБухгалтерии.ЖурналПроводокЕПСБУ.ОборотыДтКт(&НачалоПериода, &КонецПериода, Регистратор, СчетДт В (&Счет50217), , СчетКт В (&Счет50211), , ) КАК ЖурналПроводокЕПСБУОборотыДтКт
		|ГДЕ
		|	(ВЫРАЗИТЬ(ЖурналПроводокЕПСБУОборотыДтКт.СубконтоДт2 КАК Справочник.Договоры)) = &Договор
		|	И ЖурналПроводокЕПСБУОборотыДтКт.Регистратор <> &Регистратор");

		Запрос.УстановитьПараметр("НачалоПериода", НачалоГода(Реквизиты.Дата));
		Запрос.УстановитьПараметр("КонецПериода", Новый Граница(Реквизиты.Ссылка.МоментВремени(), ВидГраницы.Исключая));
		Запрос.УстановитьПараметр("Регистратор", Реквизиты.Ссылка);
		Запрос.УстановитьПараметр("Счет50217", БухгалтерскийУчет.СписокСчетов("502.17,502.27,502.37,502.47,502.97"));
		Запрос.УстановитьПараметр("Счет50211", БухгалтерскийУчет.СписокСчетов("502.11,502.21,502.31,502.41,502.91"));
		Запрос.УстановитьПараметр("Договор", Реквизиты.Договор);

		Результат = Запрос.Выполнить();
		Если  НЕ Результат.Пустой() Тогда
			Таб.ЗаполнитьЗначения(Истина, "УменьшениеЦены");

			//проверим необходимость сторнирования 
			Выборка = Результат.Выгрузить();
			Если Выборка.Итог("СуммаОборот") = - Таб.Итог("СуммаИзменений") Тогда
				//это расторжение договора, надо сторнировать экономию
				Запрос = Новый Запрос("ВЫБРАТЬ
				|	ЖурналПроводокЕПСБУОборотыДтКт.СуммаОборот КАК УчтеноЭкономии,
				|	ПОДСТРОКА(ЖурналПроводокЕПСБУОборотыДтКт.СчетДт.Код, 5, 1) КАК КодПлановогоПериода,
				|	ЖурналПроводокЕПСБУОборотыДтКт.КФО КАК КФО,
				#Вставка
				|	ЖурналПроводокЕПСБУОборотыДтКт.ИФО КАК ИФО,
				#КонецВставки
				|	ЖурналПроводокЕПСБУОборотыДтКт.КПСДт КАК КПС,
				|	ЖурналПроводокЕПСБУОборотыДтКт.КЭКДт КАК КЭК,
				|	ВЫБОР
				|		КОГДА ТИПЗНАЧЕНИЯ(ЖурналПроводокЕПСБУОборотыДтКт.СубконтоДт3) = ТИП(Справочник.ВидыЦелевыхСредств)
				|			ТОГДА ВЫРАЗИТЬ(ЖурналПроводокЕПСБУОборотыДтКт.СубконтоДт3 КАК Справочник.ВидыЦелевыхСредств)
				|		КОГДА ТИПЗНАЧЕНИЯ(ЖурналПроводокЕПСБУОборотыДтКт.СубконтоКт2) = ТИП(Справочник.ВидыЦелевыхСредств)
				|			ТОГДА ВЫРАЗИТЬ(ЖурналПроводокЕПСБУОборотыДтКт.СубконтоКт2 КАК Справочник.ВидыЦелевыхСредств)
				|		КОГДА ТИПЗНАЧЕНИЯ(ЖурналПроводокЕПСБУОборотыДтКт.СубконтоКт3) = ТИП(Справочник.ВидыЦелевыхСредств)
				|			ТОГДА ВЫРАЗИТЬ(ЖурналПроводокЕПСБУОборотыДтКт.СубконтоКт3 КАК Справочник.ВидыЦелевыхСредств)
				|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.ВидыЦелевыхСредств.ПустаяСсылка)
				|	КОНЕЦ КАК КодЦели,
				|	ВЫБОР
				|		КОГДА ТИПЗНАЧЕНИЯ(ЖурналПроводокЕПСБУОборотыДтКт.СубконтоДт3) = ТИП(Справочник.Мероприятия)
				|			ТОГДА ВЫРАЗИТЬ(ЖурналПроводокЕПСБУОборотыДтКт.СубконтоДт3 КАК Справочник.Мероприятия)
				|		КОГДА ТИПЗНАЧЕНИЯ(ЖурналПроводокЕПСБУОборотыДтКт.СубконтоКт2) = ТИП(Справочник.Мероприятия)
				|			ТОГДА ВЫРАЗИТЬ(ЖурналПроводокЕПСБУОборотыДтКт.СубконтоКт2 КАК Справочник.Мероприятия)
				|		КОГДА ТИПЗНАЧЕНИЯ(ЖурналПроводокЕПСБУОборотыДтКт.СубконтоКт3) = ТИП(Справочник.Мероприятия)
				|			ТОГДА ВЫРАЗИТЬ(ЖурналПроводокЕПСБУОборотыДтКт.СубконтоКт3 КАК Справочник.Мероприятия)
				|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.Мероприятия.ПустаяСсылка)
				|	КОНЕЦ КАК КодМероприятия,
				|	ВЫБОР
				|		КОГДА ТИПЗНАЧЕНИЯ(ЖурналПроводокЕПСБУОборотыДтКт.СубконтоДт3) = ТИП(Справочник.ДополнительнаяБюджетнаяКлассификация)
				|			ТОГДА ВЫРАЗИТЬ(ЖурналПроводокЕПСБУОборотыДтКт.СубконтоДт3 КАК Справочник.ДополнительнаяБюджетнаяКлассификация)
				|		КОГДА ТИПЗНАЧЕНИЯ(ЖурналПроводокЕПСБУОборотыДтКт.СубконтоКт2) = ТИП(Справочник.ДополнительнаяБюджетнаяКлассификация)
				|			ТОГДА ВЫРАЗИТЬ(ЖурналПроводокЕПСБУОборотыДтКт.СубконтоКт2 КАК Справочник.ДополнительнаяБюджетнаяКлассификация)
				|		КОГДА ТИПЗНАЧЕНИЯ(ЖурналПроводокЕПСБУОборотыДтКт.СубконтоКт3) = ТИП(Справочник.ДополнительнаяБюджетнаяКлассификация)
				|			ТОГДА ВЫРАЗИТЬ(ЖурналПроводокЕПСБУОборотыДтКт.СубконтоКт3 КАК Справочник.ДополнительнаяБюджетнаяКлассификация)
				|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.ДополнительнаяБюджетнаяКлассификация.ПустаяСсылка)
				|	КОНЕЦ КАК ДопКлассификация,
				|	ВЫРАЗИТЬ(ЖурналПроводокЕПСБУОборотыДтКт.СубконтоДт1 КАК Справочник.РазделыЛицевыхСчетов) КАК РазделЛицевогоСчета,
				|	ЖурналПроводокЕПСБУОборотыДтКт.ПодразделениеДт КАК Подразделение,
				|	ЛОЖЬ КАК УменьшениеЦены,
				|	ИСТИНА КАК Расторжение,
				|	0 КАК Сумма,
				|	0 КАК СуммаИзменений,
				|	0 КАК СуммаВВалютеБезусловная,
				|	0 КАК СуммаИзмененийВВалютеБезусловная
				|ИЗ
				|	РегистрБухгалтерии.ЖурналПроводокЕПСБУ.ОборотыДтКт(&НачалоПериода, &КонецПериода, , СчетДт В (&Счет50217), , СчетКт В (&СчетНазначений), , ) КАК ЖурналПроводокЕПСБУОборотыДтКт
				|ГДЕ
				|	(ВЫРАЗИТЬ(ЖурналПроводокЕПСБУОборотыДтКт.СубконтоДт2 КАК Справочник.Договоры)) = &Договор");

				Запрос.УстановитьПараметр("НачалоПериода", НачалоГода(Реквизиты.Дата));
				Запрос.УстановитьПараметр("КонецПериода", Новый Граница(Реквизиты.Ссылка.МоментВремени(), ВидГраницы.Исключая));
				Запрос.УстановитьПараметр("Счет50217", БухгалтерскийУчет.СписокСчетов("502.17,502.27,502.37,502.47,502.97"));
				Запрос.УстановитьПараметр("СчетНазначений", БухгалтерскийУчет.СписокСчетов("501.00,503.00,506.00"));
				Запрос.УстановитьПараметр("Договор", Реквизиты.Договор);

				Выборка = Запрос.Выполнить().Выбрать();
				Пока Выборка.Следующий() Цикл
					НовСтр = Таб.Добавить();
					ЗаполнитьЗначенияСвойств(НовСтр, Выборка);
				КонецЦикла;	
			КонецЕсли;	
		КонецЕсли;	

	КонецЕсли;

	Возврат Таб;

КонецФункции

&ИзменениеИКонтроль("ПустаяТаблицаПринятыеОбязательства")
Функция АБК_ПустаяТаблицаПринятыеОбязательства()

	ОписаниеТипаСуммы = Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(18, 2, ДопустимыйЗнак.Любой));

	ПустаяТаблица = Новый ТаблицаЗначений;

	ПустаяТаблица.Колонки.Добавить("ПериодПлана", 						Новый ОписаниеТипов("СправочникСсылка.Периоды"));
	ПустаяТаблица.Колонки.Добавить("КодПлановогоПериода", 				Новый ОписаниеТипов("Строка", Новый КвалификаторыСтроки(1, ДопустимаяДлина.Переменная)));
	ПустаяТаблица.Колонки.Добавить("КФО",           					Новый ОписаниеТипов("ПеречислениеСсылка.КВД"));
	#Вставка
	ПустаяТаблица.Колонки.Добавить("ИФО",           					Новый ОписаниеТипов("СправочникСсылка.ИсточникиФинансовогоОбеспечения"));
	#КонецВставки
	ПустаяТаблица.Колонки.Добавить("КПС",           					Новый ОписаниеТипов("СправочникСсылка.КлассификационныеПризнакиСчетов"));
	ПустаяТаблица.Колонки.Добавить("КЭК",           					Новый ОписаниеТипов("СправочникСсылка.КОСГУ"));
	ПустаяТаблица.Колонки.Добавить("РазделЛицевогоСчета",				Новый ОписаниеТипов("СправочникСсылка.РазделыЛицевыхСчетов"));
	ПустаяТаблица.Колонки.Добавить("КодЦели",           				Новый ОписаниеТипов("СправочникСсылка.ВидыЦелевыхСредств"));
	ПустаяТаблица.Колонки.Добавить("КодМероприятия", 					Новый ОписаниеТипов("СправочникСсылка.Мероприятия"));
	ПустаяТаблица.Колонки.Добавить("ДопКлассификация",					Новый ОписаниеТипов("СправочникСсылка.ДополнительнаяБюджетнаяКлассификация"));
	ПустаяТаблица.Колонки.Добавить("Подразделение",						Новый ОписаниеТипов("СправочникСсылка.Подразделения"));
	ПустаяТаблица.Колонки.Добавить("Сумма", 							ОписаниеТипаСуммы);
	ПустаяТаблица.Колонки.Добавить("СуммаИзменений", 					ОписаниеТипаСуммы);
	ПустаяТаблица.Колонки.Добавить("СуммаВВалютеБезусловная",			ОписаниеТипаСуммы);
	ПустаяТаблица.Колонки.Добавить("СуммаИзмененийВВалютеБезусловная",	ОписаниеТипаСуммы);
	ПустаяТаблица.Колонки.Добавить("УменьшениеЦены", 					Новый ОписаниеТипов("Булево"));
	ПустаяТаблица.Колонки.Добавить("Расторжение", 						Новый ОписаниеТипов("Булево"));
	ПустаяТаблица.Колонки.Добавить("УчтеноЭкономии",					ОписаниеТипаСуммы);


	Возврат ПустаяТаблица;

КонецФункции
