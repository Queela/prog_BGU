


&НаСервере
Процедура АБК_АктСверкиВзаиморасчетов_НесколькоДоговоров_ПриСозданииНаСервереПосле(Отказ, СтандартнаяОбработка)
	Элементы.ТаблицаДоговоровДоговор.СвязиПараметровВыбора = Элементы.Договор.СвязиПараметровВыбора; 
		
	Если ЗначениеЗаполнено(Объект.Ссылка) Тогда
		
		Свойство = НайтиСоздатьДополнительноеСведение("АБК_АктСверкиВзаиморасчетов_НесколькоДоговоров");
		
		Если ЗначениеЗаполнено(Свойство) Тогда
			НЗ = РегистрыСведений.ДополнительныеСведения.СоздатьНаборЗаписей();
			НЗ.Отбор.Объект.Установить(Объект.Ссылка);
			НЗ.Отбор.Свойство.Установить(Свойство);
			НЗ.Прочитать();
			ТЧРеквизитов = НЗ.Выгрузить();
			
			Если ТЧРеквизитов.Количество() И ЗначениеЗаполнено(ТЧРеквизитов[0].Значение) Тогда
				
				МассивДоговоров = ЗначениеИзСтрокиВнутр(ТЧРеквизитов[0].Значение);
				Для Каждого Договор из МассивДоговоров Цикл
					СтрокаДог = ТаблицаДоговоров.Добавить();
					СтрокаДог.Договор = Договор;
				КонецЦикла;
				
			КонецЕсли;
			
		КонецЕсли;  
		
	КонецЕсли;
КонецПроцедуры

&НаСервере
&ИзменениеИКонтроль("ПолучитьПараметрыПроцедуры")
Функция АБК_АктСверкиВзаиморасчетов_НесколькоДоговоров_ПолучитьПараметрыПроцедуры()

	ПараметрыПроцедуры = Новый Структура();
	ПараметрыПроцедуры.Вставить("ШапкаДокумента",			Документы.АктСверкиВзаиморасчетов.ПолучитьСтруктуруШапкиДокумента(Объект));
	ПараметрыПроцедуры.Вставить("СписокОтмеченныхСчетов",	Документы.АктСверкиВзаиморасчетов.ПолучитьСписокОтмеченныхСчетов(Объект));
	
	#Вставка 
	МассивДоговоров = Новый Массив;
	МассивДоговоров = ТаблицаДоговоров.Выгрузить(,"Договор");
	
	ПараметрыПроцедуры.ШапкаДокумента.Вставить("МассивДоговоров",МассивДоговоров);
	#КонецВставки
	
	Возврат ПараметрыПроцедуры;

КонецФункции


             
&НаСервере
Процедура АБК_ПослеЗаписиНаСервереПосле(ТекущийОбъект, ПараметрыЗаписи) 
	
	МассивДоговоров = ТаблицаДоговоров.Выгрузить().ВыгрузитьКолонку("Договор");
	Если МассивДоговоров.Количество() < 13 Тогда
		ЗаписатьЗначениеДопРеквизитаСФормы(Объект.Ссылка,МассивДоговоров,"АБК_АктСверкиВзаиморасчетов_НесколькоДоговоров");
	КонецЕсли;
		
КонецПроцедуры

&НаСервере
Процедура ЗаписатьЗначениеДопРеквизитаСФормы(СсылкаНаВладельца,ЗначениеРеквизита,ИдентификаторДляФормул)
	
	Свойство = НайтиСоздатьДополнительноеСведение(ИдентификаторДляФормул);
	
	Если ЗначениеЗаполнено(Свойство) И ЗначениеЗаполнено(СсылкаНаВладельца) Тогда
		
		МЗ = РегистрыСведений.ДополнительныеСведения.СоздатьМенеджерЗаписи();
		МЗ.Объект   	= СсылкаНаВладельца;
		МЗ.Свойство   = Свойство;
		МЗ.Значение   = ЗначениеВСтрокуВнутр(ЗначениеРеквизита);
		МЗ.Записать();
		
	КонецЕсли; 
	
КонецПроцедуры

Функция НайтиСоздатьДополнительноеСведение(ИдентификаторДляФормул)
		
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ИдентификаторДляФормул", ИдентификаторДляФормул);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ДополнительныеРеквизитыИСведения.Ссылка КАК Ссылка
	|ИЗ
	|	ПланВидовХарактеристик.ДополнительныеРеквизитыИСведения КАК ДополнительныеРеквизитыИСведения
	|ГДЕ
	|	ДополнительныеРеквизитыИСведения.ИдентификаторДляФормул = &ИдентификаторДляФормул";
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() тогда
		Возврат Выборка.Ссылка;
	КонецЕсли;
	
	ДопСведение = ПланыВидовХарактеристик.ДополнительныеРеквизитыИСведения.СоздатьЭлемент();
	ДопСведение.Наименование = ИдентификаторДляФормул;
	ДопСведение.ИдентификаторДляФормул = ИдентификаторДляФормул;
	ДопСведение.Виден = Ложь;
	ДопСведение.Комментарий = "Техничесикий доп реквизит для работы расширения";
	ДопСведение.ТипЗначения = Новый ОписаниеТипов("Строка",,,,Новый КвалификаторыСтроки(1024));
	ДопСведение.ЭтоДополнительноеСведение = Ложь;  
	
	// Формирование имени дополнительного реквизита (сведения).
	Если Не ЗначениеЗаполнено(ДопСведение.Имя) Тогда
		ДопСведение.Имя = "";
		ЗаголовокОбъекта = ДопСведение.Заголовок;
		УправлениеСвойствамиСлужебный.УдалитьНедопустимыеСимволы(ЗаголовокОбъекта);
		ЗаголовокОбъектаЧастями = СтрРазделить(ЗаголовокОбъекта, " ", Ложь);
		Для Каждого ЧастьЗаголовка Из ЗаголовокОбъектаЧастями Цикл
			ДопСведение.Имя = ДопСведение.Имя + ВРег(Лев(ЧастьЗаголовка, 1)) + Сред(ЧастьЗаголовка, 2);
		КонецЦикла;
		
		Если ИмяНачинаетсяСЦифры(ДопСведение.Имя) Тогда
			ДопСведение.Имя = "_" + ДопСведение.Имя;
		КонецЕсли;
		
		УИД = Новый УникальныйИдентификатор();
		СтрокаУИД = СтрЗаменить(Строка(УИД), "-", "");
		ДопСведение.Имя = ДопСведение.Имя + "_" + СтрокаУИД;
	КонецЕсли;
	
	ДопСведение.Записать();
	
	Возврат ДопСведение.Ссылка;
	
КонецФункции  

Функция ИмяНачинаетсяСЦифры(ИмяРеквизита)
	ПервыйСимвол = Лев(ИмяРеквизита, 1);
	Если СтрНайти("0123456789", ПервыйСимвол) > 0 Тогда
		Возврат Истина;
	КонецЕсли;
	
	Возврат Ложь;
КонецФункции
    