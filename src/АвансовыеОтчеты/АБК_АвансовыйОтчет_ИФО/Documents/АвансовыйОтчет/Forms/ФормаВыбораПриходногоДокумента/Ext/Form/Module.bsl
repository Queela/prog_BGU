
&НаСервере
Процедура АБК_ПриСозданииНаСервереВместо(Отказ, СтандартнаяОбработка)
	
	Список.ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ЖурналПроводокЕПСБУДвиженияССубконто.Регистратор КАК Регистратор,
	|	ВЫБОР
	|		КОГДА ЖурналПроводокЕПСБУДвиженияССубконто.ВалютаКт ЕСТЬ NULL
	|				ИЛИ ЖурналПроводокЕПСБУДвиженияССубконто.ВалютаКт = ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка)
	|				ИЛИ ЖурналПроводокЕПСБУДвиженияССубконто.ВалютаКт = &ВалютаРуб
	|			ТОГДА &ВалютаРуб
	|		ИНАЧЕ ЖурналПроводокЕПСБУДвиженияССубконто.ВалютаКт
	|	КОНЕЦ КАК Валюта,
	|	ВЫБОР
	|		КОГДА ЖурналПроводокЕПСБУДвиженияССубконто.Регистратор ССЫЛКА Документ.ПоступлениеМЗ
	|			ТОГДА ВЫРАЗИТЬ(ЖурналПроводокЕПСБУДвиженияССубконто.Регистратор КАК Документ.ПоступлениеМЗ).ДатаПервичногоДокумента
	|		КОГДА ЖурналПроводокЕПСБУДвиженияССубконто.Регистратор ССЫЛКА Документ.ПоступлениеОС
	|			ТОГДА ВЫРАЗИТЬ(ЖурналПроводокЕПСБУДвиженияССубконто.Регистратор КАК Документ.ПоступлениеОС).ДатаПервичногоДокумента
	|		КОГДА ЖурналПроводокЕПСБУДвиженияССубконто.Регистратор ССЫЛКА Документ.ПриходныйОрдерФондовый
	|			ТОГДА ВЫРАЗИТЬ(ЖурналПроводокЕПСБУДвиженияССубконто.Регистратор КАК Документ.ПриходныйОрдерФондовый).Дата
	|		ИНАЧЕ ДАТАВРЕМЯ(1, 1, 1)
	|	КОНЕЦ КАК ДатаПервичногоДокумента,
	|	ВЫБОР
	|		КОГДА ЖурналПроводокЕПСБУДвиженияССубконто.Регистратор ССЫЛКА Документ.ПоступлениеМЗ
	|			ТОГДА ВЫРАЗИТЬ(ЖурналПроводокЕПСБУДвиженияССубконто.Регистратор КАК Документ.ПоступлениеМЗ).НомерПервичногоДокумента
	|		КОГДА ЖурналПроводокЕПСБУДвиженияССубконто.Регистратор ССЫЛКА Документ.ПоступлениеОС
	|			ТОГДА ВЫРАЗИТЬ(ЖурналПроводокЕПСБУДвиженияССубконто.Регистратор КАК Документ.ПоступлениеОС).НомерПервичногоДокумента
	|		КОГДА ЖурналПроводокЕПСБУДвиженияССубконто.Регистратор ССЫЛКА Документ.ПриходныйОрдерФондовый
	|			ТОГДА ВЫРАЗИТЬ(ЖурналПроводокЕПСБУДвиженияССубконто.Регистратор КАК Документ.ПриходныйОрдерФондовый).Номер
	|		ИНАЧЕ """"
	|	КОНЕЦ КАК НомерПервичногоДокумента,
	|	ВЫБОР
	|		КОГДА ЖурналПроводокЕПСБУДвиженияССубконто.Регистратор ССЫЛКА Документ.ПоступлениеМЗ
	|			ТОГДА ВЫРАЗИТЬ(ЖурналПроводокЕПСБУДвиженияССубконто.Регистратор КАК Документ.ПоступлениеМЗ).ВидПервичногоДокумента
	|		КОГДА ЖурналПроводокЕПСБУДвиженияССубконто.Регистратор ССЫЛКА Документ.ПоступлениеОС
	|			ТОГДА ВЫРАЗИТЬ(ЖурналПроводокЕПСБУДвиженияССубконто.Регистратор КАК Документ.ПоступлениеОС).ВидПервичногоДокумента
	|		ИНАЧЕ """"
	|	КОНЕЦ КАК ВидПервичногоДокумента,
	|	ВЫБОР
	|		КОГДА ЖурналПроводокЕПСБУДвиженияССубконто.Регистратор ССЫЛКА Документ.ПоступлениеМЗ
	|			ТОГДА ВЫБОР
	|					КОГДА ВЫРАЗИТЬ(ЖурналПроводокЕПСБУДвиженияССубконто.Регистратор КАК Документ.ПоступлениеМЗ).Грузоотправитель.НаименованиеСокращенное = """"
	|						ТОГДА ВЫРАЗИТЬ(ЖурналПроводокЕПСБУДвиженияССубконто.Регистратор КАК Документ.ПоступлениеМЗ).Грузоотправитель.Наименование
	|					ИНАЧЕ ВЫРАЗИТЬ(ЖурналПроводокЕПСБУДвиженияССубконто.Регистратор КАК Документ.ПоступлениеМЗ).Грузоотправитель.НаименованиеСокращенное
	|				КОНЕЦ
	|		КОГДА ЖурналПроводокЕПСБУДвиженияССубконто.Регистратор ССЫЛКА Документ.ПоступлениеОС
	|			ТОГДА ВЫБОР
	|					КОГДА ВЫРАЗИТЬ(ЖурналПроводокЕПСБУДвиженияССубконто.Регистратор КАК Документ.ПоступлениеОС).Грузоотправитель.НаименованиеСокращенное = """"
	|						ТОГДА ВЫРАЗИТЬ(ЖурналПроводокЕПСБУДвиженияССубконто.Регистратор КАК Документ.ПоступлениеОС).Грузоотправитель.Наименование
	|					ИНАЧЕ ВЫРАЗИТЬ(ЖурналПроводокЕПСБУДвиженияССубконто.Регистратор КАК Документ.ПоступлениеОС).Грузоотправитель.НаименованиеСокращенное
	|				КОНЕЦ
	|		ИНАЧЕ """"
	|	КОНЕЦ КАК Грузоотправитель,
	|	СУММА(ВЫБОР
	|			КОГДА ЖурналПроводокЕПСБУДвиженияССубконто.ВалютаКт ЕСТЬ NULL
	|					ИЛИ ЖурналПроводокЕПСБУДвиженияССубконто.ВалютаКт = ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка)
	|					ИЛИ ЖурналПроводокЕПСБУДвиженияССубконто.ВалютаКт = &ВалютаРуб
	|				ТОГДА 0
	|			ИНАЧЕ ЖурналПроводокЕПСБУДвиженияССубконто.ВалютнаяСуммаКт
	|		КОНЕЦ) КАК СуммаВВалюте,
	|	СУММА(ЖурналПроводокЕПСБУДвиженияССубконто.Сумма) КАК Сумма,
	|	ЖурналПроводокЕПСБУДвиженияССубконто.ИФО КАК ИФО
	|ИЗ
	|	РегистрБухгалтерии.ЖурналПроводокЕПСБУ.ДвиженияССубконто(
	|			&НачалоПериода,
	|			&КонецПериода,
	|			Организация = &Организация
	|				И СчетКт В (&СчетаКт)
	|				И (Регистратор ССЫЛКА Документ.ПоступлениеМЗ
	|					ИЛИ Регистратор ССЫЛКА Документ.ПриходныйОрдерФондовый
	|					ИЛИ Регистратор ССЫЛКА Документ.ПоступлениеОС),
	|			,
	|			) КАК ЖурналПроводокЕПСБУДвиженияССубконто
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.АвансовыйОтчет.Израсходовано КАК АвансовыйОтчетИзрасходовано
	|		ПО (АвансовыйОтчетИзрасходовано.ПриходныйДокумент = ЖурналПроводокЕПСБУДвиженияССубконто.Регистратор)
	|ГДЕ
	|	ЖурналПроводокЕПСБУДвиженияССубконто.СубконтоКт1 = &Контрагент
	|	И (ЖурналПроводокЕПСБУДвиженияССубконто.ВалютаКт ЕСТЬ NULL
	|			ИЛИ ЖурналПроводокЕПСБУДвиженияССубконто.ВалютаКт = ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка)
	|				И &Валюта = &ВалютаРуб
	|			ИЛИ ЖурналПроводокЕПСБУДвиженияССубконто.ВалютаКт = &ВалютаРуб
	|			ИЛИ ЖурналПроводокЕПСБУДвиженияССубконто.ВалютаКт = &Валюта)
	|	И (АвансовыйОтчетИзрасходовано.Ссылка ЕСТЬ NULL
	|			ИЛИ АвансовыйОтчетИзрасходовано.Ссылка = &АвансовыйОтчет)
	|	И НЕ ЖурналПроводокЕПСБУДвиженияССубконто.Регистратор В (&Выбранные)
	|
	|СГРУППИРОВАТЬ ПО
	|	ЖурналПроводокЕПСБУДвиженияССубконто.Регистратор,
	|	ВЫБОР
	|		КОГДА ЖурналПроводокЕПСБУДвиженияССубконто.ВалютаКт ЕСТЬ NULL
	|				ИЛИ ЖурналПроводокЕПСБУДвиженияССубконто.ВалютаКт = ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка)
	|				ИЛИ ЖурналПроводокЕПСБУДвиженияССубконто.ВалютаКт = &ВалютаРуб
	|			ТОГДА &ВалютаРуб
	|		ИНАЧЕ ЖурналПроводокЕПСБУДвиженияССубконто.ВалютаКт
	|	КОНЕЦ,
	|	ЖурналПроводокЕПСБУДвиженияССубконто.ИФО"; 
	
	НовЭл = Элементы.Добавить("СписокИФО", Тип("ПолеФормы"), Элементы.Список);
	НовЭл.ПутьКДанным = "Список.ИФО";
	НовЭл.Вид = ВидПоляФормы.ПолеВвода;
	
	СчетаКт = БухгалтерскийУчетПовтИсп.ПолучитьМассивСчетов("208.00");
	
	//АпогейБК
	//Если ПолучитьФункциональнуюОпцию("ИспользоватьУчетПоИФО") Тогда
	//	
	//	СхемаЗапроса = Новый СхемаЗапроса;
	//	СхемаЗапроса.УстановитьТекстЗапроса(Список.ТекстЗапроса);
	//	
	//	ТекстВыражения = Строка(СхемаЗапроса.ПакетЗапросов[0].Операторы[0].Источники[0].Источник.Параметры[2].Выражение);
	//	ТекстВыражения = ТекстВыражения + "
	//	|И ИФО = &ИФО";
	//	СхемаЗапроса.ПакетЗапросов[0].Операторы[0].Источники[0].Источник.Параметры[2].Выражение = Новый ВыражениеСхемыЗапроса(ТекстВыражения); 
	//	
	//	Список.ТекстЗапроса = СхемаЗапроса.ПолучитьТекстЗапроса();
	//	
	//	Список.Параметры.УстановитьЗначениеПараметра("ИФО", Параметры.ИФО);
	//	
	//КонецЕсли;   
	//АпогейБК
	
	ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(Список, "НачалоПериода", Параметры.НачалоПериода);
	ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(Список, "КонецПериода", 	Параметры.КонецПериода);
	ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(Список, "Организация", 	Параметры.Организация);
	ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(Список, "Контрагент", 	ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Параметры.Сотрудник, "Контрагент"));
	ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(Список, "ВалютаРуб", 	БухгалтерскийУчетПовтИсп.ВалютаРегламентированногоУчета());
	ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(Список, "Валюта", 		Параметры.Валюта);
	ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(Список, "СчетаКт", 		СчетаКт);
	ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(Список, "Выбранные", 	Параметры.МассивВыбранных);
	ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(Список, "АвансовыйОтчет",Параметры.АвансовыйОтчет);
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "Валюта", "Видимость", ПолучитьФункциональнуюОпцию("ИспользоватьВалютныйУчет"));
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "СуммаВВалюте", "Видимость", ПолучитьФункциональнуюОпцию("ИспользоватьВалютныйУчет"));
	
	Заголовок = Параметры.ЗаголовокФормы + " (" + Параметры.Сотрудник + ")";
	
	ПериодС 	= Параметры.НачалоПериода;
	ПериодПо 	= Параметры.КонецПериода;
	
КонецПроцедуры

&НаКлиенте
Процедура АБК_СписокВыборВместо(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ОповеститьВладельцаОВыборе(Элементы.Список.ТекущиеДанные);
	
КонецПроцедуры

&НаКлиенте
Процедура АБК_СписокВыборЗначенияВместо(Элемент, Значение, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ОповеститьВладельцаОВыборе(Элементы.Список.ТекущиеДанные);
	
КонецПроцедуры

&НаКлиенте
&ИзменениеИКонтроль("ОповеститьВладельцаОВыборе")
Процедура АБК_ОповеститьВладельцаОВыборе(ТекущиеДанные)

	Результат = Новый Структура;
	Результат.Вставить("ПриходныйДокумент", ТекущиеДанные.Регистратор);
	#Вставка
	Результат.Вставить("ИФО", ТекущиеДанные.ИФО);
	#КонецВставки
	Результат.Вставить("Валюта", ТекущиеДанные.Валюта);
	Результат.Вставить("Сумма", ?(ЗначениеЗаполнено(ТекущиеДанные.СуммаВВалюте), 0, ТекущиеДанные.Сумма));
	Результат.Вставить("СуммаВВалюте", ТекущиеДанные.СуммаВВалюте);
	Результат.Вставить("СуммаПоОтчету", ?(ЗначениеЗаполнено(ТекущиеДанные.СуммаВВалюте), 0, ТекущиеДанные.Сумма));
	Результат.Вставить("СуммаВВалютеПоОтчету", ТекущиеДанные.СуммаВВалюте);
	Результат.Вставить("ДокументДата", ТекущиеДанные.ДатаПервичногоДокумента);
	Результат.Вставить("ДокументНомер", ТекущиеДанные.НомерПервичногоДокумента);

	Если ЗначениеЗаполнено(ТекущиеДанные.Грузоотправитель) И ЗначениеЗаполнено(ТекущиеДанные.ВидПервичногоДокумента) Тогда
		Результат.Вставить("КомуЗаЧто", ТекущиеДанные.Грузоотправитель + ", " + ТекущиеДанные.ВидПервичногоДокумента);
	ИначеЕсли ЗначениеЗаполнено(ТекущиеДанные.Грузоотправитель) Тогда
		Результат.Вставить("КомуЗаЧто", ТекущиеДанные.Грузоотправитель);
	ИначеЕсли ЗначениеЗаполнено(ТекущиеДанные.ВидПервичногоДокумента) Тогда
		Результат.Вставить("КомуЗаЧто", ТекущиеДанные.ВидПервичногоДокумента);
	КонецЕсли;

	ОповеститьОВыборе(Результат);

КонецПроцедуры
