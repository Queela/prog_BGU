
&ИзменениеИКонтроль("ТаблицаДенежныхОбязательствПоТЧАвансовогоОтчета")
Функция АБК_ТаблицаДенежныхОбязательствПоТЧАвансовогоОтчета(Реквизиты)

	Запрос = Новый Запрос;
	ОбщегоНазначенияБГУ.ПоместитьТабЧастьВВиртуальнуюТаблицу(
	Запрос,
	"ДенежныеОбязательства",
	#Вставка
	"НомерСтроки,"+
	#КонецВставки
	"КФО,КПС,КЭК,КодЦели,КодМероприятия,ДопКлассификация,Валюта,Обязательство,РазделЛицСчета,Сумма,СуммаВВалюте",
	Реквизиты.Ссылка,
	Реквизиты);

	Запрос.Текст = Запрос.Текст +
	"ВЫБРАТЬ
	#Вставка
	|	ДенежныеОбязательства.НомерСтроки КАК НомерСтроки,
	#КонецВставки
	|	ДенежныеОбязательства.КФО КАК КФО,
	|	ДенежныеОбязательства.КПС КАК КПС,
	|	ДенежныеОбязательства.КЭК КАК КЭК,
	|	ДенежныеОбязательства.КодЦели КАК КодЦели,
	|	ДенежныеОбязательства.КодМероприятия КАК КодМероприятия,
	|	ДенежныеОбязательства.ДопКлассификация КАК ДопКлассификация,
	|	ДенежныеОбязательства.Валюта КАК Валюта,
	|	ДенежныеОбязательства.Обязательство КАК Обязательство,
	|	ДенежныеОбязательства.РазделЛицСчета КАК РазделЛицСчета,
	|	ДенежныеОбязательства.Сумма КАК Сумма,
	|	ДенежныеОбязательства.СуммаВВалюте КАК СуммаВВалюте
	|ИЗ
	|	ТаблицаДенежныеОбязательства КАК ДенежныеОбязательства";   
	
	#Вставка
	Результат = Запрос.Выполнить().Выгрузить();
	Результат.Колонки.Добавить("ИФО", Новый ОписаниеТипов("СправочникСсылка.ИсточникиФинансовогоОбеспечения"));
	
	ЗаполнитьДопКолонку("ИФО", "ДенежныеОбязательства", Реквизиты.Ссылка, Результат);
	
	Возврат Результат;
	#КонецВставки
	
	Возврат Запрос.Выполнить().Выгрузить();   

КонецФункции     

Процедура ЗаполнитьДопКолонку(ИмяДопКолонки, ИмяДопТабЧасти, ДокументСсылка, ТаблицаДокумента)
	ДопТабЧасть = ПолучитьДопТабЧастьДокумента(ИмяДопТабЧасти, ДокументСсылка);
	ДопКолонка = Справочники.КолонкиДополнительныхТабличныхЧастей.НайтиПоНаименованию(ИмяДопКолонки,,,ДопТабЧасть);
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ЗначенияКолонокДополнительныхТабличныхЧастей.НомерСтрокиДокумента КАК НомерСтроки,
	|	ЗначенияКолонокДополнительныхТабличныхЧастей.Значение КАК Значение
	|ИЗ
	|	РегистрСведений.ЗначенияКолонокДополнительныхТабличныхЧастей КАК ЗначенияКолонокДополнительныхТабличныхЧастей
	|ГДЕ
	|	ЗначенияКолонокДополнительныхТабличныхЧастей.Объект = &Объект
	|	И ЗначенияКолонокДополнительныхТабличныхЧастей.ТабличнаяЧасть = &ТабличнаяЧасть
	|	И ЗначенияКолонокДополнительныхТабличныхЧастей.КолонкаТабличнойЧасти = &КолонкаТабличнойЧасти");
	Запрос.УстановитьПараметр("Объект", ДокументСсылка);
	Запрос.УстановитьПараметр("ТабличнаяЧасть", ДопТабЧасть);
	Запрос.УстановитьПараметр("КолонкаТабличнойЧасти", ДопКолонка);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		НайдСтроки = ТаблицаДокумента.НайтиСтроки(Новый Структура("НомерСтроки", Выборка.НомерСтроки));
		
		Если НайдСтроки.Количество() Тогда
			НайдСтроки[0][ИмяДопКолонки] = Выборка.Значение;	
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры

Функция ПолучитьДопТабЧастьДокумента(Наименование, Документ)
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ДополнительныеТабличныеЧастиДокументов.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.ДополнительныеТабличныеЧастиДокументов КАК ДополнительныеТабличныеЧастиДокументов
	|ГДЕ
	|	ДополнительныеТабличныеЧастиДокументов.ИмяТабличнойЧасти = &Наименование
	|	И ДополнительныеТабличныеЧастиДокументов.ИдентификаторДокумента = &ИдентификаторДокумента
	|	И НЕ ДополнительныеТабличныеЧастиДокументов.ПометкаУдаления"); 
	
	Запрос.УстановитьПараметр("Наименование",Наименование);
	Запрос.УстановитьПараметр("ИдентификаторДокумента",Справочники.ИдентификаторыОбъектовМетаданных.ИдентификаторОбъектаМетаданных(Документ.Метаданные(),Ложь));
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Возврат	Выборка.Ссылка;
	КонецЕсли;
	
	Возврат Справочники.ДополнительныеТабличныеЧастиДокументов.ПустаяСсылка();
КонецФункции

&ИзменениеИКонтроль("ПодготовитьТаблицуРасходовПоАвансовомуОтчету")
Функция АБК_ПодготовитьТаблицуРасходовПоАвансовомуОтчету(Документ, Параметры, Отказ)

	Если Параметры = Неопределено Тогда
		Параметры = Новый Структура;
	КонецЕсли;

	Если НЕ Параметры.Свойство("ПоВсемКФО") Тогда
		Параметры.Вставить("ПоВсемКФО", Ложь);
	КонецЕсли;
	Если НЕ Параметры.Свойство("ПоВсемКПС") Тогда
		Параметры.Вставить("ПоВсемКПС", Ложь);
	КонецЕсли;

	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ВалютаРегламентированногоУчета", БухгалтерскийУчетПовтИсп.ВалютаРегламентированногоУчета());

	ОбщегоНазначенияБГУ.ПоместитьТабЧастьВВиртуальнуюТаблицу(Запрос, "Израсходовано",
	"НомерСтроки,ИФО,КФО,КПС,КЭК,Подразделение,ПоДокументу,СуммаВключаетНДС,ПредъявленСФ,
	|СчетДебета,ПодразделениеДт,КПСДт,КЭКДт,СубконтоДебета1,СубконтоДебета2,СубконтоДебета3,
	|СчетНУ,СубконтоНУ1,СубконтоНУ2,СубконтоНУ3,
	|Сотрудник,СчетКредита,ДокументДата,КомуЗаЧто,СпособОбеспечения,
	|ОтражениеВУСН,ВалютаДокумента,Сумма,СуммаВВалюте,СуммаНДС,Количество",
	Документ, Параметры);

	Запрос.Текст = Запрос.Текст +
	"ВЫБРАТЬ
	|	""Израсходовано"" КАК ИмяСписка,
	|	""Израсходовано"" КАК СинонимСписка,
	|	Израсходовано.НомерСтроки КАК НомерСтроки,
	|	Израсходовано.ИФО КАК ИФО,
	|	Израсходовано.КФО КАК КФО,
	|	Израсходовано.КПС КАК КПС,
	|	Израсходовано.КЭК КАК КЭК,
	|	Израсходовано.Подразделение КАК Подразделение,
	|	Израсходовано.СчетДебета КАК СчетДебета,
	|	ВЫБОР
	|		КОГДА Израсходовано.КПСДт = ЗНАЧЕНИЕ(Справочник.КлассификационныеПризнакиСчетов.ПустаяСсылка)
	|			ТОГДА Израсходовано.КПС
	|		ИНАЧЕ Израсходовано.КПСДт
	|	КОНЕЦ КАК КПСДт,
	|	Израсходовано.КЭКДт КАК КЭКДт,
	|	ВЫБОР
	|		КОГДА Израсходовано.ПодразделениеДт = ЗНАЧЕНИЕ(Справочник.Подразделения.ПустаяСсылка)
	|			ТОГДА Израсходовано.Подразделение
	|		ИНАЧЕ Израсходовано.ПодразделениеДт
	|	КОНЕЦ КАК ПодразделениеДт,
	|	Израсходовано.СубконтоДебета1 КАК СубконтоДебета1,
	|	Израсходовано.СубконтоДебета2 КАК СубконтоДебета2,
	|	Израсходовано.СубконтоДебета3 КАК СубконтоДебета3,
	|	Израсходовано.Сотрудник КАК Сотрудник,
	|	Израсходовано.Сотрудник.Контрагент КАК Контрагент,
	|	Израсходовано.СчетКредита КАК СчетРасчетов,
	|	Израсходовано.ДокументДата КАК ДокументДата,
	|	Израсходовано.КомуЗаЧто КАК КомуЗаЧто,
	|	Израсходовано.ПредъявленСФ КАК ПредъявленСФ,
	|	Израсходовано.ОтражениеВУСН КАК ОтражениеВУСН,
	|	ВЫБОР
	|		КОГДА Израсходовано.Сумма = 0
	|			ТОГДА Израсходовано.ВалютаДокумента
	|		ИНАЧЕ &ВалютаРегламентированногоУчета
	|	КОНЕЦ КАК Валюта,
	|	Израсходовано.СпособОбеспечения КАК СпособОбеспечения,
	|	Израсходовано.Сумма КАК Сумма,
	|	Израсходовано.СуммаВВалюте КАК СуммаВВалюте,
	|	ВЫБОР
	|		КОГДА Израсходовано.Сумма = 0
	|			ТОГДА Израсходовано.СуммаВВалюте
	|		КОГДА СуммаВключаетНДС
	|			ТОГДА Израсходовано.Сумма
	|		ИНАЧЕ Израсходовано.Сумма + Израсходовано.СуммаНДС
	|	КОНЕЦ КАК СуммаРасчетов,
	|	Израсходовано.СуммаНДС КАК СуммаНДС,
	|	Израсходовано.Количество КАК Количество,
	|	Израсходовано.СчетНУ КАК СчетНУ,
	|	Израсходовано.СубконтоНУ1 КАК СубконтоНУ1,
	|	Израсходовано.СубконтоНУ2 КАК СубконтоНУ2,
	|	Израсходовано.СубконтоНУ3 КАК СубконтоНУ3
	|ИЗ
	|	ТаблицаИзрасходовано КАК Израсходовано
	|ГДЕ
	|	НЕ Израсходовано.ПоДокументу
	|
	|УПОРЯДОЧИТЬ ПО
	|	ДокументДата,
	|	НомерСтроки";

	ТаблицаИзрасходовано = Запрос.Выполнить().Выгрузить();   
	
	#Вставка
	ЗаполнитьДопКолонку("ИФО", "Израсходовано", Документ, ТаблицаИзрасходовано);
	#КонецВставки

	Возврат ТаблицаИзрасходовано;

КонецФункции

&ИзменениеИКонтроль("ТаблицаКорректировкиДенежныхОбязательствПоАвансовомуОтчету")
Функция АБК_ТаблицаКорректировкиДенежныхОбязательствПоАвансовомуОтчету(Реквизиты, РасходыПоАвансовомуОтчету)

	Запрос = Новый Запрос;  
	

	Запрос.УстановитьПараметр("ВалютаРегУчета", Константы.ВалютаРегламентированногоУчета.Получить());
	Запрос.УстановитьПараметр("ЗаявлениеНаВыдачуАванса", Реквизиты.ЗаявлениеНаВыдачуАванса);
	Запрос.УстановитьПараметр("Обязательство", Реквизиты.Обязательство);
	Запрос.УстановитьПараметр("РазделЛицСчета", Реквизиты.РазделЛицСчета);
	Запрос.УстановитьПараметр("Израсходовано", РасходыПоАвансовомуОтчету);
	Запрос.УстановитьПараметр("АвансовыйОтчетСсылка", Реквизиты.Ссылка);
	Запрос.УстановитьПараметр("Счет208", БухгалтерскийУчет.СписокСчетов("208.00"));
	Запрос.УстановитьПараметр("Счет20834", БухгалтерскийУчет.СчетПоКоду("208.34"));
	Запрос.УстановитьПараметр("СчетаМатериалы", БухгалтерскийУчет.СписокСчетов("105.00, 106.2П, 106.3П"));

	Запрос.Текст =
	"ВЫБРАТЬ
	|	ЕПСБУ.Ссылка КАК СчетЕПСБУ
	|ПОМЕСТИТЬ СчетаДтГруппы
	|ИЗ
	|	ПланСчетов.ЕПСБУ КАК ЕПСБУ
	|ГДЕ
	|	ЕПСБУ.Код = ""502.11""
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЕПСБУ.Ссылка КАК СчетЕПСБУ
	|ПОМЕСТИТЬ СчетаДт
	|ИЗ
	|	ПланСчетов.ЕПСБУ КАК ЕПСБУ
	|ГДЕ
	|	ЕПСБУ.Ссылка В ИЕРАРХИИ
	|			(ВЫБРАТЬ
	|				СчетаДтГруппы.СчетЕПСБУ
	|			ИЗ
	|				СчетаДтГруппы)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	СчетЕПСБУ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ СчетаДтГруппы
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЕПСБУ.Ссылка КАК СчетЕПСБУ
	|ПОМЕСТИТЬ СчетаКтГруппы
	|ИЗ
	|	ПланСчетов.ЕПСБУ КАК ЕПСБУ
	|ГДЕ
	|	ЕПСБУ.Код = ""502.12""
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЕПСБУ.Ссылка КАК СчетЕПСБУ
	|ПОМЕСТИТЬ СчетаКт
	|ИЗ
	|	ПланСчетов.ЕПСБУ КАК ЕПСБУ
	|ГДЕ
	|	ЕПСБУ.Ссылка В ИЕРАРХИИ
	|			(ВЫБРАТЬ
	|				СчетаКтГруппы.СчетЕПСБУ
	|			ИЗ
	|				СчетаКтГруппы)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	СчетЕПСБУ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ СчетаКтГруппы
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	&Обязательство КАК Обязательство,
	|	&РазделЛицСчета КАК РазделЛицСчета,
	|	ЖурналПроводокЕПСБУДвиженияССубконто.КФО КАК КФО,
	#Вставка
	|	ЖурналПроводокЕПСБУДвиженияССубконто.ИФО КАК ИФО,
	#КонецВставки
	|	ЖурналПроводокЕПСБУДвиженияССубконто.КПСКт КАК КПС,
	|	ЖурналПроводокЕПСБУДвиженияССубконто.КЭККт КАК КЭК,
	|	ВЫБОР
	|		КОГДА ЖурналПроводокЕПСБУДвиженияССубконто.ВалютаКт ЕСТЬ NULL
	|				ИЛИ ЖурналПроводокЕПСБУДвиженияССубконто.ВалютаКт = &ВалютаРегУчета
	|			ТОГДА """"
	|		ИНАЧЕ ЖурналПроводокЕПСБУДвиженияССубконто.ВалютаКт
	|	КОНЕЦ КАК Валюта,
	|	ВЫБОР
	|		КОГДА ЖурналПроводокЕПСБУДвиженияССубконто.ВалютаКт ЕСТЬ NULL
	|				ИЛИ ЖурналПроводокЕПСБУДвиженияССубконто.ВалютаКт = &ВалютаРегУчета
	|			ТОГДА ЖурналПроводокЕПСБУДвиженияССубконто.Сумма
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК Сумма,
	|	ВЫБОР
	|		КОГДА ЖурналПроводокЕПСБУДвиженияССубконто.ВалютаКт ЕСТЬ NULL
	|				ИЛИ ЖурналПроводокЕПСБУДвиженияССубконто.ВалютаКт = &ВалютаРегУчета
	|			ТОГДА 0
	|		ИНАЧЕ ЖурналПроводокЕПСБУДвиженияССубконто.ВалютнаяСуммаКт
	|	КОНЕЦ КАК СуммаВВалюте
	|ПОМЕСТИТЬ ОБЯЗАТЕЛЬСТВАПОЗАЯВЛЕНИЮ
	|ИЗ
	|	РегистрБухгалтерии.ЖурналПроводокЕПСБУ.ДвиженияССубконто(
	|			,
	|			,
	|			СчетДт В
	|					(ВЫБРАТЬ
	|						СчетаДт.СчетЕПСБУ
	|					ИЗ
	|						СчетаДт)
	|				И СчетКт В
	|					(ВЫБРАТЬ
	|						СчетаКт.СчетЕПСБУ
	|					ИЗ
	|						СчетаКт)
	|				И Регистратор = &ЗаявлениеНаВыдачуАванса,
	|			,
	|			) КАК ЖурналПроводокЕПСБУДвиженияССубконто
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	&Обязательство КАК Обязательство,
	|	&РазделЛицСчета КАК РазделЛицСчета,
	|	Израсходовано.КФО КАК КФО,
	#Вставка
	|	Израсходовано.ИФО КАК ИФО,
	#КонецВставки
	|	Израсходовано.КПС КАК КПС,
	|	Израсходовано.КЭК КАК КЭК,
	|	Израсходовано.СуммаРубПоКурсуАванса КАК Сумма,
	|	ВЫБОР
	|		КОГДА НЕ Израсходовано.РасчетыВВалюте
	|			ТОГДА 0
	|		ИНАЧЕ Израсходовано.СуммаРасчетов
	|	КОНЕЦ КАК СуммаВВалюте,
	|	ВЫБОР
	|		КОГДА НЕ Израсходовано.РасчетыВВалюте
	|			ТОГДА """"
	|		ИНАЧЕ Израсходовано.Валюта
	|	КОНЕЦ КАК Валюта
	|ПОМЕСТИТЬ ИЗРАСХОДОВАНО
	|ИЗ
	|	&Израсходовано КАК Израсходовано
	|ГДЕ
	|	Израсходовано.СпособОбеспечения <> ЗНАЧЕНИЕ(Перечисление.СпособыОбеспеченияКомандировки.ВыдачаДенежныхДокументов)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
	|	АвансовыйОтчетИзрасходовано.ПриходныйДокумент КАК ПриходныйДокумент
	|ПОМЕСТИТЬ ДОКУМЕНТЫПРИХОДНЫЕ
	|ИЗ
	|	Документ.АвансовыйОтчет.Израсходовано КАК АвансовыйОтчетИзрасходовано
	|ГДЕ
	|	АвансовыйОтчетИзрасходовано.Ссылка = &АвансовыйОтчетСсылка
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ПриходныйДокумент
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЖурналПроводокЕПСБУДвиженияССубконто.Регистратор КАК Регистратор,
	|	МАКСИМУМ(ЖурналПроводокЕПСБУДвиженияССубконто.КЭКДт) КАК КЭК
	|ПОМЕСТИТЬ ПриходныеДокументыСЕдинственнымКЭК
	|ИЗ
	|	РегистрБухгалтерии.ЖурналПроводокЕПСБУ.ДвиженияССубконто(
	|			,
	|			,
	|			СчетКт В (&Счет208)
	|				И СчетДт В (&СчетаМатериалы)
	|				И Регистратор В
	|					(ВЫБРАТЬ
	|						ДОКУМЕНТЫПРИХОДНЫЕ.ПриходныйДокумент
	|					ИЗ
	|						ДОКУМЕНТЫПРИХОДНЫЕ),
	|			,
	|			) КАК ЖурналПроводокЕПСБУДвиженияССубконто
	|
	|СГРУППИРОВАТЬ ПО
	|	ЖурналПроводокЕПСБУДвиженияССубконто.Регистратор
	|
	|ИМЕЮЩИЕ
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ЖурналПроводокЕПСБУДвиженияССубконто.КЭКДт) = 1
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	&Обязательство КАК Обязательство,
	|	&РазделЛицСчета КАК РазделЛицСчета,
	|	ЖурналПроводокЕПСБУДвиженияССубконто.КФО КАК КФО,
	#Вставка
	|	ЖурналПроводокЕПСБУДвиженияССубконто.ИФО КАК ИФО,
	#КонецВставки
	|	ЖурналПроводокЕПСБУДвиженияССубконто.КПСКт КАК КПС,
	|	ВЫБОР
	|		КОГДА НЕ ПриходныеДокументыСЕдинственнымКЭК.КЭК ЕСТЬ NULL
	|			ТОГДА ПриходныеДокументыСЕдинственнымКЭК.КЭК
	|		КОГДА ЖурналПроводокЕПСБУДвиженияССубконто.СчетКт = &Счет20834
	|				И АвансовыйОтчетИзрасходовано.КЭК <> ЗНАЧЕНИЕ(Справочник.КОСГУ.ПустаяСсылка)
	|			ТОГДА АвансовыйОтчетИзрасходовано.КЭК
	|		ИНАЧЕ СоответствияАналитическихСчетовКЭК.КЭК
	|	КОНЕЦ КАК КЭК,
	|	ВЫБОР
	|		КОГДА ЖурналПроводокЕПСБУДвиженияССубконто.ВалютаКт ЕСТЬ NULL
	|				ИЛИ ЖурналПроводокЕПСБУДвиженияССубконто.ВалютаКт = &ВалютаРегУчета
	|			ТОГДА """"
	|		ИНАЧЕ ЖурналПроводокЕПСБУДвиженияССубконто.ВалютаКт
	|	КОНЕЦ КАК Валюта,
	|	ЖурналПроводокЕПСБУДвиженияССубконто.Сумма КАК Сумма,
	|	ВЫБОР
	|		КОГДА ЖурналПроводокЕПСБУДвиженияССубконто.ВалютаКт ЕСТЬ NULL
	|				ИЛИ ЖурналПроводокЕПСБУДвиженияССубконто.ВалютаКт = &ВалютаРегУчета
	|			ТОГДА 0
	|		ИНАЧЕ ЖурналПроводокЕПСБУДвиженияССубконто.ВалютнаяСуммаКт
	|	КОНЕЦ КАК СуммаВВалюте
	|ПОМЕСТИТЬ ИЗРАСХОДОВАНОПОДОКУМЕНТАМПРИХОДА
	|ИЗ
	|	Документ.АвансовыйОтчет.Израсходовано КАК АвансовыйОтчетИзрасходовано
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрБухгалтерии.ЖурналПроводокЕПСБУ.ДвиженияССубконто(
	|				,
	|				,
	|				СчетКт В (&Счет208)
	|					И НЕ СчетДт В (&СчетаМатериалы)
	|					И Регистратор В
	|						(ВЫБРАТЬ
	|							ДОКУМЕНТЫПРИХОДНЫЕ.ПриходныйДокумент
	|						ИЗ
	|							ДОКУМЕНТЫПРИХОДНЫЕ),
	|				,
	|				) КАК ЖурналПроводокЕПСБУДвиженияССубконто
	|		ПО АвансовыйОтчетИзрасходовано.ПриходныйДокумент = ЖурналПроводокЕПСБУДвиженияССубконто.Регистратор
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СоответствияАналитическихСчетовКЭК КАК СоответствияАналитическихСчетовКЭК
	|		ПО (ЖурналПроводокЕПСБУДвиженияССубконто.СчетКт = СоответствияАналитическихСчетовКЭК.Счет)
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПриходныеДокументыСЕдинственнымКЭК КАК ПриходныеДокументыСЕдинственнымКЭК
	|		ПО АвансовыйОтчетИзрасходовано.ПриходныйДокумент = ПриходныеДокументыСЕдинственнымКЭК.Регистратор
	|ГДЕ
	|	АвансовыйОтчетИзрасходовано.ПоДокументу
	|	И АвансовыйОтчетИзрасходовано.СпособОбеспечения <> ЗНАЧЕНИЕ(Перечисление.СпособыОбеспеченияКомандировки.ВыдачаДенежныхДокументов)
	|	И АвансовыйОтчетИзрасходовано.Ссылка = &АвансовыйОтчетСсылка
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	&Обязательство,
	|	&РазделЛицСчета,
	|	ЖурналПроводокЕПСБУДвиженияССубконто.КФО,
	#Вставка
	|	ЖурналПроводокЕПСБУДвиженияССубконто.ИФО,
	#КонецВставки
	|	ЖурналПроводокЕПСБУДвиженияССубконто.КПСКт,
	|	ЖурналПроводокЕПСБУДвиженияССубконто.КЭКДт,
	|	ВЫБОР
	|		КОГДА ЖурналПроводокЕПСБУДвиженияССубконто.ВалютаКт ЕСТЬ NULL
	|				ИЛИ ЖурналПроводокЕПСБУДвиженияССубконто.ВалютаКт = &ВалютаРегУчета
	|			ТОГДА """"
	|		ИНАЧЕ ЖурналПроводокЕПСБУДвиженияССубконто.ВалютаКт
	|	КОНЕЦ,
	|	ЖурналПроводокЕПСБУДвиженияССубконто.Сумма,
	|	ВЫБОР
	|		КОГДА ЖурналПроводокЕПСБУДвиженияССубконто.ВалютаКт ЕСТЬ NULL
	|				ИЛИ ЖурналПроводокЕПСБУДвиженияССубконто.ВалютаКт = &ВалютаРегУчета
	|			ТОГДА 0
	|		ИНАЧЕ ЖурналПроводокЕПСБУДвиженияССубконто.ВалютнаяСуммаКт
	|	КОНЕЦ
	|ИЗ
	|	Документ.АвансовыйОтчет.Израсходовано КАК АвансовыйОтчетИзрасходовано
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрБухгалтерии.ЖурналПроводокЕПСБУ.ДвиженияССубконто(
	|				,
	|				,
	|				СчетКт В (&Счет208)
	|					И СчетДт В (&СчетаМатериалы)
	|					И Регистратор В
	|						(ВЫБРАТЬ
	|							ДОКУМЕНТЫПРИХОДНЫЕ.ПриходныйДокумент
	|						ИЗ
	|							ДОКУМЕНТЫПРИХОДНЫЕ),
	|				,
	|				) КАК ЖурналПроводокЕПСБУДвиженияССубконто
	|		ПО АвансовыйОтчетИзрасходовано.ПриходныйДокумент = ЖурналПроводокЕПСБУДвиженияССубконто.Регистратор
	|ГДЕ
	|	АвансовыйОтчетИзрасходовано.ПоДокументу
	|	И АвансовыйОтчетИзрасходовано.СпособОбеспечения <> ЗНАЧЕНИЕ(Перечисление.СпособыОбеспеченияКомандировки.ВыдачаДенежныхДокументов)
	|	И АвансовыйОтчетИзрасходовано.Ссылка = &АвансовыйОтчетСсылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ИЗРАСХОДОВАНО.Обязательство КАК Обязательство,
	|	ИЗРАСХОДОВАНО.РазделЛицСчета КАК РазделЛицСчета,
	|	ИЗРАСХОДОВАНО.КФО КАК КФО,
	#Вставка
	|	ИЗРАСХОДОВАНО.ИФО КАК ИФО,
	#КонецВставки
	|	ИЗРАСХОДОВАНО.КПС КАК КПС,
	|	ИЗРАСХОДОВАНО.КЭК КАК КЭК,
	|	ИЗРАСХОДОВАНО.Валюта КАК Валюта,
	|	ИЗРАСХОДОВАНО.Сумма КАК Сумма,
	|	ИЗРАСХОДОВАНО.СуммаВВалюте КАК СуммаВВалюте
	|ИЗ
	|	ИЗРАСХОДОВАНО КАК ИЗРАСХОДОВАНО
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ИЗРАСХОДОВАНОПОДОКУМЕНТАМПРИХОДА.Обязательство,
	|	ИЗРАСХОДОВАНОПОДОКУМЕНТАМПРИХОДА.РазделЛицСчета,
	|	ИЗРАСХОДОВАНОПОДОКУМЕНТАМПРИХОДА.КФО,
	#Вставка
	|	ИЗРАСХОДОВАНОПОДОКУМЕНТАМПРИХОДА.ИФО,
	#КонецВставки
	|	ИЗРАСХОДОВАНОПОДОКУМЕНТАМПРИХОДА.КПС,
	|	ИЗРАСХОДОВАНОПОДОКУМЕНТАМПРИХОДА.КЭК,
	|	ИЗРАСХОДОВАНОПОДОКУМЕНТАМПРИХОДА.Валюта,
	|	ИЗРАСХОДОВАНОПОДОКУМЕНТАМПРИХОДА.Сумма,
	|	ИЗРАСХОДОВАНОПОДОКУМЕНТАМПРИХОДА.СуммаВВалюте
	|ИЗ
	|	ИЗРАСХОДОВАНОПОДОКУМЕНТАМПРИХОДА КАК ИЗРАСХОДОВАНОПОДОКУМЕНТАМПРИХОДА
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ОБЯЗАТЕЛЬСТВАПОЗАЯВЛЕНИЮ.Обязательство,
	|	ОБЯЗАТЕЛЬСТВАПОЗАЯВЛЕНИЮ.РазделЛицСчета,
	|	ОБЯЗАТЕЛЬСТВАПОЗАЯВЛЕНИЮ.КФО,    
	#Вставка
	|	ОБЯЗАТЕЛЬСТВАПОЗАЯВЛЕНИЮ.ИФО,
	#КонецВставки
	|	ОБЯЗАТЕЛЬСТВАПОЗАЯВЛЕНИЮ.КПС,
	|	ОБЯЗАТЕЛЬСТВАПОЗАЯВЛЕНИЮ.КЭК,
	|	ОБЯЗАТЕЛЬСТВАПОЗАЯВЛЕНИЮ.Валюта,
	|	-ОБЯЗАТЕЛЬСТВАПОЗАЯВЛЕНИЮ.Сумма,
	|	-ОБЯЗАТЕЛЬСТВАПОЗАЯВЛЕНИЮ.СуммаВВалюте
	|ИЗ
	|	ОБЯЗАТЕЛЬСТВАПОЗАЯВЛЕНИЮ КАК ОБЯЗАТЕЛЬСТВАПОЗАЯВЛЕНИЮ";

	ТаблицаРезультат = Запрос.Выполнить().Выгрузить();
	#Удаление
	ТаблицаРезультат.Свернуть("Обязательство, РазделЛицСчета, КФО, КПС, КЭК, Валюта", "Сумма, СуммаВВалюте");
	#КонецУдаления
	#Вставка
	ТаблицаРезультат.Свернуть("Обязательство, РазделЛицСчета, КФО, КПС, КЭК, Валюта, ИФО", "Сумма, СуммаВВалюте");
	#КонецВставки

	МассивУдаляемыхСтрок = Новый Массив;
	Для Каждого СтрРезультат Из ТаблицаРезультат Цикл
		Если СтрРезультат.Сумма = 0 И СтрРезультат.СуммаВВалюте = 0 Тогда
			МассивУдаляемыхСтрок.Добавить(СтрРезультат);
		КонецЕсли;
	КонецЦикла;
	Для Каждого СтрРезультат Из МассивУдаляемыхСтрок Цикл
		ТаблицаРезультат.Удалить(СтрРезультат);
	КонецЦикла;

	Возврат ТаблицаРезультат;

КонецФункции

