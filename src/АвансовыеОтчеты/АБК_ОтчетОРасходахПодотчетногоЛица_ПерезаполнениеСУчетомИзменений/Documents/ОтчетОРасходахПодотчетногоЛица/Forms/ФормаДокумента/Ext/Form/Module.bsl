&НаСервере
&Перед("ПодготовитьФормуНаСервере")
Процедура АБК_ПодготовитьФормуНаСервере()
	Если Элементы.Найти("ПерезаполнитьПоДокументуОснованию") = Неопределено Тогда
		НовЭл = Элементы.Вставить("ПерезаполнитьПоДокументуОснованию",Тип("КнопкаФормы"),Элементы.ГруппаАвансы,Элементы.НазначениеАванса);		
		НовЭл.ИмяКоманды = "ПерезаполнитьПоДокументуОснованию";	
		НовЭл.Вид = ВидКнопкиФормы.ОбычнаяКнопка;     
	КонецЕсли;
КонецПроцедуры   

&НаСервере
&ИзменениеИКонтроль("ЗаполнитьПоДокументуОснованиюНаСервере")
Процедура АБК_ЗаполнитьПоДокументуОснованиюНаСервере()

	ДокументОбъект = РеквизитФормыВЗначение("Объект");
	ДокументОбъект.Заполнить(Объект.ДокументОснование);
	ЗначениеВРеквизитФормы(ДокументОбъект, "Объект");  
	#Вставка
	ОбработатьДопСвойстваДокументаПослеЗаполнения(ДокументОбъект);    
	#КонецВставки

	ПодготовитьФормуНаСервере();

КонецПроцедуры

&НаСервере
Процедура АБК_ПерезаполнитьПоДокументуОснованиюПослеНаСервере()
	
	ДокументОбъект = РеквизитФормыВЗначение("Объект");       
	
	ДокументОбъект.ДополнительныеСвойства.Вставить("СУчетомИзменений",Истина);
	
	ДокументОбъект.Заполнить(Объект.ДокументОснование); 
	ЗначениеВРеквизитФормы(ДокументОбъект, "Объект");    
	
	ЭтаФорма.Модифицированность = Истина;
	
	ОбработатьДопСвойстваДокументаПослеЗаполнения(ДокументОбъект);    
	
	ПодготовитьФормуНаСервере();  
	
КонецПроцедуры

&НаКлиенте
Процедура АБК_ПерезаполнитьПоДокументуОснованиюПосле(Команда) 
	Если ЭтаФорма.Модифицированность Тогда
		ПоказатьПредупреждение(,"Документ должен быть записан");	
	Иначе
		АБК_ПерезаполнитьПоДокументуОснованиюПослеНаСервере(); 
	КонецЕсли;
КонецПроцедуры  

&НаСервере
Процедура ЗаполнитьНомераСтрокДокументовОснований(СтруктураТаблицСНомерамиСтрок)    
	
	Для Каждого Строка из СтруктураТаблицСНомерамиСтрок Цикл 
		
		Если ТипЗнч(Строка.Значение) = Тип("ТаблицаЗначений") Тогда      
			
			ДанныеДопКолонки = ПолучитьДанныеДопКолонки("НомерСтрокиДокументаОснования", Строка.Ключ);
			
			Если ЗначениеЗаполнено(ДанныеДопКолонки.ИмяРеквизита) Тогда     
				
				Для Каждого СтрокаТаблицыНомеровСтрок из Строка.Значение Цикл 
					
					НайдСтроки = Объект[Строка.Ключ].НайтиСтроки(Новый Структура("НомерСтроки",СтрокаТаблицыНомеровСтрок.НомерСтрокиОтчетаОРасходах)); 
					Если НайдСтроки.Количество() Тогда
						НайдСтроки[0][ДанныеДопКолонки.ИмяРеквизита] = СтрокаТаблицыНомеровСтрок.НомерСтрокиДокументаОснования;	
					КонецЕсли;
					
				КонецЦикла;  
				
			КонецЕсли;  
			
		КонецЕсли;  
		
	КонецЦикла;    
	
КонецПроцедуры    

&НаСервере
Функция ПолучитьДанныеДопКолонки(ИмяКолонки, ИмяТаблицы) 
	СтруктураДопКолонки = Новый Структура("ИмяРеквизита, ИмяЭлемента","","");
	
	НайденныеСтроки = ЭтаФорма.БухгалтерскиеОперации_ДобавленныеРеквизитыОперации.НайтиСтроки(Новый Структура("ИмяДляФормулы,ИмяТаблицы",ИмяКолонки,ИмяТаблицы));	
	Если НайденныеСтроки.Количество() Тогда
		СтруктураДопКолонки.ИмяРеквизита = НайденныеСтроки[0].ИмяРеквизита;	
		СтруктураДопКолонки.ИмяЭлемента = НайденныеСтроки[0].ИмяЭлемента;	
	КонецЕсли;         
	
	Возврат СтруктураДопКолонки;
КонецФункции                                   

&НаСервере
Процедура ОбработатьДопСвойстваДокументаПослеЗаполнения(ДокументОбъект) 
	Если ДокументОбъект.ДополнительныеСвойства.Свойство("СтруктураТаблицСНомерамиСтрок") Тогда 
		ЗаполнитьНомераСтрокДокументовОснований(ДокументОбъект.ДополнительныеСвойства.СтруктураТаблицСНомерамиСтрок);	
		ДокументОбъект.ДополнительныеСвойства.Удалить("СтруктураТаблицСНомерамиСтрок");	
	КонецЕсли;    
	
	Если ДокументОбъект.ДополнительныеСвойства.Свойство("СУчетомИзменений") Тогда 
		ДокументОбъект.ДополнительныеСвойства.Удалить("СУчетомИзменений");	
	КонецЕсли;    
КонецПроцедуры    


