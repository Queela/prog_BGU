
&ИзменениеИКонтроль("МоментВремениДокументаНачальногоОстатка")
Функция АБК_МоментВремениДокументаНачальногоОстатка(АвансовыйОтчет)

	МоментВремени = АвансовыйОтчет.МоментВремени();

	Авансы = АвансовыйОтчет.Получено.ВыгрузитьКолонку("ДокументАванса");   
	#Вставка
	Если ОбщегоНазначенияБГУ.ЕстьТабЧастьДокумента("ТоварыРаботыУслуги", АвансовыйОтчет.Метаданные()) Тогда
		ОбщегоНазначенияКлиентСервер.ДополнитьМассив(
		Авансы, АвансовыйОтчет.ТоварыРаботыУслуги.ВыгрузитьКолонку("ПриходныйДокумент"), Истина); 
	ИначеЕсли ОбщегоНазначенияБГУ.ЕстьТабЧастьДокумента("Израсходовано", АвансовыйОтчет.Метаданные()) Тогда
		#КонецВставки
		ОбщегоНазначенияКлиентСервер.ДополнитьМассив(
		Авансы, АвансовыйОтчет.Израсходовано.ВыгрузитьКолонку("ПриходныйДокумент"), Истина); 
		#Вставка
	КонецЕсли;
	#КонецВставки

	ОбщегоНазначенияКлиентСервер.УдалитьВсеВхожденияЗначенияИзМассива(Авансы, Неопределено);
	ОбщегоНазначенияКлиентСервер.УдалитьВсеВхожденияТипаИзМассива(Авансы, Тип("Строка"));

	Для Каждого Аванс ИЗ Авансы Цикл
		МоментВремениНовый = Аванс.МоментВремени();
		Если МоментВремени.Сравнить(МоментВремениНовый) > 0 Тогда
			МоментВремени = МоментВремениНовый;
		КонецЕсли;
	КонецЦикла;

	Возврат МоментВремени;

КонецФункции
