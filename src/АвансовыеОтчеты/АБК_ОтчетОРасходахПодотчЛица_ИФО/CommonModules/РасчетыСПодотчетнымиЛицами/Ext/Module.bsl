
&ИзменениеИКонтроль("ПустаяТаблицаОбязательствПоЗаявкеОбоснованиюЗакупки")
Функция АБК_ПустаяТаблицаОбязательствПоЗаявкеОбоснованиюЗакупки()
	
	ПустаяТаблица = Новый ТаблицаЗначений;
	
	ПустаяТаблица.Колонки.Добавить("Подразделение", Новый ОписаниеТипов("СправочникСсылка.Подразделения"));
	ПустаяТаблица.Колонки.Добавить("Договор", Новый ОписаниеТипов("СправочникСсылка.Договоры"));
	
	ПустаяТаблица.Колонки.Добавить("КФО", Новый ОписаниеТипов("ПеречислениеСсылка.КВД"));
	ПустаяТаблица.Колонки.Добавить("КПС", Новый ОписаниеТипов("СправочникСсылка.КлассификационныеПризнакиСчетов"));
	ПустаяТаблица.Колонки.Добавить("КЭК", Новый ОписаниеТипов("СправочникСсылка.КОСГУ"));
	
	ПустаяТаблица.Колонки.Добавить("КодЦели", Новый ОписаниеТипов("СправочникСсылка.ВидыЦелевыхСредств"));
	ПустаяТаблица.Колонки.Добавить("КодМероприятия", Новый ОписаниеТипов("СправочникСсылка.Мероприятия"));
	ПустаяТаблица.Колонки.Добавить("ДопКлассификация", Новый ОписаниеТипов("СправочникСсылка.ДополнительнаяБюджетнаяКлассификация"));
	
	ПустаяТаблица.Колонки.Добавить("ФинансовыйГод", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(4, 0, ДопустимыйЗнак.Неотрицательный)));
	ПустаяТаблица.Колонки.Добавить("Сумма", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(15, 2, ДопустимыйЗнак.Неотрицательный)));
	#Вставка     
	ПустаяТаблица.Колонки.Добавить("ИФО", Новый ОписаниеТипов("СправочникСсылка.ИсточникиФинансовогоОбеспечения"));
	#КонецВставки
	Возврат ПустаяТаблица;
	
КонецФункции

&ИзменениеИКонтроль("ТаблицаОбязательствПоЗаявкеОбоснованиюЗакупки")
Функция АБК_ТаблицаОбязательствПоЗаявкеОбоснованиюЗакупки(Документ)
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ЗаявкаОбоснованиеЗакупкиМалогоОбъемаТоварыРаботыУслуги.Подразделение КАК Подразделение, 
	|	ЗаявкаОбоснованиеЗакупкиМалогоОбъемаТоварыРаботыУслуги.Ссылка.Договор КАК Договор,
	|	ЗаявкаОбоснованиеЗакупкиМалогоОбъемаТоварыРаботыУслуги.КФО КАК КФО,
	|	ЗаявкаОбоснованиеЗакупкиМалогоОбъемаТоварыРаботыУслуги.КПС КАК КПС,
	|	ЗаявкаОбоснованиеЗакупкиМалогоОбъемаТоварыРаботыУслуги.КЭК КАК КЭК,
	|	ЗаявкаОбоснованиеЗакупкиМалогоОбъемаТоварыРаботыУслуги.КодЦели КАК КодЦели,
	|	ЗаявкаОбоснованиеЗакупкиМалогоОбъемаТоварыРаботыУслуги.КодМероприятия КАК КодМероприятия,
	|	ЗаявкаОбоснованиеЗакупкиМалогоОбъемаТоварыРаботыУслуги.ДопКлассификация КАК ДопКлассификация,
	|	ЗаявкаОбоснованиеЗакупкиМалогоОбъемаТоварыРаботыУслуги.ФинансовыйГод КАК ФинансовыйГод,
	|	СУММА(ЗаявкаОбоснованиеЗакупкиМалогоОбъемаТоварыРаботыУслуги.Сумма) КАК Сумма
	|ИЗ
	|	Документ.ЗаявкаОбоснованиеЗакупкиМалогоОбъема.ТоварыРаботыУслуги КАК ЗаявкаОбоснованиеЗакупкиМалогоОбъемаТоварыРаботыУслуги
	|ГДЕ
	|	ЗаявкаОбоснованиеЗакупкиМалогоОбъемаТоварыРаботыУслуги.Ссылка = &Ссылка
	|	И ЗаявкаОбоснованиеЗакупкиМалогоОбъемаТоварыРаботыУслуги.СпособОбеспечения <> ЗНАЧЕНИЕ(Перечисление.СпособыОбеспеченияКомандировки.ВыдачаДенежныхДокументов)
	|
	|СГРУППИРОВАТЬ ПО
	|	ЗаявкаОбоснованиеЗакупкиМалогоОбъемаТоварыРаботыУслуги.Подразделение, 
	|	ЗаявкаОбоснованиеЗакупкиМалогоОбъемаТоварыРаботыУслуги.Ссылка.Договор,
	|	ЗаявкаОбоснованиеЗакупкиМалогоОбъемаТоварыРаботыУслуги.КФО,
	|	ЗаявкаОбоснованиеЗакупкиМалогоОбъемаТоварыРаботыУслуги.КПС,
	|	ЗаявкаОбоснованиеЗакупкиМалогоОбъемаТоварыРаботыУслуги.КЭК,
	|	ЗаявкаОбоснованиеЗакупкиМалогоОбъемаТоварыРаботыУслуги.КодЦели,
	|	ЗаявкаОбоснованиеЗакупкиМалогоОбъемаТоварыРаботыУслуги.КодМероприятия,
	|	ЗаявкаОбоснованиеЗакупкиМалогоОбъемаТоварыРаботыУслуги.ДопКлассификация,
	|	ЗаявкаОбоснованиеЗакупкиМалогоОбъемаТоварыРаботыУслуги.ФинансовыйГод";
	
	Запрос.УстановитьПараметр("Ссылка", Документ); 
	
	#Вставка    
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ЗначенияКолонокДополнительныхТабличныхЧастей.НомерСтрокиДокумента КАК НомерСтрокиДокумента,
	|	ВЫРАЗИТЬ(ЗначенияКолонокДополнительныхТабличныхЧастей.Значение КАК Справочник.ИсточникиФинансовогоОбеспечения) КАК ИФО
	|ПОМЕСТИТЬ ВТ_ИФО
	|ИЗ
	|	РегистрСведений.ЗначенияКолонокДополнительныхТабличныхЧастей КАК ЗначенияКолонокДополнительныхТабличныхЧастей
	|ГДЕ
	|	ЗначенияКолонокДополнительныхТабличныхЧастей.Объект = &Ссылка
	|	И ЗначенияКолонокДополнительныхТабличныхЧастей.ТабличнаяЧасть.ИмяТабличнойЧасти = &ИмяТабличнойЧасти
	|	И ЗначенияКолонокДополнительныхТабличныхЧастей.КолонкаТабличнойЧасти.ИмяКолонки = &ИмяКолонки"
	+ ОбщегоНазначения.РазделительПакетаЗапросов() + Запрос.Текст;     
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст,
	"Документ.ЗаявкаОбоснованиеЗакупкиМалогоОбъема.ТоварыРаботыУслуги КАК ЗаявкаОбоснованиеЗакупкиМалогоОбъемаТоварыРаботыУслуги",
	"Документ.ЗаявкаОбоснованиеЗакупкиМалогоОбъема.ТоварыРаботыУслуги КАК ЗаявкаОбоснованиеЗакупкиМалогоОбъемаТоварыРаботыУслуги
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ИФО КАК ВТ_ИФО
	|		ПО (ЗаявкаОбоснованиеЗакупкиМалогоОбъемаТоварыРаботыУслуги.НомерСтроки = ВТ_ИФО.НомерСтрокиДокумента)");
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст,
	"ВЫБРАТЬ
	|	ЗаявкаОбоснованиеЗакупкиМалогоОбъемаТоварыРаботыУслуги.Подразделение КАК Подразделение",
	"ВЫБРАТЬ
	|	ЗаявкаОбоснованиеЗакупкиМалогоОбъемаТоварыРаботыУслуги.Подразделение КАК Подразделение,
	|	ВТ_ИФО.ИФО КАК ИФО");  
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст,
	"СГРУППИРОВАТЬ ПО
	|	ЗаявкаОбоснованиеЗакупкиМалогоОбъемаТоварыРаботыУслуги.Подразделение",
	"СГРУППИРОВАТЬ ПО
	|	ЗаявкаОбоснованиеЗакупкиМалогоОбъемаТоварыРаботыУслуги.Подразделение,
	|	ВТ_ИФО.ИФО");
	
	Запрос.УстановитьПараметр("ИмяТабличнойЧасти",		"ТоварыРаботыУслуги");
	Запрос.УстановитьПараметр("ИмяКолонки",				"ИФО");    
	#КонецВставки	     
	
	Если ТипЗнч(Документ) = Тип("ДокументСсылка.ЗаявкаОбоснованиеЗакупкиМалогоОбъемаЧерезПодотчетноеЛицо") Тогда 
		Запрос.Текст = СтрЗаменить(Запрос.Текст,
		"Документ.ЗаявкаОбоснованиеЗакупкиМалогоОбъема",
		"Документ.ЗаявкаОбоснованиеЗакупкиМалогоОбъемаЧерезПодотчетноеЛицо");
		Запрос.Текст = СтрЗаменить(Запрос.Текст,
		"И ЗаявкаОбоснованиеЗакупкиМалогоОбъемаТоварыРаботыУслуги.СпособОбеспечения <> ЗНАЧЕНИЕ(Перечисление.СпособыОбеспеченияКомандировки.ВыдачаДенежныхДокументов)",
		"И ЗаявкаОбоснованиеЗакупкиМалогоОбъемаТоварыРаботыУслуги.Ссылка.СпособВыдачиДенежныхСредств <> ЗНАЧЕНИЕ(Перечисление.СпособыВыдачиДенежныхСредствПодотчетномуЛицу.ДенежныеДокументы)");
		Запрос.Текст = СтрЗаменить(Запрос.Текст,
		"ЗаявкаОбоснованиеЗакупкиМалогоОбъемаТоварыРаботыУслуги.Ссылка.Договор",
		"ЗаявкаОбоснованиеЗакупкиМалогоОбъемаТоварыРаботыУслуги.Договор"); 
	КонецЕсли;
	
	Возврат Запрос.Выполнить().Выгрузить(); 
	
КонецФункции

&ИзменениеИКонтроль("ПодготовитьТаблицуРасходовПоОтчетуОРасходах")
Функция АБК_ПодготовитьТаблицуРасходовПоОтчетуОРасходах(Документ, Параметры, Отказ)

	Если НЕ Параметры.Свойство("ПоВсемКФО") Тогда
		Параметры.Вставить("ПоВсемКФО", Ложь);
	КонецЕсли;
	Если НЕ Параметры.Свойство("ПоВсемКПС") Тогда
		Параметры.Вставить("ПоВсемКПС", Ложь);
	КонецЕсли;

	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ВалютаРегламентированногоУчета", БухгалтерскийУчетПовтИсп.ВалютаРегламентированногоУчета());
	Запрос.УстановитьПараметр("Подразделение", Параметры.Подразделение);

	ОбщегоНазначенияБГУ.ПоместитьТабЧастьВВиртуальнуюТаблицу(Запрос, "Суточные",
	"НомерСтроки,ИФО,КФО,КПС,КЭК,Подразделение,Сотрудник,НаименованиеРасхода,Валюта,СуммаПринято,СпособОбеспечения,
	|СчетДебета,ПодразделениеДт,КПСДт,СубконтоДебета1,СубконтоДебета2,СубконтоДебета3,
	|СчетНУ,СубконтоНУ1,СубконтоНУ2,СубконтоНУ3,ОтражениеВУСН",
	Документ, Параметры);

	ОбщегоНазначенияБГУ.ПоместитьТабЧастьВВиртуальнуюТаблицу(Запрос, "Проезд",
	"НомерСтроки,ИФО,КФО,КПС,КЭК,Подразделение,Сотрудник,НаименованиеРасхода,Валюта,СуммаПринято,Количество,СпособОбеспечения,
	|СчетДебета,ПодразделениеДт,КПСДт,СубконтоДебета1,СубконтоДебета2,СубконтоДебета3,
	|СчетНУ,СубконтоНУ1,СубконтоНУ2,СубконтоНУ3,ОтражениеВУСН,
	|СуммаВключаетНДСПроезд,ПредъявленСФ,СуммаНДС",
	Документ, Параметры);

	ОбщегоНазначенияБГУ.ПоместитьТабЧастьВВиртуальнуюТаблицу(Запрос, "Проживание",
	"НомерСтроки,ИФО,КФО,КПС,КЭК,Подразделение,Сотрудник,НаименованиеРасхода,Валюта,СуммаПринято,СпособОбеспечения,
	|СчетДебета,ПодразделениеДт,КПСДт,СубконтоДебета1,СубконтоДебета2,СубконтоДебета3,
	|СчетНУ,СубконтоНУ1,СубконтоНУ2,СубконтоНУ3,ОтражениеВУСН,
	|СуммаВключаетНДСПроживание,ПредъявленСФ,СуммаНДС",
	Документ, Параметры);

	ОбщегоНазначенияБГУ.ПоместитьТабЧастьВВиртуальнуюТаблицу(Запрос, "ПрочиеРасходы",
	"НомерСтроки,ИФО,КФО,КПС,КЭК,Подразделение,Сотрудник,НаименованиеРасхода,Валюта,СуммаПринято,Количество,СпособОбеспечения,
	|СчетДебета,ПодразделениеДт,КПСДт,СубконтоДебета1,СубконтоДебета2,СубконтоДебета3,
	|СчетНУ,СубконтоНУ1,СубконтоНУ2,СубконтоНУ3,ОтражениеВУСН,
	|СуммаВключаетНДСПрочиеРасходы,ПредъявленСФ,СуммаНДС",
	Документ, Параметры);

	ОбщегоНазначенияБГУ.ПоместитьТабЧастьВВиртуальнуюТаблицу(Запрос, "ТоварыРаботыУслуги",
	"НомерСтроки,ИФО,КФО,КПС,КЭК,Подразделение,Сотрудник,Номенклатура,СуммаПринято,СуммаВключаетНДС,Количество,ПоДокументу,СпособОбеспечения,
	|СчетДебета,ПодразделениеДт,КПСДт,КЭКДт,СубконтоДебета1,СубконтоДебета2,СубконтоДебета3,
	|СчетНУ,СубконтоНУ1,СубконтоНУ2,СубконтоНУ3,ОтражениеВУСН,
	|ПредъявленСФ,СуммаНДС",
	Документ, Параметры);
    #Вставка     
	Запрос.Текст = Запрос.Текст + ТекстЗапросаДопКолонок() + ОбщегоНазначенияБГУ.ТекстРазделителяЗапросовПакета();
	Запрос.УстановитьПараметр("Ссылка", Документ);
	Запрос.УстановитьПараметр("ИдентификаторДокумента",ОбщегоНазначения.ИдентификаторОбъектаМетаданных(ТипЗнч(Документ)));
	#КонецВставки
	Запрос.Текст = Запрос.Текст +
	"ВЫБРАТЬ
	|	1 КАК Порядок,
	|	""Суточные"" КАК ИмяСписка,
	|	""Суточные"" КАК СинонимСписка,
	|	Суточные.НомерСтроки КАК НомерСтроки,
	#Удаление
	|	Суточные.ИФО КАК ИФО,
	#КонецУдаления
	#Вставка     
	|	ВТ_Суточные.ИФО КАК ИФО, 
	#КонецВставки
	|	Суточные.КФО КАК КФО,
	|	Суточные.КПС КАК КПС,
	|	Суточные.КЭК КАК КЭК,
	|	ВЫБОР
	|		КОГДА Суточные.Подразделение <> ЗНАЧЕНИЕ(Справочник.Подразделения.ПустаяСсылка)
	|			ТОГДА Суточные.Подразделение
	|		ИНАЧЕ &Подразделение
	|	КОНЕЦ КАК Подразделение,
	|	Суточные.СчетДебета КАК СчетДебета,
	|	ВЫБОР
	|		КОГДА Суточные.КПСДт = ЗНАЧЕНИЕ(Справочник.КлассификационныеПризнакиСчетов.ПустаяСсылка)
	|			ТОГДА Суточные.КПС
	|		ИНАЧЕ Суточные.КПСДт
	|	КОНЕЦ КАК КПСДт,
	|	ЗНАЧЕНИЕ(Справочник.КОСГУ.ПустаяСсылка) КАК КЭКДт,
	|	ВЫБОР
	|		КОГДА Суточные.ПодразделениеДт <> ЗНАЧЕНИЕ(Справочник.Подразделения.ПустаяСсылка)
	|			ТОГДА Суточные.ПодразделениеДт
	|		КОГДА Суточные.Подразделение <> ЗНАЧЕНИЕ(Справочник.Подразделения.ПустаяСсылка)
	|			ТОГДА Суточные.Подразделение
	|		ИНАЧЕ &Подразделение
	|	КОНЕЦ КАК ПодразделениеДт,
	|	Суточные.СубконтоДебета1 КАК СубконтоДебета1,
	|	Суточные.СубконтоДебета2 КАК СубконтоДебета2,
	|	Суточные.СубконтоДебета3 КАК СубконтоДебета3,
	|	Суточные.Сотрудник КАК Сотрудник,
	|	Суточные.Сотрудник.Контрагент КАК Контрагент,
	|	ЗНАЧЕНИЕ(ПланСчетов.ЕПСБУ.ПустаяСсылка) КАК СчетРасчетов,
	|	Суточные.НаименованиеРасхода КАК НаименованиеРасхода,
	|	ВЫБОР
	|		КОГДА Суточные.Валюта ЕСТЬ NULL
	|				ИЛИ Суточные.Валюта = ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка)
	|				ИЛИ Суточные.Валюта = &ВалютаРегламентированногоУчета
	|			ТОГДА &ВалютаРегламентированногоУчета
	|		ИНАЧЕ Суточные.Валюта
	|	КОНЕЦ КАК Валюта,
	|	0 КАК Количество,
	|	Суточные.СуммаПринято КАК СуммаВВалюте,
	|	Суточные.СуммаПринято КАК СуммаРасчетов,
	|	Суточные.СчетНУ КАК СчетНУ,
	|	Суточные.СубконтоНУ1 КАК СубконтоНУ1,
	|	Суточные.СубконтоНУ2 КАК СубконтоНУ2,
	|	Суточные.СубконтоНУ3 КАК СубконтоНУ3,
	|	Суточные.ОтражениеВУСН КАК ОтражениеВУСН,
	|	ЛОЖЬ КАК ПредъявленСФ,
	|	0 КАК СуммаНДС,
	|	Суточные.СпособОбеспечения КАК СпособОбеспечения
	|ПОМЕСТИТЬ Расходы
	|ИЗ
	|	ТаблицаСуточные КАК Суточные 
	#Вставка
	|	ЛЕВОЕ СОЕДИНЕНИЕ ВТ_Суточные КАК ВТ_Суточные
	|	ПО (Суточные.НомерСтроки = ВТ_Суточные.НомерСтрокиДокумента)
	#КонецВставки
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	2,
	|	""Проезд"",
	|	""Проезд"",
	|	Проезд.НомерСтроки, 
	#Удаление
	|	Проезд.ИФО,  
	#КонецУдаления
	#Вставка     
	|	ВТ_Проезд.ИФО,
	#КонецВставки
	|	Проезд.КФО,
	|	Проезд.КПС,
	|	Проезд.КЭК,
	|	ВЫБОР
	|		КОГДА Проезд.Подразделение <> ЗНАЧЕНИЕ(Справочник.Подразделения.ПустаяСсылка)
	|			ТОГДА Проезд.Подразделение
	|		ИНАЧЕ &Подразделение
	|	КОНЕЦ,
	|	Проезд.СчетДебета,
	|	ВЫБОР
	|		КОГДА Проезд.КПСДт = ЗНАЧЕНИЕ(Справочник.КлассификационныеПризнакиСчетов.ПустаяСсылка)
	|			ТОГДА Проезд.КПС
	|		ИНАЧЕ Проезд.КПСДт
	|	КОНЕЦ,
	|	ЗНАЧЕНИЕ(Справочник.КОСГУ.ПустаяСсылка),
	|	ВЫБОР
	|		КОГДА Проезд.ПодразделениеДт <> ЗНАЧЕНИЕ(Справочник.Подразделения.ПустаяСсылка)
	|			ТОГДА Проезд.ПодразделениеДт
	|		КОГДА Проезд.Подразделение <> ЗНАЧЕНИЕ(Справочник.Подразделения.ПустаяСсылка)
	|			ТОГДА Проезд.Подразделение
	|		ИНАЧЕ &Подразделение
	|	КОНЕЦ КАК ПодразделениеДт,
	|	Проезд.СубконтоДебета1,
	|	Проезд.СубконтоДебета2,
	|	Проезд.СубконтоДебета3,
	|	Проезд.Сотрудник,
	|	Проезд.Сотрудник.Контрагент,
	|	ЗНАЧЕНИЕ(ПланСчетов.ЕПСБУ.ПустаяСсылка),
	|	Проезд.НаименованиеРасхода,
	|	ВЫБОР
	|		КОГДА Проезд.Валюта ЕСТЬ NULL
	|				ИЛИ Проезд.Валюта = ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка)
	|				ИЛИ Проезд.Валюта = &ВалютаРегламентированногоУчета
	|			ТОГДА &ВалютаРегламентированногоУчета
	|		ИНАЧЕ Проезд.Валюта
	|	КОНЕЦ,
	|	Проезд.Количество,
	|	Проезд.СуммаПринято,
	|	ВЫБОР
	|		КОГДА Проезд.СуммаВключаетНДСПроезд
	|			ТОГДА Проезд.СуммаПринято
	|		ИНАЧЕ Проезд.СуммаПринято + Проезд.СуммаНДС
	|	КОНЕЦ,
	|	Проезд.СчетНУ,
	|	Проезд.СубконтоНУ1,
	|	Проезд.СубконтоНУ2,
	|	Проезд.СубконтоНУ3,
	|	Проезд.ОтражениеВУСН,
	|	Проезд.ПредъявленСФ,
	|	Проезд.СуммаНДС,
	|	Проезд.СпособОбеспечения
	|ИЗ
	|	ТаблицаПроезд КАК Проезд
	#Вставка     
	|	ЛЕВОЕ СОЕДИНЕНИЕ ВТ_Проезд КАК ВТ_Проезд
	|	ПО (Проезд.НомерСтроки = ВТ_Проезд.НомерСтрокиДокумента)
	#КонецВставки
	|ГДЕ
	|	(Проезд.СпособОбеспечения = ЗНАЧЕНИЕ(Перечисление.СпособыОбеспеченияКомандировки.АвансПодотчетномуЛицу)
	|			ИЛИ Проезд.СпособОбеспечения = ЗНАЧЕНИЕ(Перечисление.СпособыОбеспеченияКомандировки.ВыдачаДенежныхДокументов)
	|			ИЛИ Проезд.СпособОбеспечения = ЗНАЧЕНИЕ(Перечисление.СпособыОбеспеченияКомандировки.ВозмещениеПоФакту))
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	3,
	|	""Проживание"",
	|	""Проживание"",
	|	Проживание.НомерСтроки,  
	#Удаление
	|	Проживание.ИФО,
	#КонецУдаления
	#Вставка     
	|	ВТ_Проживание.ИФО,
	#КонецВставки
	|	Проживание.КФО,
	|	Проживание.КПС,
	|	Проживание.КЭК,
	|	ВЫБОР
	|		КОГДА Проживание.Подразделение <> ЗНАЧЕНИЕ(Справочник.Подразделения.ПустаяСсылка)
	|			ТОГДА Проживание.Подразделение
	|		ИНАЧЕ &Подразделение
	|	КОНЕЦ,
	|	Проживание.СчетДебета,
	|	ВЫБОР
	|		КОГДА Проживание.КПСДт = ЗНАЧЕНИЕ(Справочник.КлассификационныеПризнакиСчетов.ПустаяСсылка)
	|			ТОГДА Проживание.КПС
	|		ИНАЧЕ Проживание.КПСДт
	|	КОНЕЦ,
	|	ЗНАЧЕНИЕ(Справочник.КОСГУ.ПустаяСсылка),
	|	ВЫБОР
	|		КОГДА Проживание.ПодразделениеДт <> ЗНАЧЕНИЕ(Справочник.Подразделения.ПустаяСсылка)
	|			ТОГДА Проживание.ПодразделениеДт
	|		КОГДА Проживание.Подразделение <> ЗНАЧЕНИЕ(Справочник.Подразделения.ПустаяСсылка)
	|			ТОГДА Проживание.Подразделение
	|		ИНАЧЕ &Подразделение
	|	КОНЕЦ КАК ПодразделениеДт,
	|	Проживание.СубконтоДебета1,
	|	Проживание.СубконтоДебета2,
	|	Проживание.СубконтоДебета3,
	|	Проживание.Сотрудник,
	|	Проживание.Сотрудник.Контрагент,
	|	ЗНАЧЕНИЕ(ПланСчетов.ЕПСБУ.ПустаяСсылка),
	|	Проживание.НаименованиеРасхода,
	|	ВЫБОР
	|		КОГДА Проживание.Валюта ЕСТЬ NULL
	|				ИЛИ Проживание.Валюта = ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка)
	|				ИЛИ Проживание.Валюта = &ВалютаРегламентированногоУчета
	|			ТОГДА &ВалютаРегламентированногоУчета
	|		ИНАЧЕ Проживание.Валюта
	|	КОНЕЦ,
	|	0,
	|	Проживание.СуммаПринято,
	|	ВЫБОР
	|		КОГДА Проживание.СуммаВключаетНДСПроживание
	|			ТОГДА Проживание.СуммаПринято
	|		ИНАЧЕ Проживание.СуммаПринято + Проживание.СуммаНДС
	|	КОНЕЦ,
	|	Проживание.СчетНУ,
	|	Проживание.СубконтоНУ1,
	|	Проживание.СубконтоНУ2,
	|	Проживание.СубконтоНУ3,
	|	Проживание.ОтражениеВУСН,
	|	Проживание.ПредъявленСФ,
	|	Проживание.СуммаНДС,
	|	Проживание.СпособОбеспечения
	|ИЗ
	|	ТаблицаПроживание КАК Проживание
	#Вставка     
	|	ЛЕВОЕ СОЕДИНЕНИЕ ВТ_Проживание КАК ВТ_Проживание
	|	ПО (Проживание.НомерСтроки = ВТ_Проживание.НомерСтрокиДокумента)
	#КонецВставки
	|ГДЕ
	|	(Проживание.СпособОбеспечения = ЗНАЧЕНИЕ(Перечисление.СпособыОбеспеченияКомандировки.АвансПодотчетномуЛицу)
	|			ИЛИ Проживание.СпособОбеспечения = ЗНАЧЕНИЕ(Перечисление.СпособыОбеспеченияКомандировки.ВыдачаДенежныхДокументов)
	|			ИЛИ Проживание.СпособОбеспечения = ЗНАЧЕНИЕ(Перечисление.СпособыОбеспеченияКомандировки.ВозмещениеПоФакту))
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	4,
	|	""ПрочиеРасходы"",
	|	""Прочие расходы"",
	|	ПрочиеРасходы.НомерСтроки, 
	#Удаление
	|	ПрочиеРасходы.ИФО,
	#КонецУдаления
	#Вставка     
	|	ВТ_ПрочиеРасходы.ИФО,
	#КонецВставки
	|	ПрочиеРасходы.КФО,
	|	ПрочиеРасходы.КПС,
	|	ПрочиеРасходы.КЭК,
	|	ВЫБОР
	|		КОГДА ПрочиеРасходы.Подразделение <> ЗНАЧЕНИЕ(Справочник.Подразделения.ПустаяСсылка)
	|			ТОГДА ПрочиеРасходы.Подразделение
	|		ИНАЧЕ &Подразделение
	|	КОНЕЦ,
	|	ПрочиеРасходы.СчетДебета,
	|	ВЫБОР
	|		КОГДА ПрочиеРасходы.КПСДт = ЗНАЧЕНИЕ(Справочник.КлассификационныеПризнакиСчетов.ПустаяСсылка)
	|			ТОГДА ПрочиеРасходы.КПС
	|		ИНАЧЕ ПрочиеРасходы.КПСДт
	|	КОНЕЦ,
	|	ЗНАЧЕНИЕ(Справочник.КОСГУ.ПустаяСсылка),
	|	ВЫБОР
	|		КОГДА ПрочиеРасходы.ПодразделениеДт <> ЗНАЧЕНИЕ(Справочник.Подразделения.ПустаяСсылка)
	|			ТОГДА ПрочиеРасходы.ПодразделениеДт
	|		КОГДА ПрочиеРасходы.Подразделение <> ЗНАЧЕНИЕ(Справочник.Подразделения.ПустаяСсылка)
	|			ТОГДА ПрочиеРасходы.Подразделение
	|		ИНАЧЕ &Подразделение
	|	КОНЕЦ КАК ПодразделениеДт,
	|	ПрочиеРасходы.СубконтоДебета1,
	|	ПрочиеРасходы.СубконтоДебета2,
	|	ПрочиеРасходы.СубконтоДебета3,
	|	ПрочиеРасходы.Сотрудник,
	|	ПрочиеРасходы.Сотрудник.Контрагент,
	|	ЗНАЧЕНИЕ(ПланСчетов.ЕПСБУ.ПустаяСсылка),
	|	ПрочиеРасходы.НаименованиеРасхода,
	|	ВЫБОР
	|		КОГДА ПрочиеРасходы.Валюта ЕСТЬ NULL
	|				ИЛИ ПрочиеРасходы.Валюта = ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка)
	|				ИЛИ ПрочиеРасходы.Валюта = &ВалютаРегламентированногоУчета
	|			ТОГДА &ВалютаРегламентированногоУчета
	|		ИНАЧЕ ПрочиеРасходы.Валюта
	|	КОНЕЦ,
	|	ПрочиеРасходы.Количество,
	|	ПрочиеРасходы.СуммаПринято,
	|	ВЫБОР
	|		КОГДА ПрочиеРасходы.СуммаВключаетНДСПрочиеРасходы
	|			ТОГДА ПрочиеРасходы.СуммаПринято
	|		ИНАЧЕ ПрочиеРасходы.СуммаПринято + ПрочиеРасходы.СуммаНДС
	|	КОНЕЦ,
	|	ПрочиеРасходы.СчетНУ,
	|	ПрочиеРасходы.СубконтоНУ1,
	|	ПрочиеРасходы.СубконтоНУ2,
	|	ПрочиеРасходы.СубконтоНУ3,
	|	ПрочиеРасходы.ОтражениеВУСН,
	|	ПрочиеРасходы.ПредъявленСФ,
	|	ПрочиеРасходы.СуммаНДС,
	|	ПрочиеРасходы.СпособОбеспечения
	|ИЗ
	|	ТаблицаПрочиеРасходы КАК ПрочиеРасходы
	#Вставка     
	|	ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ПрочиеРасходы КАК ВТ_ПрочиеРасходы
	|	ПО (ПрочиеРасходы.НомерСтроки = ВТ_ПрочиеРасходы.НомерСтрокиДокумента)
	#КонецВставки
	|ГДЕ
	|	(ПрочиеРасходы.СпособОбеспечения = ЗНАЧЕНИЕ(Перечисление.СпособыОбеспеченияКомандировки.АвансПодотчетномуЛицу)
	|			ИЛИ ПрочиеРасходы.СпособОбеспечения = ЗНАЧЕНИЕ(Перечисление.СпособыОбеспеченияКомандировки.ВыдачаДенежныхДокументов)
	|			ИЛИ ПрочиеРасходы.СпособОбеспечения = ЗНАЧЕНИЕ(Перечисление.СпособыОбеспеченияКомандировки.ВозмещениеПоФакту))
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	5,
	|	""ТоварыРаботыУслуги"",
	|	""Товары, работы, услуги"",
	|	ТоварыРаботыУслуги.НомерСтроки, 
	#Удаление
	|	ТоварыРаботыУслуги.ИФО,     
	#КонецУдаления
	#Вставка     
	|	ВТ_ТоварыРаботыУслуги.ИФО,
	#КонецВставки
	|	ТоварыРаботыУслуги.КФО,
	|	ТоварыРаботыУслуги.КПС,
	|	ТоварыРаботыУслуги.КЭК,
	|	ВЫБОР
	|		КОГДА ТоварыРаботыУслуги.Подразделение <> ЗНАЧЕНИЕ(Справочник.Подразделения.ПустаяСсылка)
	|			ТОГДА ТоварыРаботыУслуги.Подразделение
	|		ИНАЧЕ &Подразделение
	|	КОНЕЦ,
	|	ТоварыРаботыУслуги.СчетДебета,
	|	ВЫБОР
	|		КОГДА ТоварыРаботыУслуги.КПСДт = ЗНАЧЕНИЕ(Справочник.КлассификационныеПризнакиСчетов.ПустаяСсылка)
	|			ТОГДА ТоварыРаботыУслуги.КПС
	|		ИНАЧЕ ТоварыРаботыУслуги.КПСДт
	|	КОНЕЦ,
	|	ТоварыРаботыУслуги.КЭКДт,
	|	ВЫБОР
	|		КОГДА ТоварыРаботыУслуги.ПодразделениеДт <> ЗНАЧЕНИЕ(Справочник.Подразделения.ПустаяСсылка)
	|			ТОГДА ТоварыРаботыУслуги.ПодразделениеДт
	|		КОГДА ТоварыРаботыУслуги.Подразделение <> ЗНАЧЕНИЕ(Справочник.Подразделения.ПустаяСсылка)
	|			ТОГДА ТоварыРаботыУслуги.Подразделение
	|		ИНАЧЕ &Подразделение
	|	КОНЕЦ КАК ПодразделениеДт,
	|	ТоварыРаботыУслуги.СубконтоДебета1,
	|	ТоварыРаботыУслуги.СубконтоДебета2,
	|	ТоварыРаботыУслуги.СубконтоДебета3,
	|	ТоварыРаботыУслуги.Сотрудник,
	|	ТоварыРаботыУслуги.Сотрудник.Контрагент,
	|	ЗНАЧЕНИЕ(ПланСчетов.ЕПСБУ.ПустаяСсылка),
	|	ТоварыРаботыУслуги.Номенклатура.Наименование,
	|	&ВалютаРегламентированногоУчета,
	|	ТоварыРаботыУслуги.Количество,
	|	ТоварыРаботыУслуги.СуммаПринято,
	|	ВЫБОР
	|		КОГДА ТоварыРаботыУслуги.СуммаВключаетНДС
	|			ТОГДА ТоварыРаботыУслуги.СуммаПринято
	|		ИНАЧЕ ТоварыРаботыУслуги.СуммаПринято + ТоварыРаботыУслуги.СуммаНДС
	|	КОНЕЦ,
	|	ТоварыРаботыУслуги.СчетНУ,
	|	ТоварыРаботыУслуги.СубконтоНУ1,
	|	ТоварыРаботыУслуги.СубконтоНУ2,
	|	ТоварыРаботыУслуги.СубконтоНУ3,
	|	ТоварыРаботыУслуги.ОтражениеВУСН,
	|	ТоварыРаботыУслуги.ПредъявленСФ,
	|	ТоварыРаботыУслуги.СуммаНДС,
	|	ТоварыРаботыУслуги.СпособОбеспечения
	|ИЗ
	|	ТаблицаТоварыРаботыУслуги КАК ТоварыРаботыУслуги
	#Вставка     
	|	ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ТоварыРаботыУслуги КАК ВТ_ТоварыРаботыУслуги
	|	ПО (ТоварыРаботыУслуги.НомерСтроки = ВТ_ТоварыРаботыУслуги.НомерСтрокиДокумента)
	#КонецВставки
	|ГДЕ
	|	НЕ ТоварыРаботыУслуги.ПоДокументу
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Расходы.Порядок КАК Порядок,
	|	Расходы.ИмяСписка КАК ИмяСписка,
	|	Расходы.СинонимСписка КАК СинонимСписка,
	|	Расходы.НомерСтроки КАК НомерСтроки,
	|	Расходы.ИФО КАК ИФО,
	|	Расходы.КФО КАК КФО,
	|	Расходы.КПС КАК КПС,
	|	Расходы.КЭК КАК КЭК,
	|	Расходы.Подразделение КАК Подразделение,
	|	Расходы.СчетДебета КАК СчетДебета,
	|	Расходы.КПСДт КАК КПСДт,
	|	Расходы.КЭКДт КАК КЭКДт,
	|	Расходы.ПодразделениеДт КАК ПодразделениеДт,
	|	Расходы.СубконтоДебета1 КАК СубконтоДебета1,
	|	Расходы.СубконтоДебета2 КАК СубконтоДебета2,
	|	Расходы.СубконтоДебета3 КАК СубконтоДебета3,
	|	Расходы.Сотрудник КАК Сотрудник,
	|	Расходы.Контрагент КАК Контрагент,
	|	Расходы.СчетРасчетов КАК СчетРасчетов,
	|	Расходы.НаименованиеРасхода КАК НаименованиеРасхода,
	|	Расходы.Валюта КАК Валюта,
	|	Расходы.Количество КАК Количество,
	|	Расходы.СуммаВВалюте КАК СуммаВВалюте,
	|	Расходы.СуммаРасчетов КАК СуммаРасчетов,
	|	Расходы.СчетНУ КАК СчетНУ,
	|	Расходы.СубконтоНУ1 КАК СубконтоНУ1,
	|	Расходы.СубконтоНУ2 КАК СубконтоНУ2,
	|	Расходы.СубконтоНУ3 КАК СубконтоНУ3,
	|	Расходы.ОтражениеВУСН КАК ОтражениеВУСН,
	|	Расходы.ПредъявленСФ КАК ПредъявленСФ,
	|	Расходы.СуммаНДС КАК СуммаНДС,
	|	Расходы.СпособОбеспечения КАК СпособОбеспечения
	|ИЗ
	|	Расходы КАК Расходы
	|
	|УПОРЯДОЧИТЬ ПО
	|	Порядок,
	|	НомерСтроки";

	ТаблицаРасходы = Запрос.Выполнить().Выгрузить();

	Счета208 = БухгалтерскиеОперации.СчетПоКоду("208.00", Параметры.Дата);
	Для Каждого СтрокаРасходы Из ТаблицаРасходы Цикл
		СтрокаРасходы.СчетРасчетов = БухгалтерскийУчет.ПолучитьАналитическийСчетПоКЭК(
		СтрокаРасходы.КЭК, Счета208, , Параметры.Дата, Параметры.Организация, СтрокаРасходы.ИФО);
	КонецЦикла;

	Возврат ТаблицаРасходы;

КонецФункции

Функция ТекстЗапросаДопКолонок()
	
	ТекстЗапроса =      
	"ВЫБРАТЬ
	|	ЗначенияКолонокДополнительныхТабличныхЧастей.НомерСтрокиДокумента КАК НомерСтрокиДокумента,
	|	ВЫРАЗИТЬ(ЗначенияКолонокДополнительныхТабличныхЧастей.Значение КАК Справочник.ИсточникиФинансовогоОбеспечения) КАК ИФО
	|ПОМЕСТИТЬ ВТ_Суточные
	|ИЗ
	|	РегистрСведений.ЗначенияКолонокДополнительныхТабличныхЧастей КАК ЗначенияКолонокДополнительныхТабличныхЧастей
	|ГДЕ
	|	ЗначенияКолонокДополнительныхТабличныхЧастей.Объект = &Ссылка
	|	И ЗначенияКолонокДополнительныхТабличныхЧастей.ТабличнаяЧасть.ИмяТабличнойЧасти = ""Суточные""
	|	И ЗначенияКолонокДополнительныхТабличныхЧастей.КолонкаТабличнойЧасти.ИмяКолонки = ""ИФО""
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЗначенияКолонокДополнительныхТабличныхЧастей.НомерСтрокиДокумента КАК НомерСтрокиДокумента,
	|	ВЫРАЗИТЬ(ЗначенияКолонокДополнительныхТабличныхЧастей.Значение КАК Справочник.ИсточникиФинансовогоОбеспечения) КАК ИФО
	|ПОМЕСТИТЬ ВТ_Проезд
	|ИЗ
	|	РегистрСведений.ЗначенияКолонокДополнительныхТабличныхЧастей КАК ЗначенияКолонокДополнительныхТабличныхЧастей
	|ГДЕ
	|	ЗначенияКолонокДополнительныхТабличныхЧастей.Объект = &Ссылка
	|	И ЗначенияКолонокДополнительныхТабличныхЧастей.ТабличнаяЧасть.ИмяТабличнойЧасти = ""Проезд""
	|	И ЗначенияКолонокДополнительныхТабличныхЧастей.КолонкаТабличнойЧасти.ИмяКолонки = ""ИФО""
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЗначенияКолонокДополнительныхТабличныхЧастей.НомерСтрокиДокумента КАК НомерСтрокиДокумента,
	|	ВЫРАЗИТЬ(ЗначенияКолонокДополнительныхТабличныхЧастей.Значение КАК Справочник.ИсточникиФинансовогоОбеспечения) КАК ИФО
	|ПОМЕСТИТЬ ВТ_Проживание
	|ИЗ
	|	РегистрСведений.ЗначенияКолонокДополнительныхТабличныхЧастей КАК ЗначенияКолонокДополнительныхТабличныхЧастей
	|ГДЕ
	|	ЗначенияКолонокДополнительныхТабличныхЧастей.Объект = &Ссылка
	|	И ЗначенияКолонокДополнительныхТабличныхЧастей.ТабличнаяЧасть.ИмяТабличнойЧасти = ""Проживание""
	|	И ЗначенияКолонокДополнительныхТабличныхЧастей.КолонкаТабличнойЧасти.ИмяКолонки = ""ИФО""
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЗначенияКолонокДополнительныхТабличныхЧастей.НомерСтрокиДокумента КАК НомерСтрокиДокумента,
	|	ВЫРАЗИТЬ(ЗначенияКолонокДополнительныхТабличныхЧастей.Значение КАК Справочник.ИсточникиФинансовогоОбеспечения) КАК ИФО
	|ПОМЕСТИТЬ ВТ_ПрочиеРасходы
	|ИЗ
	|	РегистрСведений.ЗначенияКолонокДополнительныхТабличныхЧастей КАК ЗначенияКолонокДополнительныхТабличныхЧастей
	|ГДЕ
	|	ЗначенияКолонокДополнительныхТабличныхЧастей.Объект = &Ссылка
	|	И ЗначенияКолонокДополнительныхТабличныхЧастей.ТабличнаяЧасть.ИмяТабличнойЧасти = ""ПрочиеРасходы""
	|	И ЗначенияКолонокДополнительныхТабличныхЧастей.КолонкаТабличнойЧасти.ИмяКолонки = ""ИФО""
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЗначенияКолонокДополнительныхТабличныхЧастей.НомерСтрокиДокумента КАК НомерСтрокиДокумента,
	|	ВЫРАЗИТЬ(ЗначенияКолонокДополнительныхТабличныхЧастей.Значение КАК Справочник.ИсточникиФинансовогоОбеспечения) КАК ИФО
	|ПОМЕСТИТЬ ВТ_ТоварыРаботыУслуги
	|ИЗ
	|	РегистрСведений.ЗначенияКолонокДополнительныхТабличныхЧастей КАК ЗначенияКолонокДополнительныхТабличныхЧастей
	|ГДЕ
	|	ЗначенияКолонокДополнительныхТабличныхЧастей.Объект = &Ссылка
	|	И ЗначенияКолонокДополнительныхТабличныхЧастей.ТабличнаяЧасть.ИмяТабличнойЧасти = ""ТоварыРаботыУслуги""
	|	И ЗначенияКолонокДополнительныхТабличныхЧастей.КолонкаТабличнойЧасти.ИмяКолонки = ""ИФО""";
	
	Возврат ТекстЗапроса
	
КонецФункции // ТекстЗапросаДопКолонок()

